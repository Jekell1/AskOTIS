      * COPYMEMBER: LIBLP/LPLCAP
      ******************************************************************
      *          LATE CHARGE APPLY ROUTINE
      *
      *   NAME:  LPLCAP
      *   DESC:  PROVIDED WITH 1ST PAYDATE AND A CURRENT PAYMENT
      *          AND TRANSACTION DATE, DETERMINE HOW MUCH
      *          REPRESENTS LATE CHG (LCAP-APP) OR DEFICENCY TO
      *          COVER LATE CHG (LCAP-OWE), AND PROVIDE AN UPDATED
      *          LATE CHG PAID THRU DATE (LCAP-LPPDTH).
      *
      *          WHEN LCAP-LPTRCD = "AL", ALL POSSIBLE LATE CHARGES
      *          ARE ASSESSED, THUS A LCAP-TRAMT LARGE ENOUGH TO
      *          CONSUME ALL LATE CHARGES DUE IS DEFAULTED.
      *
      *   IN  :  LCAP-TRAMT              (LP-TRAMT)
      *          LCAP-1STPYDATE          (LN-1STPYDATE)
      *          LCAP-PAYDATE            (LP-PAYDATE)
      *          LCAP-LPTRCD             (LP-TRCD)  ** ONLY NEED FOR "AL" **
      *          LCAP-LPAPLC             (LP-APLC)
      *          LCAP-LPAPINT            (LP-APINT)
      *          LCAP-POFF-LPTRCD        (POFF-LPTRCD) ONLY NEED FOR PAYOFFS
      *          LCAP-BATCH-REFCD        (SET FOR BATCH REFCD PAYMENTS)
      *          LCAP-BATCH-REFCD-LC-FG  (SET FOR BATCH REFCD PAYMENTS)
      *
      *   OUT :  LCAP-OWE
      *          LCAP-APP
      *          LCAP-LCPDTH-DATE
      *          LCAP-PARTIALS
      *          LCAP-LCPAID
      *          LCAP-NOLCHG
      *
      *   USED:  LN-TOTPAYMNTD, LN-LCPAID, LN-REGPYAMT, LN-1STPYAMT,
      *          LN-TOTNODEF, LN-LCPDTH-DATE, LN-PARTIALS
      *          SP-LCFRMLA, SP-YEARTYPE
      *   COPY:  LPLCAPW, SPPDTHW, LPLCAS, LPLCASW, INCRMM, DATEWK
      *          LPNDTEW, LPNDTE, LPLCHGW, LPLCHG, LPMDTEW, LPMDTE
      *          JULW, TIMW, JUL
      * REV :
      *  010787 JTG ADDED ROUTINE (LATE-CHARGE-AL-CHECK)
      *             TO STOP ADVANCING PAID THRU AT MMYY
      *             LESS THAN ACCESSMENT DATE.
      *  060287 JTG ADDED LOGIC FOR FORMULA "D"
      *  101287 AMW ADDED GENERIC ROUTINE FOR "E"
      *  112487 JTG CORRECTED BUG IN "D", FOR "AL" TRCD
      *  121687 JTG ADDED LOGIC TO TEST ALLOTMENT
      *              CONDITIONS FOR NO LATE CHARGES
      *  122187 JTG REMOVED FORCED 0% TO SP-LCDELFAC
      *  012588 JTG CORRECTED FORMULA E TO NOT TAKE LATE
      *             CHARGES UNTIL CONTRACTUAL DOLLARS
      *             CATCHES UP WITH LPPDTH.
      *             ALSO; CORRECTED ROUTINE TO EVALULATE
      *             WHETHER PAYMENT IS LATE TO USE STATE
      *             PARAMETER LATE CHARGE YEARTYPE
      *             SP-LCYRTYPE.
      *  022588 JTG CORRECTED BUG IN COMPUTATION OF
      *             PAID THRU PAYMENTS.
      *  052088 BAW ADDED LCAP-LPTRCD FOR "AL" INSTEAD OF
      *             CHANGING THE VALUE OF LCAP-LPTRCD
      *             ADDED LCAP-LPAPLC FOR INPUT
      *  053188 JTG ADDED LOGIC FOR FORMULA "G"
      *  061588 JTG CORRECTED FORMULA "F" TO NOT USE
      *             LP-TRAMT, BUT LCAP-TRAMT.
      *  081188 JTG CORRECTED BUG FOR FORMULA "E", IN
      *             COMPUTATION OF LCAP-PDTH-PAYMENTS
      *  040389 JTG CORRECTED BUG IN FORMULA "D" & "G", WHEN
      *             LATE CHARGES ARE PAID AHEAD OF PDTH
      *             AMOUNT NEEDED SHOULD BE FULL PAYMENT
      *  051889 JTG CHANGED ROUTINES FOR CONTRACTUAL PAYMENTS
      *             TOWARD PDTH TO BE INDEPENDENT OF LC FORMULA
      *             CONTRACTUAL REQUIREMENTS
      *  042390 BAH ADDED LCAP-WRK2 TO REPLACE ALL (X+Y) > Z TYPE IF'S
      *  051790 BAH REPLACE LCPAID WITH TOTLCHG FOR LCFRMLA'S "B" "G"
      *  061290 JTG ADDED LOGIC FOR LCFRMLA = "H" & "I"
      *  072490 JTG ADDED REQUIREMENT TO SEND AMOUNTS APPLIED TO INTEREST
      *             AND AMOUNTS APPLIED TO LATE CHARGES
      *  080190 JTG CORRECTED "I", DON'T USE AMT-NEEDED IF PAID THRU
      *             IS PRIOR TO CURRENT PERIOD
      *  091390 SLC ADDED LOGIC FOR MULLEN LATE CHARGES USING
      *             THE PAYMENT SCHEDULES. FORMULA D ONLY!!
      *  120590 JTG CORRECTED LCAP-A FOR USING ELAPSED-DAYS FROM LPLCAS
      *  121290 JTG FIXED LCAP-I-L, RE: FULL LATE CHG FOR WHOLE DUE PERIODS
      *  121890 JTG CORRECTED ALL FORMULAS TO CONSIDER INTEREST OWING
      *  050892 JTG ALLOWED LCAP-G TO WORK WITH PAY SCHEDULE
      *  060592 JTG ADDED LOGIC FOR LN-LC-CODE (ROSE SHANIS)
      *  061992 JTG ADDED MORE SETS OF LCHG-CURPAY FOR SP-LCTYPE = "M"
      *                    SP-LCFRMLA 'D' OR 'G' ONLY
      *  071092 JTG CORRECTED LCAP-G RE: LATE CHARGES PAID IN EXCESS
      *             OF SCHEDULED PAYMENT, CAUSING NO ASSESS
      *  100692 JTG ADDED LOGIC FOR FORMULA "J" AND "K"
      *             NOTE:
      *                   "B" == "J" (USES LCPAID)
      *                   "G" == "K" (USES LCPAID)
      *  020393 JTG CHANGED TO WORK FOR LT-PAY-SCHLD-TBL (17)
      *  030393 JTG CORRECTED LCAP-B-J TO ADVANCE LCPDTH ON WHEN
      *             THE AMOUNT NEEDED IS RECEIVED
      *  040893 JTG ADDED MORE SETS OF LCHG-CURPAY FOR SP-LCTYPE = "M"
      *                     FOR SP-LCFRMLA 'B' OR 'J'
      *  070695 JTG ADDED SP-LCFRMLA 'L', ALMOST 'I', MERCURY 'UCCC'
      *  082595 JTG ADDED SP-LCFRMLA 'M', MERCURY 'LA'
      *  112995 JTG ADDED SP-LCFRMLA 'N', MERCURY 'NV'
      *  071696 JTG ADDED LOGIC FOR UNIT PERIODS
      *  080696 BAH FIXED BUG USING ELAPSED-MONTHS INSTEAD OF ELAPSED-UNITPER
      *  103096 JTG CHANGED TEST ON LN-LC-CODEX TO TEST FOR "00", WHICH
      *             SHOULD ELIMINATE UNNECESSARY IO ON EL-FILE
      *  112297 JFF ADDED LOGIC FOR SCHEDULE PAYMENTS IN LCAP-I-L
      *  120696 JTG CORRECTED BUG LCAP-I, LCAP-I-CURRENT-PERIOD-NEWCUR
      *             INDEX OUT OF BOUNDS
      *  022597 JTG CORRECTED LCAP-I-L, ASSESSING LATE CHARGES ON
      *             CURRECT ACCOUNTS
      *  041097 JTG CHG TO TEST SP-EXPR-FRMLA-CONT & NOT SP-EXPR-FRMLA = " "
      *  050997 JTG ADDED LOGIC TO LCAP-F FOR PAYMENT SCHEDULES
      *  053097 JTG FIXED LCAP-E, WAS ADVANCING LCPDTH 1 TO MANY TIMES
      *  071597 JTG CORRECTED LCAP-F RE: PAYMENT SCHEDULES, ADVANCE
      *  060898 JTG CORRECTED LCAP-I-L TO STOP ADVANCING LCPDTH UNNECESSARLY
      *             RE: WORLD #JG001
      *  101498 BAH CORRECTED GENERIC-LCAP-BYPASS-CHG, LCAP-LCPDTH-DATE WAS
      *             NOT FORCING 1STPYDA INTO LCPDTH, SEE COMMENTS BELOW.
      *  101498 BAH CORRECTED LCAP-A-C ROUTINE, WAS MOVING 4 DIGIT PDTH-DATE
      *             TO LCAP-LCPDTH-DATE INSTEAD OF PDTH-DATE-FULL, REGENCY (PA)
      *  101598 JTG ADDED LOGIC FOR LCFRMLA = 'R', REVOLVING REGENCY
      *             WHICH BE COMPUTED IN LONPFC.C AND RVSTUP.C
      *  030399 GLF ADDED SET OF LCHG-DAYS-AFTER-DUE, MULLEN #804
      *  990323 JTG ADDED LOGIC FOR BR-LATECHG-ON-ALLOTMENTS, TFC #018
      *  990402 JTG CORRECTED LATE CHARGE ASSESS FOR 'B' & 'J'
      *  990402 JTG REMOVED ATTEMPT TO CORRECTED LATE CHARGE ASSESS FOR 'B'&'J'
      *  990422 JTG CHANGED TO ASSESS A LATE CHARGE EVEN IF ACCOUNT
      *             IS PAID AHEAD EX. LN-AGEING-AMT(1) < 0
      *  JTG 990423 CHANGED TO ASSESS A LATE CHARGE ON CURRENT CHARGES
      *             AND PAST DUE CHARGES
      *  JTG 990812 ADDED LOGIC FOR SP-LCFRMLA = J TO NOT CONSIDER PREVIOUS
      *             PAID LATE CHARGES                                    KEARNEY
      *  JTG 990826 ADDED SCCS-COPY-ROUTINE
      *  JTG 990930 CHANGED LCAP-F TO SET PMT-TO-DATE TEMP TO INCLUDE APPLIED
      *             TO INTEREST, CITIZENS FINANCE:
      *  991102 JTG CHANGED LOGIC TO TEST ALLOTMENT CONDITIONS FOR NO LATE
      *             CHARGE AND ALLOW A LATE CHARGE ON PAYMENTS OTHER THAN 'PA'
      *             CFSC - CINDY
      *  991103 JTG CHANGED LCAP-G-K TO TRULY MAKE LCFRMLA 'K' NOT CONSIDER
      *             PRIOR LATE CHARGES PAID VIA LCPAID, CITIZENS (TN) - JUDY
      *  JTG 991217 CHANGED LOGIC FOR LCFRMLA = 'R', REVOLVING REGENCY
      *             TO ONLY TEST MONTH AND YEAR, SINCE PAYMENT DUE DAY CAN
      *             CHANGE MONTH TO MONTH, WHEN SEEING IF LATE CHARGE WAS
      *             ASSESED SINCE LAST STATEMENT
      *  JTG 000111 ADDED LCAP-NOLCHG  WORLD #218
      *             TO TRACK THE NUMBER OF LATE CHARGES ASSESSED
      *  JTG 000203 CORRECTED LOGIC FOR SP-LCFRMLA = J TO NOT CONSIDER PREVIOUS
      *             PAID LATE CHARGES AND TO CORRECTLY CONSIDER INTESEST PAID
      *             FROM OWING (IB ACCOUNTS)     KEARNEY, REGENCY BR:121 #500157
      *  JTG 000724 ADDED LOGIC FOR LCFRMLA = "H"  RE: TOTAL MONIES REGMGM #1262
      *  JTG 000822 ADDED LOGIC FOR KENTUCKY 60 DAY NO CHARGE LAW, ADDED
      *             SP-RBSPOPT1() = 19, WORLD #233
      *  JTG 000829 CORRECTED LCAP-G-K BUG, TO MAKE LCFRMLA 'K' CONSIDER THE SPR
      *             LATE CHARGE DELINQUENCY FACTOR%    REGENCY #294  (TN) - DIRK
      *  MJD 000911 ADDED TEST OF FOR LOCK BOX ACCOUNTS (MFC PR#227)
      *  JTG 001006 CHANGED LCAP-I-L TO NOT SET LCAP-CURDUE AMOUNT WHEN IT
      *             IS LESS THAN THE DELINQUENCY FACTOR, THIS WILL ALLOW
      *             THE NEXT PAYMENT IF POSTED TO LATE TO TAKE A LATE CHARGE
      *             ACE FINANCIAL PR#297
      *  JTG 001009 CHANGED LCAP-M TO NOT SET LCAP-CURDUE AMOUNT WHEN IT
      *             IS LESS THAN THE DELINQUENCY FACTOR, THIS WILL ALLOW
      *             THE NEXT PAYMENT IF POSTED TO LATE TO TAKE A LATE CHARGE
      *             ACE FINANCIAL PR#297
      *  JTG 010130 ADDED LOGIC TO ALLOW TEST FOR LOCK BOX OR ALLOTMENT ACCOUNTS
      *             THAT WERE POSTED IN LONPW5.C OR LONPW6.C AS "PO"
      *                                                           MERCURY PR#239
      *  JTG 010209 CHANGED NAME "GENERIC-LCAP" TO "LCAP-E"
      *             ADDED LCAP-TRAMT-PLUS-AP, IDEAL PR#315 LCAP-E
      *  JTG 010212 REWRITE LCAP-E LOGIC BY TAKING LCAP-B-J AND LETTING IT
      *             WORK FOR TOTAL MONIES,    IDEAL PR#315
      *  JTG 010212 MODIFIED LCAP-B-J, LCAP-E AND LCAP-N ROUTINES TO TEST 'AL'
      *             AND STOP ASSESSING WHEN THERE IS NO ASSESSMENT
      *  JTG 010326 MODIFIED LCAP-A-C ROUTINE TO TEST 'AL' AND STOP
      *             ASSESSING WHEN THERE IS NO ASSESSMENT
      *  JTG 010625 CREATED LCAP-X (BAD LOGIC) BY PUTTING BACK THE OLD LOGIC
      *             FOR LCAP-E                                     IDEAL PR#1509
      *  JTG 010627 ADDED LOGIC IN EXIT ROUTINE OF ALL FORMULAS TO ASSURE
      *             LCAP-LCPDTH-DATE HAD A VALID DAY IF ACCOUNT IS MONTHLY
      *                                                            IDEAL PR#1509
      *  JTG 010629 REMOVED LCAP-X (BAD LOGIC) AND ADDED LCAP-E-X  IDEAL PR#1509
      *             AND CHANGED TO SUBTRACT LCHG-CHARGE FROM LCAP-AMTNEEDED
      *  JTG 010731 CHANGED LCAP-E-X FOR ANOTHER ATTEMPT TO MAKE 'X'
      *             WORK LIKE DIANE AT IDEAL WANTS. AFTER A FULL
      *             PAYMENT IS SATISFIED AND THE NEXT PAYMENT IS LATE AND
      *             DUE A LATE CHARGE, IF THERE IS NOT ENOUGH MONIES TO APPLY
      *             TOWARDS THE FULL LATE CHARGE TAKE IT ANYWAY FROM MONIES
      *             APPLIED TO SATISFY THE PREVIOUS SHORTAGE       IDEAL PR#1509
      *  JTG 020404 INSURED THAT ACCELERATED ACCOUNTS WILL NOT COMPUTE/TAKE
      *             LATE CHARGES
      *             RE: ALLOWED IB TYPE LOANS TO ACCELERATE JIM R. (ALL) PR#1582
      *  JTG-CS     WHEN SEMI MONTHLY WITH FIRST PAYDATE OF 14TH OR 15TH,
      *      020913 LCPDTH WILL GO TO 02/28/YY OR 02/29/YY WHEN LCPDTH
      *             WAS UPDATED FROM 02/14/YY OR 02/15/YY. THE FOLLOWING
      *             LOGIC RE-ESTABLISHES CYCLES BACK TO THE 14TH OR 15TH.
      *  JTG 021219 ADDED NEW CODE FILE TYPE 'BR' -BATCH PAYMENT REFERENCE CODES
      *             BATCH PAYMENT REVISIONS                         REGACC #0006
      *  BAH 030507 ADDED NEW BRANCH FLAG TO STOP ASSESSING LATE CHARGES FOR
      *             THE ENTIRE BRANCH..THEY MUST SET IT = "N", OTHERWISE IT
      *             WILL CONTINUE TO CHARGE LATE CHARGES...MODERN PR# 2036
      *  JTG 030528 ADDED SP-LCFRMLA = 'P' LIKE 'I'                  PACE #408
      *  JTG 030519 ADDED LOGIC TO LCAP-B-J TO CORRECTLY HANDLE
      *             IB ACCOUNTS WHERE MONEY IS APPLIED TO INTEREST  REGENCY #403
      *  BAH 030620 ADDED TEST FOR GP-I-AM-MODERN TO THE BR-ASSESS-LATE-CHGS
      *             RE USED A BR FLAG THAT MUST OF HAD A "N" IN IT...
      *  BAH 030814 LCAP-A-C WAS MISSING MOVE TO LCHG-LATE FOR FORMULA C,
      *             CAUSING NO LATE CHARGE BUT EVERYTHING ADVANCED AND ACTED
      *             AS IF IT TOOK A LATE CHARGE, BUT IT DIDNT CAUSE THIS FIELD
      *             WAS 0, DAVID (BAY FINANCE)
      *  040401 BAH ADDED SP-LC-ON-JUDGEMENTS FOR RIVERSIDE, PR# 2272, STATE
      *             LAW SAYS NO LATE CHARGES ON JUDGEMENT ACCOUNTS
      *  040405 BAH PACESETTER "I" WAS STILL USING 4 DIGIT PDTH....CHANGED
      *             PDTH-DATE TO PDTH-DATE-FULL
      *  040708 JTG CORRECTED LIBLP/LPLCAP, LCAP-A-C TO NOT GO INTO A
      *             NEVER ENDING LOOP WHEN OERATOR POSTS A PAYMENT
      *             IN EXCESS OF LOAN'S BALANCE                    REGENCY #J047
      *  051206JG&CS ACCOUNTS WITH LCPDTH OF 2/28 DID NOT ADVANCE LCPDTH
      *             CORRECTLY IF 1STPYDA > 28 FOR MARCH.  ON SEMI MONTHLY WITH
      *             DUE DATES OF 14 OR 15 OR 30, IT CHANGED THE CYCLE DATES
      *             CAUSING LATE CHARGES ON SUBSEQUENT PMTS PR#502 TRITON
      *  100211CS   FIXED LCAP-CALC-PDTH-ADDON SECTION.  WAS MOVING PDTH-DATE-
      *             FULL TO NDTE-DATE, INCREMENTING UNITPER, AND THEN MOVING
      *             IN THE FIRST PAY DAY. THIS WAS CAUSING A PROBLEM BECAUSE
      *             IT CAUSED PDTH-DATE-FULL IN FEB IF FIRST DUE DAY WAS 29,
      *             30,OR 31, BECAUSE IT MADE PDTH-DATE-FULL = 02/31/09.  THIS
      *             LATER GOT TESTED IN LCAP-B-J & BECAUSE LCAP-LCPDTH-DATE WAS 2/28
      *             THEY WERE NOT EQUAL & LCPDTH WAS NOT ADVANCED PR#675
      *  100309 BLM BARB & CINDY, FIX FORMULA E & X FOR PAYMENT SCHEDULES.
      *             IN LCAP-E-X-PMT IT WAS MOVING REGPAY TO LCAP-AMTNEEDED
      *             INSTEAD OF SCHEDULED AMOUNT, REGWAN PR# 3506
      *  100309 BLM BARB & CINDY, FIX FORMULA B & J FOR PAYMENT SCHEDULES.
      *             IT WAS MOVING REGPAY TO LCAP-AMTNEEDED
      *             INSTEAD OF SCHEDULED AMOUNT, REGWAN PR# 3506
      *  100309  CS  WHEN WE FIXED B/J/E/X TO WORK WITH PAYMENT SCHEDULES WE
      *              MISSED AN ADD TO LCAP-PMT-NO AFTER SATISFYING ANY SHORTAGE       *              FROM PRIOR PAYMENTS. RAN IN TO THIS WHEN WE WERE ON THE
      *              LAST PAYMENT IN A LEVEL OF SCHEDULE WITH A SHORTAGE, IT
      *              COVERED THE SHORTAGE, BUT THEN STILL THOUGHT YOU WERE ON
      *              THE SAME PAYMENT.  ADDED THIS TEST IN LPCAP-B-J-PMT, LCAP-
      *              E-X-PMT: IF LCAP-AMTNEEDED < LCAP-TRAMT, AFTER SUBTRACT
      *              OF LCAP-AMTNEEDED, IF LN-PAY-SCHLD-FG = "Y", ADD 1 TO
      *              LCAP-PMT-NO. REGWAN PR# 3506
      *  100330 BLM  BARB & CINDY, WHEN CHECKING TO SEE IF TRAMT IS ENOUGH TO
      *              COVER LATE-CHARGE (LCHG-CHARGE), NEED TO SUBTRACT ESCROW
      *              AMT FROM TRAMT IF ACCOUNT HAS ESCROW.  WE ADD ESCROW AMT
      *              BACK IN TO LCAP-TRAMT TO MAKE SURE IT COMPUTES AMT
      *              NEEDED CORRECTLY, BUT FOR EXAMPLE, IF THE ENTIRE TRAMT
      *              IS PUT TO ESCROW, THERE IS NOTHING LEFT TO APPLY TO
      *              LATE CHARGE AND IT WILL GO NEGATIVE!  REGWAN PR# 3506
      *  100813CS    FIXED LCAP-I-L-NOASSESS NOT TO INCLUDE LCAP-LPAPINT IN
      *              COMPUTE OF LCAP-WRK, SEE EXPLANATION AT TOP OF LCAP-I-L-
      *              NO-ASSESS  PL694
      *  110105CS    ATTEMPT TO FIX "H" TO USE SP-DELFAC. THIS CODE IS SUPPOSED
      *              TO ACT LIKE 'F' BUT KEVIN/JIM SAID IT NEEDED TO CONSIDER.        *              ORIG SAVE AS LPLCAP.110105  REGACC #279
      *  110215 MJD  ADDED LOGIC FOR SP-LCFRMLA = "S"  SIMILAR TO "F" WORLD #723
      *                ******** CHANGES STILL IN PROGRESS *******
      *  120730  CS  IF SP-LC-INT-ON-REPO-NON-PL = "N", DONT ASSESS LATE CHG
      *                                                   REGACC PR#314
      *  160113 BAH SPLIT OUT LTFILE, LTPFILE, RENAMED LT- LTP- PD0003
      *  160615 BLM FIX LCAP-R TO CALL GETLTR SINCE REVOLVING FIELDS ARE IN
      *             LTRFILE, NOT LNFILE, PD0003
      * KEC 2016.1116 {PD#00003} PARADATA <A30> [JAY CISZEWSKI]
      *         ADDED LTR-SEQNO TO LTR1-KEY.
      * BLM 170309 CHANGE COMPARE OF PDTH-WK-YR TO USE PDTH-WK-CCYY, PD0006
      * BAH 170403 LCAP-LCPAID CAN BE A DATE AND MUST STAY MMDDYY BECAUSE
      *            WE CAN NOT DECIPHER WHICH ONES ARE DATES AND WHICH ARE AMTS
      *            WHEN UPGRADING THE LOAN FILE AND PAYMENT FILE! SO WE CANT
      *            CHANGE THE WAY THE DATE WAS FORMATTED IN A15
      * KEC 20190219 REMOVED REDEFINE FOR LN-LC-CODEX; UNUSED
      * BAH 20190625 REMOVED LN-REVOLVING, PT0153 LCFRMLA "R"
      * BAH 20220121 COMMENTED OUT INDIVIDUAL MOVES OF LCAP-CURDATE-MM,-YY
      *              ADDED MOVE LCAP-CURDATE TO LCAS-LCPDTH-DATE.
      *              CINDY FOUND WHILE TESTING
      * BAH 20230328 ADDED SP-LCFRMLA "T" FOR "WI" #1597
      **************************************************************************
       LATE-CHARGE-APPLY SECTION.
           MOVE 0 TO LCAP-APP
                     LCAP-OWE
                     LCAP-NOLCHG.
           MOVE LN-LCPARTIALS TO LCAP-PARTIALS.
           MOVE LN-LCPDTH-DATE     TO LCAP-LCPDTH-DATE.

      * INSURED THAT ACCOUNTS THAT ARE ACCELERATED WILL NOT COMPUTE
      * LATE CHARGES
           IF LN-ACCELDATE NOT = 0
              GO TO LCAP-APP-EXIT.

      * RIVERSIDE PR# 2272 4/04/04
           IF SP-LC-ON-JUDGEMENTS = "N"
              IF LN-JDDATE NOT = 0
                 GO TO LCAP-APP-EXIT.

      * REGACC
           IF SP-LC-INT-ON-REPO-NON-PL = "N"
             IF LN-REPOCD = "X" OR "S"
                IF LN-PLDATE = ZEROS
                   MOVE 0 TO SP-LCRATE
                   MOVE 0 TO SP-LCMINCHG.

      * NEW FLAG TO STOP LATE CHARGES IN AN ENTIRE BRANCH (LEGAL)
      *    IF BR-ASSESS-LATE-CHGS = "N" AND GP-I-AM-MODERN
      *       GO TO LCAP-APP-EXIT.

           MOVE LN-UNITPER-CD   TO DATER-UNITPER-CD.
           MOVE LN-UNITPER-FREQ TO DATER-UNITPER-FREQ.

           IF ( LN-LC-CODE NOT = ZEROES )
              PERFORM LATE-CHARGE-TEST-ELREAD.

           IF SP-LCFRMLA = "B" OR "G" OR "N"
              MOVE LN-TOTLCHG TO LCAP-LCPAID
           ELSE
              MOVE LN-LCPAID  TO LCAP-LCPAID.

           IF LCAP-1STPYDATE = 0 OR SP-LCFRMLA = " "
              GO TO LCAP-APP-EXIT.

      * ESTABLISH CORRECT PAYMENTS TO DATE FOR SPECIFIC ROUTINE:

           IF SP-LCFRMLA = "A" OR "C" OR "D" OR "E"
                               OR "H" OR "N" OR "X" OR "T"
              IF SP-CONTRFRMLA = " "
                 ADD LN-TOTPAYMNTD LN-TOTLCHG GIVING LCAP-TOTPAYMNTD
              ELSE
                 MOVE LN-TOTPAYMNTD TO LCAP-TOTPAYMNTD
           ELSE
              IF SP-LCFRMLA = "B" OR "F" OR "G" OR "I"
                                  OR "J" OR "K" OR "L"
                                  OR "M" OR "P" OR "S"
                 IF SP-CONTRFRMLA = "A"
                    SUBTRACT LN-TOTLCHG FROM LN-TOTPAYMNTD
                                           GIVING LCAP-TOTPAYMNTD
                 ELSE
                    MOVE LN-TOTPAYMNTD TO LCAP-TOTPAYMNTD.

      * ASSESS ALL POSSIBLE LATE CHARGES:
           IF LCAP-LPTRCD = "AL"
              MOVE 9999999.99 TO LCAP-TRAMT.

      * RECALCULATE PAID THRU DATE, AMT NEEDED TO COMPLETE ONE FULL PAYMENT
      * (WITH OR W/O REGARD TO LATE CHGS PAID, DEPENDING ON THE CONTENT
      * OF LCAP-TOTPAYMNTD FOR SPECIFIC LCFRMLA)
      *
           MOVE LCAP-TOTPAYMNTD TO PDTH-PAYMNTD LCAP-PAYMNTD.
           MOVE LCAP-LCPDTH-DATE     TO LCHG-LCPDTH-DATE.
           IF LN-PAY-SCHLD-FG = "Y"
              PERFORM LCAP-CALC-PDTH-ADDON
           ELSE
              PERFORM LCAP-CALC-PDTH.

      * AMOUNT THAT NEEDS TO BE LATE IN ORDER TO ASSESS LC:
      
           MOVE 0 TO LCAP-PMT-NO.
           IF LN-PAY-SCHLD-FG = "Y"
              PERFORM UNPACK-PAYMENT-SCHEDULE
              MOVE LN-LCPDTH-DATE TO NUM-DATE
              MOVE LN-1STPYDATE TO SYS-DATE
              PERFORM TIMUPER
              COMPUTE LCAP-PMT-NO = (ELAPSED-UNITPER * (-1)) + 2 -
                      LN-TOTNODEF - LN-TOTNOALDEL
              IF LCAP-PMT-NO = 0
                 MOVE 1 TO LCAP-PMT-NO
                 MOVE SCHD-AMT(1) TO LCAP-WRK
                                     LCHG-CURPAY
              ELSE
                 MOVE SCHD-AMT(LCAP-PMT-NO) TO LCAP-WRK
                                               LCHG-CURPAY
                 IF LN-ESCROW-FG = "Y"
                    MOVE LN-REGPYAMT TO LCAP-WRK
                                        LCHG-CURPAY
                 END-IF
           ELSE
              MOVE LN-REGPYAMT TO LCAP-WRK
                                  LCHG-CURPAY
              IF LCAP-TOTPAYMNTD < LN-1STPYAMT
                 MOVE LN-1STPYAMT TO LCAP-WRK.

           COMPUTE LCAP-DELFAC ROUNDED = LCAP-WRK * (SP-LCDELFAC / 100).
           IF LCAP-DELFAC = 0
              MOVE .01 TO LCAP-DELFAC.

           PERFORM MATURITY-DATE-CALCULATION.

      * DETERMINE AMT NEEDED TO COMPLETE A FULL PAYMENT:
      
           IF LN-PAY-SCHLD-FG = "Y"
              MOVE SCHD-AMT(LCAP-PMT-NO) TO LCAP-AMTNEEDED
              MOVE LCAP-REMAIN TO PDTH-REMAIN
           ELSE
              MOVE LN-REGPYAMT TO LCAP-AMTNEEDED
              IF LCAP-TOTPAYMNTD < LN-1STPYAMT
                 MOVE LN-1STPYAMT TO LCAP-AMTNEEDED.
           COMPUTE LCAP-AMTNEEDED ROUNDED =
                             (1 - PDTH-REMAIN) * LCAP-AMTNEEDED.

      * SELECT APPROPRIATE LCFRMLA
      
           IF SP-LCFRMLA = "A" OR "C"
              PERFORM LCAP-A-C
           ELSE
           IF SP-LCFRMLA = "B" OR "J"
              PERFORM LCAP-B-J
           ELSE
           IF SP-LCFRMLA = "D"
              PERFORM LCAP-D
           ELSE
           IF SP-LCFRMLA = "E" OR "X"
              PERFORM LCAP-E-X
           ELSE
           IF SP-LCFRMLA = "F"
              PERFORM LCAP-F
           ELSE
           IF SP-LCFRMLA = "G" OR "K"
              PERFORM LCAP-G-K
           ELSE
           IF SP-LCFRMLA = "H"
              PERFORM LCAP-H
           ELSE
           IF SP-LCFRMLA = "I" OR "L"
              PERFORM LCAP-I-L
           ELSE
           IF SP-LCFRMLA = "M"
              PERFORM LCAP-M
           ELSE
           IF SP-LCFRMLA = "N"
              PERFORM LCAP-N
           ELSE
           IF SP-LCFRMLA = "P"
              PERFORM LCAP-P
           ELSE
           IF SP-LCFRMLA = "S" OR "T"
              PERFORM LCAP-S.

       LCAP-APP-EXIT.
           IF LCAP-LPTRCD = "AL"
              ADD LCAP-APP TO LCAP-OWE
              MOVE 0 TO LCAP-APP.

      * ASSURE LATE CHARGE PAID THRU HAS VALID DAY:

           PERFORM LCAP-ASSURE-LCPDTH.

      ************************************************
      *   SP-LCFRMLA = A OR C
      ************************************************
       LCAP-A-C SECTION.

      * SET PMT-TO-DATE TEMP; ALSO SET LCAP-LCPAID AS TEMP
      * FOR LN-CURBAL (LCPAID IS NOT USED FOR THIS ROUTINE)

           ADD LCAP-TOTPAYMNTD LCAP-LPAPLC LCAP-LPAPINT
                                            GIVING LCAP-PAYMNTD.
           MOVE LN-CURBAL TO LCAP-LCPAID.
           MOVE "Y" TO LCAP-AL-OK.

       LCAP-A-C-BEGIN.

      * PAYMENT CONSUMED ?
           IF LCAP-TRAMT = 0
              GO TO LCAP-A-C-EXIT.

      * LOAN BALANCE 0 ? (THIS IMPLIES ASSESSMENT AFTER MATURITY)
           IF SP-EXPR-FRMLA-CONT
              IF LCAP-LCPAID NOT > 0 OR
                 LCAP-AL-OK = "N"
                 GO TO LCAP-A-C-EXIT.

      * PAST MATURITY ?
           PERFORM LCAP-MDTE-CHK.
           IF NOT SP-EXPR-FRMLA-CONT
              IF ELAPSED-DAYS NOT > 0
                 GO TO LCAP-A-C-EXIT.

      * LCAP-LPTRCD = AL, IF WHERE LCPDTH IS PAID WILL NOT CAUSE
      *                      AN ASSESSMENT, EXIT
           IF LCAP-LPTRCD = "AL"
              PERFORM LCAP-ASSESS-CHK
              IF LCAS-ASSESS NOT = "Y"
                 GO TO LCAP-A-C-EXIT.

      * CHECK FOR PARTIALS
           IF LCAP-TRAMT < LCAP-AMTNEEDED
              MOVE LCAP-TRAMT TO LCAP-WRK
           ELSE
              MOVE LCAP-AMTNEEDED TO LCAP-WRK.

      * TEST FOR OVER LOAN BALANCE:
           IF LCAP-WRK > LCAP-LCPAID
              GO TO LCAP-A-AL-OK-TEST.

           PERFORM LCAP-ASSESS-CHK.

      * DOES PAYMENT AND LCPDTH INDICATE AN ASSESSMENT ?
           IF LCAS-ASSESS NOT = "Y"
              GO TO LCAP-A-C-PMT.

           IF SP-LCFRMLA = "C"
              IF LCAP-AMTNEEDED NOT = LCAP-WRK
                 MOVE 0 TO LCAP-PARTIALS
                           LCHG-CHARGE
                 GO TO LCAP-A-C-PMT
              ELSE
      * LCHG-LATE INITIALIZED FOR SP-LCTYPE = "C" OR "J"
      * SINCE LATE CHARGE IS BASED ON % OF DELINQUENT AMOUNT
                 MOVE LCAP-AMTNEEDED TO LCHG-LATE
                 MOVE LCAP-LCPDTH-DATE TO LCHG-LCPDTH-DATE
                 PERFORM LCAP-CALC-TEST
                 MOVE 0 TO LCAP-PARTIALS
                 GO TO LCAP-A-ACCUM.

      * FOR FORMULA "A" CALCULATE LCHG:
      * ALSO IF ON ALLOTMENT OR LOCK BOX PLAN OR POSTING WITH A BATCH REFCD
      * TEST IF LATE CHARGES SHOULD BE CHARGED:

           IF ( ( LCAP-BATCH-REFCD NOT = " "        ) AND
                ( LCAP-BATCH-REFCD NOT = LOW-VALUES ) AND
                ( LCAP-BATCH-REFCD-LC-FG NOT = "Y"  ) AND
                ( ( LCAP-LPTRCD = "PA" ) OR
                  ( LCAP-POFF-LPTRCD = "PO" AND LCAP-LPTRCD = "AL" )
                )
              )
             OR
              ( ( ( LCAP-BATCH-REFCD = " "          ) OR
                  ( LCAP-BATCH-REFCD = LOW-VALUES   )
                )
               AND
                ( LN-ALLOTCD = "Y"                   ) AND
                ( BR-LATECHG-ON-ALLOTMENTS NOT = "Y" ) AND
                ( ( LCAP-LPTRCD = "PA" ) OR
                  ( LCAP-POFF-LPTRCD = "PO" AND LCAP-LPTRCD = "AL" )
                )
              )
             OR
              ( ( ( LCAP-BATCH-REFCD = " "          ) OR
                  ( LCAP-BATCH-REFCD = LOW-VALUES   )
                )
               AND
                ( LN-LBOX = "Y"                ) AND
                ( BR-LATECHG-ON-LBOX NOT = "Y" ) AND
                ( ( LCAP-LPTRCD = "PA" ) OR
                  ( LCAP-POFF-LPTRCD = "PO" AND LCAP-LPTRCD = "AL" )
                )
              )
             OR
              ( ( LCAP-POFF-LPTRCD = "PB" OR "RN" OR
                                     "SC" OR "RB" OR "RO" ) AND
                ( SP-RBSPOPT1(7) = 19                     ) AND
                ( LCAP-NOLCHG    = 2                      )
              )
              MOVE 0 TO LCHG-CHARGE
           ELSE
              COMPUTE LCHG-CHARGE ROUNDED =
                 ((SP-LCRATE / 100) / 30)
                        * LCAS-ELAPSED-DAYS * LCAP-WRK
              IF LCAP-AMTNEEDED = LCAP-WRK
                 COMPUTE LCAP-WRK2 = LCHG-CHARGE + LCAP-PARTIALS
                 IF LCAP-WRK2 < SP-LCMINCHG
                    SUBTRACT LCAP-PARTIALS FROM SP-LCMINCHG
                    GIVING LCHG-CHARGE.

           IF LCHG-CHARGE > SP-LCMAXAMT
              MOVE SP-LCMAXAMT TO LCHG-CHARGE.

           IF LCAP-AMTNEEDED = LCAP-WRK
              MOVE 0 TO LCAP-PARTIALS
              ADD 1 TO LCAP-NOLCHG
           ELSE
              ADD LCHG-CHARGE TO LCAP-PARTIALS.

       LCAP-A-ACCUM.
           ADD LCHG-CHARGE TO LCAP-PAYMNTD
                              LCAP-APP.
           SUBTRACT LCHG-CHARGE FROM LCAP-TRAMT
                                     LCAP-WRK.

       LCAP-A-C-PMT.

      * TEST FOR ASSESS AND 0 CURBAL REACHED:
           IF LCAP-LPTRCD = "AL"
              IF LCAP-LCPAID = 0
                 MOVE LCAP-PAYDATE TO LCAP-LCPDTH-DATE
                 GO TO LCAP-A-C-EXIT.

      * REDUCE LOAN BALANCE TEMP STORED IN LCAP-LCPAID
      * USED WHEN LATE CHARGES ARE ASSESSED BEYOND MATURITY. SEE ABOVE
           SUBTRACT LCAP-WRK FROM LCAP-LCPAID
                                  LCAP-TRAMT.
           ADD LCAP-WRK TO LCAP-PAYMNTD.
           MOVE LCAP-PAYMNTD TO PDTH-PAYMNTD.

      * COMPUTE LCPDTH BASED ON TOTAL PMTS (PMTS + LATE CHGS)
           PERFORM LCAP-CALC-PDTH.

      * IF LCPDTH AS COMPUTED WILL ADVANCE, UPDATE.
      * (THIS IS TO PROVIDE FOR CODES SUCH AS "AL" WHICH
      * ADVANCE LCPDTH AS IF ACTUAL PMTS WERE MADE, EVEN
      * THOUGH LCAP-TOTPAYMNTD WILL ONLY INCLUDE THE LATE CHARGE)

           MOVE LCAP-LCPDTH-DATE    TO NUM-DATE.
           MOVE 1                   TO NUM-DA.
           MOVE PDTH-DATE-FULL      TO SYS-DATE.
           MOVE 1                   TO S-DD.
           PERFORM TIM365.
           IF ELAPSED-DAYS > 0
              PERFORM LCAP-A-AL-OK-TEST
              IF LCAP-AL-OK = "Y"
                 MOVE PDTH-DATE-FULL TO LCAP-LCPDTH-DATE.

           MOVE LN-REGPYAMT TO LCAP-AMTNEEDED.
           GO TO LCAP-A-C-BEGIN.

      * IF SP-LCFRMLA = "A" WE MUST NOT ADVANCE LATE CHARGE PAID THRU
      * TO THE MONTH & YEAR OF THE "AL" DATE, IF THE ACCESS DATE IS
      * PRIOR TO THE NORMAL PAY DAY OF THAT MONTH.
      
       LCAP-A-AL-OK-TEST.
           MOVE "Y" TO LCAP-AL-OK.
           IF LCAP-LPTRCD = "AL"
              IF SP-LCFRMLA = "A"
                 MOVE LCAP-PAYDATE TO NUM-DATE
                 IF PDTH-WK-CCYY = NUM-CCYY
                    IF PDTH-WK-MM = NUM-MO
                       MOVE LCAP-1STPYDATE TO SYS-DATE
                       IF NUM-DA < S-DD
                          MOVE "N" TO LCAP-AL-OK.
       LCAP-A-C-EXIT.
           EXIT.

      ***********************************************
      *  SP-LCFRMLA = B OR J:
      *     "B" == "J" WHICH USES LCPAID
      ***********************************************
       LCAP-B-J SECTION.

      * TEST FOR PRIOR PAYMENT SHORTAGES:

           IF LCAP-LCPDTH-DATE NOT = PDTH-DATE-FULL
              IF (LCAP-TRAMT + LCAP-LPAPLC + LCAP-LPAPINT)
                                                > LCAP-AMTNEEDED
                 GO TO LCAP-B-J-PMT
              ELSE
                 GO TO LCAP-B-J-EXIT.

       LCAP-B-J-DATE-CHK.

      * VERIFY DATES FOR ASSESSMENT
           PERFORM LCAP-MDTE-CHK.
           IF ELAPSED-DAYS NOT > 0
              GO TO LCAP-B-J-EXIT.

           PERFORM LCAP-ASSESS-CHK.
           IF LCAS-ASSESS NOT = "Y"
              IF LCAP-LPTRCD = "AL"
                 GO TO LCAP-B-J-EXIT
              ELSE
                 GO TO LCAP-B-J-RETRY.

      * LCHG-LATE INITIALIZED FOR SP-LCTYPE = "C" OR "J",
      * WHERE LATE CHG IS BASED ON % OF LATE PMT:
           MOVE LCAP-AMTNEEDED TO LCHG-LATE.
           MOVE LCAP-LCPDTH-DATE    TO LCHG-LCPDTH-DATE.
           PERFORM LCAP-CALC-TEST.

      * IF TRAMT INSUFFICIENT TO COVER CHARGE, OWING IS FOUND:
           IF LN-ESCROW-FG = "Y"
              IF (LCAP-TRAMT - LCAP-ESCROW-AMT) < LCHG-CHARGE
                 COMPUTE LCAP-OWE = LCHG-CHARGE
                                  - (LCAP-TRAMT - LCAP-ESCROW-AMT)
                 COMPUTE LCAP-APP = LCAP-APP
                                  + (LCAP-TRAMT - LCAP-ESCROW-AMT)
                 PERFORM LCAP-UPD-LCPD
                 GO TO LCAP-B-J-EXIT
              END-IF
           ELSE
              IF LCAP-TRAMT < LCHG-CHARGE
                 SUBTRACT LCAP-TRAMT FROM LCHG-CHARGE GIVING LCAP-OWE
                 ADD LCAP-TRAMT TO LCAP-APP
                 PERFORM LCAP-UPD-LCPD
                 GO TO LCAP-B-J-EXIT.

      * OTHERWISE, TAKE LATE CHARGE AND (ATTEMPT) PMT RECEIPT:
           ADD LCHG-CHARGE TO LCAP-APP
                              LCAP-LCPAID.
           SUBTRACT LCHG-CHARGE FROM LCAP-TRAMT.
           PERFORM LCAP-UPD-LCPD.
           IF (LCAP-TRAMT + LCAP-LPAPLC + LCAP-LPAPINT)
                                           NOT > LCAP-AMTNEEDED
              GO TO LCAP-B-J-EXIT.

      * REDUCE TRANSACTION AMOUNT BY PAYMENT:
       LCAP-B-J-PMT.
           IF LCAP-AMTNEEDED >= LCAP-TRAMT
              SUBTRACT LCAP-TRAMT FROM LCAP-AMTNEEDED
              MOVE 0 TO LCAP-TRAMT
           ELSE
              SUBTRACT LCAP-AMTNEEDED FROM LCAP-TRAMT
              MOVE 0 TO LCAP-AMTNEEDED
              IF LN-PAY-SCHLD-FG = "Y"
                 ADD 1 TO LCAP-PMT-NO.
   
           IF LCAP-AMTNEEDED >= LCAP-LPAPINT
              SUBTRACT LCAP-LPAPINT FROM LCAP-AMTNEEDED
              MOVE 0 TO LCAP-LPAPINT
           ELSE
              SUBTRACT LCAP-AMTNEEDED FROM LCAP-LPAPINT
              MOVE 0 TO LCAP-AMTNEEDED.
   
           IF LCAP-AMTNEEDED >= LCAP-LPAPLC
              SUBTRACT LCAP-LPAPLC FROM LCAP-AMTNEEDED
              MOVE 0 TO LCAP-LPAPLC
           ELSE
              SUBTRACT LCAP-AMTNEEDED FROM LCAP-LPAPLC
              MOVE 0 TO LCAP-AMTNEEDED.
   
           IF LN-PAY-SCHLD-FG = "Y"
              MOVE SCHD-AMT(LCAP-PMT-NO) TO LCAP-AMTNEEDED
                                            LCHG-CURPAY
           ELSE
              MOVE LN-REGPYAMT TO LCAP-AMTNEEDED
                                  LCHG-CURPAY.
           GO TO LCAP-B-J-DATE-CHK.

       LCAP-B-J-RETRY.
           IF (LCAP-TRAMT + LCAP-LPAPLC + LCAP-LPAPINT)
                                                > LCAP-AMTNEEDED
              PERFORM LCAP-UPD-LCPD
              GO TO LCAP-B-J-PMT.

      * TEST TO SEE IF THE SHORTAGE IS NOT GREATER
      * THAN THE DELINQUINCY FACTOR, SETUP IN STATE PARAMETERS.
           COMPUTE LCAP-WRK2 =
                   LCAP-AMTNEEDED - LCAP-TRAMT
                                  - LCAP-LCPAID
                                  - LCAP-LPAPLC
                                  - LCAP-LPAPINT.

           IF LCAP-WRK2 < LCAP-DELFAC
              PERFORM LCAP-UPD-LCPD.

       LCAP-B-J-EXIT.
           EXIT.

      *************************************************
      *   SP-LCFRML = D
      *
      *   NOTE: THIS IS THE SAME AS FORMULA "G",
      *         EXCEPT CONTRACTUAL USES MONIES TOWARDS
      *         PRINCIPAL AND LATE CHARGES.
      *************************************************
       LCAP-D SECTION.
           ADD LCAP-LPAPLC LCAP-LPAPINT LCAP-TOTPAYMNTD
                           GIVING LCAP-PAYMNTD.

       LCAP-D-BEGIN.

      * PAST MATURITY ?
           PERFORM LCAP-MDTE-CHK.
           IF ELAPSED-DAYS NOT > 0
              GO TO LCAP-D-EXIT.

      * ? ASSESSING LATE CHARGES (AL)
      * STOP ASSESSING WHEN LATE CHARGES ARE BROUGHT UP TODATE,
      * LCAP-LCPDTH-DATE >= PAYMENT DATE:
           IF LCAP-LPTRCD = "AL"
              MOVE LCAP-PAYDATE TO SYS-DATE
              PERFORM LCAP-LCPDTH-DATE-TEST
              IF ELAPSED-DAYS NOT > 0
                 GO TO LCAP-D-EXIT.

      * DOES PAYMENT AND LCPDTH INDICATE AN ASSESSMENT ?
           PERFORM LCAP-ASSESS-CHK.
           IF LCAS-ASSESS NOT = "Y"
              GO TO LCAP-D-NOASSESS.

      * TEST FOR SHORTAGE NOT GREATER
      * THAN THE DELINQUINCY FACTOR IN (SPR'S):
           IF LCAP-LCPDTH-DATE = PDTH-DATE-FULL
              IF LCAP-AMTNEEDED < LCAP-DELFAC
                 GO TO LCAP-D-ADVANCE.

      * LCHG-LATE INITIALIZED FOR SP-LCTYPE = "C" OR "J"
      * SINCE LATE CHARGE IS BASED ON % OF DELINQUENT AMOUNT.
           MOVE LCAP-AMTNEEDED TO LCHG-LATE.
           MOVE LCAP-LCPDTH-DATE TO LCHG-LCPDTH-DATE.
           PERFORM LCAP-CALC-TEST.

      * IF LCAP-TRAMT IS NOT ENOUGH TO COVER CHARGE, USE OWING:
           IF LCAP-TRAMT < LCHG-CHARGE
              COMPUTE LCAP-OWE = LCAP-OWE + LCHG-CHARGE
                                               - LCAP-TRAMT
              ADD LCAP-TRAMT TO LCAP-APP
                                LCAP-PAYMNTD
              MOVE 0 TO LCAP-TRAMT
           ELSE
              ADD LCHG-CHARGE TO LCAP-APP
                                 LCAP-PAYMNTD
              SUBTRACT LCHG-CHARGE FROM LCAP-TRAMT.

       LCAP-D-ADVANCE.
           PERFORM LCAP-UPD-LCPD.
           IF LN-PAY-SCHLD-FG = "Y"
              ADD 1 TO LCAP-PMT-NO
              MOVE SCHD-AMT(LCAP-PMT-NO) TO LCAP-AMTNEEDED
                                            LCHG-CURPAY
              COMPUTE LCAP-DELFAC ROUNDED =
                        SCHD-AMT(LCAP-PMT-NO) * (SP-LCDELFAC / 100)
           ELSE
              MOVE LN-REGPYAMT TO LCAP-AMTNEEDED
                                  LCHG-CURPAY.
           GO TO LCAP-D-BEGIN.

      * NO ASSESSMENT REQUIRED, ADVANCE LCAP-LCPDTH-DATE
      * TO WHERE PAYMENTS TODATE ARE PAID THRU
      * IF IT WILL ADVANCE LCPDTH ONLY, SINCE
      * ADVANCEMENT OF LCPDTH COULD HAVED OCCURED
      * VIA TIME.
       LCAP-D-NOASSESS.
           IF LCAP-LPTRCD NOT = "AL"
              ADD LCAP-TRAMT LCAP-PAYMNTD GIVING PDTH-PAYMNTD
                                                 LCAP-PAYMNTD
              PERFORM LCAP-PICK-PDTH-CALC
              MOVE NDTE-DATE TO SYS-DATE
              PERFORM LCAP-LCPDTH-DATE-TEST
              IF ELAPSED-DAYS > 0
                 MOVE PDTH-DATE-FULL TO LCAP-LCPDTH-DATE.

      * ? PAST MATURITY
      * IF SO FORCE LCAP-LCPDTH-DATE TO MATURITY:
           PERFORM LCAP-MDTE-CHK.
           IF ELAPSED-DAYS NOT > 0
              MOVE MDTE-DATE TO LCAP-LCPDTH-DATE.

       LCAP-D-EXIT.
           EXIT.

      ***********************************************
      *  SP-LCFRMLA = E AND X
      ***********************************************
       LCAP-E-X SECTION.

           ADD LCAP-LPAPINT LCAP-LPAPLC LCAP-TRAMT
                                    GIVING LCAP-TRAMT-PLUS-AP.
           MOVE 0 TO LCAP-APPLIED-TOWARD-SHORTAGE.

      * TEST FOR PRIOR PAYMENT SHORTAGES:
           IF LCAP-LCPDTH-DATE NOT = PDTH-DATE-FULL
              IF LCAP-TRAMT-PLUS-AP > LCAP-AMTNEEDED
                 GO TO LCAP-E-X-PMT
              ELSE
                 GO TO LCAP-E-X-EXIT.

       LCAP-E-X-DATE-CHK.

      * VERIFY DATES FOR ASSESSMENT
           PERFORM LCAP-MDTE-CHK.
           IF ELAPSED-DAYS NOT > 0
              GO TO LCAP-E-X-EXIT.

           PERFORM LCAP-ASSESS-CHK.
           IF LCAS-ASSESS NOT = "Y"
              IF LCAP-LPTRCD = "AL"
                 GO TO LCAP-E-X-EXIT
              ELSE
                 GO TO LCAP-E-X-RETRY.

      * LCHG-LATE INITIALIZED FOR SP-LCTYPE = "C" OR "J",
      * WHERE LATE CHG IS BASED ON % OF LATE PMT:
           MOVE LCAP-AMTNEEDED TO LCHG-LATE.
           MOVE LCAP-LCPDTH-DATE    TO LCHG-LCPDTH-DATE.
           PERFORM LCAP-CALC-TEST.

      * IF TRAMT INSUFFICIENT TO COVER CHARGE,
      * IF 'E' PARTIAL OWING IS FOUND
      * IF 'X' FULL LATE CHARGE IS PUT INTO OWING UNLESS
      *        ENOUGH MONIE WAS PAID TOWARDS THE PRIOR PAYMENT
      *        SHORTAGE TO COVER CHARGE
      
           IF LCAP-TRAMT < LCHG-CHARGE
              IF SP-LCFRMLA = "E"
                 SUBTRACT LCAP-TRAMT FROM LCHG-CHARGE GIVING LCAP-OWE
                 ADD LCAP-TRAMT TO LCAP-APP
              ELSE
                 IF LCHG-CHARGE <= LCAP-APPLIED-TOWARD-SHORTAGE
                    ADD  LCHG-CHARGE TO LCAP-APP
                 ELSE
                    MOVE LCHG-CHARGE TO LCAP-OWE
                 END-IF
              END-IF
              PERFORM LCAP-UPD-LCPD
              GO TO LCAP-E-X-EXIT.

      * OTHERWISE, TAKE LATE CHARGE AND (ATTEMPT) PMT RECEIPT:
           ADD LCHG-CHARGE TO LCAP-APP.
           SUBTRACT LCHG-CHARGE FROM LCAP-TRAMT
                                     LCAP-TRAMT-PLUS-AP
                                     LCAP-AMTNEEDED.
           PERFORM LCAP-UPD-LCPD.
           IF LCAP-TRAMT-PLUS-AP <= LCAP-AMTNEEDED
              GO TO LCAP-E-X-EXIT.

      * REDUCE TRANSACTION AMOUNT BY PAYMENT:
       LCAP-E-X-PMT.
           SUBTRACT LCAP-AMTNEEDED FROM LCAP-TRAMT-PLUS-AP.
           IF LCAP-AMTNEEDED <= LCAP-TRAMT
              SUBTRACT LCAP-AMTNEEDED FROM LCAP-TRAMT
              ADD LCAP-AMTNEEDED TO LCAP-APPLIED-TOWARD-SHORTAGE
              IF LN-PAY-SCHLD-FG = "Y"
                 ADD 1 TO LCAP-PMT-NO.

           IF LN-PAY-SCHLD-FG = "Y"
              MOVE SCHD-AMT(LCAP-PMT-NO) TO LCAP-AMTNEEDED
                                            LCHG-CURPAY
           ELSE
              MOVE LN-REGPYAMT TO LCAP-AMTNEEDED
                                  LCHG-CURPAY.
           GO TO LCAP-E-X-DATE-CHK.

       LCAP-E-X-RETRY.
           IF LCAP-TRAMT-PLUS-AP > LCAP-AMTNEEDED
              PERFORM LCAP-UPD-LCPD
              GO TO LCAP-E-X-PMT.

      * TEST TO SEE IF THE SHORTAGE IS NOT GREATER
      * THAN THE DELINQUINCY FACTOR, SETUP IN STATE PARAMETERS.
           COMPUTE LCAP-WRK2 =
                  LCAP-AMTNEEDED - LCAP-TRAMT-PLUS-AP.

           IF LCAP-WRK2 < LCAP-DELFAC
              PERFORM LCAP-UPD-LCPD.

       LCAP-E-X-EXIT.
           EXIT.

      ***************************************************************
      *   SP-LCFRML = F    LOUISIANA LATE CHARGE FORMULA
      *                    LIKE 'H' EXCEPT PRINCIPAL ONLY
      *
      *   NOTE:
      *        LCAP-PARTIALS (HOLDS LCAP-CURDUE AT EXIT)
      *        LCAP-LCPAID   (HOLDS LCAP-CURDATE [MMDD.YY] AT EXIT)
      ***************************************************************
       LCAP-F SECTION.
      * SET PMT-TO-DATE TEMP TO INCLUDE APPLIED TO INTEREST, CITIZENS FINANCE:
           ADD LCAP-TOTPAYMNTD LCAP-LPAPINT GIVING LCAP-PAYMNTD.

           MOVE LCAP-PARTIALS TO LCAP-LST-CURDUE.
      * REARRANGE LAST PAYDATE (MMDD.YY) INTO (MMDDYY):
           MULTIPLY LCAP-LCPAID BY 100 GIVING DATE-MMDDYY.
           PERFORM CONVERT-MMDDYY-TO-YYYYMMDD.
           MOVE DATE-YYYYMMDD TO LCAP-CURDATE
                                 LCAP-LST-CURDATE.

      * SEE IF CONTRACTUAL PAID THRU IS LESS THAN LCPDTH,
      * IF SO SET AMTNEEDED TO REGULAR PAYMENT:
           MOVE PDTH-DATE-FULL TO SYS-DATE.
           PERFORM LCAP-LCPDTH-DATE-TEST.
           IF ELAPSED-DAYS < 0
              IF LN-PAY-SCHLD-FG = "Y"
                 MOVE SCHD-AMT(LCAP-PMT-NO) TO LCAP-AMTNEEDED
              ELSE
                 MOVE LN-REGPYAMT TO LCAP-AMTNEEDED.

      * SETUP CURRENT PAYMENT AND LAST CURRENT PAYMENT,
      * SEE IF CURRENT PAYMENT IS ON TIME:
           PERFORM LCAP-F-CURRENT-PERIOD.

       LCAP-F-BEGIN.
      * TEST IF LATE CHARGE PAID THRU (LCAP-LCPDTH) IS
      * ON OR AFTER MATURITY, IF SO EXIT:
           PERFORM LCAP-MDTE-CHK.
           IF ELAPSED-DAYS NOT > 0
              GO TO LCAP-F-EXIT.

      * ASSESSING LATE CHARGES? (AL)
      * IF SO, STOP ASSESSING WHEN LATE CHARGES ARE BROUGHT UP TODATE,
      * WHEN LATE CHARGE PAID THRU (LCAP-LCPDTH) IS ON OR AFTER PAYDATE:
           IF LCAP-LPTRCD = "AL"
              MOVE LCAP-PAYDATE TO SYS-DATE
              PERFORM LCAP-LCPDTH-DATE-TEST
              IF ELAPSED-DAYS NOT > 0
                 GO TO LCAP-F-EXIT
              ELSE
                 MOVE 0 TO LCAP-CURDATE
                           LCAP-CURDUE.

      * DOES PAYMENT AND LCPDTH INDICATE AN ASSESSMENT ?
           PERFORM LCAP-ASSESS-CHK.
           IF LCAS-ASSESS NOT = "Y"
              GO TO LCAP-F-NOASSESS.

      * TEST FOR AMTNEEDED LESS THAN THE DELINQUINCY FACTOR
      * IN (SPR'S), IF SO ASSESS NO LATE CHARGE:
      * TEST FOR LAST CURRENT PAYMENT:
           IF LCAP-LST-CURDATE NOT = 0
              MOVE LCAP-LST-CURDATE TO SYS-DATE
              PERFORM LCAP-LCPDTH-DATE-TEST
              IF ELAPSED-DAYS = 0
                 MOVE LCAP-LST-CURDUE TO LCAP-AMTNEEDED
                 IF LCAP-LST-CURDUE < LCAP-DELFAC
                    GO TO LCAP-F-ADVANCE.

      * DETERMINE THE AMOUNT OF THE LATE CHARGE:
      * IF SP-LCTYPE = 'C', LCHG-LATE IS INITIALIZED,
      * SINCE LATE CHARGE IS BASED ON % OF AMOUNT DELINQUENT:
           MOVE LCAP-AMTNEEDED TO LCHG-LATE.
           MOVE LCAP-LCPDTH-DATE TO LCHG-LCPDTH-DATE.
           PERFORM LCAP-CALC-TEST.

      * IF LCAP-TRAMT IS NOT ENOUGH TO COVER CHARGE, USE OWING:
           IF LCAP-TRAMT < LCHG-CHARGE
              COMPUTE LCAP-OWE = LCAP-OWE +
                                  (LCHG-CHARGE - LCAP-TRAMT)
              ADD LCAP-TRAMT TO LCAP-APP
              MOVE 0 TO LCAP-TRAMT
           ELSE
              ADD LCHG-CHARGE TO LCAP-APP
              SUBTRACT LCHG-CHARGE FROM LCAP-TRAMT.

       LCAP-F-ADVANCE.
           PERFORM LCAP-UPD-LCPD.
           IF LN-PAY-SCHLD-FG = "Y"
              ADD 1 TO LCAP-PMT-NO
              MOVE SCHD-AMT(LCAP-PMT-NO) TO LCAP-AMTNEEDED
                                            LCHG-CURPAY
           ELSE
              MOVE LN-REGPYAMT TO LCAP-AMTNEEDED.
           GO TO LCAP-F-BEGIN.

      * NO ASSESSMENT IS REQUIRED; ADVANCE LCAP-LCPDTH-DATE
      * TO WHERE PAYMENTS TODATE ARE PAID THRU, ONLY
      * IF IT WILL ADVANCE LCPDTH, SINCE LCPDTH COULD
      * HAVE ADVANCED BEYOND PAID THRU VIA TIME.
       LCAP-F-NOASSESS.
           IF LCAP-LPTRCD NOT = "AL"
              ADD LCAP-TRAMT LCAP-PAYMNTD GIVING PDTH-PAYMNTD
                                                 LCAP-PAYMNTD
              PERFORM LCAP-PICK-PDTH-CALC
              MOVE NDTE-DATE TO SYS-DATE
              PERFORM LCAP-LCPDTH-DATE-TEST
              IF ELAPSED-DAYS > 0
                 MOVE PDTH-DATE-FULL TO LCAP-LCPDTH-DATE.

      * INSURE THAT LCPDTH ADVANCES TO CURRENT PAYMENT DATE
      * IF  CURRENT  PAYMENT IS NOT DELIQUENT:
           IF LCAP-CURDATE NOT = 0
              MOVE LCAP-CURDATE TO SYS-DATE
              PERFORM LCAP-LCPDTH-DATE-TEST
              IF ELAPSED-DAYS > 0
                 IF LCAP-CURDUE < LCAP-DELFAC
                    MOVE LCAP-CURDATE TO LCAP-LCPDTH-DATE.

      * ? PAST MATURITY,
      * IF SO FORCE LCAP-LCPDTH-DATE TO MATURITY:
           PERFORM LCAP-MDTE-CHK.
           IF ELAPSED-DAYS NOT > 0
              MOVE SYS-DATE TO LCAP-LCPDTH-DATE.

       LCAP-F-EXIT.
           MOVE LCAP-CURDUE TO LCAP-PARTIALS.
      * REARRANGE DATE TO (MMDD.YY):
           MOVE LCAP-CURDATE TO DATE-YYYYMMDD.
           PERFORM CONVERT-YYYYMMDD-TO-MMDDYY.
           MULTIPLY DATE-MMDDYY BY 0.01 GIVING LCAP-LCPAID.

      ***********************************************
      *    TEST CURRENT PAYMENT ON TIME
      *
      *    IF SO, ESTABLISH WHETHER ITS A NEW PERIOD
      *    OR LAST PAYMENT PERIOD AND SETUP AMOUNTS
      *    DELIQUENT FOR LAST OR BOTH PERIODS.
      ***********************************************
       LCAP-F-CURRENT-PERIOD SECTION.
           MOVE 0 TO LCAP-CURDUE.
           MOVE LCAP-PAYDATE TO LCAP-CURDATE.

      * IF TODAY IS ON OR AFTER PAYMENT DUE DATE OF THIS MONTH,
      *    SET UP THIS MONTHS DUE DATE TO SEE IF THE PAYMENT WAS ON TIME:
      * IF THE PAYMENT CAME IN BEFORE THIS MONTHS DUE DATE, SET UP
      *    LAST MONTHS DUE DATE TO SEE IF THE PAYMENT WAS ON TIME:
      *
           IF LCAP-CURDATE-DD NOT < LCAP-1STPYDATE-DD
              MOVE -1 TO NDTE-HOLD
           ELSE
              MOVE -2 TO NDTE-HOLD.

           MOVE LCAP-1STPYDATE-DD TO LCAP-CURDATE-DD.
           MOVE LCAP-CURDATE TO NDTE-DATE.
           PERFORM INCREMENT-MONTHS.
           MOVE NDTE-DATE TO LCAP-CURDATE.

      * TEST TO SEE IF THE PAYMENT WAS MADE ON TIME:
           MOVE LCAP-CURDATE TO LCAS-LCPDTH-DATE.
           MOVE LCAP-1STPYDATE TO LCAS-1STPYDATE.
           MOVE LCAP-PAYDATE TO LCAS-PAYDATE.
           PERFORM LATE-ASSESS-ROUTINE.
           IF LCAS-ASSESS = "Y"
              MOVE 0 TO LCAP-CURDATE
              GO TO LCAP-F-CURRENT-PERIOD-EXIT.

      * PAYMENT WAS ON TIME, SO SET IT UP AS CURRENT PAYMENT:
           MOVE LCAP-CURDATE TO NDTE-DATE.
           MOVE 1 TO NDTE-HOLD.
           PERFORM INCREMENT-MONTHS.
           MOVE NDTE-DATE TO LCAP-CURDATE.

      * IF CURRENT PAYDATE IS THE SAME AS LAST CURRENT PAYDATE,
      * SET CURRENT PAYMENT DATE TO LAST CURRENT PAYMENT DATE,
      * MAKE CURRENT DUE EQUAL TO REDUCED LAST CURRENT DUE:
              IF LCAP-CURDATE NOT = LCAP-LST-CURDATE
                 GO TO LCAP-F-CURRENT-PERIOD-NEWCUR.
              MOVE LCAP-LST-CURDATE TO LCAP-CURDATE.
              IF LCAP-TRAMT < LCAP-LST-CURDUE
                 SUBTRACT LCAP-TRAMT FROM LCAP-LST-CURDUE
                                           GIVING LCAP-CURDUE
              ELSE
                 MOVE 0 TO LCAP-CURDUE.
              MOVE 0 TO LCAP-LST-CURDATE
                        LCAP-LST-CURDUE.
              GO TO LCAP-F-CURRENT-PERIOD-EXIT.

       LCAP-F-CURRENT-PERIOD-NEWCUR.
      * SET UP AMOUNT LATE CHARGES CAN STILL BE CALCULATED ON:
           IF LN-PAY-SCHLD-FG = "Y"
              MOVE LCAP-1STPYDATE TO NUM-DATE
              MOVE LCAP-CURDATE TO SYS-DATE
              PERFORM TIM
              IF ELAPSED-MONTHS < 0
                 MOVE 0 TO ELAPSED-MONTHS
              END-IF
              ADD 1 ELAPSED-MONTHS GIVING LCAP
              MOVE SCHD-AMT(LCAP) TO LCAP-CURDUE
           ELSE
              MOVE LN-REGPYAMT TO LCAP-CURDUE
              IF LN-1STPYAMT NOT = 0
                 IF LCAP-CURDATE = LCAP-1STPYDATE
                    MOVE LN-1STPYAMT TO LCAP-CURDUE.

              IF LCAP-TRAMT < LCAP-CURDUE
                 SUBTRACT LCAP-TRAMT FROM LCAP-CURDUE
              ELSE
                 MOVE 0 TO LCAP-CURDUE.

       LCAP-F-CURRENT-PERIOD-EXIT.
      * SET LAST CURRENT DATE BACK ONE MONTH TO ALLOW
      * COMPARISON WITH LCPDTH:
           IF LCAP-LST-CURDATE NOT = 0
              MOVE LCAP-LST-CURDATE TO NDTE-DATE
              MOVE -1 TO NDTE-HOLD
              PERFORM INCREMENT-MONTHS
              MOVE NDTE-DATE TO LCAP-LST-CURDATE.

      ***************************************************************
      *   SP-LCFRML = H    LOUISIANA LATE CHARGE FORMULA
      *                    LIKE 'F' EXCEPT TOTAL MONIES
      *
      *   NOTE:
      *        LCAP-PARTIALS (HOLDS LCAP-CURDUE AT EXIT)
      *        LCAP-LCPAID   (HOLDS LCAP-CURDATE [MMDD.YY] AT EXIT)
      ***************************************************************
       LCAP-H SECTION.
      * SET PMT-TO-DATE TEMP TO INCLUDE APPLIED TO INTEREST, CITIZENS FINANCE:
           ADD LCAP-TOTPAYMNTD LCAP-LPAPINT GIVING LCAP-PAYMNTD.

           MOVE LCAP-PARTIALS TO LCAP-LST-CURDUE.
      * REARRANGE LAST PAYDATE (MMDD.YY) INTO (MMDDYY):
           MULTIPLY LCAP-LCPAID BY 100 GIVING DATE-MMDDYY.
           PERFORM CONVERT-MMDDYY-TO-YYYYMMDD.
           MOVE DATE-YYYYMMDD TO LCAP-CURDATE
                                 LCAP-LST-CURDATE.

      * SEE IF CONTRACTUAL PAID THRU IS LESS THAN LCPDTH,
      * IF SO SET AMTNEEDED TO REGULAR PAYMENT:
           MOVE PDTH-DATE-FULL TO SYS-DATE.
           PERFORM LCAP-LCPDTH-DATE-TEST.
           IF ELAPSED-DAYS < 0
              IF LN-PAY-SCHLD-FG = "Y"
                 MOVE SCHD-AMT(LCAP-PMT-NO) TO LCAP-AMTNEEDED
              ELSE
                 MOVE LN-REGPYAMT TO LCAP-AMTNEEDED.

      * SETUP CURRENT PAYMENT AND LAST CURRENT PAYMENT,
      * SEE IF CURRENT PAYMENT IS ON TIME:
           PERFORM LCAP-H-CURRENT-PERIOD.

       LCAP-H-BEGIN.
      * TEST IF LATE CHARGE PAID THRU (LCAP-LCPDTH) IS
      * ON OR AFTER MATURITY, IF SO EXIT:
           PERFORM LCAP-MDTE-CHK.
           IF ELAPSED-DAYS NOT > 0
              GO TO LCAP-H-EXIT.

      * ASSESSING LATE CHARGES? (AL)
      * IF SO, STOP ASSESSING WHEN LATE CHARGES ARE BROUGHT
      * UP TODATE, WHEN LATE CHARGE PAID THRU (LCAP-LCPDTH)
      * IS ON OR AFTER PAYDATE:
           IF LCAP-LPTRCD = "AL"
              MOVE LCAP-PAYDATE TO SYS-DATE
              PERFORM LCAP-LCPDTH-DATE-TEST
              IF ELAPSED-DAYS NOT > 0
                 GO TO LCAP-H-EXIT
              ELSE
                 MOVE 0 TO LCAP-CURDATE
                           LCAP-CURDUE.

      * DOES PAYMENT AND LCPDTH INDICATE AN ASSESSMENT ?
           PERFORM LCAP-ASSESS-CHK.
           IF LCAS-ASSESS NOT = "Y"
              GO TO LCAP-H-NOASSESS.

      * TEST FOR AMTNEEDED LESS THAN THE DELINQUINCY FACTOR
      * IN (SPR'S), IF SO ASSESS NO LATE CHARGE:
      * TEST FOR LAST CURRENT PAYMENT:
           IF LCAP-LST-CURDATE NOT = 0
              MOVE LCAP-LST-CURDATE TO SYS-DATE
              PERFORM LCAP-LCPDTH-DATE-TEST
              IF ELAPSED-DAYS = 0
                 MOVE LCAP-LST-CURDUE TO LCAP-AMTNEEDED
                 IF LCAP-LST-CURDUE < LCAP-DELFAC
                    GO TO LCAP-H-ADVANCE.

      * DETERMINE THE AMOUNT OF THE LATE CHARGE:
      * IF SP-LCTYPE = 'C', LCHG-LATE IS INITIALIZED,
      * SINCE LATE CHARGE IS BASED ON % OF AMOUNT DELINQUENT:
           MOVE LCAP-AMTNEEDED TO LCHG-LATE.
           MOVE LCAP-LCPDTH-DATE TO LCHG-LCPDTH-DATE.
           PERFORM LCAP-CALC-TEST.

      * IF LCAP-TRAMT IS NOT ENOUGH TO COVER CHARGE, USE OWING:
           IF LCAP-TRAMT < LCHG-CHARGE
              COMPUTE LCAP-OWE = LCAP-OWE +
                                  (LCHG-CHARGE - LCAP-TRAMT)
              ADD LCAP-TRAMT TO LCAP-APP
              MOVE 0 TO LCAP-TRAMT
           ELSE
              ADD LCHG-CHARGE TO LCAP-APP
              SUBTRACT LCHG-CHARGE FROM LCAP-TRAMT.

       LCAP-H-ADVANCE.
           PERFORM LCAP-UPD-LCPD.
           IF LN-PAY-SCHLD-FG = "Y"
              ADD 1 TO LCAP-PMT-NO
              MOVE SCHD-AMT(LCAP-PMT-NO) TO LCAP-AMTNEEDED
                                            LCHG-CURPAY
           ELSE
              MOVE LN-REGPYAMT TO LCAP-AMTNEEDED.
           GO TO LCAP-H-BEGIN.

      * NO ASSESSMENT IS REQUIRED; ADVANCE LCAP-LCPDTH-DATE
      * TO WHERE PAYMENTS TODATE ARE PAID THRU, ONLY
      * IF IT WILL ADVANCE LCPDTH, SINCE LCPDTH COULD
      * HAVE ADVANCED BEYOND PAID THRU VIA TIME.
       LCAP-H-NOASSESS.
           IF LCAP-LPTRCD NOT = "AL"
              ADD LCAP-TRAMT LCAP-PAYMNTD GIVING PDTH-PAYMNTD
                                                 LCAP-PAYMNTD
      *1262
              ADD LCAP-APP TO PDTH-PAYMNTD LCAP-PAYMNTD
              PERFORM LCAP-PICK-PDTH-CALC
              MOVE NDTE-DATE TO SYS-DATE
              PERFORM LCAP-LCPDTH-DATE-TEST
              IF ELAPSED-DAYS NOT < 0
                 MOVE PDTH-DATE-FULL TO LCAP-LCPDTH-DATE
                 IF LCAP-UNPAID-PORTION NOT = 0
                     AND LCAP-UNPAID-PORTION < LCAP-DELFAC
                     PERFORM LCAP-UPD-LCPD.

      * INSURE THAT LCPDTH ADVANCES TO CURRENT PAYMENT DATE
      * IF  CURRENT  PAYMENT IS NOT DELIQUENT:
           IF LCAP-CURDATE NOT = 0
              MOVE LCAP-CURDATE TO SYS-DATE
              PERFORM LCAP-LCPDTH-DATE-TEST
              IF ELAPSED-DAYS > 0
                 IF LCAP-CURDUE < LCAP-DELFAC
                    MOVE LCAP-CURDATE TO LCAP-LCPDTH-DATE.

      * ? PAST MATURITY,
      * IF SO FORCE LCAP-LCPDTH-DATE TO MATURITY:
           PERFORM LCAP-MDTE-CHK.
           IF ELAPSED-DAYS NOT > 0
              MOVE SYS-DATE TO LCAP-LCPDTH-DATE.

       LCAP-H-EXIT.
           MOVE LCAP-CURDUE TO LCAP-PARTIALS.
      * REARRANGE DATE TO (MMDD.YY):
           MOVE LCAP-CURDATE TO DATE-YYYYMMDD.
           PERFORM CONVERT-YYYYMMDD-TO-MMDDYY.
           MULTIPLY DATE-MMDDYY BY 0.01 GIVING LCAP-LCPAID.

      ***********************************************
      *    TEST CURRENT PAYMENT ON TIME
      *
      *    IF SO, ESTABLISH WHETHER ITS A NEW PERIOD
      *    OR LAST PAYMENT PERIOD AND SETUP AMOUNTS
      *    DELIQUENT FOR LAST OR BOTH PERIODS.
      ***********************************************
       LCAP-H-CURRENT-PERIOD SECTION.
           MOVE 0 TO LCAP-CURDUE.
           MOVE LCAP-PAYDATE TO LCAP-CURDATE.
      * IF INTEREST COLLECT SHOULD COUNT TOWARDS LCAP-PARTIALS
           ADD LCAP-LPAPLC LCAP-LPAPINT TO LCAP-TRAMT.

      * IF TODAY IS ON OR AFTER PAYMENT DUE DATE OF THIS MONTH,
      *    SET UP THIS MONTHS DUE DATE TO SEE IF THE PAYMENT WAS ON TIME:
      * IF THE PAYMENT CAME IN BEFORE THIS MONTHS DUE DATE, SET UP
      *    LAST MONTHS DUE DATE TO SEE IF THE PAYMENT WAS ON TIME:
      *
           IF LCAP-CURDATE-DD NOT < LCAP-1STPYDATE-DD
              MOVE -1 TO NDTE-HOLD
           ELSE
              MOVE -2 TO NDTE-HOLD.

           MOVE LCAP-1STPYDATE-DD TO LCAP-CURDATE-DD.
           MOVE LCAP-CURDATE TO NDTE-DATE.
           PERFORM INCREMENT-MONTHS.
           MOVE NDTE-DATE TO LCAP-CURDATE.

      * TEST TO SEE IF THE PAYMENT WAS MADE ON TIME:
           MOVE LCAP-CURDATE TO LCAS-LCPDTH-DATE.
           MOVE LCAP-1STPYDATE TO LCAS-1STPYDATE.
           MOVE LCAP-PAYDATE TO LCAS-PAYDATE.
           PERFORM LATE-ASSESS-ROUTINE.
           IF LCAS-ASSESS = "Y"
              MOVE 0 TO LCAP-CURDATE
              GO TO LCAP-H-CURRENT-PERIOD-EXIT.

      * PAYMENT WAS ON TIME, SO SET IT UP AS CURRENT PAYMENT:
           MOVE LCAP-CURDATE TO NDTE-DATE.
           MOVE 1 TO NDTE-HOLD.
           PERFORM INCREMENT-MONTHS.
           MOVE NDTE-DATE TO LCAP-CURDATE.

      * IF CURRENT PAYDATE IS THE SAME AS LAST CURRENT PAYDATE,
      * SET CURRENT PAYMENT DATE TO LAST CURRENT PAYMENT DATE,
      * MAKE CURRENT DUE EQUAL TO REDUCED LAST CURRENT DUE:

              IF LCAP-CURDATE NOT = LCAP-LST-CURDATE
                 GO TO LCAP-H-CURRENT-PERIOD-NEWCUR.
              MOVE LCAP-LST-CURDATE TO LCAP-CURDATE.
              IF LCAP-TRAMT < LCAP-LST-CURDUE
                 SUBTRACT LCAP-TRAMT FROM LCAP-LST-CURDUE
                                           GIVING LCAP-CURDUE
              ELSE
                 MOVE 0 TO LCAP-CURDUE.
              MOVE 0 TO LCAP-LST-CURDATE
                        LCAP-LST-CURDUE.
              GO TO LCAP-H-CURRENT-PERIOD-EXIT.

       LCAP-H-CURRENT-PERIOD-NEWCUR.
      * SET UP AMOUNT LATE CHARGES CAN STILL BE CALCULATED ON:
           IF LN-PAY-SCHLD-FG = "Y"
              MOVE LCAP-1STPYDATE TO NUM-DATE
              MOVE LCAP-CURDATE TO SYS-DATE
              PERFORM TIM
              IF ELAPSED-MONTHS < 0
                 MOVE 0 TO ELAPSED-MONTHS
              END-IF
              ADD 1 ELAPSED-MONTHS GIVING LCAP
              MOVE SCHD-AMT(LCAP) TO LCAP-CURDUE
           ELSE
              MOVE LN-REGPYAMT TO LCAP-CURDUE
              IF LN-1STPYAMT NOT = 0
                 IF LCAP-CURDATE = LCAP-1STPYDATE
                    MOVE LN-1STPYAMT TO LCAP-CURDUE.

              IF LCAP-TRAMT < LCAP-CURDUE
                 SUBTRACT LCAP-TRAMT FROM LCAP-CURDUE
              ELSE
                 MOVE 0 TO LCAP-CURDUE.

       LCAP-H-CURRENT-PERIOD-EXIT.
      * SET LAST CURRENT DATE BACK ONE MONTH TO ALLOW
      * COMPARISON WITH LCPDTH:
           IF LCAP-LST-CURDATE NOT = 0
              MOVE LCAP-LST-CURDATE TO NDTE-DATE
              MOVE -1 TO NDTE-HOLD
              PERFORM INCREMENT-MONTHS
              MOVE NDTE-DATE TO LCAP-LST-CURDATE.
      * RESET LCAP-TRAMT ADDED AT TOP OF LCAP-H-CURRENT
           SUBTRACT LCAP-LPAPLC LCAP-LPAPINT FROM LCAP-TRAMT.


      **************************************************************
      *   SP-LCFRML = I & L   INDIANA   LATE CHARGE FORMULA (PRIN ONLY)
      *
      *   NOTE:
      *        LCAP-PARTIALS (HOLDS LCAP-CURDUE AT EXIT)
      *        LCAP-LCPAID   (HOLDS LCAP-CURDATE [MMDD.YY] AT EXIT)
      **************************************************************
       LCAP-I-L SECTION.
           MOVE LCAP-TOTPAYMNTD TO LCAP-PAYMNTD.
           MOVE LCAP-PARTIALS TO LCAP-LST-CURDUE.
      * REARRANGE LAST PAYDATE (MMDD.YY) INTO (MMDDYY):
           MULTIPLY LCAP-LCPAID BY 100 GIVING DATE-MMDDYY.
           PERFORM CONVERT-MMDDYY-TO-YYYYMMDD.
           MOVE DATE-YYYYMMDD TO LCAP-CURDATE
                                 LCAP-LST-CURDATE.

           ADD LN-TOTLCHG LCAP-LPAPLC LCAP-LPAPINT GIVING LCAP-WRK.
           IF SP-LCFRMLA = "L"
              IF LCAP-WRK < LCAP-AMTNEEDED
                 SUBTRACT LCAP-WRK FROM LCAP-AMTNEEDED
              ELSE
                 NEXT SENTENCE
           ELSE
              IF LCAP-AMTNEEDED NOT = LCAP-UNPAID-PORTION
                 SUBTRACT LCAP-WRK FROM LCAP-AMTNEEDED.

      * SEE IF CONTRACTUAL PAID THRU IS LESS THAN LCPDTH,
      * IF SO SET AMTNEEDED TO REGULAR PAYMENT:

           MOVE LCAP-TOTPAYMNTD TO LCAP-WRK.
      
           COMPUTE PDTH-PAYMNTD
                   LCAP-TOTPAYMNTD = LCAP-TOTPAYMNTD + LN-TOTLCHG.

           IF LN-PAY-SCHLD-FG = "Y"
              MOVE LCAP-PAYMNTD TO LCAP-HOLDER
              MOVE PDTH-PAYMNTD TO LCAP-PAYMNTD
              PERFORM LCAP-PICK-PDTH-CALC
              MOVE LCAP-HOLDER TO LCAP-PAYMNTD
           ELSE
              PERFORM LCAP-PICK-PDTH-CALC.

           IF LN-PAY-SCHLD-FG = "Y"
              MOVE SCHD-AMT(LCAP-PMT-NO) TO LCAP-WRK2
              COMPUTE LCAP-WRK2 ROUNDED = LCAP-REMAIN * LCAP-WRK2
           ELSE
              MOVE LN-REGPYAMT TO LCAP-WRK2
              IF LCAP-TOTPAYMNTD < LN-1STPYAMT
                 MOVE LN-1STPYAMT TO LCAP-WRK2
              END-IF
              COMPUTE LCAP-WRK2 ROUNDED =
                      PDTH-REMAIN * LCAP-WRK2.

           IF LCAP-WRK2 < LN-TOTLCHG
              IF LN-PAY-SCHLD-FG = "Y"
                 MOVE SCHD-AMT(LCAP-PMT-NO) TO LCAP-AMTNEEDED
              ELSE
                 MOVE LN-REGPYAMT TO LCAP-AMTNEEDED
                 IF LCAP-TOTPAYMNTD < LN-1STPYAMT
                    MOVE LN-1STPYAMT TO LCAP-AMTNEEDED
                    IF SP-LCFRMLA = "L"
                       SUBTRACT LCAP-WRK2 FROM LCAP-AMTNEEDED
                    ELSE
                       NEXT SENTENCE
                 ELSE
                    IF SP-LCFRMLA = "L"
                       SUBTRACT LCAP-WRK2 FROM LCAP-AMTNEEDED.
           MOVE LCAP-WRK TO LCAP-TOTPAYMNTD.

           IF LCAP-LST-CURDATE = 0
                    OR (LCAP-LST-CURDATE NOT = 0
                               AND LCAP-LST-CURDUE = 0)
              MOVE PDTH-DATE-FULL TO SYS-DATE
              PERFORM LCAP-LCPDTH-DATE-TEST
              IF ELAPSED-DAYS < 0
                 IF LN-PAY-SCHLD-FG = "Y"
                    MOVE SCHD-AMT(LCAP-PMT-NO) TO LCAP-AMTNEEDED
                 ELSE
                    MOVE LN-REGPYAMT TO LCAP-AMTNEEDED.

      * SETUP CURRENT PAYMENT AND LAST CURRENT PAYMENT,
      * SEE IF CURRENT PAYMENT IS ON TIME:
           PERFORM LCAP-I-L-CURRENT-PERIOD.

       LCAP-I-L-BEGIN.
      * TEST IF LATE CHARGE PAID THRU (LCAP-LCPDTH) IS
      * ON OR AFTER MATURITY, IF SO EXIT:
           PERFORM LCAP-MDTE-CHK.
           IF ELAPSED-DAYS NOT > 0
              GO TO LCAP-I-L-EXIT.

      * ASSESSING LATE CHARGES? (AL)
      * IF SO, STOP ASSESSING WHEN LATE CHARGES ARE BROUGHT UP TODATE,
      * WHEN LATE CHARGE PAID THRU (LCAP-LCPDTH) IS ON OR AFTER PAYDATE:
           IF LCAP-LPTRCD = "AL"
              MOVE LCAP-PAYDATE TO SYS-DATE
              PERFORM LCAP-LCPDTH-DATE-TEST
              IF ELAPSED-DAYS NOT > 0
                 GO TO LCAP-I-L-EXIT
              ELSE
                 MOVE 0 TO LCAP-CURDATE
                           LCAP-CURDUE.

      * DOES PAYMENT AND LCPDTH INDICATE AN ASSESSMENT ?
           PERFORM LCAP-ASSESS-CHK.
           IF LCAS-ASSESS NOT = "Y"
              GO TO LCAP-I-L-NOASSESS.

      * TEST FOR LAST CURRENT PAYMENT:
           IF LCAP-LST-CURDATE NOT = 0
              MOVE LCAP-LST-CURDATE TO SYS-DATE
              PERFORM LCAP-LCPDTH-DATE-TEST
              IF ELAPSED-DAYS = 0
                 MOVE LCAP-LST-CURDATE TO NUM-DATE
                 MOVE PDTH-DATE-FULL   TO SYS-DATE
                 PERFORM TIM
                 IF ELAPSED-DAYS < 0
                    MOVE LCAP-LST-CURDUE TO LCAP-AMTNEEDED
                 ELSE
                    IF LCAP-AMTNEEDED > LCAP-LST-CURDUE
                       MOVE LCAP-LST-CURDUE TO LCAP-AMTNEEDED.

      * TEST FOR AMTNEEDED LESS THAN THE DELINQUINCY FACTOR
      * IN (SPR'S), IF SO ASSESS NO LATE CHARGE:
           IF LCAP-AMTNEEDED < LCAP-DELFAC
              GO TO LCAP-I-L-ADVANCE.

      * DETERMINE THE AMOUNT OF THE LATE CHARGE:
      * IF SP-LCTYPE = "C" OR "J", LCHG-LATE IS INITIALIZED,
      * SINCE LATE CHARGE IS BASED ON % OF AMOUNT DELINQUENT:
           MOVE LCAP-AMTNEEDED TO LCHG-LATE.
           MOVE LCAP-LCPDTH-DATE TO LCHG-LCPDTH-DATE.
           PERFORM LCAP-CALC-TEST.

      * IF LCAP-TRAMT IS NOT ENOUGH TO COVER CHARGE, USE OWING:
           IF LCAP-TRAMT < LCHG-CHARGE
              COMPUTE LCAP-OWE = LCAP-OWE +
                                  (LCHG-CHARGE - LCAP-TRAMT)
              ADD LCAP-TRAMT TO LCAP-APP
              MOVE 0 TO LCAP-TRAMT
           ELSE
              ADD LCHG-CHARGE TO LCAP-APP
              SUBTRACT LCHG-CHARGE FROM LCAP-TRAMT.

       LCAP-I-L-ADVANCE.
           PERFORM LCAP-UPD-LCPD.
           IF LN-PAY-SCHLD-FG = "Y"
              ADD 1 TO LCAP-PMT-NO
              MOVE SCHD-AMT(LCAP-PMT-NO) TO LCAP-AMTNEEDED
                                            LCHG-CURPAY
           ELSE
              MOVE LN-REGPYAMT TO LCAP-AMTNEEDED.

           GO TO LCAP-I-L-BEGIN.

      *******************************************************
      * NO ASSESSMENT IS REQUIRED; ADVANCE LCAP-LCPDTH-DATE
      * TO WHERE PAYMENTS TODATE ARE PAID THRU, ONLY
      * IF IT WILL ADVANCE LCPDTH, SINCE LCPDTH COULD
      * HAVE ADVANCED BEYOND PAID THRU VIA TIME.
      *******************************************************
       LCAP-I-L-NOASSESS.

           IF LCAP-LPTRCD NOT = "AL"
              ADD LCAP-TRAMT LCAP-PAYMNTD GIVING PDTH-PAYMNTD.

      *08/13/15 PR#694
      *MULLEN REPORTED ACCOUNT THAT WAS PAID AHEAD DID NOT ADVANCE
      *LATE CHARGE PAID THRU, CAUSING THE NEXT ON TIME PAYMENT TO
      *ASSESS A LATE CHARGE. THE ACCOUNT WAS PAID AHEAD BUT THE PAYMENT WENT
      *TO INTEREST-OWING. WHEN I WAIVED THE INTEREST-DUE, THE ACCOUNT ADVANCED
      *LCPDTH SO I ADDED SUBTRACT OF LCAP-LPAPINT(INT-OWE)FROM LCAP-WRK. I
      *AM NOT QUITE SURE WHY WE ADD LN-TOTLCHG,LCAP-LPAPLC,LCAP-APP
      *LCAP-LPAPINT TO THIS WORKER. I DO REMEMBER PACESETTER DID NOT WANT
      *AN ACCT TO BE PAST DUE BECAUSE OF PAID LATE CHGS BUT ALSO DID NOT
      *WANT HIM TO BE PAID AHEAD IF HE DID PAY THEM BUT I SEE NO REASON THAT
      *COLLECTED INTEREST OWING SHOULD BE INCLUDED IN THIS WORKER.
      *
      *IN MULLEN'S EXAMPLE LCAP-WRK WAS 30.00(LN-TOTLCHG) + 408.92(INT
      *OWE, WHEN IT COMPUTED PDTH-PAYMENTS IT WAS > 1, SO IT SUBTRACTED
      *A REGPYAMT FROM PAYMENTS TO DATE.
      *
      *HERE IS THE ACCOUNT
      *          TRCD   AMT   INT       PDTH  LCPD       INT/LC OWE
      * 03/28/10  D2                    05/10  05/10        789.73
      * 05/28/10  PY   408.92 408.92    06/10  05/10        868.96
      *WHEN I WAIVED THE INTEREST OWING
      *          TRCD   AMT   INT       PDTH  LCPD       INT/LC OWE
      * 03/28/10  D2                    05/10  05/10        789.73
      * 05/28/10  PY   408.92           06/10  06/10        868.96
      *
      *PAYMENTS TO DATE WAS THE SAME AMOUNT IN BOTH EXAMPLES, BUT DID NOT
      *ADVANCE LCPDTH WITH INTEREST OWING COLLECTED.

           IF LCAP-LPTRCD NOT = "AL"
              IF SP-LCFRMLA = "I" OR "L"
                 ADD LN-TOTLCHG LCAP-LPAPLC LCAP-APP LCAP-LPAPINT
                                                    GIVING LCAP-WRK
                 ADD LCAP-WRK TO PDTH-PAYMNTD

      *PL#694 SEE ABOVE
                 SUBTRACT LCAP-LPAPINT FROM LCAP-WRK

                 COMPUTE PDTH-PAYMENTS = LCAP-WRK / LN-REGPYAMT
                 IF PDTH-PAYMENTS > 1
                    COMPUTE PDTH-PAYMNTD =
                       PDTH-PAYMNTD - ( PDTH-PAYS * LN-REGPYAMT).

           IF LCAP-LPTRCD NOT = "AL"
              IF LN-PAY-SCHLD-FG = "Y"
                 MOVE LCAP-PAYMNTD TO LCAP-HOLDER
                 MOVE PDTH-PAYMNTD TO LCAP-PAYMNTD
                 PERFORM LCAP-PICK-PDTH-CALC
                 MOVE LCAP-HOLDER TO LCAP-PAYMNTD
              ELSE
                 PERFORM LCAP-PICK-PDTH-CALC
              END-IF
              MOVE NDTE-DATE TO SYS-DATE
              PERFORM LCAP-LCPDTH-DATE-TEST
              IF ELAPSED-DAYS NOT < 0
                 MOVE PDTH-DATE-FULL TO LCAP-LCPDTH-DATE
                 IF LCAP-UNPAID-PORTION NOT = 0
                          AND LCAP-UNPAID-PORTION < LCAP-DELFAC
                    PERFORM LCAP-UPD-LCPD.

      * INSURE THAT LCPDTH ADVANCES TO CURRENT PAYMENT DATE
      * IF  CURRENT  PAYMENT IS NOT DELIQUENT:
           IF LCAP-CURDATE NOT = 0
              MOVE LCAP-CURDATE TO SYS-DATE
              PERFORM LCAP-LCPDTH-DATE-TEST
              IF ELAPSED-DAYS > 0
                 IF LCAP-CURDUE < LCAP-DELFAC
                    IF ELAPSED-DAYS > 0
                       MOVE LCAP-CURDATE TO LCAP-LCPDTH-DATE
                    ELSE
                       PERFORM LCAP-UPD-LCPD.

      * ? PAST MATURITY,
      * IF SO FORCE LCAP-LCPDTH-DATE TO MATURITY:
       LCAP-I-L-MATURITY-TEST.
           PERFORM LCAP-MDTE-CHK.
           IF ELAPSED-DAYS NOT > 0
              MOVE SYS-DATE TO LCAP-LCPDTH-DATE.

       LCAP-I-L-EXIT.
           MOVE LCAP-CURDUE TO LCAP-PARTIALS.
      * REARRANGE DATE (MMDDYY) TO (MMDD.YY):
           MOVE LCAP-CURDATE TO DATE-YYYYMMDD.
           PERFORM CONVERT-YYYYMMDD-TO-MMDDYY.
           MULTIPLY DATE-MMDDYY BY 0.01 GIVING LCAP-LCPAID.

      ***********************************************
      *    TEST CURRENT PAYMENT ON TIME
      *
      *    IF SO, ESTABLISH WHETHER ITS A NEW PERIOD
      *    OR LAST PAYMENT PERIOD AND SETUP AMOUNTS
      *    DELIQUENT FOR LAST OR BOTH PERIODS.
      ***********************************************
       LCAP-I-L-CURRENT-PERIOD SECTION.
           MOVE " " TO LCAP-PRIOR-1STPY.
           MOVE 0 TO LCAP-CURDUE.
           ADD LCAP-LPAPLC LCAP-LPAPINT TO LCAP-TRAMT.

      * IF PAYMENT IS BEFORE 1ST PAYMENT IS DUE,
      * FORCE CURRENT DUE DATE TO 1 MONTH PRIOR TO
      * FIRST PAYMENT DUE DATE:
           MOVE LCAP-PAYDATE TO LCAP-CURDATE
                                NUM-DATE.
           MOVE LCAP-1STPYDATE TO SYS-DATE.
           PERFORM TIM.
           IF ELAPSED-DAYS > 0
              MOVE "Y" TO LCAP-PRIOR-1STPY
              MOVE LCAP-1STPYDATE TO NDTE-DATE
              MOVE -1 TO NDTE-HOLD
              PERFORM INCREMENT-MONTHS
              MOVE NDTE-DATE TO LCAP-CURDATE
              GO TO LCAP-I-L-CURRENT-PERIOD-SETDUE.

      * IF TODAY IS ON OR AFTER PAYMENT DUE DATE OF THIS MONTH,
      *  SET UP THIS MONTHS DUE DATE TO SEE IF THE PAYMENT WAS ON TIME:
      * IF THE PAYMENT CAME IN BEFORE THIS MONTHS DUE DATE, SET UP
      *  LAST MONTHS DUE DATE TO SEE IF THE PAYMENT WAS ON TIME:
           IF LCAP-CURDATE-DD NOT < LCAP-1STPYDATE-DD
              MOVE -1 TO NDTE-HOLD
           ELSE
              MOVE -2 TO NDTE-HOLD.

           MOVE LCAP-1STPYDATE-DD TO LCAP-CURDATE-DD.
           MOVE LCAP-CURDATE TO NDTE-DATE.
           PERFORM INCREMENT-MONTHS.
           MOVE NDTE-DATE TO LCAP-CURDATE.

      * TEST TO SEE IF THE PAYMENT WAS MADE ON TIME:
           MOVE LCAP-CURDATE TO LCAS-LCPDTH-DATE.
           MOVE LCAP-1STPYDATE TO LCAS-1STPYDATE.
           MOVE LCAP-PAYDATE TO LCAS-PAYDATE.
           PERFORM LATE-ASSESS-ROUTINE.
           IF LCAS-ASSESS = "Y"
              MOVE 0 TO LCAP-CURDATE
              GO TO LCAP-I-L-CURRENT-PERIOD-EXIT.

      * PAYMENT WAS ON TIME, SO SET IT UP AS CURRENT PAYMENT:
           MOVE LCAP-CURDATE TO NDTE-DATE.
           MOVE 1 TO NDTE-HOLD.
           PERFORM INCREMENT-MONTHS.
           MOVE NDTE-DATE TO LCAP-CURDATE.

      * IF CURRENT PAYDATE IS THE SAME AS LAST CURRENT PAYDATE,
      * SET CURRENT PAYMENT DATE TO LAST CURRENT PAYMENT DATE,
      * MAKE CURRENT DUE EQUAL TO REDUCED LAST CURRENT DUE:
       LCAP-I-L-CURRENT-PERIOD-SETDUE.
           IF LCAP-CURDATE NOT = LCAP-LST-CURDATE
              GO TO LCAP-I-L-CURRENT-PERIOD-NEWCUR.
           MOVE LCAP-LST-CURDATE TO LCAP-CURDATE.
           IF LCAP-TRAMT < LCAP-LST-CURDUE
              SUBTRACT LCAP-TRAMT FROM LCAP-LST-CURDUE
                                        GIVING LCAP-CURDUE
           ELSE
              MOVE 0 TO LCAP-CURDUE.
           MOVE 0 TO LCAP-LST-CURDATE
                     LCAP-LST-CURDUE.
           GO TO LCAP-I-L-CURRENT-PERIOD-EXIT.

       LCAP-I-L-CURRENT-PERIOD-NEWCUR.
      * SET UP AMOUNT LATE CHARGES CAN STILL BE CALCULATED ON:
           IF LN-PAY-SCHLD-FG = "Y"
              MOVE LCAP-1STPYDATE TO NUM-DATE
              MOVE LCAP-CURDATE TO SYS-DATE
              PERFORM TIM
              IF ELAPSED-MONTHS < 0
                 MOVE 0 TO ELAPSED-MONTHS
              END-IF
              ADD 1 ELAPSED-MONTHS GIVING LCAP
              MOVE SCHD-AMT(LCAP) TO LCAP-CURDUE
           ELSE
              MOVE LN-REGPYAMT TO LCAP-CURDUE
              IF LN-1STPYAMT NOT = 0
                 IF LCAP-CURDATE = LCAP-1STPYDATE
                    MOVE LN-1STPYAMT TO LCAP-CURDUE.

           IF LCAP-TRAMT < LCAP-CURDUE
              SUBTRACT LCAP-TRAMT FROM LCAP-CURDUE
              IF LCAP-CURDUE < LCAP-DELFAC
                 MOVE 0 TO LCAP-CURDUE
              END-IF
           ELSE
              MOVE 0 TO LCAP-CURDUE.

       LCAP-I-L-CURRENT-PERIOD-EXIT.
      * SET LAST CURRENT DATE BACK ONE MONTH TO ALLOW
      * COMPARISON WITH LCPDTH:
           IF LCAP-LST-CURDATE NOT = 0
              MOVE LCAP-LST-CURDATE TO NDTE-DATE
              MOVE -1 TO NDTE-HOLD
              PERFORM INCREMENT-MONTHS
              MOVE NDTE-DATE TO LCAP-LST-CURDATE.

           SUBTRACT LCAP-LPAPLC LCAP-LPAPINT FROM LCAP-TRAMT.

      ***********************************************
      *   SP-LCFRML = G AND K
      *
      *   NOTE: THIS IS THE SAME AS FORMULA "D",
      *         EXCEPT USING PRINCIPLE ONLY AS
      *         CONTRACTUAL MONIES.
      ***********************************************
       LCAP-G-K SECTION.

           ADD LCAP-TOTPAYMNTD LCAP-LPAPINT GIVING LCAP-PAYMNTD.

       LCAP-G-K-BEGIN.

      * PAST MATURITY ?
           PERFORM LCAP-MDTE-CHK.
           IF ELAPSED-DAYS NOT > 0
              GO TO LCAP-G-K-EXIT.

      * ? ASSESSING LATE CHARGES (AL)
      * STOP ASSESSING WHEN LATE CHARGES ARE BROUGHT
      * UP TODATE, LCAP-LCPDTH-DATE >= PAYMENT DATE:
           IF LCAP-LPTRCD = "AL"
              MOVE LCAP-PAYDATE TO SYS-DATE
              PERFORM LCAP-LCPDTH-DATE-TEST
              IF ELAPSED-DAYS NOT > 0
                 GO TO LCAP-G-K-EXIT.

      * DOES PAYMENT AND LCPDTH INDICATE AN ASSESSMENT ?
           PERFORM LCAP-ASSESS-CHK.
           IF LCAS-ASSESS NOT = "Y"
              GO TO LCAP-G-K-NOASSESS.

      * TEST FOR SHORTAGE NOT GREATER
      * THAN THE DELINQUINCY FACTOR IN (SPR'S):
           IF LCAP-LCPDTH-DATE = PDTH-DATE-FULL
              IF LCAP-AMTNEEDED < LCHG-CURPAY
                 MOVE LCAP-AMTNEEDED TO LCAP-WRK2
                 IF SP-LCFRMLA = "G"
                    SUBTRACT LCAP-LCPAID FROM LCAP-WRK2
                 END-IF
                 IF LCAP-WRK2 < LCAP-DELFAC
                    GO TO LCAP-G-K-ADVANCE.

      * LCHG-LATE INITIALIZED FOR SP-LCTYPE = "C" OR "J"
      * SINCE LATE CHARGE IS BASED ON % OF DELINQUENT AMOUNT.
           MOVE LCAP-AMTNEEDED TO LCHG-LATE.
           MOVE LCAP-LCPDTH-DATE TO LCHG-LCPDTH-DATE.
           PERFORM LCAP-CALC-TEST.

      * IF LCAP-TRAMT IS NOT ENOUGH TO COVER CHARGE, USE OWING:
           IF LCAP-TRAMT < LCHG-CHARGE
              COMPUTE LCAP-OWE = LCAP-OWE + LCHG-CHARGE
                                               - LCAP-TRAMT
              ADD LCAP-TRAMT TO LCAP-APP
                                LCAP-LCPAID
              MOVE 0 TO LCAP-TRAMT
           ELSE
              ADD LCHG-CHARGE TO LCAP-APP
                                 LCAP-LCPAID
              SUBTRACT LCHG-CHARGE FROM LCAP-TRAMT.

       LCAP-G-K-ADVANCE.
           PERFORM LCAP-UPD-LCPD.
           IF LN-PAY-SCHLD-FG = "Y"
              ADD 1 TO LCAP-PMT-NO
              MOVE SCHD-AMT(LCAP-PMT-NO) TO LCAP-AMTNEEDED
                                            LCHG-CURPAY
              COMPUTE LCAP-DELFAC ROUNDED =
                 SCHD-AMT(LCAP-PMT-NO) * (SP-LCDELFAC / 100)
           ELSE
              MOVE LN-REGPYAMT TO LCAP-AMTNEEDED
                                  LCHG-CURPAY.
           GO TO LCAP-G-K-BEGIN.

      * NO ASSESSMENT REQUIRED, ADVANCE LCAP-LCPDTH-DATE
      * TO WHERE PAYMENTS TODATE ARE PAID THRU
      * IF IT WILL ADVANCE LCPDTH ONLY, SINCE
      * ADVANCEMENT OF LCPDTH COULD HAVED OCCURED
      * VIA TIME.
       LCAP-G-K-NOASSESS.
           IF LCAP-LPTRCD NOT = "AL"
              ADD LCAP-TRAMT LCAP-PAYMNTD GIVING PDTH-PAYMNTD
                                                 LCAP-PAYMNTD
              PERFORM LCAP-PICK-PDTH-CALC
              MOVE NDTE-DATE TO SYS-DATE
              PERFORM LCAP-LCPDTH-DATE-TEST
              IF ELAPSED-DAYS > 0
                 MOVE PDTH-DATE-FULL TO LCAP-LCPDTH-DATE.

      * ? PAST MATURITY
      * IF SO FORCE LCAP-LCPDTH-DATE TO MATURITY:
           PERFORM LCAP-MDTE-CHK.
           IF ELAPSED-DAYS NOT > 0
              MOVE SYS-DATE TO LCAP-LCPDTH-DATE.

       LCAP-G-K-EXIT.
           EXIT.

      **************************************************************
      *   SP-LCFRML = M   LA LATE CHARGE FORMULA
      *
      *   NOTE:
      *        LCAP-PARTIALS (HOLDS LCAP-CURDUE AT EXIT)
      *        LCAP-LCPAID   (HOLDS LCAP-CURDATE (MMDD.YY) AT EXIT)
      **************************************************************
       LCAP-M SECTION.
           MOVE LCAP-TOTPAYMNTD TO LCAP-PAYMNTD.
           MOVE LCAP-PARTIALS TO LCAP-LST-CURDUE.
      * REARRANGE LAST PAYDATE (MMDD.YY) INTO (MMDDYY):
           MULTIPLY LCAP-LCPAID BY 100 GIVING DATE-MMDDYY.
           PERFORM CONVERT-MMDDYY-TO-YYYYMMDD.
           MOVE DATE-YYYYMMDD TO LCAP-CURDATE
                                 LCAP-LST-CURDATE.

           ADD LN-TOTLCHG LCAP-LPAPLC LCAP-LPAPINT GIVING LCAP-WRK.
           IF LCAP-WRK < LCAP-AMTNEEDED
              SUBTRACT LCAP-WRK FROM LCAP-AMTNEEDED.

      * SEE IF CONTRACTUAL PAID THRU IS LESS THAN LCPDTH,
      * IF SO SET AMTNEEDED TO REGULAR PAYMENT:
           MOVE LCAP-TOTPAYMNTD TO LCAP-WRK.
           COMPUTE PDTH-PAYMNTD LCAP-TOTPAYMNTD =
                             LCAP-TOTPAYMNTD + LN-TOTLCHG.
           PERFORM LCAP-CALC-PDTH.

           MOVE LN-REGPYAMT TO LCAP-UNPAID-PORTION.
           IF LCAP-TOTPAYMNTD < LN-1STPYAMT
              MOVE LN-1STPYAMT TO LCAP-UNPAID-PORTION.
           COMPUTE LCAP-UNPAID-PORTION ROUNDED =
              PDTH-REMAIN * LCAP-UNPAID-PORTION.

           IF LCAP-UNPAID-PORTION < LN-TOTLCHG
              MOVE LN-REGPYAMT TO LCAP-AMTNEEDED
              IF LCAP-TOTPAYMNTD < LN-1STPYAMT
                 MOVE LN-1STPYAMT TO LCAP-AMTNEEDED
              END-IF
              SUBTRACT LCAP-UNPAID-PORTION FROM LCAP-AMTNEEDED.
           MOVE LCAP-WRK TO LCAP-TOTPAYMNTD.

           IF LCAP-LST-CURDATE = 0
                     OR (LCAP-LST-CURDATE NOT = 0
                               AND LCAP-LST-CURDUE = 0)
              MOVE PDTH-DATE-FULL TO SYS-DATE
              PERFORM LCAP-LCPDTH-DATE-TEST
              IF ELAPSED-DAYS < 0
                 MOVE LN-REGPYAMT TO LCAP-AMTNEEDED.

      * SETUP CURRENT PAYMENT AND LAST CURRENT PAYMENT,
      * SEE IF CURRENT PAYMENT IS ON TIME:
           PERFORM LCAP-M-CURRENT-PERIOD.

       LCAP-M-BEGIN.
      * ASSESSING LATE CHARGES? (AL)
      * IF SO, STOP ASSESSING WHEN LATE CHARGES ARE BROUGHT
      * UP TODATE, WHEN LATE CHARGE PAID THRU (LCAP-LCPDTH)
      * IS ON OR AFTER PAYDATE:
           IF LCAP-LPTRCD = "AL"
              MOVE LCAP-PAYDATE TO SYS-DATE
              PERFORM LCAP-LCPDTH-DATE-TEST
              IF ELAPSED-DAYS NOT > 0
                 GO TO LCAP-M-EXIT
              ELSE
                 MOVE 0 TO LCAP-CURDATE
                           LCAP-CURDUE.

      * DOES PAYMENT AND LCPDTH INDICATE AN ASSESSMENT ?
           PERFORM LCAP-ASSESS-CHK.
           IF LCAS-ASSESS NOT = "Y"
              GO TO LCAP-M-NOASSESS.

      * TEST FOR LAST CURRENT PAYMENT:
           IF LCAP-LST-CURDATE NOT = 0
              MOVE LCAP-LST-CURDATE TO SYS-DATE
              PERFORM LCAP-LCPDTH-DATE-TEST
              IF ELAPSED-DAYS = 0
                 MOVE LCAP-LST-CURDATE TO NUM-DATE
                 MOVE PDTH-DATE-FULL   TO SYS-DATE
                 PERFORM TIM
                 IF ELAPSED-DAYS < 0
                    MOVE LCAP-LST-CURDUE TO LCAP-AMTNEEDED
                 ELSE
                    IF LCAP-AMTNEEDED > LCAP-LST-CURDUE
                       MOVE LCAP-LST-CURDUE TO LCAP-AMTNEEDED.

      * TEST FOR AMTNEEDED LESS THAN THE DELINQUINCY FACTOR
      * IN (SPR'S), IF SO ASSESS NO LATE CHARGE:
           IF LCAP-AMTNEEDED < LCAP-DELFAC
              GO TO LCAP-M-ADVANCE.

      * DETERMINE THE AMOUNT OF THE LATE CHARGE:
      * IF SP-LCTYPE = "C" OR "J", LCHG-LATE IS INITIALIZED,
      * SINCE LATE CHARGE IS BASED ON % OF AMOUNT DELINQUENT:
           MOVE LCAP-AMTNEEDED TO LCHG-LATE.
           MOVE LCAP-LCPDTH-DATE TO LCHG-LCPDTH-DATE.
           PERFORM LCAP-CALC-TEST.

      * IF LCAP-TRAMT IS NOT ENOUGH TO COVER CHARGE, USE OWING:
           IF LCAP-TRAMT < LCHG-CHARGE
              COMPUTE LCAP-OWE = LCAP-OWE +
                                  (LCHG-CHARGE - LCAP-TRAMT)
              ADD LCAP-TRAMT TO LCAP-APP
              MOVE 0 TO LCAP-TRAMT
           ELSE
              ADD LCHG-CHARGE TO LCAP-APP
              SUBTRACT LCHG-CHARGE FROM LCAP-TRAMT.

       LCAP-M-ADVANCE.
           PERFORM LCAP-UPD-LCPD.
           MOVE LN-REGPYAMT TO LCAP-AMTNEEDED.
           GO TO LCAP-M-BEGIN.

      *******************************************************
      * NO ASSESSMENT IS REQUIRED; ADVANCE LCAP-LCPDTH-DATE
      * TO WHERE PAYMENTS TODATE ARE PAID THRU, ONLY
      * IF IT WILL ADVANCE LCPDTH, SINCE LCPDTH COULD
      * HAVE ADVANCED BEYOND PAID THRU VIA TIME.
      *******************************************************
       LCAP-M-NOASSESS.
           IF LCAP-LPTRCD NOT = "AL"
              ADD LCAP-TRAMT LCAP-PAYMNTD GIVING PDTH-PAYMNTD.

           IF LCAP-LPTRCD NOT = "AL"
              ADD LN-TOTLCHG LCAP-LPAPLC LCAP-APP LCAP-LPAPINT
                                                 GIVING LCAP-WRK
              ADD LCAP-WRK TO PDTH-PAYMNTD
              COMPUTE PDTH-PAYMENTS = LCAP-WRK / LN-REGPYAMT
              IF PDTH-PAYMENTS > 1
                 COMPUTE PDTH-PAYMNTD =
                    PDTH-PAYMNTD - ( PDTH-PAYS * LN-REGPYAMT).

           IF LCAP-LPTRCD NOT = "AL"
              PERFORM LCAP-CALC-PDTH
              MOVE NDTE-DATE TO SYS-DATE
              PERFORM LCAP-LCPDTH-DATE-TEST
              IF ELAPSED-DAYS NOT < 0
                 MOVE PDTH-DATE-FULL TO LCAP-LCPDTH-DATE
                 IF LCAP-UNPAID-PORTION NOT = 0
                          AND LCAP-UNPAID-PORTION < LCAP-DELFAC
                    PERFORM LCAP-UPD-LCPD.

      * INSURE THAT LCPDTH ADVANCES TO CURRENT PAYMENT DATE
      * IF  CURRENT  PAYMENT IS NOT DELIQUENT:
           IF LCAP-CURDATE NOT = 0
              MOVE LCAP-CURDATE TO SYS-DATE
              PERFORM LCAP-LCPDTH-DATE-TEST
              IF ELAPSED-DAYS > 0
                 IF LCAP-CURDUE < LCAP-DELFAC
                    IF ELAPSED-DAYS > 0
                       MOVE LCAP-CURDATE TO LCAP-LCPDTH-DATE
                    ELSE
                       PERFORM LCAP-UPD-LCPD.

       LCAP-M-EXIT.
           MOVE LCAP-CURDUE TO LCAP-PARTIALS.
      * REARRANGE DATE (MMDDYY) TO (MMDD.YY):
           MOVE LCAP-CURDATE TO DATE-YYYYMMDD.
           PERFORM CONVERT-YYYYMMDD-TO-MMDDYY.
           MULTIPLY DATE-MMDDYY BY 0.01 GIVING LCAP-LCPAID.

      ***********************************************
      *    TEST CURRENT PAYMENT ON TIME
      *
      *    IF SO, ESTABLISH WHETHER ITS A NEW PERIOD
      *    OR LAST PAYMENT PERIOD AND SETUP AMOUNTS
      *    DELIQUENT FOR LAST OR BOTH PERIODS.
      ***********************************************
       LCAP-M-CURRENT-PERIOD SECTION.
           MOVE " " TO LCAP-PRIOR-1STPY.
           MOVE 0   TO LCAP-CURDUE.
           ADD LCAP-LPAPLC LCAP-LPAPINT TO LCAP-TRAMT.

      * IF PAYMENT IS BEFORE 1ST PAYMENT IS DUE,
      * FORCE CURRENT DUE DATE TO 1 MONTH PRIOR TO
      * FIRST PAYMENT DUE DATE:
           MOVE LCAP-PAYDATE TO LCAP-CURDATE
                                NUM-DATE.
           MOVE LCAP-1STPYDATE TO SYS-DATE.
           PERFORM TIM.
           IF ELAPSED-DAYS > 0
              MOVE "Y" TO LCAP-PRIOR-1STPY
              MOVE LCAP-1STPYDATE TO NDTE-DATE
              MOVE -1 TO NDTE-HOLD
              PERFORM INCREMENT-MONTHS
              MOVE NDTE-DATE TO LCAP-CURDATE
              GO TO LCAP-M-CURRENT-PERIOD-SETDUE.

      * IF TODAY IS ON OR AFTER PAYMENT DUE DATE OF THIS MONTH,
      *  SET UP THIS MONTHS DUE DATE TO SEE IF THE PAYMENT WAS ON TIME:
      * IF THE PAYMENT CAME IN BEFORE THIS MONTHS DUE DATE, SET UP
      *  LAST MONTHS DUE DATE TO SEE IF THE PAYMENT WAS ON TIME:
           IF LCAP-CURDATE-DD NOT < LCAP-1STPYDATE-DD
              MOVE -1 TO NDTE-HOLD
           ELSE
              MOVE -2 TO NDTE-HOLD.

           MOVE LCAP-1STPYDATE-DD TO LCAP-CURDATE-DD.
           MOVE LCAP-CURDATE TO NDTE-DATE.
           PERFORM INCREMENT-MONTHS.
           MOVE NDTE-DATE TO LCAP-CURDATE.

      * TEST TO SEE IF THE PAYMENT WAS MADE ON TIME:
           MOVE LCAP-CURDATE TO LCAS-LCPDTH-DATE.
           MOVE LCAP-1STPYDATE TO LCAS-1STPYDATE.
           MOVE LCAP-PAYDATE TO LCAS-PAYDATE.
           PERFORM LATE-ASSESS-ROUTINE.
           IF LCAS-ASSESS = "Y"
              MOVE 0 TO LCAP-CURDATE
              GO TO LCAP-M-CURRENT-PERIOD-EXIT.

      * PAYMENT WAS ON TIME, SO SET IT UP AS CURRENT PAYMENT:
           MOVE LCAP-CURDATE TO NDTE-DATE.
           MOVE 1 TO NDTE-HOLD.
           PERFORM INCREMENT-MONTHS.
           MOVE NDTE-DATE TO LCAP-CURDATE.

      * IF CURRENT PAYDATE IS THE SAME AS LAST CURRENT PAYDATE,
      * SET CURRENT PAYMENT DATE TO LAST CURRENT PAYMENT DATE,
      * MAKE CURRENT DUE EQUAL TO REDUCED LAST CURRENT DUE:
       LCAP-M-CURRENT-PERIOD-SETDUE.
           IF LCAP-CURDATE NOT = LCAP-LST-CURDATE
              GO TO LCAP-M-CURRENT-PERIOD-NEWCUR.
           MOVE LCAP-LST-CURDATE TO LCAP-CURDATE.
           IF LCAP-TRAMT < LCAP-LST-CURDUE
              SUBTRACT LCAP-TRAMT FROM LCAP-LST-CURDUE
                                        GIVING LCAP-CURDUE
           ELSE
              MOVE 0 TO LCAP-CURDUE.
           MOVE 0 TO LCAP-LST-CURDATE
                     LCAP-LST-CURDUE.
           GO TO LCAP-M-CURRENT-PERIOD-EXIT.

       LCAP-M-CURRENT-PERIOD-NEWCUR.
      * SET UP AMOUNT LATE CHARGES CAN STILL BE CALCULATED ON:
           MOVE LN-REGPYAMT TO LCAP-CURDUE.
           IF LN-1STPYAMT NOT = 0
              IF LCAP-CURDATE = LCAP-1STPYDATE
                 MOVE LN-1STPYAMT TO LCAP-CURDUE.

           IF LCAP-TRAMT < LCAP-CURDUE
              SUBTRACT LCAP-TRAMT FROM LCAP-CURDUE
              IF LCAP-CURDUE < LCAP-DELFAC
                 MOVE 0 TO LCAP-CURDUE
              END-IF
           ELSE
              MOVE 0 TO LCAP-CURDUE.

       LCAP-M-CURRENT-PERIOD-EXIT.
      * SET LAST CURRENT DATE BACK ONE MONTH TO ALLOW
      * COMPARISON WITH LCPDTH:
           IF LCAP-LST-CURDATE NOT = 0
              MOVE LCAP-LST-CURDATE TO NDTE-DATE
              MOVE -1 TO NDTE-HOLD
              PERFORM INCREMENT-MONTHS
              MOVE NDTE-DATE TO LCAP-LST-CURDATE.

           SUBTRACT LCAP-LPAPLC LCAP-LPAPINT FROM LCAP-TRAMT.

      ***********************************************
      *  SP-LCFRMLA = N   (MERCURY, NV)
      ***********************************************
       LCAP-N SECTION.

      * SETUP LATE CHARGES PAID INCLUSIVE OF
      * ANY SHORTAGES PAID TODAY.

       LCAP-N-DATE-CHK.

      * VERIFY DATES FOR ASSESSMENT
           PERFORM LCAP-MDTE-CHK.
           IF ELAPSED-DAYS NOT > 0
              GO TO LCAP-N-EXIT.

      * FOR THIS CODE, STOP ASSESSING WHEN LATE CHARGES
      * ARE BROUGHT UP TODATE
           IF LCAP-LPTRCD = "AL"
              PERFORM LCAP-ASSESS-CHK
              IF LCAS-ASSESS NOT = "Y"
                 GO TO LCAP-N-EXIT.
           PERFORM LCAP-ASSESS-CHK.
           IF (LCAS-ASSESS NOT = "Y")
                     OR (LCAP-TRAMT < LCAP-AMTNEEDED)
              GO TO LCAP-N-RETRY.

      * LCHG-LATE INITIALIZED FOR SP-LCTYPE = "C" OR "J",
      * WHERE LATE CHG IS BASED ON % OF LATE PMT:
           MOVE LCAP-AMTNEEDED TO LCHG-LATE.
           MOVE LCAP-LCPDTH-DATE TO LCHG-LCPDTH-DATE.
           PERFORM LCAP-CALC-TEST.

      * IF TRAMT INSUFFICIENT TO COVER CHARGE, OWING IS FOUND:
           IF LCAP-TRAMT < LCHG-CHARGE
              SUBTRACT LCAP-TRAMT FROM LCHG-CHARGE GIVING LCAP-OWE
              ADD LCAP-TRAMT TO LCAP-APP
              PERFORM LCAP-UPD-LCPD
              GO TO LCAP-N-EXIT.

      * OTHERWISE, TAKE LATE CHARGE AND (ATTEMPT) PMT RECEIPT:
           ADD LCHG-CHARGE TO LCAP-APP.
           PERFORM LCAP-UPD-LCPD.

      * REDUCE TRANSACTION AMOUNT BY PAYMENT:
       LCAP-N-PMT.
           SUBTRACT LCAP-AMTNEEDED FROM LCAP-TRAMT.
           MOVE LN-REGPYAMT TO LCAP-AMTNEEDED
                               LCHG-CURPAY.
           GO TO LCAP-N-DATE-CHK.

       LCAP-N-RETRY.
           IF LCAP-TRAMT > LCAP-AMTNEEDED
              PERFORM LCAP-UPD-LCPD
              GO TO LCAP-N-PMT.

      * TEST TO SEE IF THE SHORTAGE IS NOT GREATER
      * THAN THE DELINQUINCY FACTOR, SETUP IN STATE PARAMETERS.
      *
           COMPUTE LCAP-WRK2 = LCAP-AMTNEEDED - LCAP-TRAMT.
           IF LCAP-WRK2 < LCAP-DELFAC
              PERFORM LCAP-UPD-LCPD.

       LCAP-N-EXIT.
           EXIT.

      **************************************************************
      *   SP-LCFRML = P    INDIANA   LATE CHARGE FORMULA (PRIN ONLY)
      *                    PACESETTERS
      *
      *   NOTE:
      *        LCAP-PARTIALS (HOLDS LCAP-CURDUE AT EXIT)
      *        LCAP-LCPAID   (HOLDS LCAP-CURDATE AT EXIT)
      **************************************************************
       LCAP-P SECTION.
           MOVE LCAP-TOTPAYMNTD TO LCAP-PAYMNTD.
           MOVE LCAP-PARTIALS TO LCAP-LST-CURDUE.
      * REARRANGE LAST PAYDATE (MMDD.YY) INTO (MMDDYY):
           MULTIPLY LCAP-LCPAID BY 100 GIVING DATE-MMDDYY.
           PERFORM CONVERT-MMDDYY-TO-YYYYMMDD.
           MOVE DATE-YYYYMMDD TO LCAP-CURDATE
                                 LCAP-LST-CURDATE.

           ADD LN-TOTLCHG LCAP-LPAPLC LCAP-LPAPINT GIVING LCAP-WRK.
           IF LCAP-AMTNEEDED NOT = LCAP-UNPAID-PORTION
              SUBTRACT LCAP-WRK FROM LCAP-AMTNEEDED.

      *-----------------------------------------------------------
      * DETERMINE PAID THRU & AMD AMT-NEEDED, BASED ON
      *    LCAP-TOTPAYMNTD & LN-TOTLCHG.
      * IF REMAINING PORTION OF PAYMENT IS LESS THAN TOTAL LATE
      *    CHARGES PAID, SET FULL PAYMENT AS AMTNEEDED.
      *-----------------------------------------------------------
      * SAVE LCAP-TOTPAYMNTD:
           MOVE LCAP-TOTPAYMNTD TO LCAP-WRK.
           COMPUTE PDTH-PAYMNTD
                   LCAP-PAYMNTD
                   LCAP-TOTPAYMNTD = LCAP-TOTPAYMNTD + LN-TOTLCHG.
           PERFORM LCAP-PICK-PDTH-CALC.

           IF LN-PAY-SCHLD-FG = "Y"
              MOVE SCHD-AMT(LCAP-PMT-NO) TO LCAP-WRK2
              COMPUTE LCAP-WRK2 ROUNDED = LCAP-REMAIN * LCAP-WRK2
           ELSE
              MOVE LN-REGPYAMT TO LCAP-WRK2
              IF LCAP-TOTPAYMNTD < LN-1STPYAMT
                 MOVE LN-1STPYAMT TO LCAP-WRK2
              END-IF
              COMPUTE LCAP-WRK2 ROUNDED =
                           PDTH-REMAIN * LCAP-WRK2.

           IF LCAP-WRK2 < LN-TOTLCHG
              IF LN-PAY-SCHLD-FG = "Y"
                 MOVE SCHD-AMT(LCAP-PMT-NO) TO LCAP-AMTNEEDED
              ELSE
                 MOVE LN-REGPYAMT TO LCAP-AMTNEEDED.
      * RESTORE LCAP-TOTPAYMNTD:
           MOVE LCAP-WRK TO LCAP-TOTPAYMNTD.

      * IF LCAP-LST-CURDATE = 0 (121290 JTG)
      * IF NO LAST CURRENT DATE OR NO CURRENT DUE AND LATE
      * CHARGE PDTH AHEAD OF PAID THRU MOVE REGPAY OR SCHEDULE AMT TO
      * AMTNEEDED.
      
           IF LCAP-LST-CURDATE = 0 OR
               (LCAP-LST-CURDATE NOT = 0 AND LCAP-LST-CURDUE = 0)
              MOVE PDTH-DATE-FULL TO SYS-DATE
              PERFORM LCAP-LCPDTH-DATE-TEST
              IF ELAPSED-DAYS < 0
                 IF LN-PAY-SCHLD-FG = "Y"
                    MOVE SCHD-AMT(LCAP-PMT-NO) TO LCAP-AMTNEEDED
                 ELSE
                    MOVE LN-REGPYAMT TO LCAP-AMTNEEDED.

      * SETUP CURRENT PAYMENT AND LAST CURRENT PAYMENT,
      * SEE IF CURRENT PAYMENT IS ON TIME:
           PERFORM LCAP-P-CURRENT-PERIOD.

       LCAP-P-BEGIN.
      * TEST IF LATE CHARGE PAID THRU (LCAP-LCPDTH) IS
      * ON OR AFTER MATURITY, IF SO EXIT:
           PERFORM LCAP-MDTE-CHK.
           IF ELAPSED-DAYS NOT > 0
              GO TO LCAP-P-EXIT.

      * ASSESSING LATE CHARGES? (AL)
      * IF SO, STOP ASSESSING WHEN LATE CHARGES ARE BROUGHT
      * UP TODATE, WHEN LATE CHARGE PAID THRU (LCAP-LCPDTH)
      * IS ON OR AFTER PAYDATE:
           IF LCAP-LPTRCD = "AL"
              MOVE LCAP-PAYDATE TO SYS-DATE
              PERFORM LCAP-LCPDTH-DATE-TEST
              IF ELAPSED-DAYS NOT > 0
                 GO TO LCAP-P-EXIT
              ELSE
                 MOVE 0 TO LCAP-CURDATE LCAP-CURDUE.

      * DOES PAYMENT AND LCPDTH INDICATE AN ASSESSMENT ?
           PERFORM LCAP-ASSESS-CHK.
           IF LCAS-ASSESS NOT = "Y"
              GO TO LCAP-P-NOASSESS.

      * TEST FOR LAST CURRENT PAYMENT:
           IF LCAP-LST-CURDATE NOT = 0
              MOVE LCAP-LST-CURDATE TO SYS-DATE
              PERFORM LCAP-LCPDTH-DATE-TEST
              IF ELAPSED-DAYS = 0
                 MOVE LCAP-LST-CURDATE TO NUM-DATE
                 MOVE PDTH-DATE-FULL TO SYS-DATE
                 PERFORM TIM
                 IF ELAPSED-DAYS < 0
                    MOVE LCAP-LST-CURDUE TO LCAP-AMTNEEDED
                 ELSE
                    IF LCAP-AMTNEEDED > LCAP-LST-CURDUE
                       MOVE LCAP-LST-CURDUE TO LCAP-AMTNEEDED.

      * TEST FOR AMTNEEDED LESS THAN THE DELINQUINCY FACTOR
      * IN (SPR'S), IF SO ASSESS NO LATE CHARGE:
           IF LCAP-AMTNEEDED < LCAP-DELFAC
              GO TO LCAP-P-ADVANCE.

      * DETERMINE THE AMOUNT OF THE LATE CHARGE:
      * IF SP-LCTYPE = "C" OR "J", LCHG-LATE IS INITIALIZED,
      * SINCE LATE CHARGE IS BASED ON % OF AMOUNT DELINQUENT:
           MOVE LCAP-AMTNEEDED TO LCHG-LATE.
           MOVE LCAP-LCPDTH-DATE TO LCHG-LCPDTH-DATE.
           PERFORM LCAP-CALC-TEST.

      * IF LCAP-TRAMT IS NOT ENOUGH TO COVER CHARGE, USE OWING:
           IF LCAP-TRAMT < LCHG-CHARGE
              COMPUTE LCAP-OWE = LCAP-OWE +
                                  (LCHG-CHARGE - LCAP-TRAMT)
              ADD LCAP-TRAMT TO LCAP-APP
              MOVE 0 TO LCAP-TRAMT
           ELSE
              ADD LCHG-CHARGE TO LCAP-APP
              SUBTRACT LCHG-CHARGE FROM LCAP-TRAMT.

       LCAP-P-ADVANCE.
      *UPDATE LATE CHARGE PAID THRU
           PERFORM LCAP-UPD-LCPD.
           IF LN-PAY-SCHLD-FG = "Y"
              ADD 1 TO LCAP-PMT-NO
              MOVE SCHD-AMT(LCAP-PMT-NO) TO LCAP-AMTNEEDED
                                            LCHG-CURPAY
           ELSE
              MOVE LN-REGPYAMT TO LCAP-AMTNEEDED
                                  LCHG-CURPAY.
           GO TO LCAP-P-BEGIN.

      *******************************************************
      * NO ASSESSMENT IS REQUIRED; ADVANCE LCAP-LCPDTH-DATE
      * TO WHERE PAYMENTS TODATE ARE PAID THRU, ONLY
      * IF IT WILL ADVANCE LCPDTH, SINCE LCPDTH COULD
      * HAVE ADVANCED BEYOND PAID THRU VIA TIME.
      *******************************************************
       LCAP-P-NOASSESS.
           IF LCAP-LPTRCD NOT = "AL"
              ADD LCAP-TRAMT LCAP-PAYMNTD GIVING PDTH-PAYMNTD
                                                 LCAP-PAYMNTD
              IF SP-LCFRMLA = "H"
                 ADD LCAP-APP TO PDTH-PAYMNTD
                                 LCAP-PAYMNTD.

      * IF LATE CHARGES, INTEREST TOTAL MORE THAN PAYMENT
      * RECOMPUTE PDTH PAYMENTS.
      * ??????? ON WHAT TO DO IF PAYMENT SCHEDULES:
           IF LCAP-LPTRCD NOT = "AL"
              IF SP-LCFRMLA = "P"
                 ADD LN-TOTLCHG LCAP-LPAPLC LCAP-APP LCAP-LPAPINT
                                                 GIVING LCAP-WRK
                 ADD LCAP-WRK TO PDTH-PAYMNTD
                                 LCAP-PAYMNTD
                 COMPUTE PDTH-PAYMENTS = LCAP-WRK / LN-REGPYAMT
                 IF PDTH-PAYMENTS > 1
                    COMPUTE PDTH-PAYMNTD
                            LCAP-PAYMNTD = PDTH-PAYMNTD
                                   - ( PDTH-PAYS * LN-REGPYAMT).

      * CALCULATE PAID THRU, IF SAME AS LATE CHARGE PAID
      * MOVE PDTH TO LATE PAID THRU, IF UNPAID LESS THAN DELFAC
      * UPDATE LATE CHARGE PAID THRU
           IF LCAP-LPTRCD NOT = "AL"
              PERFORM LCAP-PICK-PDTH-CALC
              MOVE NDTE-DATE TO SYS-DATE
              PERFORM LCAP-LCPDTH-DATE-TEST
              IF ELAPSED-DAYS NOT < 0
                 MOVE PDTH-DATE-FULL TO LCAP-LCPDTH-DATE
                 IF LCAP-UNPAID-PORTION NOT = 0
                          AND LCAP-UNPAID-PORTION < LCAP-DELFAC
                    PERFORM LCAP-UPD-LCPD.

      * INSURE THAT LCPDTH ADVANCES TO CURRENT PAYMENT DATE
      * IF  CURRENT  PAYMENT IS NOT DELIQUENT:
           IF LCAP-CURDATE NOT = 0
              MOVE LCAP-CURDATE TO SYS-DATE
              PERFORM LCAP-LCPDTH-DATE-TEST
              IF ELAPSED-DAYS > 0
                 IF LCAP-CURDUE < LCAP-DELFAC
                    IF ELAPSED-DAYS > 0
                       MOVE LCAP-CURDATE TO LCAP-LCPDTH-DATE
                    ELSE
                       PERFORM LCAP-UPD-LCPD.

      * ? PAST MATURITY,
      * IF SO FORCE LCAP-LCPDTH-DATE TO MATURITY:
       LCAP-P-MATURITY-TEST.
           PERFORM LCAP-MDTE-CHK.
           IF ELAPSED-DAYS NOT > 0
              MOVE SYS-DATE TO LCAP-LCPDTH-DATE.

       LCAP-P-EXIT.
           MOVE LCAP-CURDUE TO LCAP-PARTIALS.
      * REARRANGE DATE (MMDDYY) TO (MMDD.YY):
           MOVE LCAP-CURDATE TO DATE-YYYYMMDD.
           PERFORM CONVERT-YYYYMMDD-TO-MMDDYY.
           MULTIPLY DATE-MMDDYY BY 0.01 GIVING LCAP-LCPAID.

      ***********************************************
      *    TEST CURRENT PAYMENT ON TIME
      *
      *    IF SO, ESTABLISH WHETHER ITS A NEW PERIOD
      *    OR LAST PAYMENT PERIOD AND SETUP AMOUNTS
      *    DELIQUENT FOR LAST OR BOTH PERIODS.
      ***********************************************
       LCAP-P-CURRENT-PERIOD SECTION.
           MOVE " " TO LCAP-PRIOR-1STPY.
           MOVE 0 TO LCAP-CURDUE.
           ADD LCAP-LPAPLC LCAP-LPAPINT TO LCAP-TRAMT.

      * IF PAYMENT IS BEFORE 1ST PAYMENT IS DUE,
      * FORCE CURRENT DUE DATE TO 1 MONTH PRIOR TO
      * FIRST PAYMENT DUE DATE:
           MOVE LCAP-PAYDATE TO LCAP-CURDATE NUM-DATE.
           MOVE LCAP-1STPYDATE TO SYS-DATE.
           PERFORM TIM.
           IF ELAPSED-DAYS > 0
              MOVE "Y" TO LCAP-PRIOR-1STPY
              MOVE LCAP-1STPYDATE TO NDTE-DATE
              MOVE -1 TO NDTE-HOLD
              PERFORM INCREMENT-MONTHS
              MOVE NDTE-DATE TO LCAP-CURDATE
              GO TO LCAP-P-CURRENT-PERIOD-SETDUE.

      * IF TODAY IS ON OR AFTER PAYMENT DUE DATE OF THIS MONTH,
      *  SET UP THIS MONTHS DUE DATE TO SEE IF THE PAYMENT WAS ON TIME:
      * IF THE PAYMENT CAME IN BEFORE THIS MONTHS DUE DATE, SET UP
      *  LAST MONTHS DUE DATE TO SEE IF THE PAYMENT WAS ON TIME:
           IF LCAP-CURDATE-DD NOT < LCAP-1STPYDATE-DD
              MOVE -1 TO NDTE-HOLD
           ELSE
              MOVE -2 TO NDTE-HOLD.

           MOVE LCAP-1STPYDATE-DD TO LCAP-CURDATE-DD.
           MOVE LCAP-CURDATE TO NDTE-DATE.
           PERFORM INCREMENT-MONTHS.
           MOVE NDTE-DATE TO LCAP-CURDATE.

      * TEST TO SEE IF THE PAYMENT WAS MADE ON TIME:
           MOVE LCAP-CURDATE    TO LCAS-LCPDTH-DATE.
           MOVE LCAP-1STPYDATE  TO LCAS-1STPYDATE.
           MOVE LCAP-PAYDATE    TO LCAS-PAYDATE.
           PERFORM LATE-ASSESS-ROUTINE.
           IF LCAS-ASSESS = "Y"
              MOVE 0 TO LCAP-CURDATE
              GO TO LCAP-P-CURRENT-PERIOD-EXIT.

      * PAYMENT WAS ON TIME, SO SET IT UP AS CURRENT PAYMENT:
           MOVE LCAP-CURDATE TO NDTE-DATE.
           MOVE 1 TO NDTE-HOLD.
           PERFORM INCREMENT-MONTHS.
           MOVE NDTE-DATE TO LCAP-CURDATE.

      * IF CURRENT PAYDATE IS THE SAME AS LAST CURRENT PAYDATE,
      * SET CURRENT PAYMENT DATE TO LAST CURRENT PAYMENT DATE,
      * MAKE CURRENT DUE EQUAL TO REDUCED LAST CURRENT DUE:
       LCAP-P-CURRENT-PERIOD-SETDUE.
           IF LCAP-CURDATE NOT = LCAP-LST-CURDATE
              GO TO LCAP-P-CURRENT-PERIOD-NEWCUR.
           MOVE LCAP-LST-CURDATE TO LCAP-CURDATE.
           IF LCAP-TRAMT < LCAP-LST-CURDUE
              SUBTRACT LCAP-TRAMT FROM LCAP-LST-CURDUE
                                        GIVING LCAP-CURDUE
           ELSE
              MOVE 0 TO LCAP-CURDUE.
           MOVE 0 TO LCAP-LST-CURDATE LCAP-LST-CURDUE.
           GO TO LCAP-P-CURRENT-PERIOD-EXIT.

       LCAP-P-CURRENT-PERIOD-NEWCUR.
      * SET UP AMOUNT LATE CHARGES CAN STILL BE CALCULATED ON:
           IF LN-PAY-SCHLD-FG = "Y"
              MOVE LCAP-1STPYDATE TO NUM-DATE
              MOVE LCAP-CURDATE TO SYS-DATE
              PERFORM TIM360
              IF ELAPSED-MONTHS < 0
                 MOVE 0 TO ELAPSED-MONTHS
              END-IF
              ADD 1 ELAPSED-MONTHS GIVING LCAP
              MOVE SCHD-AMT(LCAP) TO LCAP-CURDUE
           ELSE
              MOVE LN-REGPYAMT TO LCAP-CURDUE
              IF LN-1STPYAMT NOT = 0
                 IF LCAP-CURDATE = LCAP-1STPYDATE
                    MOVE LN-1STPYAMT TO LCAP-CURDUE.

           IF LCAP-TRAMT < LCAP-CURDUE
              SUBTRACT LCAP-TRAMT FROM LCAP-CURDUE
           ELSE
              MOVE 0 TO LCAP-CURDUE.

       LCAP-P-CURRENT-PERIOD-EXIT.
      * SET LAST CURRENT DATE BACK ONE MONTH TO ALLOW
      * COMPARISON WITH LCPDTH:
           IF LCAP-LST-CURDATE NOT = 0
              MOVE LCAP-LST-CURDATE TO NDTE-DATE
              MOVE -1 TO NDTE-HOLD
              PERFORM INCREMENT-MONTHS
              MOVE NDTE-DATE TO LCAP-LST-CURDATE.

           SUBTRACT LCAP-LPAPLC LCAP-LPAPINT FROM LCAP-TRAMT.

      ***************************************************************
      *   SP-LCFRML = S    WISCONSIN LATE CHARGE FORMULA
      *                    WORKS LIKE "F" BUT WILL NOT TAKE
      *                    A LC AFTER MATURITY.
      *               T   WISCONSIN LATE CHARGE FORMULA
      *                   SAME AS 'S' BUT WITH TOTAL MONEY
      *
      *   NOTE:
      *        LCAP-PARTIALS (HOLDS LCAP-CURDUE AT EXIT)
      *        LCAP-LCPAID   (HOLDS LCAP-CURDATE [MMDD.YY] AT EXIT)
      ***************************************************************
       LCAP-S SECTION.
      * SET PMT-TO-DATE TEMP TO INCLUDE APPLIED TO INTEREST, CITIZENS FINANCE:
           ADD LCAP-TOTPAYMNTD LCAP-LPAPINT GIVING LCAP-PAYMNTD.

           MOVE LCAP-PARTIALS TO LCAP-LST-CURDUE.
      * REARRANGE LAST PAYDATE (MMDD.YY) INTO (MMDDYY):
           MULTIPLY LCAP-LCPAID BY 100 GIVING DATE-MMDDYY.
           PERFORM CONVERT-MMDDYY-TO-YYYYMMDD.
           MOVE DATE-YYYYMMDD TO LCAP-CURDATE
                                 LCAP-LST-CURDATE.

      * SEE IF CONTRACTUAL PAID THRU IS LESS THAN LCPDTH,
      * IF SO SET AMTNEEDED TO REGULAR PAYMENT:
           MOVE PDTH-DATE-FULL TO SYS-DATE.
           PERFORM LCAP-LCPDTH-DATE-TEST.
           IF ELAPSED-DAYS < 0
              IF LN-PAY-SCHLD-FG = "Y"
                 MOVE SCHD-AMT(LCAP-PMT-NO) TO LCAP-AMTNEEDED
              ELSE
                 MOVE LN-REGPYAMT TO LCAP-AMTNEEDED.

      * SETUP CURRENT PAYMENT AND LAST CURRENT PAYMENT,
      * SEE IF CURRENT PAYMENT IS ON TIME:
           PERFORM LCAP-S-CURRENT-PERIOD.

       LCAP-S-BEGIN.
      * TEST IF LATE CHARGE PAID THRU (LCAP-LCPDTH) IS
      * ON OR AFTER MATURITY, IF SO EXIT:
           PERFORM LCAP-MDTE-CHK.
           IF ELAPSED-DAYS NOT > 0
              GO TO LCAP-S-EXIT.

      * IS PAYMENT ON OF AFTER MATURITY+GRACE?
      *    IS THE LCPDTH GREATER THEN THE 2ND TO LST PAYDATE?
      *       IS THE PAYMENT MADE AFTER THE GRACE PERIOD?
      *          THEN ADVANCE LCPDTH TO MAT DATE AND DO NOT CHARGE LC
      
           MOVE MDTE-DATE TO NDTE-DATE.
           MOVE SP-LCGRACE TO NDTE-HOLD.
           PERFORM INCREMENT-DAYS.
           IF LCAP-PAYDATE > NDTE-DATE
              MOVE MDTE-DATE TO NDTE-DATE
              MOVE LN-UNITPER-CD TO DATER-UNITPER-CD
              MOVE LN-UNITPER-FREQ TO DATER-UNITPER-FREQ
              MOVE -1 TO NDTE-HOLD
              PERFORM INCREMENT-UNITPER
              IF LCAP-LCPDTH-DATE NOT < NDTE-DATE
                 MOVE MDTE-DATE TO LCAP-LCPDTH-DATE
                 GO TO LCAP-S-EXIT.

      * ASSESSING LATE CHARGES? (AL)
      * IF SO, STOP ASSESSING WHEN LATE CHARGES ARE BROUGHT
      * UP TODATE, WHEN LATE CHARGE PAID THRU (LCAP-LCPDTH)
      * IS ON OR AFTER PAYDATE:
           IF LCAP-LPTRCD = "AL"
              MOVE LCAP-PAYDATE TO SYS-DATE
              PERFORM LCAP-LCPDTH-DATE-TEST
              IF ELAPSED-DAYS NOT > 0
                 GO TO LCAP-S-EXIT
              ELSE
                 MOVE 0 TO LCAP-CURDATE
                           LCAP-CURDUE.

      * DOES PAYMENT AND LCPDTH INDICATE AN ASSESSMENT ?
           PERFORM LCAP-ASSESS-CHK.
           IF LCAS-ASSESS NOT = "Y"
              GO TO LCAP-S-NOASSESS.

      * TEST FOR AMTNEEDED LESS THAN THE DELINQUINCY FACTOR
      * IN (SPR'S), IF SO ASSESS NO LATE CHARGE:
      * TEST FOR LAST CURRENT PAYMENT:
           IF LCAP-LST-CURDATE NOT = 0
              MOVE LCAP-LST-CURDATE TO SYS-DATE
              PERFORM LCAP-LCPDTH-DATE-TEST
              IF ELAPSED-DAYS = 0
                 MOVE LCAP-LST-CURDUE TO LCAP-AMTNEEDED
                 IF LCAP-LST-CURDUE < LCAP-DELFAC
                    GO TO LCAP-S-ADVANCE.

      * DETERMINE THE AMOUNT OF THE LATE CHARGE:
      * IF SP-LCTYPE = 'C', LCHG-LATE IS INITIALIZED,
      * SINCE LATE CHARGE IS BASED ON % OF AMOUNT DELINQUENT:
           MOVE LCAP-AMTNEEDED TO LCHG-LATE.
           MOVE LCAP-LCPDTH-DATE TO LCHG-LCPDTH-DATE.
           PERFORM LCAP-CALC-TEST.

      * IF LCAP-TRAMT IS NOT ENOUGH TO COVER CHARGE, USE OWING:
           IF LCAP-TRAMT < LCHG-CHARGE
              COMPUTE LCAP-OWE = LCAP-OWE +
                                  (LCHG-CHARGE - LCAP-TRAMT)
              ADD LCAP-TRAMT TO LCAP-APP
              MOVE 0 TO LCAP-TRAMT
           ELSE
              ADD LCHG-CHARGE TO LCAP-APP
              SUBTRACT LCHG-CHARGE FROM LCAP-TRAMT.

       LCAP-S-ADVANCE.
           PERFORM LCAP-UPD-LCPD.
           IF LN-PAY-SCHLD-FG = "Y"
              ADD 1 TO LCAP-PMT-NO
              MOVE SCHD-AMT(LCAP-PMT-NO) TO LCAP-AMTNEEDED
                                            LCHG-CURPAY
           ELSE
              MOVE LN-REGPYAMT TO LCAP-AMTNEEDED.
           GO TO LCAP-S-BEGIN.

      * NO ASSESSMENT IS REQUIRED; ADVANCE LCAP-LCPDTH-DATE
      * TO WHERE PAYMENTS TODATE ARE PAID THRU, ONLY
      * IF IT WILL ADVANCE LCPDTH, SINCE LCPDTH COULD
      * HAVE ADVANCED BEYOND PAID THRU VIA TIME.
       LCAP-S-NOASSESS.
           IF LCAP-LPTRCD NOT = "AL"
              ADD LCAP-TRAMT LCAP-PAYMNTD GIVING PDTH-PAYMNTD
                                                 LCAP-PAYMNTD
              PERFORM LCAP-PICK-PDTH-CALC
              MOVE NDTE-DATE TO SYS-DATE
              PERFORM LCAP-LCPDTH-DATE-TEST
              IF ELAPSED-DAYS > 0
                 MOVE PDTH-DATE-FULL TO LCAP-LCPDTH-DATE.

      * INSURE THAT LCPDTH ADVANCES TO CURRENT PAYMENT DATE
      * IF  CURRENT  PAYMENT IS NOT DELIQUENT:
           IF LCAP-CURDATE NOT = 0
              MOVE LCAP-CURDATE TO SYS-DATE
              PERFORM LCAP-LCPDTH-DATE-TEST
              IF ELAPSED-DAYS > 0
                 IF LCAP-CURDUE < LCAP-DELFAC
                    MOVE LCAP-CURDATE TO LCAP-LCPDTH-DATE.

      * ? PAST MATURITY,
      * IF SO FORCE LCAP-LCPDTH-DATE TO MATURITY:
           PERFORM LCAP-MDTE-CHK.
           IF ELAPSED-DAYS NOT > 0
              MOVE SYS-DATE TO LCAP-LCPDTH-DATE.

       LCAP-S-EXIT.
           MOVE LCAP-CURDUE TO LCAP-PARTIALS.
      * REARRANGE DATE TO (MMDD.YY):
           MOVE LCAP-CURDATE TO DATE-YYYYMMDD.
           PERFORM CONVERT-YYYYMMDD-TO-MMDDYY.
           MULTIPLY DATE-MMDDYY BY 0.01 GIVING LCAP-LCPAID.

      ***********************************************
      *    TEST CURRENT PAYMENT ON TIME
      *
      *    IF SO, ESTABLISH WHETHER ITS A NEW PERIOD
      *    OR LAST PAYMENT PERIOD AND SETUP AMOUNTS
      *    DELIQUENT FOR LAST OR BOTH PERIODS.
      ***********************************************
       LCAP-S-CURRENT-PERIOD SECTION.
           MOVE 0 TO LCAP-CURDUE.
           MOVE LCAP-PAYDATE TO LCAP-CURDATE.

      * IF TODAY IS ON OR AFTER PAYMENT DUE DATE OF THIS MONTH,
      *    SET UP THIS MONTHS DUE DATE TO SEE IF THE PAYMENT WAS ON TIME:
      * IF THE PAYMENT CAME IN BEFORE THIS MONTHS DUE DATE, SET UP
      *    LAST MONTHS DUE DATE TO SEE IF THE PAYMENT WAS ON TIME:
      *
           IF LCAP-CURDATE-DD NOT < LCAP-1STPYDATE-DD
              MOVE -1 TO NDTE-HOLD
           ELSE
              MOVE -2 TO NDTE-HOLD.

           MOVE LCAP-1STPYDATE-DD TO LCAP-CURDATE-DD.
           MOVE LCAP-CURDATE TO NDTE-DATE.
           PERFORM INCREMENT-MONTHS.
           MOVE NDTE-DATE TO LCAP-CURDATE.

      * TEST TO SEE IF THE PAYMENT WAS MADE ON TIME:
           MOVE LCAP-CURDATE TO LCAS-LCPDTH-DATE.
           MOVE LCAP-1STPYDATE TO LCAS-1STPYDATE.
           MOVE LCAP-PAYDATE TO LCAS-PAYDATE.
           PERFORM LATE-ASSESS-ROUTINE.
           IF LCAS-ASSESS = "Y"
              MOVE 0 TO LCAP-CURDATE
              GO TO LCAP-S-CURRENT-PERIOD-EXIT.

      * PAYMENT WAS ON TIME, SO SET IT UP AS CURRENT PAYMENT:
           MOVE LCAP-CURDATE TO NDTE-DATE.
           MOVE 1 TO NDTE-HOLD.
           PERFORM INCREMENT-MONTHS.
           MOVE NDTE-DATE TO LCAP-CURDATE.

      * IF CURRENT PAYDATE IS THE SAME AS LAST CURRENT PAYDATE,
      * SET CURRENT PAYMENT DATE TO LAST CURRENT PAYMENT DATE,
      * MAKE CURRENT DUE EQUAL TO REDUCED LAST CURRENT DUE:
              IF LCAP-CURDATE NOT = LCAP-LST-CURDATE
                 GO TO LCAP-S-CURRENT-PERIOD-NEWCUR.
              MOVE LCAP-LST-CURDATE TO LCAP-CURDATE.
              IF LCAP-TRAMT < LCAP-LST-CURDUE
                 SUBTRACT LCAP-TRAMT FROM LCAP-LST-CURDUE
                                           GIVING LCAP-CURDUE
              ELSE
                 MOVE 0 TO LCAP-CURDUE.
              MOVE 0 TO LCAP-LST-CURDATE
                        LCAP-LST-CURDUE.
              GO TO LCAP-S-CURRENT-PERIOD-EXIT.

       LCAP-S-CURRENT-PERIOD-NEWCUR.
      * SET UP AMOUNT LATE CHARGES CAN STILL BE CALCULATED ON:
           IF LN-PAY-SCHLD-FG = "Y"
              MOVE LCAP-1STPYDATE TO NUM-DATE
              MOVE LCAP-CURDATE TO SYS-DATE
              PERFORM TIM
              IF ELAPSED-MONTHS < 0
                 MOVE 0 TO ELAPSED-MONTHS
              END-IF
              ADD 1 ELAPSED-MONTHS GIVING LCAP
              MOVE SCHD-AMT(LCAP) TO LCAP-CURDUE
           ELSE
              MOVE LN-REGPYAMT TO LCAP-CURDUE
              IF LN-1STPYAMT NOT = 0
                 IF LCAP-CURDATE = LCAP-1STPYDATE
                    MOVE LN-1STPYAMT TO LCAP-CURDUE.

              IF LCAP-TRAMT < LCAP-CURDUE
                 SUBTRACT LCAP-TRAMT FROM LCAP-CURDUE
              ELSE
                 MOVE 0 TO LCAP-CURDUE.

       LCAP-S-CURRENT-PERIOD-EXIT.
      * SET LAST CURRENT DATE BACK ONE MONTH TO ALLOW
      * COMPARISON WITH LCPDTH:
           IF LCAP-LST-CURDATE NOT = 0
              MOVE LCAP-LST-CURDATE TO NDTE-DATE
              MOVE -1 TO NDTE-HOLD
              PERFORM INCREMENT-MONTHS
              MOVE NDTE-DATE TO LCAP-LST-CURDATE.


      *********************************************************
      *
      *   SHARED LATE CHARGE SECTIONS
      *
      *********************************************************
       LCAP-CALC-PDTH SECTION.
           MOVE LCAP-1STPYDATE TO PDTH-DATE-FULL.
           MOVE 0 TO NDTE-HOLD
                     LCAP-PDTH-PAYMENTS.
           MOVE PDTH-PAYMNTD TO LCAP-UNPAID-PORTION.

           IF LN-1STPYAMT NOT = 0
              IF PDTH-PAYMNTD > LN-1STPYAMT
                 SUBTRACT LN-1STPYAMT FROM PDTH-PAYMNTD
                                           LCAP-UNPAID-PORTION
                 MOVE 1 TO NDTE-HOLD
              ELSE
                 COMPUTE PDTH-PAYMENTS ROUNDED =
                                    PDTH-PAYMNTD / LN-1STPYAMT
                 GO TO LCAP-COMPUTE-PAYS.

           IF LN-REGPYAMT NOT = 0
              COMPUTE PDTH-PAYMENTS ROUNDED =
                                    PDTH-PAYMNTD / LN-REGPYAMT
              MOVE PDTH-PAYMENTS TO LCAP-PDTH-PAYMENTS
              COMPUTE LCAP-UNPAID-PORTION = LCAP-UNPAID-PORTION
                           - (LCAP-PDTH-PAYMENTS * LN-REGPYAMT)
              ADD NDTE-HOLD TO LCAP-PDTH-PAYMENTS.

       LCAP-COMPUTE-PAYS.
           COMPUTE NDTE-HOLD ROUNDED =
              NDTE-HOLD + LN-TOTNODEF + LN-TOTNOALDEL
                                             - 1 + PDTH-PAYS.

      * UPDATE PDTH-DATE BY ADDING # OF FULL
      * PAYMENTS MADE TO 1STPYDATE IN PDTH-DATE-WORK:
      *
           MOVE PDTH-DATE-FULL TO NDTE-DATE.
      * CHANGED TO CORRECT CYCLE OF DUE ON 30TH RE: FEB
           IF LN-UNITPER-CD = "M"
              MOVE LN-1STPYDA TO NDTE-DD.
           PERFORM INCREMENT-UNITPER.
           MOVE NDTE-DATE TO PDTH-DATE-FULL.

      * TEST FOR ADJUSTMENT IN AMOUNT NEEDED,
      * IF LATE CHARGES ARE PAID AHEAD OF PDTH:
      *
           IF SP-LCFRMLA = "D" OR "G" OR "K"
              MOVE PDTH-DATE-FULL TO SYS-DATE
              PERFORM LCAP-LCPDTH-DATE-TEST
              IF ELAPSED-DAYS < 0
                 MOVE 0 TO PDTH-REMAIN.

           IF NOT (SP-LCFRMLA = "F" OR "I" OR "L" OR "M"
                                    OR "P" OR "H" OR "S")
              GO TO LCAP-CALC-PDTH-EXIT.

      * DETERMINE AMT NEEDED TO COMPLETE A FULL PMT:
           MOVE LN-REGPYAMT TO LCAP-UNPAID-PORTION.
           IF LCAP-TOTPAYMNTD < LN-1STPYAMT
              MOVE LN-1STPYAMT TO LCAP-UNPAID-PORTION.
           COMPUTE LCAP-UNPAID-PORTION ROUNDED =
              (1 - PDTH-REMAIN) * LCAP-UNPAID-PORTION.

       LCAP-CALC-PDTH-EXIT.
           EXIT.

      *****************************************************
      *    CALCULATE PATH WITH ADDONS
      *****************************************************
       LCAP-CALC-PDTH-ADDON SECTION.
           MOVE 0 TO LCAP-PAYMENTS.
           MOVE LCAP-1STPYDATE TO PDTH-DATE-FULL.
           PERFORM GET-LT-P-REC.
           IF IO-FG = 9
              GO TO IO-ERROR
           ELSE
              MOVE 0 TO NDTE-HOLD
              MOVE 1 TO LCAP
              MOVE LTP-SCHLD-PAYMNT(1) TO LCAP-CUR-SCHD-PAY
              PERFORM LCAP-PAID-THRU-ADDON-SUM
                 UNTIL LCAP > 17
                          OR LCAP-PAYMNTD = 0.

           COMPUTE NDTE-HOLD ROUNDED =
              NDTE-HOLD + LN-TOTNODEF + LN-TOTNOALDEL - 1.

      * UPDATE PDTH-DATE BY ADDING # OF FULL
      * PAYMENTS MADE TO 1STPYDATE IN PDTH-DATE-WORK:
      *
           MOVE PDTH-DATE-FULL TO NDTE-DATE.
      * CHANGED TO CORRECT CYCLE OF DUE ON 30 RE: FEB
           IF LN-UNITPER-CD = "M"
              MOVE LN-1STPYDA TO NDTE-DD.
           PERFORM INCREMENT-UNITPER.
           MOVE NDTE-DATE TO PDTH-DATE-FULL.

      * TEST FOR ADJUSTMENT IN AMOUNT NEEDED,
      * IF LATE CHARGES ARE PAID AHEAD OF PDTH:
      
           IF SP-LCFRMLA = "D" OR "G" OR "K"
              MOVE NDTE-DATE TO SYS-DATE
              PERFORM LCAP-LCPDTH-DATE-TEST
              IF ELAPSED-DAYS < 0
                 MOVE 0 TO PDTH-REMAIN.

           IF LCAP-REMAIN = 0
              MOVE LCAP-CUR-SCHD-PAY TO LCAP-UNPAID-PORTION
           ELSE
              COMPUTE LCAP-UNPAID-PORTION ROUNDED =
                  LCAP-CUR-SCHD-PAY * (1 - LCAP-REMAIN).

      *************************************************
       LCAP-PAID-THRU-ADDON-SUM SECTION.
      *************************************************
           IF LTP-SCHLD-TERM(LCAP) = 0
              MOVE 17 TO LCAP
              GO TO LCAP-PAID-THRU-ADDON-SUM-EXIT.

           COMPUTE LCAP-WORKER =
                     LTP-SCHLD-PAYMNT(LCAP) * LTP-SCHLD-TERM(LCAP).
           MOVE LTP-SCHLD-PAYMNT(LCAP) TO LCAP-CUR-SCHD-PAY.
           IF LCAP-PAYMNTD > LCAP-WORKER
              ADD LTP-SCHLD-TERM(LCAP) TO NDTE-HOLD
              SUBTRACT LCAP-WORKER FROM LCAP-PAYMNTD
              GO TO LCAP-PAID-THRU-ADDON-SUM-EXIT.

           MOVE 0 TO LCAP-CNT.
       LCAP-PAID-THRU-ADDON-NEXT.
           IF LCAP-CNT < LTP-SCHLD-TERM(LCAP)
              IF LCAP-PAYMNTD > LTP-SCHLD-PAYMNT(LCAP)
                 SUBTRACT LTP-SCHLD-PAYMNT(LCAP) FROM LCAP-PAYMNTD
                 ADD 1 TO NDTE-HOLD
                          LCAP-CNT
                 GO TO LCAP-PAID-THRU-ADDON-NEXT
              ELSE
                 COMPUTE LCAP-PAYMENTS ROUNDED =
                            LCAP-PAYMNTD / LTP-SCHLD-PAYMNT(LCAP)
                 IF LCAP-PAYMNTD = LTP-SCHLD-PAYMNT(LCAP)
                            AND LTP-SCHLD-TERM(LCAP) = (LCAP-CNT + 1)
                    MOVE LTP-SCHLD-PAYMNT(LCAP + 1) TO LCAP-CUR-SCHD-PAY
                    ADD 1 TO NDTE-HOLD
                             LCAP-CNT
                    MOVE 0 TO LCAP-PAYMNTD
                 ELSE
                 IF LCAP-PAYMNTD = LTP-SCHLD-PAYMNT(LCAP)
                    ADD 1 TO NDTE-HOLD
                             LCAP-CNT
                 END-IF
                    MOVE 0 TO LCAP-PAYMNTD
           ELSE
              MOVE 0 TO LCAP-PAYMNTD.

       LCAP-PAID-THRU-ADDON-SUM-EXIT.
           ADD 1 TO LCAP.

      *********************************************************
      * ADVANCE LCAP-LCPDTH-DATE BY ONE PAYMENT FREQUENCY:
      *********************************************************
       LCAP-UPD-LCPD SECTION.
           MOVE LCAP-LCPDTH-DATE TO NDTE-DATE NUM-DATE.

      * CHANGED TO CORRECT CYCLE OF DUE ON 30 RE: FEB
           IF LN-UNITPER-CD = "M"
              MOVE LN-1STPYDA TO NDTE-DD.
           MOVE 1 TO NDTE-HOLD.
           PERFORM INCREMENT-UNITPER.

      * NOTE: WHEN SEMI MONTHLY WITH FIRST PAYDATE OF 14TH OR 15TH,
      *       THE INCREMENT OF LCPDTH WILL GO TO 02/28/YY OR 02/29/YY
      *       WHEN LCPDTH WAS INCREMENTED FROM 02/14/YY OR 02/15/YY.
      *       THE FOLLOWING LOGIC RE-ESTABLISHES CYCLES BACK TO
      *       THE 14TH OR 15TH.
      *12/06/05 ADDED TESTS ON DUE DATE OF 29 OR 30 OR 31. WAS NOT SETTING
      *         LCPDTH CORRECT IN THE FOLLOWING EXAMPLE:
      *         1STPYDA = 30, LCPDTH 2/28, PDTHRU 2/15, CYCLE DATES 15/30
      *         POSTING A 'PY' ADVANCED 1 UNITPER TO 3/13 SHOULD BE 3/15
      *         TO CORRECT: NUM-DATE HOLDS LCPDTH PRIOR TO INCREMENT UNITPER
      *         NDTE-DATE IS LCPDTH AFTER INCREMENT. NUM-DATE IS 2/28, NDTE
      *         DATE IS 3/13... GETS # OF DAYS BETWEEN THE 28TH OR 29 & 1ST
      *         PYDA & ADDS THEM TO THE CALCULATED LCPDTH(3/13 + 2) TO RESET
      *         CYCLE.
           IF LN-UNITPER-CD = "S"
              IF (LN-1STPYDA = 14 OR 15)
                 IF (NUM-DA = 28 OR 29) AND NDTE-MM = 03
                    MOVE LN-1STPYDA TO NDTE-DD
                 END-IF
              ELSE
              IF (LN-1STPYDA = 29 OR 30 OR 31)
                 IF (NUM-DA = 28 OR 29) AND NDTE-MM = 03
                    SUBTRACT NUM-DA FROM LN-1STPYDA GIVING NDTE-HOLD
                    PERFORM INCREMENT-DAYS.
           MOVE NDTE-DATE TO LCAP-LCPDTH-DATE.


      ****************************************************
      * ASSURE LATE CHARGE PAID THRU HAS VALID DAY:
      ****************************************************
       LCAP-ASSURE-LCPDTH SECTION.
           IF LN-UNITPER-CD = "M" AND LN-1STPYDA > 28
              MOVE LCAP-LCPDTH-DATE TO NDTE-DATE
              MOVE LN-1STPYDA  TO NDTE-DD
              PERFORM NEW-DATE-CALCULATION
              MOVE NDTE-DATE   TO LCAP-LCPDTH-DATE.

      ****************************************************
      * COMPARE LCAP-LCPDTH-DATE (UPDATED) WITH MATURITY DATE:
      ****************************************************
       LCAP-MDTE-CHK SECTION.
           MOVE MDTE-DATE TO SYS-DATE.
           PERFORM LCAP-LCPDTH-DATE-TEST.

      ****************************************************
      * FOR THIS CODE, STOP ASSESSING WHEN LATE CHARGES
      * ARE BROUGHT UP TO DATE (DETERMINED BY LCAP-PAYDATE):
      * IF NOT AL, ELAPSED SET TO 1 FOR UNIFORM EXIT CHECK
      ****************************************************
       LCAP-AL-CHECK SECTION.
           IF LCAP-LPTRCD = "AL"
              MOVE LCAP-PAYDATE TO SYS-DATE
              PERFORM LCAP-LCPDTH-DATE-TEST
           ELSE
              MOVE 1 TO ELAPSED-DAYS.

      ****************************************************
      * DOES LCAP-LCPDTH-DATE INDICATE A LATE CHARGE?
      ****************************************************
       LCAP-ASSESS-CHK SECTION.
           MOVE LCAP-LCPDTH-DATE TO LCAS-LCPDTH-DATE.
           MOVE LCAP-1STPYDATE TO LCAS-1STPYDATE.
           MOVE LCAP-PAYDATE TO LCAS-PAYDATE.
           PERFORM LATE-ASSESS-ROUTINE.
      * ADDED SET OF LCHG-DAYS-AFTER-DUE PER MULLEN #804 LCTYPE = 'Q'
           MOVE LCAS-ELAPSED-DAYS TO LCHG-DAYS-AFTER-DUE.

      ****************************************************
      * TEST ELAPSED AGAINST LCAP-LCPDTH-DATE:
      ****************************************************
       LCAP-LCPDTH-DATE-TEST SECTION.
           MOVE LCAP-LCPDTH-DATE TO NUM-DATE.
           PERFORM TIM365.

      ****************************************************
      * TEST IF THE ACCOUNT IS ON AN ALLOTMENT PLAN
      *         OR POSTING WITH A BATCH REFCD
      * AND SHOULD LATE CHARGES SHOULD NOT BE CHARGED:
      ****************************************************
       LCAP-CALC-TEST SECTION.
           IF ( ( LCAP-BATCH-REFCD NOT = " "        ) AND
                ( LCAP-BATCH-REFCD NOT = LOW-VALUES ) AND
                ( LCAP-BATCH-REFCD-LC-FG NOT = "Y"  ) AND
                ( ( LCAP-LPTRCD = "PA" ) OR
                  ( LCAP-POFF-LPTRCD = "PO" AND LCAP-LPTRCD = "AL" )
                )
              )
             OR
              ( ( ( LCAP-BATCH-REFCD = " "          ) OR
                  ( LCAP-BATCH-REFCD = LOW-VALUES   )
                )
               AND
                ( LN-ALLOTCD = "Y"                   ) AND
                ( BR-LATECHG-ON-ALLOTMENTS NOT = "Y" ) AND
                ( ( LCAP-LPTRCD = "PA" ) OR
                  ( LCAP-POFF-LPTRCD = "PO" AND LCAP-LPTRCD = "AL" )
                )
              )
             OR
              ( ( ( LCAP-BATCH-REFCD = " "          ) OR
                  ( LCAP-BATCH-REFCD = LOW-VALUES   )
                )
               AND
                ( LN-LBOX = "Y"                ) AND
                ( BR-LATECHG-ON-LBOX NOT = "Y" ) AND
                ( ( LCAP-LPTRCD = "PA" ) OR
                  ( LCAP-POFF-LPTRCD = "PO" AND LCAP-LPTRCD = "AL" )
                )
              )
             OR
              ( ( LCAP-POFF-LPTRCD = "PB" OR "RN" OR
                                     "SC" OR "RB" OR "RO" ) AND
                ( SP-RBSPOPT1(7) = 19                     ) AND
                ( LCAP-NOLCHG    = 2                      )
              )
              MOVE 0 TO LCHG-CHARGE
           ELSE
              PERFORM LATE-CHARGE-CALCULATION
              ADD 1 TO LCAP-NOLCHG.

      ****************************************************
      * DETERMINE WHICH PAID THRU CALCULATION TO PERFORM
      ****************************************************
       LCAP-PICK-PDTH-CALC SECTION.
           IF LN-PAY-SCHLD-FG = "Y"
              PERFORM LCAP-CALC-PDTH-ADDON
           ELSE
              PERFORM LCAP-CALC-PDTH.
