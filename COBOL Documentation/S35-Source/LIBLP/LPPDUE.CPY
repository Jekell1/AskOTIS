      * COPYMEMBER: LIBLP/LPPDUE
      ***********************************************************************
      *          PAST DUE DAYS CALCULATION
      *
      *    NAME: LPPDUE
      *    DESC: ROUTINE COMPUTES DAYS PAST DUE FROM
      *          PAID THRU DATE
      *    IN  : PDUE-TODAY-DATE        (TODAYS DATE)
      *          PDUE-1STPYDATE    (LN-1STPYDATE)
      *    OUT : PDUE-DAYS
      *    USED: LN-CURBAL, LN-PAYFREQ
      *    COPY: LPPDUEW, LPMDTEW, LPMDTE, SPPDTHW, SPPDTH
      * REV:
      *  050986 JTG TOOK OUT CHECK ON PAST MATURITY
      *             WHICH CAUSED INPROPER VIEW OF
      *             PAST DUE DAYS EVEN THOUGH ALL
      *             PAYMENTS BECOME DUE THEN.
      *  041289 SLC ADDED CODE TO ADJUST THE DUE DATE
      *             TO AVOID MONTHEND PROBLEMS.
      *  041693 JTG ADDED LOGIC FOR UNIT PERIODS
      *  040495 JTG CORRECT LOGIC FOR FEB OVER 28TH
      *  JTG 030307 PUT BACK LOGIC FOR TESTING MATURITY:
      *             WHEN PAID-THRU IS EQUAL TO MATURITY AND THE ACCOUNT
      *             STILL HAS A BALANCE, COUNT PAST DUE DAYS FROM MATURITY
      *             AND NOT FROM NEXT DUE DATE                  CREATIVE #398
      *  JTG 030313 CORRECTED CHANGE DONE ON 030307 TO EXAMINE
      *             WHEN GP-PDTH-GT-SCHED-PAY = 'Y'
      *             PAID THRU IS CAPPED AT THE NUMBER OF SCHEDULED PAYS
      *             SO SEE IF PAID-THRU IS AT THE CAP (MATURITY)
      *             AND COUNT PAST DUE DAYS FROM MATURITY
      *             NOT FROM MATURITY + 1 PAY FREQUENCY         CREATIVE #398
      *   CS 130327 PAST DUE DAYS WERE DISPLAYING AS 1 ON 3/14 WHEN
      *             PDTH-DATE-FULL 2/28 & INCREMENT-UNITPER INCREMENTED TO
      *             3/13 (1STPYDA 30) SHOULD BE 3/15. FORCED IF PAID THRU
      *             2/28 SUBTRACT NDTE-DD (28) FROM LN-1STPYDA =2, ADD 2 TO
      *             THE INCORRECT 3/13/ DATE YIELDING 3/15 AND PDUE-DAYS SHOULD
      *             BE 0   SOAUTO PL#793
      *   CS 160318 FIXED BUG IN ABOVE, THIS TEST AND SUBTRACT
      *             (SUBTRACT NDTE-DD FROM LN-1STPYDA) SHOULD ONLY BE DONE
      *             WHEN LN-1STPYDA > 28 #922
      ***********************************************************************
       PAST-DUE-DAYS-CALCULATION SECTION.
           MOVE 0 TO PDUE-DAYS.

      * TEST IF ACCOUNT CLOSED:
           IF LN-CURBAL = 0
              GO TO PAST-DUE-DAYS-EXIT.

      * WHEN GP-PDTH-GT-SCHED-PAY = 'Y'
      *   PAID THRU IS CAPPED AT THE NUMBER OF SCHEDULED PAYS
      *   SO SEE IF PAID-THRU IS AT THE CAP (MATURITY)
      *   AND COUNT PAST DUE DAYS FROM MATURITY
      
           IF GP-PDTH-GT-SCHED-PAY = "Y"
              MOVE LN-1STPYDATE TO PDTH-DATE-WORK
              PERFORM PAID-THRU-CALCULATION
              PERFORM MATURITY-DATE-CALCULATION
              MOVE MDTE-DATE      TO NUM-DATE
              MOVE PDTH-DATE-FULL TO SYS-DATE
              IF SYS-DATE NOT < NUM-DATE
                 GO TO PAST-DUE-DAYS-COMPUTE.

      * SETUP NEXT PAYMENT DUE AND
      * COUNT PAST DUE DAYS FROM NEXT PAYMENT DUE DATE:

           MOVE PDUE-1STPYDATE TO PDTH-DATE-WORK.
           PERFORM PAID-THRU-CALCULATION.
           MOVE PDTH-DATE-FULL TO NDTE-DATE.

           IF LN-UNITPER-CD = "M"
              MOVE PDUE-1STPYDA TO NDTE-DD.

           MOVE 1 TO NDTE-HOLD.
           MOVE LN-UNITPER-CD   TO DATER-UNITPER-CD.
           MOVE LN-UNITPER-FREQ TO DATER-UNITPER-FREQ.
           PERFORM INCREMENT-UNITPER.
           MOVE NDTE-DATE TO NUM-DATE.

      * PAST DUE DAYS WERE DISPLAYING AS 1 ON 3/14 WHEN PDTH-DATE-FULL 2/28
      * & INCREMENT-UNITPER INCREMENTED TO 3/13 (1STPYDA 30) SHOULD BE 3/15.
      * FORCED IF PAID THRU 2/28 SUBTRACT NDTE-DD (28) FROM LN-1STPYDA =2,
      * ADD 2 TO THE INCORRECT 3/13/ DATE. PDUE-DAYS SHOULD BE 0

           IF LN-UNITPER-CD = "S"
              MOVE PDTH-DATE-FULL TO NDTE-DATE
              IF (NDTE-DD = 28 OR NDTE-DD = 29) AND NDTE-MM = 2
              IF LN-1STPYDA > NDTE-DD
                 SUBTRACT NDTE-DD FROM LN-1STPYDA GIVING NDTE-HOLD
                 MOVE NUM-DATE TO NDTE-DATE
                 PERFORM INCREMENT-DAYS
                 MOVE NDTE-DATE TO NUM-DATE.

       PAST-DUE-DAYS-COMPUTE.
           MOVE PDUE-TODAY-DATE      TO SYS-DATE.
           PERFORM TIM.
           IF ELAPSED-DAYS > 0
              MOVE ELAPSED-DAYS TO PDUE-DAYS.

       PAST-DUE-DAYS-EXIT.
           EXIT.
