      * COPYMEMBER: LIBLP/LPTDEL
      **************************************************************************
      *         COMPUTE TOTAL CONTRACTUAL DELINQUENCY AMT
      *                  AND  TOTAL ARREARAGE
      *         (INCLUDING INT AND LATE CHGS IF APPLICABLE)
      *
      *   NAME: LPTDEL
      *   DESC: COMPUTES TOTAL CONTRACTUAL DELINQUECY
      *                   AND TOTAL ARREARAGE
      *         IN DOLLARS, INCLUDING PRINCIPAL, INTEREST,
      *         AND LATE CHARGES (WHEN APPLICABLE).
      *         CONTRAST WITH LPCDEL.IN.C
      *
      *   IN  : CEPP1-DATE       (LN-1STPYDATE)
      *         CEPP2-DATE       (TODAYS DATE)
      *         CEPP-MAKERCD     (LN-MAKERCD(X))
      *   OUT : CDEL-DELINQUENT
      *         CDEL-ARREARAGE
      *               +  PAID AHEAD
      *               0  CURRENT
      *               -  DELINQUENT
      *   USED:
      *   COPY:
      * REV:
      *  030686 JTG ADDED TEST ON LN-CURBAL = 0
      *  091186 JTG CHANGED TO PRODUCE ARREARAGE
      *  040188 JTG ADDED MOVE TO INDU-LPTRCD
      *  070988 JTG CHANGED MOVE OF "PY" TO RATE-LPTRCD
      *  120188 BAH FIXED 20202020 BUG ON INQUIRIES
      *  120288 SLC PUT MATURITY CHECK BACK IN
      *             NOW ADD LN-LCBAL IF IB AND BEFORE MATURITY
      *             INITIALIZE INDU-INTEREST LCAP-OWE
      *  012489 SLC FIXED MATURITY CHECK. WAS CALLING LOAN
      *             MATURED ON MATURITY DATE. SHOULD NOT!!
      *  072590 JTG SET LCAP-LPAPLC AND LCAP-LPAPINT
      *  JTG 010904 ALLOWED IB TYPE LOANS TO ACCELERATE     JIM R. (ALL) PR#1582
      **************************************************************************
       TOTAL-DELINQUENCY SECTION.
           MOVE 0 TO INDU-INTEREST LCAP-OWE
                     CDEL-DELINQUENT CDEL-ARREARAGE.
           IF LN-CURBAL = 0
              GO TO TOTAL-DELINQUENCY-EXIT.

      * NORMAL PAYMENTS:
           PERFORM CONTRACTUAL-DELINQUENCY.

           COMPUTE CDEL-DELINQUENT = CDEL-DELINQUENT - LN-LCBAL.
           COMPUTE CDEL-ARREARAGE  = CDEL-ARREARAGE - LN-LCBAL.

      * TEST FOR PRECOMPUTE OR INTEREST BEARING:
           MOVE CEPP2-DATE TO IBPC-DATE.
           PERFORM IBPC-TEST.

      * ONLY TAKE LATE CHARGES WHEN THE ACCOUNT IS NOT ACCELERATED
      * AND IT IS IN A PRECOMP STATUS OR IS LOAN CLASS TYPE IB

           IF (LN-ACCELDATE = 0) AND
              (IBPC-FG = "P" OR LN-LOANTYPE = "I")
              MOVE CEPP1-DATE TO LCAP-1STPYDATE
              MOVE CEPP2-DATE TO LCAP-PAYDATE
              MOVE "AL" TO LCAP-LPTRCD
              MOVE 0 TO LCAP-LPAPLC LCAP-LPAPINT
              PERFORM LATE-CHARGE-APPLY
              COMPUTE CDEL-DELINQUENT = CDEL-DELINQUENT - LCAP-OWE
              COMPUTE CDEL-ARREARAGE  = CDEL-ARREARAGE - LCAP-OWE.

      * COMPUTE MATURITY DATE:
           PERFORM MATURITY-DATE-CALCULATION.

      * IF BEFORE MATURITY;
      * EXIT WITH THE CURRENT VALUE OF CDEL-DELINQUENT:
           MOVE MDTE-DATE TO NUM-DATE.
           MOVE CEPP2-DATE TO SYS-DATE.
           PERFORM TIM.
           IF ELAPSED-DAYS NOT > 0
              GO TO TOTAL-DELINQUENCY-EXIT.

      * AFTER MATURITY:
           IF IBPC-FG = "I" OR
              (IBPC-FG = "P" AND IBPC-EXPIRED-FLAG = "E")
              MOVE CEPP2-DATE TO RATE-DATE
              MOVE CEPP-MAKERCD TO RATE-MAKERCD
              MOVE "PY" TO RATE-LPTRCD
              PERFORM SET-PY-RATE.
           COMPUTE CDEL-DELINQUENT = CDEL-DELINQUENT - INDU-INTEREST
                                                         - LN-INTBAL.
           COMPUTE CDEL-ARREARAGE = CDEL-ARREARAGE - INDU-INTEREST
                                                         - LN-INTBAL.
      * ADD LN-OTHBAL IF "I" OR "P":
           SUBTRACT LN-OTHBAL FROM CDEL-DELINQUENT CDEL-ARREARAGE.

       TOTAL-DELINQUENCY-EXIT.
           EXIT.
