      * COPYMEMBER: LIBLP/LPAVCR
      ********************************************************************
      *   NAME: LPAVCR
      *   DESC: COMPUTE AVAILABLE CREDIT
      *   IN  : AVCR-PAYDATE               (DATE OF PAYMENT)
      *         AVCR-MAKERCD               (LN-MAKERCD(X))
      *         AVCR-LPTRCD                (PY) BY DEFAULT VALUE CLAUSE
      *         AVCR-DEF-STOP         PASSED TO POFF ROUTINE
      *                                      " " BY DEFAULT VALUE CLAUSE
      *
      *------>  AVCR-POFF-FG               (Y- DON'T COMPUTE PAYOFF)
      *         AVCR-POFF-NETDUE
      *         NOTE:
      *           COMPUTATION OF THE PAYOFF ROUTINE CAN BE AVOIDED
      *           WHEN THE NET PAYOFF AMOUNT IS AVAILABLE.
      *           MOVE POFF-NETDUE TO AVCR-POFF-NETDUE, ALONG WITH
      *           SETTING AVCR-POFF-FG TO "Y".
      *
      *   OUT : AVCR-AVAILCREDIT
      *         AVCR-POFF-NETDUE
      *         AVCR-RESTRICT-FG
      *         AVCR-CURRENT-AVAIL (WDT NEW SPECIAL OPTION)
      *
      *   USED: LN-CREDITLIM
      *   COPY : PAYOFF LOAN ROUTINE  (LPPOFF), LPAVCRW
      * REV :
      *  JTG 103188 ADDED NEW OUTPUT FIELD AVCR-POFF-NETDUE
      *             STOPPED NEGATIVE AVAILCREDIT.
      *  JTG 052190 ADDED AVCR-RESTRICT-FG (GA)
      *  JTG 092491 ADDED INCLUSION OF PAID AHEAD
      *             MONIES TO DETERMINE (GA) SERVICE CHARGE
      *  JTG 022593 ADDED SET OF POFF-LPTRCD & POFF-DEF-STOP
      *  BAH 111496 ADDED COMPUTE OF AVCR-CURRENT-AVAIL FOR WDT, THEY WILL
      *             ENTER CREDIT LIMIT AT LOAN INPUT TIME, THEN WE DISPLAY
      *             WHAT THE SYSTEM WUOLD HAVE GENERATED IN THE INQUIRIES
      *  JTG 111398 ADDED LOGIC FOR REVOLVING ACCOUNTS, REGENCY
      *  JTG 010426 CHANGED RE: GEORGIA GILA CHANGE EFF: 04/13/01 WORLD PR#263
      *  BAH 110302 ADDED SP-RENEWAL-EDIT "A" RESTRICTED FLAG WORLD PR# 734
      *  BAH 110401 FOR SP-RENEWAL-EDIT "A" IF RESTRICTED, ZERO OUT
      *             AVCR-AVAILCREDIT AVCR-CURRENT-AVAIL, WORLD PR# 734
      *  BAH 110913 FOR SP-RENEWAL-EDIT "A", DONT TEST ON A PAIDOUT, ONLY
      *             RESTRICT OPEN ACCOUNTS, WORLD PR# 767
      *  MJD 130605 ADDED LOGIC FOR SP-SCFRMLA = 54.  FUNCTIONS THE SAME AS 12
      *             BUT INCLUDES AN EXTRA 4% ADDED IN AT THE END BASED
      *             ON TOTAL NOTE                                    WORLD #0874
      *  BAH 131125 ADDED SP-CASH-AVAIL-PERCENT, WORLD PR# 944
      *  BAH 161010 ADDED SP-PERCENT-REGPAY-AVAIL, WORLD PR# 1194
      *             NEW POLICY WILL REQUIRE THAT THE CUSTOMER HAVE AT LEAST
      *             THIS % OF THE REGPYAMT AVAILABLE IN ORDER TO DISPLAY
      *             CASH AVAILABLE
      *  BAH 161027 CHANGE ABOVE CALCULATION TO USE LN-CREDITLIM INSTEAD OF
      *             HIGH-CREDIT, WORLD PR#1201
      *  BAH 161102 ADDED SCFRMLA 18 RESTRICTION FOR WORLD, THIS WAS DONE
      *             BACK IN 2008 UNDER PR# 593 AND NEEDED TO BE IN HERE
      *             INSTEAD OF CHECKING IT EVERYWHERE AVAIL CREDIT IS USED
      *                                                    WORLD PR#1199
      *  BAH 20190625 REMOVED LN-REVOLVING, PT0153
      ************************************************************************
       COMPUTE-AVAILABLE-CREDIT SECTION.
           MOVE 0 TO AVCR-AVAILCREDIT AVCR-CURRENT-AVAIL.
           MOVE " " TO AVCR-RESTRICT-FG.

      * COMPUTE PAYOFF FIGURE:
      * ALWAYS COMPUTE FOR WDT SO THEY GET RENEWAL RESTRICTIONS
      * WORLD NEW PERCENT OPTION NEEDS PB COMPANY PAYOFF ALSO
           IF BR-AVAIL-CREDIT = "S" OR
                  (SP-CASH-AVAIL-PERCENT NUMERIC AND
                    SP-CASH-AVAIL-PERCENT > 0)
              MOVE "PB" TO AVCR-LPTRCD.
           IF AVCR-POFF-FG = " " OR BR-AVAIL-CREDIT = "S"
              MOVE AVCR-PAYDATE TO POFF-PAYDATE
              MOVE AVCR-MAKERCD TO POFF-MAKERCD
              MOVE AVCR-LPTRCD TO POFF-LPTRCD
              MOVE AVCR-DEF-STOP TO POFF-DEF-STOP
              PERFORM PAYOFF-LOAN-ROUTINE
              MOVE POFF-NETDUE TO AVCR-POFF-NETDUE.
           IF AVCR-POFF-NETDUE < 0
              MOVE 0 TO AVCR-POFF-NETDUE.

      * DETERMINE AVAILABLE CREDIT:
           IF LN-CREDITLIM = 0
            OR LN-PLDATE NOT = 0
             OR LN-JDDATE NOT = 0
              OR (LN-REPOCD = "X" OR "S")
               OR LN-BNKRPTDATE NOT = 0
                OR LN-CONFIDENTCD = "Y"
                   GO TO COMPUTE-AVAILABLE-CREDIT-EXIT.

           IF LN-CREDITLIM > AVCR-POFF-NETDUE
              COMPUTE AVCR-AVAILCREDIT =
                            LN-CREDITLIM - AVCR-POFF-NETDUE.

      * NEW FOR WDT 11/14/96 BAH
           PERFORM LOAN-CALCULATIONS.
           IF HIGH-CREDIT > POFF-NETDUE
              COMPUTE AVCR-CURRENT-AVAIL =
                              HIGH-CREDIT - AVCR-POFF-NETDUE.

      * WORLD PR# 734 "IL" RESTRICTION
      * DISPLAY "R" RESTRICTED IF LOANDATE < 75 DAYS FROM TRANS DATE
      * 9/12/2011 PR# 767 DONT TEST ON A PAIDOUT, ONLY RESTRICT OPEN ACCOUNTS
           IF SP-RENEWAL-EDIT = "A" AND LN-POFFDATE = 0
              MOVE LN-LOANDATE TO NUM-DATE
              MOVE AVCR-PAYDATE TO SYS-DATE
              PERFORM TIM
              IF ELAPSED-DAYS < 75
                 MOVE 0 TO AVCR-AVAILCREDIT AVCR-CURRENT-AVAIL
                 MOVE "R" TO AVCR-RESTRICT-FG.

      * 11/25/2013 PR# 944 WORLD
      *    TEST "IL" FIRST, WHICH IS ABOVE, DO THIS BEFORE THE "GA" TEST,
      *          WHICH IS BELOW.
      * COMPANY/PB PAYOFF, COMPUTE AVAILABLE CREDIT BY SUBTRACTING THE
      * PAYOFF AMOUNT FROM THE HIGH CREDIT AND TEST THE NEW PERCENTAGE
      * IN THE SPR
           IF SP-CASH-AVAIL-PERCENT NOT NUMERIC OR
                SP-CASH-AVAIL-PERCENT = 0
              NEXT SENTENCE
           ELSE
      * SPR PERCENT HAS A VALUE IN IT
              IF AVCR-AVAILCREDIT NOT >
                     (AVCR-POFF-NETDUE * (SP-CASH-AVAIL-PERCENT / 100))
                 MOVE 0 TO AVCR-AVAILCREDIT AVCR-CURRENT-AVAIL.

      * 10/10/2016 PR#1194 WORLD
      * 10/27/2016 PR#1201 WORLD - CHANGE HIGH-CREDIT TO LN-CREDITLIM
      * NEW POLICY WILL REQUIRE THAT THE CUSTOMER HAVE AT LEAST 50% OF THE
      * REGULAR PAYMENT AMT AVAILABLE IN ORDER TO DISPLAY CASH AVAILABLE
           IF SP-PERCENT-REGPAY-AVAIL NOT NUMERIC OR
                SP-PERCENT-REGPAY-AVAIL = 0
              NEXT SENTENCE
           ELSE
      * SPR PERCENT HAS A VALUE IN IT
              IF (LN-CREDITLIM - AVCR-POFF-NETDUE) < 
                       (LN-REGPYAMT * (SP-PERCENT-REGPAY-AVAIL / 100))
                  MOVE 0 TO AVCR-AVAILCREDIT AVCR-CURRENT-AVAIL.

      * CHECK FOR THE SOUTH CAROLINA SERVICE CHARGE FRMLA 18 RESTRICTION
      * IF WE WOULD REJECT A RENEWAL, DONT DISPLAY AVAIL CREDIT
      * IF CASH AVAILBLE IS < 10% OF LOANS AMOUNT FINANCED, RESTRICT IT
      * NOT ALLOWED TO RENEW AN ACCOUNT FOR A CASH ADVANCE LESS THAN 10%
      * MORE THAN ONCE IN 15 MONTHS.
           IF SP-SCFRMLA = 18
              PERFORM LOAN-CALCULATIONS
              IF AVCR-AVAILCREDIT < (FINANCED-AMOUNT * .1)
                 IF (BW-LAST-RN-FOR10-DATE NOT = 0)
                    MOVE BW-LAST-RN-FOR10-DATE TO NUM-DATE
                    MOVE AVCR-PAYDATE          TO SYS-DATE
                    PERFORM TIM360
                    IF ELAPSED-MONTHS < 15
                       MOVE 0 TO AVCR-AVAILCREDIT AVCR-CURRENT-AVAIL.

      **************************************************************
      * TEST FOR RENEWAL RESTRICTIONS:
      *      N O T E:
      *              THIS LOGIC IS TAKEN FROM CALCZ3.C (ZAA-CTU-SETUP)
      *              IF YOU CHANGE THIS CHANGE CALCZ3.C.

      * GEORGIA:
           IF NOT (SP-SCFRMLA = 12 OR 13 OR 54)
              GO TO COMPUTE-AVAILABLE-CREDIT-EXIT.

           MOVE AVCR-POFF-NETDUE TO AVCR-WORKER.

      * TEST FOR GEORGIA SC REQUIREMENT FOR PAID AHEAD MONIES:
           MOVE LN-1STPYDATE TO CEPP1-DATE.
           MOVE AVCR-PAYDATE TO CEPP2-DATE.
           MOVE 1 TO CEPP-MAKERCD.
           PERFORM TOTAL-DELINQUENCY.
           IF CDEL-ARREARAGE > 0
              ADD CDEL-ARREARAGE TO AVCR-WORKER.

           MOVE LN-LOANDATE  TO NUM-DATE.
           MOVE AVCR-PAYDATE TO SYS-DATE.
           MOVE SP-YEARTYPE  TO ELAPSED-YRTYPE.
           PERFORM TIMALL.

      *   ----------------------------------------------------
      *   TEST FOR NEW GILA LAW
      *   IF LOAN UNDER CONSIDERATION FOR RESTRICTION WAS MADE
      *   ON OR PRIOR TO 041301 USE $200.00 ELSE $300.00
      *   ----------------------------------------------------
           IF LN-LOANDATE NOT > 20010413
              MOVE 200.00 TO AVCR-WORKER2
           ELSE
              MOVE 300.00 TO AVCR-WORKER2.
           IF AVCR-WORKER <= AVCR-WORKER2
              IF ELAPSED-DAYS < 61
                 MOVE "R" TO AVCR-RESTRICT-FG
              ELSE
                 GO TO COMPUTE-AVAILABLE-CREDIT-EXIT
           ELSE
              IF ELAPSED-DAYS < 181
                 MOVE "R" TO AVCR-RESTRICT-FG
                 GO TO COMPUTE-AVAILABLE-CREDIT-EXIT.

       COMPUTE-AVAILABLE-CREDIT-EXIT.
           MOVE " " TO AVCR-POFF-FG.
