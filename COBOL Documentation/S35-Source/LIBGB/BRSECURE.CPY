      * COPYMEMBER: LIBGB/BRSECURE
      *******************************************************************
      *                 LIBGB/BRSECURE
      *
      * CHECK SECURITY OF BRANCH # RANGE OR GROUP FOR REPORTS
      * ASSUMPTIONS:  REPORT HAS GROUP LOGIC, BRANCH RANGES
      *          ARE IN BEG-BRANCH AND END-BRANCH
      *          NEED BR-SECURITY-OKAY & CASE-BUF IN WORKING STORAGE
      *          NEED LIBGB/PASSWDW, LIBGB/UPPER.CMD
      * REV:
      *  MJD 021998 ADDED BRSECURE WORKERS FOR CALLING OF PASSWD.
      *             THIS WILL ALLOW PROGRAMS TO CHANGES ID AND SEQNO
      *             WITHOUT CHANGING BRSECURE COPY MEMBER.
      *  BAH 010409 REMOVED PARAGRAPH NAME CHECK-BRANCH-SECURITY, IT WAS
      *             A DUPLICATE NAME, AND WAS NOT NEEDED !!
      *  BAH 140516 REMOVED ALL MOVES TO EXT-CONAME-USER-GROUP, NEEDS TO
      *             USE WGR-WORD, DO NOT SET EXTERNAL VARIABLE!
      *  BLM 140520 FIX PROBLEM IN SEARCH-GR-INIT, MOVE CASE-BUF TO GR-ID
      *             SINCE EXT-CONAME-USER-GROUP IS LOWER CASE
      *  BLM 140520 CHANGE TO NOT USER UPPER_CMD.CPY ROUTINE ANYMORE,
      *             JUST MOVE USER GROUP TO WGR-WORD OR GR-ID & CALL
      *             C$TOUPPER
      *******************************************************************
       BR-RANGE-SECURITY SECTION.
       CHECK-RANGE-SECURITY.
           MOVE "N" TO BR-SECURITY-OKAY.

           IF  GROUP-FLAG = "Y"
               GO TO CHECK-GROUP-SECURITY.

           IF BEG-BRANCH NOT = END-BRANCH
              GO TO PASSWORD-REQUIRED-BR.

      * IF ONE BRANCH SELECTED & IT'S = LOGIN BRANCH, OKAY
           IF ( (BEG-BRANCH = 0) AND (END-BRANCH = 0) )
              MOVE "Y" TO BR-SECURITY-OKAY
              GO TO EXIT-RANGE-SECURITY.

           IF BEG-BRANCH = EXT-CONAME-USER-LOC
              MOVE "Y" TO BR-SECURITY-OKAY
              GO TO EXIT-RANGE-SECURITY.

           MOVE EXT-CONAME-USER-GROUP TO WGR-WORD.
           CALL "C$TOUPPER" USING WGR-WORD, VALUE 3.
           PERFORM CHECK-MEMBER.
      * IF LAST 3 CHARACTERS OF LOGIN NOT A VALID GROUP, ENTER PASSWORD
           IF WGR-ERR > 0
              GO TO PASSWORD-REQUIRED-BR.

           PERFORM OPEN-GR1-FILE.
           MOVE EXT-CONAME-USER-GROUP TO GR-ID.
           CALL "C$TOUPPER" USING GR-ID, VALUE 3.
           PERFORM SEARCH-GR-INIT.
           PERFORM CLOSE-GR1-FILE.

           MOVE BEG-BRANCH TO WGR-SRESULT.
           PERFORM SEARCH-GR-VERIFY.
      * 1 BRANCH RANGE, LOGIN HAS VALID GROUP; IS BRANCH IN THAT GROUP?
           IF WGR-ERR > 0
              GO TO PASSWORD-REQUIRED-BR.

           MOVE "Y" TO BR-SECURITY-OKAY.
           GO TO EXIT-RANGE-SECURITY.

       PASSWORD-REQUIRED-BR.
           PERFORM VERIFY-BR-PASSWORD.
           IF PASSWDW-STAT = "OK"
              MOVE "Y" TO BR-SECURITY-OKAY.

           GO TO EXIT-RANGE-SECURITY.

       CHECK-GROUP-SECURITY.
           MOVE EXT-CONAME-USER-GROUP TO WGR-WORD.
           CALL "C$TOUPPER" USING WGR-WORD, VALUE 3.
      * IF GROUP SELECTED & IT'S = LOGIN GROUP, OKAY
           IF WGR-WORD = ENT-GROUP
              MOVE "Y" TO BR-SECURITY-OKAY
              GO TO EXIT-RANGE-SECURITY.
           PERFORM CHECK-MEMBER.
      * IF LAST 3 CHARACTERS OF LOGIN NOT A VALID GROUP, ENTER PASSWORD
           IF WGR-ERR > 0
              GO TO PASSWORD-REQUIRED.

           PERFORM OPEN-GR1-FILE.
           MOVE EXT-CONAME-USER-GROUP TO GR-ID.
           CALL "C$TOUPPER" USING GR-ID, VALUE 3.
           PERFORM SEARCH-GR-INIT.
           PERFORM CLOSE-GR1-FILE.

           MOVE ENT-GROUP TO WGR-TEST-GROUP.
           PERFORM VERIFY-GROUP-MEMBER.
      * GROUP RANGE, LOGIN HAS VALID GROUP; IS GROUP WITHIN THAT GROUP?
           IF WGR-ERR > 0
              GO TO PASSWORD-REQUIRED.
           MOVE "Y" TO BR-SECURITY-OKAY.
           GO TO EXIT-RANGE-SECURITY.

       PASSWORD-REQUIRED.
           PERFORM VERIFY-BR-PASSWORD.
           IF PASSWDW-STAT = "OK"
              MOVE "Y" TO BR-SECURITY-OKAY.

       EXIT-RANGE-SECURITY.
           IF BR-SECURITY-OKAY = "N" MOVE
           "YOU ARE NOT AUTHORIZED TO REPORT ON THESE BRANCHES."
                                                TO MESS
              PERFORM SEND-MESS.

      ***************************************************
      *   ENTER PASSWORD TO VIEW BRANCH RANGE OR GROUP
      ***************************************************
       VERIFY-BR-PASSWORD SECTION.
      *--->MOVE " " TO PASSWDW-STAT.
      *    MOVE "ALLBR" TO PASSWDW-PGM.
      *    MOVE 0 TO PASSWDW-SEQ.
      *    MOVE "PLEASE ENTER THE PASSWORD " TO PASSWDW-PROMPT1.
      *--->MOVE "TO REPORT ON THESE BRANCHES:" TO PASSWDW-PROMPT2.
           MOVE BRSECURE-REC-STAT TO PASSWDW-STAT.
           MOVE BRSECURE-REC-PGM TO PASSWDW-PGM.
           MOVE BRSECURE-REC-SEQ TO PASSWDW-SEQ.
           MOVE BRSECURE-REC-PROMPT1 TO PASSWDW-PROMPT1.
           MOVE BRSECURE-REC-PROMPT2 TO PASSWDW-PROMPT2.
           MOVE "CL/PASSWD" TO FORM-NAM.
           CALL FORM-PROGX USING FORM-PATH PASSWDW-BUF.
           CANCEL FORM-PROGX.

