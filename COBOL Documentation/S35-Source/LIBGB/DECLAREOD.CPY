      * COPYMEMBER: LIBGB/DECLAREOD
      *  BAH 130312 CHANGED CALL SUSP TO CALL "C$SLEEP"
      *  BAH 130320 UNCOMMENTED ALARM, SHOULD WORKING USING BEEP
      *  BLM 130530 FIX RESET-MESS
      *  BLM 130605 REMOVE RESET-MESS ROUTINE, PUT IN LIBGB/SCREEN
      *  BLM 131031 STARRED OFF TMRSP
      *  BLM 140507 CHANGED TO USE DISPLAY BOX FOR SEND-MESS INSTEAD
      *             OF CALLING GB/SENDME, VERYANT
      *  BAH 140521 COMMENTED OUT ALARM, CAUSING SCREEN SHIFTS
      * BAH 190207 ADDED IO-MULTI-FG FOR USE WITH FILES THAT HAVE BEEN
      *
      * JKC 2020-0217 ADDED LOGIC TO GET ACU4GL ERRORS IN ORDER TO
      *               DISPLAY THEM; MORE DETAILS FROM ACU4GL.
      * BAH 2020-0911 WAS DISPLAYING READ ERROR, STAT = 99 WITH THE PROGRAM
      *               NAME, CHANGED TO DISPLAY "RECORD LOCKED BY ANOTHER USER"
      *               AND THE KEY, LIKE A15 DID.
      *
      * JKC 2021-0512 ADDED SQL-IO-VALIDATION ROUTINE
      * JKC 2022-0222 IN SQL-IO-VALIDATION ROUTINE ADDED A CHECK TO SEE
      *               IF THE SQL CONNECTION (EXT-ACUSQL-CONNECT-STAT)
      *               IS OPEN (GOOD) THEN `PERFORM CLOSE-FILES`;
      *               NEED TO ONLY CLOSE FILES/TABLES WHEN THERE IS
      *               AN SQL CONNECTION OTHERWISE YOU CAN GET INTO
      *               AN INIFITE LOOP DUE TO THE SQL-DISCONNECT IN
      *               SQL-ERROR WHICH THEN YOU CANNOT CLOSE CURSORS
      *               IF THE CONNECTION HAS BEEN CLOSED.
      *
      * JKC 2023-0901 ADDED CALL TO CL/ACUMEM FOR OTIS MEMORY USAGE LOG
      * JKC 2023-0929 ADDED COUNT-IT PARAGRAPH FOR SQL ROW COUNT IO
      * JKC 2023-1201 ADDED CHECK OF "ROW " IN SQL-IO-VALIDATION
      * JKC 2024-0124 ADDED OTIS TRACE LOGGING LOGIC
      * JKC 2024-0304 REMOVED ACU4GL ERROR DISPLAY LOGIC
      *
      * JKC 2024-0501 ADDED USE OF EXT-API-SCREEN-DISABLE AND
      *         EXT-API-MESS FOR SUPPRESS SCREEN INTERACTIONS FOR
      *         USE IN API PROGRAMS WITHOUT SCREENS.
      *
      ******************************************************************
      *            SPLIT SO WE CAN IDENTIFY WHICH FILE HAD BAD I/O
       CHECK-ERROR-TYPE.
           IF E-MSG4 = "STAR"
              GO TO END-DECLARE.

           MOVE SPACES TO IO-MULTI-FG.
           MOVE 9      TO IO-FG.

           IF FILE-STAT = "93"
              IF LOCK-STATUS-93-RETRIES NOT = ZERO
                 MOVE 8 TO IO-FG
                 PERFORM SUSPEND
                 SUBTRACT 1 FROM LOCK-STATUS-93-RETRIES
                 GO TO END-DECLARE
              ELSE
                 MOVE FILE-STAT TO E-STATUS-ERROR
                 MOVE "FILE(S) EXCLUSIVELY RESERVED" TO MESS
                 MOVE E-FILE TO E-FILEX
                 MOVE FORM-PATHNAME TO E-PROG
                 MOVE ERROR-MSG-HEAD TO ERRMSG-HEAD
                 MOVE ERROR-MSG-STAT TO ERRMSG-MSG1
                 MOVE ERROR-MSG-PATH TO ERRMSG-MSG2
                 MOVE SPACES         TO ERRMSG-MSG3
                 PERFORM CALL-ERRMSG
                 MOVE "E" TO ERRCD
                 PERFORM CLOSE-FILES
                 GO TO EXIT-PROG.
           IF FILE-STAT = "99"
              MOVE 8 TO IO-FG
              PERFORM SUSPEND
              ADD 1 TO LOCK-STATUS-99-RETRIES
              IF LOCK-STATUS-99-RETRIES = 10
                 MOVE 0 TO LOCK-STATUS-99-RETRIES
                 MOVE FILE-STAT TO E-STATUS-ERROR
                 MOVE E-FILE TO E-FILEX
                 MOVE FORM-PATHNAME TO E-PROG
                 MOVE ERROR-MSG-HEAD TO ERRMSG-HEAD
                 MOVE "RECORD LOCKED BY ANOTHER USER" TO ERRMSG-MSG1
                 MOVE ERROR-MSG-PATH TO ERRMSG-MSG2
                 MOVE ERROR-MSG-KEY  TO ERRMSG-MSG3
                 PERFORM CALL-ERRMSG.

           CALL "C$RERR" USING EXTENDED-FILE-STAT.
           MOVE FILE-STAT TO FILE-STAT-DISP.
           IF SECONDARY-FILE-STAT NOT = SPACES
              MOVE "," TO FILE-STATC
              MOVE SECONDARY-FILE-STAT TO FILE-STATD2.
           MOVE FILE-STAT-DISP TO E-STATUS-ERROR.
           IF FILE-STAT = "41"
              MOVE 7 TO IO-FG.
       CHECK-IO9.
           IF IO-FG NOT = 9
              GO TO END-DECLARE.
           IF E-MSG4 = "OPEN" OR E-MSG4 = "REWR" OR
              E-MSG4 = "DELE" OR FILE-STAT = "30"
              GO TO FILE-ERRORS.
           GO TO END-DECLARE.
       OPEN-IT.
           MOVE "OPEN ERROR" TO E-MSG.
           MOVE SPACES TO E-KEYX.
           IF IO-FG NOT = 8 MOVE 0 TO LOCK-STATUS-99-RETRIES.
           MOVE 0 TO IO-FG.
       COUNT-IT.
           MOVE "ROW COUNT ERROR" TO E-MSG.
           IF IO-FG NOT = 8 MOVE 0 TO LOCK-STATUS-99-RETRIES.
           MOVE 0 TO IO-FG IO-STATUS.
       READ-IT.
           MOVE "READ ERROR" TO E-MSG.
           IF IO-FG NOT = 8 MOVE 0 TO LOCK-STATUS-99-RETRIES.
           MOVE 0 TO IO-FG.
       WRITE-IT.
           MOVE "WRITE ERROR" TO E-MSG.
           IF IO-FG NOT = 8 MOVE 0 TO LOCK-STATUS-99-RETRIES.
           MOVE 0 TO IO-FG.
       REWRITE-IT.
           MOVE "REWRITE ERROR" TO E-MSG.
           IF IO-FG NOT = 8 MOVE 0 TO LOCK-STATUS-99-RETRIES.
           MOVE 0 TO IO-FG.
       DELETE-IT.
           MOVE "DELETE ERROR" TO E-MSG.
           IF IO-FG NOT = 8 MOVE 0 TO LOCK-STATUS-99-RETRIES.
           MOVE 0 TO IO-FG.
       START-IT.
           MOVE "START ERROR" TO E-MSG.
           IF IO-FG NOT = 8 MOVE 0 TO LOCK-STATUS-99-RETRIES.
           MOVE 0 TO IO-FG.
       CLOSE-IT.
           MOVE "CLOSE ERROR" TO E-MSG.
       FORM-IT.
           MOVE "FORM ERROR" TO E-MSG.
           MOVE STAT TO E-STATUS-ERROR.
           MOVE SPACES TO E-KEYX.
       SUSPEND.
           CALL "C$SLEEP" USING 1.
      *    CALL SUSP.
       ALARM.
      *    DISPLAY OMITTED WITH BEEP.
       FILE-ERRORS.
           MOVE E-FILE TO E-FILEX ERRLOGW-FILE.
           MOVE FORM-PATHNAME TO E-PROG ERRLOGW-PROGX.
           MOVE E-MSG TO ERRLOGW-TYPE.
           MOVE E-CODE TO ERRLOGW-CODE.
           MOVE E-SUBCODE TO ERRLOGW-SUBCODE.
           MOVE E-KEYX TO ERRLOGW-KEY.
           MOVE "E" TO ERRLOGW-ACTION.
           MOVE FORM-PATHNAME TO FORM-PATH.
           MOVE "CL/ERRLOG" TO FORM-NAM.
           CALL FORM-PROGX USING FORM-PATH ERRLOGW-AREA EXIT-PATHNAME.
           CANCEL FORM-PROGX.
           MOVE FORM-PATHNAME TO FORM-PATH.
           PERFORM ALARM.
           MOVE SPACES TO MESS.
           MOVE ERROR-MSG-HEAD TO ERRMSG-HEAD
           MOVE ERROR-MSG-STAT TO ERRMSG-MSG1
           MOVE ERROR-MSG-PATH TO ERRMSG-MSG2
           MOVE SPACES         TO ERRMSG-MSG3
           PERFORM CALL-ERRMSG
           MOVE SPACES TO MESS.
       FILE-ERRORS-TERMINATE.
      *    IF WINDOWS-OPEN > ZERO
      *       CALL DELWIN
      *       SUBTRACT 1 FROM WINDOWS-OPEN
      *       GO TO FILE-ERRORS-TERMINATE.
           MOVE "E" TO ERRCD.
           PERFORM CLOSE-FILES.
           GO TO EXIT-PROG.
       SEND-MESS.
           IF ( EXT-API-SCREEN-DISABLE = "Y" )
              MOVE MESS TO EXT-API-MESS
           ELSE
              PERFORM ALARM
              DISPLAY MESSAGE BOX MESS.
      *    MOVE MESS TO SENDMESS-MESS.
      * HAD TO ADD THE SAVE-FORM-PATH LIKE BARB DID FOR CHKSEC
      * WHEN RETURNING FROM A SECURITY ERROR, AFTER DISPLAYING THE
      * ERROR MESSAGE, IT WENT BACK TO LOADIT, AND ALL IT KNEW WAS
      * UT/SENDME AND LOST THE PATH OF WHERE IT WAS GOING BEFORE THE
      * ERROR...
      *    MOVE FORM-PATH TO SAVE-FORM-PATH.
      *    MOVE FORM-PATHNAME TO FORM-PATH.
      *    MOVE "GB/SENDME" TO FORM-NAM.
      *    CALL FORM-PROGX USING SENDMESS-BUF.
      *    CANCEL FORM-PROGX.
      *    MOVE SAVE-FORM-PATH TO FORM-PATH.
      *    CALL TMMSG USING MESS.
      *    CALL TMRSP USING MESS.
      *    MOVE SENDMESS-STAT TO STAT.
      *    IF STAT EQUAL "~~"
      *       DISPLAY OMITTED ERASE SCREEN
      *       STOP RUN.
      *    MOVE SPACES TO MESS.
      *    CALL TMMSG USING MESS.
       COPY "LIBGB/ERRMSG.CPY".
       COPY "LIBGB/TMMSG.CPY".

      *=================================================================
       SQL-IO-VALIDATION.

      * GOOD
           IF ( E-MSG4  = "OPEN" ) AND 
              ( SQLCODE = 1      )
              EXIT PARAGRAPH
           ELSE
      * GOOD
           IF ( SQLCODE = 0      )
              EXIT PARAGRAPH.

           MOVE 9 TO IO-FG.

      * EOF OR ROW NOT FOUND
           IF (( E-MSG4  = "READ" )  OR 
               ( E-MSG4  = "ROW " )) AND
              (  SQLCODE = 100     )
              EXIT PARAGRAPH.

      * DUPLICATE ROW
           IF ( E-MSG4   = "WRIT"  ) AND 
      * LIKE VISION "22,00"
              ( SQLSTATE = "23000" ) 
              MOVE "22"    TO FILE-STAT
              MOVE "00"    TO SECONDARY-FILE-STAT
              MOVE "22,00" TO FILE-STAT-DISP
                              E-STATUS-ERROR
              EXIT PARAGRAPH.

           MOVE E-FILE        TO E-FILEX ERRLOGW-FILE.
           MOVE FORM-PATHNAME TO E-PROG ERRLOGW-PROGX.
           MOVE E-MSG         TO ERRLOGW-TYPE.
           MOVE E-CODE        TO ERRLOGW-CODE.
           MOVE E-SUBCODE     TO ERRLOGW-SUBCODE.
           MOVE E-KEYX        TO ERRLOGW-KEY.
           MOVE "E"           TO ERRLOGW-ACTION.
           MOVE FORM-PATHNAME TO FORM-PATH.
           MOVE "CL/ERRSQL"   TO FORM-NAM.
           CALL FORM-PROGX USING FORM-PATH ERRLOGW-AREA
                                 SQLCA EXIT-PATHNAME.
           CANCEL FORM-PROGX.
           MOVE FORM-PATHNAME TO FORM-PATH.

           MOVE "E" TO ERRCD.

      * DEFINED IN LIBGB/CONNECT_SQL.CPY
           PERFORM SQL-ERROR.

      * ONLY CLOSE FILES/TABLES WHEN THERE IS AN SQL CONNECTION
      * OTHERWISE YOU CAN GET INTO AN INIFITE LOOP DUE TO THE
      * SQL-DISCONNECT IN SQL-ERROR WHICH THEN YOU CANNOT CLOSE CURSORS
      * IF THE CONNECTION HAS BEEN CLOSED.
      * NOTE: THIS WORKS FOR TABLES; NOT SURE WHAT HAPPENS WITH VISION
      *       WORK FILES, IMPORT & EXPORT EXTRACTION FILES; BELIEVE
      *       THEY SHOULD WORK FINE IF GOING RIGHT BACK INTO THE
      *       PROGRAM (THEORY IS THE CLOSING OF A PROGRAM WILL CLOSE
      *       THE FILES).                           BAH & JKC 2022-0214
      
      * CLOSE VISION FILES
           IF ( EXT-ACUSQL-CONNECT-STAT-GOOD )
              PERFORM CLOSE-FILES.

      * FORCE EXIT OF PROGRAM
           GO TO EXIT-PROG. 

       CLOSE-FILES.

      * OTIS MEMORY USAGE LOG
           IF ( EXT-TCLP-ACUMEM-UPDATE )
              CALL "ACUMEM" USING FORM-PATHNAME 
              CANCEL "ACUMEM".

      * OTIS TRACE USAGE LOG
           IF ( EXT-TCLP-TRACE-UPDATE ) 
              MOVE "PROGRAM FINISH (LIBGB/DECLAREOD)"
                TO EXT-TCLP-TRACE-MESS
              CALL "TRACE" USING FORM-PATHNAME
              CANCEL "TRACE".

           MOVE "CLOSE ERROR" TO E-MSG.
