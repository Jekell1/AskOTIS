      * COPYMEMBER: LIBGB/REBATE
      ***************************************************************
      * ADDON NOTES: SHOULD EST. REB-LN-EXTCHG. REB-LN-SERCHG?
      *              REB-LN-INTCHG?
      *
      * NOTE : EARLY PROVISIONS DO NOT WORK WITH ADDONS!!!
      *          SEE CINDY'S NOTE FROM 10/12/2023
      *
      *
      *          ---REBATE CALCULATIONS
      *
      *   NAME:  REBATE
      *   DESC:  GENERALIZED REBATE CALCULATIONS
      *
      *   W A R N I N G:
      *            THIS PROGRAM CALLS A SUB PROGRAM AND REQUIRES
      *            THAT THE PROGRAM CONTAINING THIS COPY MEMBER
      *            HAVE THE STATEMENT:
      *
      *       =======>  MOVE FORM-PATHNAME TO FORM-PATH <=======
      *
      *---------------------------------------------------------
      *    N O T E:
      *
      *  ------> REB-REMTERM IS NOW AN OUTPUT FIELD, SO
      *          INSURE ITS INTEGRITY AFTER IT IS USED
      *          TO CALCULATED THE REFUND.
      *
      *         NEW FORMULAS DEALING WITH MULTIPLE CHARGE REBATES
      *         EX. INTEREST + SERVICE CHARGE.....DEFERMENTS
      *         MUST REVIEW EARNINGS VERY CARFULLY SINCE
      *         EARNINGS ARE REFLECTED IN COMPONET FORMAT.
      *           (LPEADJ)
      *---------------------------------------------------------
      *
      *   IN  :  REB-PAYDATE    - DATE OF PAYOFF
      *          REB-ORGTERM    - ORIGINAL TERM TO REBATE ON
      *          REB-SUB        - INS/INT TABLE LEVEL IN SP-FILE
      *          REB-TOTCHG     - PREMIUM/INTEREST TO REBATE
      *          REB-LPTRCD     - CURRENT TRANSACTION CODE
      *                           NEEDED FOR SP-RBSPOPT1 = 2
      *                             WITH  TRCD PB, RN, AND SC
      *                           NEEDED FOR SP-RBSPOPT2 = 2
      *                             WITH  TRCD PB AND RN
      *          REB-STATEWK    - INPUT WORK FIELD
      *                           ALWAYS CLEARED AT REBATE-EXIT.
      *                           NEEDED FOR RBFRMLA 'H' (GA)
      *                           INITIALIZED IN LPPOFF TO AMOUNT
      *                           OF INTEREST REBATE
      *          REB-RECAST     - Y - ALLOW RECASTING.
      *          REB-DEF-STOP   - SET TO 'Y' IN LONPFB AND LONPFA
      *                           TO IDENTIFY REBATE IS CALL FROM
      *                           A POSTING ROUTINE
      *   OUT :  REB-REBATE     - REBATE AMT; NEVER < 0
      *          REB-SUB        - = 0 SIGNALS ERROR
      *          REB-PRES-VALUE - USED ONLY BY LPCERN ERNFRMLA = 11
      *          REB-REMTERM    - REMAINING TERM USED IN CALCULATIONS
      *   USED:
      *   COPY:  REBATEW, LPNDTE, LPNDTEW, JUL, JULW
      *          TIMALL, TIM365, TIM360, LPAMTSW, LPAMTS
      *          TIMW, DATEWK, INCRMM
      * REV:
      *  052086 JTG MODIFIED 78THS ANTIC. TO TEST
      *            OVRDRATE 1ST.
      *  061886 JTG ADDED LOGIC FOR SP-RBDEFER CODE
      *  061886 JTG ADDED LOGIC TO FIX SP-RBDET CODES FOR SP-RBFRMLA 3
      *  061886 JTG ADDED LOGIC TO TEST FOR PRIOR TO REFDATE.
      *  112586 JTG ADDED TEST FOR SP-RBDEFER( ) = 2
      *             AND IF SO TO NOT CONSIDER DEFERMENTS
      *             IN THE COMPUTATION OF REBATE.
      *  082487 AMW ADDED REBATE FORMULA 7 FOR ACTUARIAL
      *             SECURITY OF AMERICA
      *  082887 AMW ADDED 'X' LOGIC FOR SP-RBDTE
      *             TO USE INTDATE/PURDATE
      *  120187 JTG MODIFIED ACTUARIAL REBATE FORMULA 2
      *             TO REBATE IN FULL IF PAYOFF PRIOR TO START DATE.
      *             ALSO, ADDED NEW FLAG REB-SKIPDEF-FG SO
      *             EARNINGS ROUTINES COULD DO AN ACTUARIAL
      *             -REBATE WITHOUT DEFERMENTS CONSIDERED
      *  021988 JPN ADDED ROUTINES FOR EARLY REBATE FORMULAS 1 AND 2.
      *  022388 JTG ADDED LOGIC FOR NEW DEDUCT FROM CHARGE CODE, 9999.01
      *  042888 BAW ADDED LOGIC FOR FORMULA 8
      *  042988 JPN ADDED LOGIC FOR SP-RBSPOPT1=2 SP-DEDCHG=9999.02
      *  051688 JPN ADDED LOGIC FOR FORMULA 'A'
      *  062188 JTG ADDED LOGIC FOR FORMULA 'B' AND 'C'
      *  090788 JTG OPENED UP FOR 13 REBATE LEVELS
      *  092188 JTG CHANGED TO PERFORM (COMPUTE INSURANCE TERM)
      *  110188 JTG ADDED LOGIC FOR SP-RBREDUC = 9999.01
      *             AND LOGIC TO CALL SUB PROGRAMS
      *             ADDED LOGIC FOR SP-RBEARLY = 3
      *             ADDED LOGIC FOR SP-RBSPOPT1 = 3
      *  121388 JTG ADDED LOGIC FOR SP-RBMINRET 9999.01 IOWA
      *  011788 JTG ADDED LOGIC FOR FORMULA 'D' IOWA
      *  021089 JTG DISABLED SP-RBDEFER FOR FORMULA 'D' IOWA
      *                 AND  SP-RBNOMON FOR FORMULA 'D'
      *  022889 JTG ADDDED TENNESSEE DEFERMENT REBATE LOGIC
      *  030389 JTG CORRECTED FORMULA "D" WHEN COMPUTED
      *             -REBATE WAS GREATER THAN LN-INTCHG
      *  030689 JTG ADDED SP-RBSPOPT2 = 1, HOLLYWOOD (FL)
      *  041089 JTG ADDED LOGIC FOR SP-RBSPOPT1 = 4
      *  041989 JTG ADDED LOGIC FOR SP-RBFRMLA = F
      *  071289 SLC CORRECTED FORMULA "F" WHEN COMPUTED
      *             -REBATE WAS GREATER THAN LN-INTCHG
      *  072089 JTG CHANGE LOGIC TO USE LN-ORIG-1STPYDATE
      *             AND NOT LN-1STPYDATE
      *  072789 JTG ADDED REBATE FORMULA "G" FOR
      *             IDEAL (MA) AH REBATE
      *  080189 JTG ADDED SP-RBSPOPT1 = 5 FOR SPRING FINANCE
      *  081189 JTG ALLOWED FORMULA F TO USE SWING RULE
      *  090189 JTG ADDED SP-RBSPOPT2 = 3 (GEORGIA)
      *  091489 JTG CHANGED SP-RBSPOPT1 = 5 TO BE A REBATE
      *             REDUCTION AND NOT A DEDUCT FROM CHARGE
      *  092889 JTG FIXED FRMLA 'F', SWING 0 ELAPSED-REM NOT TO TAKE
      *             EXTRA MONTH ELAPSED
      *  100489 JTG FURTHER FIXED SWING RULE TO WORK
      *  102489 JTG ADDED RBFRMLA = H AND I, SERCHG (GA)
      *  110889 JTG ADDED LOGIC FOR SP-RBEARLY = 4 OR 5 (GA)
      *  121589 JTG FIXED BUG FRMLA F (2ND FROM LAST PAY PERIOD)
      *  010890 SLC ADDED RBFRMLA = J, FOR MURDOCK.
      *  011890 JTG ADDED LOGIC FOR SP-RBMIN(7) = 999.01 (GA)
      *  021690 JTG ALLOWED SP-RBDEF FOR DEFERMENT REBATES
      *  041790 JTG LET ACTUARIAL-F TREAT SINGLE PAY LOANS
      *             AS IF NOT COMPOUNDING TAKES PLACE
      *  042690 JTG ADDED LOGIC FOR SP-RBEARLY = 7 & 8, PACESETTER
      *  051090 JTG ALLOWED USRULE, FOR ACTUARIAL-F
      *  052990 JTG ADDED RBFRMLA = K (PACESETTER DEFERMENT)
      *  080990 JTG CHANGED ROUTINE TO WORK FOR ADDONS
      *  090690 BAH ADDED RBFRMLA = M (MULLEN DEFERMENT)
      *  113090 JTG TOOK OUT LOGIC FRO DATE-ROUTINE (D-DATE)
      *  21090 SLC ADDED REB-RECAST TO INDICATE IF WE ARE TO RECAST.
      *             ALSO ADDED CALL TO REBC01 (RECAST).
      *  011091 JTG CHANGED SPOPT2 = 5 FROM 30 TO 15 DAYS
      *  011591 JTG CHANGED TEST ON REB-ADDON-FG = "N"
      *                   TO ONLY LOOK AT ACCOUNTS WITH ADDONS
      *  011791 JTG FURTHER CORRECTION FOR ABOVE CHANGE
      *  011891 JTG STOPPED SP-RBMINRET TEST ON ADDON RECORDS
      *  011591 JTG CHANGED TEST ON REB-ADDON-FG = "N"
      *                   AND NO CHANGE IN FINCHG OCCURED
      *  032791 JTG CORRECTED SP-RBMINRET TEST ON ADDON RECORDS
      *  052191 JTG CHANGED SP-RBEARLY = 8 (PACESETTER) 78 CALC
      *  090391 JTG ADDED SP-RBFRMLA = "N", PACESETTER
      *             CORRECTED PROBLEM FOR AM. THRIFT SINCE THEY
      *             BOTH USE SP-RBEARLY = 8
      *  100491 JTG CORRECTED TRISTATE FORMULA 'D'
      *  110191 JTG CORRECTED MISSISSIPPI INT REBATE
      *             EXTCHG ADDED INTO REB-TOTCHG TWICE
      *  123091 JTG ADDED REB-PRES-VALUE (EDDIE GOLDSTICKER)
      *  041592 JTG CHANGED TO PRORATA REFUND IF 'PB' OR 'RN'
      *             RE: SP-RBSPOPT = 7 (MULLEN)
      *  071392 JTG ADDED SP-RBFRMLA = 'O' & SET OF B01-FRMLA,
      *             TENN DEF REBATE, COLONIAL
      *  071792 JTG ADDED SP-RBFRMLA = 'P' UVALDE TX (AH)
      *  082092 JTG SET B01-DEF-STOP FROM REB-DEF-STOP
      *              FOR COLONIAL, TN DEFERMENT REBATE ROUTINE
      *  110692 JTG CORRECTED PRORATA REFUND IF 'PB' OR 'RN'
      *             RE: SP-RBSPOPT = 7 (MULLEN)
      *  122192 JTG ADDED SP-RBFRMLA = 'Q', VIRGINIA INS. ACTUARIAL
      *  011893 JTG SET B01-DEF-STOP FROM REB-DEF-STOP
      *              FOR CALIFORNIA, DEFERMENT REBATE ROUTINE
      *  012293 JTG ADDED SP-RBSPOPT1 = 10 FOR TIME FINANCE (CA)
      *                   TO BE USED WITH REFUND FORMULA 'L'
      *  021993 JTG ADDED SP-RBDEDCHG = 9999.03 FOR HOME CREDIT
      *  071993 JTG ADDED SP-RBSPOPT1 = 11 FOR MULLEN FINANCE (CA)
      *                   TO BE USED WITH REFUND FORMULA 'L'
      *  090193 JTG INCLUDED REM-REMTERM IN OUTPUT
      *  100793 BAH ADDED SP-RBEARLY = 99, FLAT CANCEL IF WITHIN
      *                                    3 DAYS, WORLD, TN
      *  082294 JTG CHANGED LOGIC FOR SP-RBREDUC = 9999.01 TO TEST
      *             SPECIFICLY FOR 'PB" OR "RN" OR "SC", SO CALLED
      *             PAYOFF WILL WORK
      *  113094 JTG ADDED SP-RBFRMLA = 'R' FOR TIME LOAN, CA AH REFUND
      *  021095 JTG ADDED SP-RBFRMLA = 'S' FOR TIME LOAN, CA CL REFUND
      *  051995 SLC ADDED SP-RBFRMLA = 'U' FOR AH REFUND FOR MERC.
      *  061095 JTG ADDED SP-RBEARLY = 9, FULL REFUND PRIOR TO START
      *             DATE (1ST - 30) MERCURY
      *  061595 JTG ADDED TEST ON BR-NOT-OURS-REFUND, MERCURY
      *  062795 JTG ADDED SP-RBEARLY = 10 (MERCURY) 1/30 78THS BF 1ST
      *  070295 SLC ADDED SP-RBFRMLA = 'V' AND 'W' FOR DEFER REFUND FOR MERC.
      *  070595 JTG CHANGED REBATE-FIND-INS-RATE TO TEST FOR INTERPOLATION
      *             OR NEXT HIGH
      *  092695 BAH ADDED SP-RBEARLY = 11 "MS" (MERCURY), FULL REFUND PRIOR
      *             TO FIRST PAY DATE, REFUNDISPRO RATA TO THE DAY
      *  101695 JTG ADDED SP-RBFRMLA = 'X', MERCURY 'WI' DEFERMENT REFUND
      *  101795 BAH ADDED SP-RBMIN(6) = 999.02, MERCURY 'OK' PP #L022
      *  111095 BAH ADDED SP-RBEARLY = 12 "AL" (MERCURY), PREPAID WITHIN 90
      *             DAYS BY COMPANY, PRO RATA TO THE DAY (SPREAR=1).
      *  111595 BAH ADDED SP-SPOPT1 = 12, MERCURY 'AL' INTEREST REFUND FORMULA
      *             ACTION SPRR78 = 13 RULE OF 78/PRIOR TO 1STPYDATE
      *  111695 BAH ADDED SP-RBFRMLA = "Y", MERCURY 'SC' DEF REFUND FORMULA
      *  111695 BAH ADDED SP-RBFRMLA = "Z", MERCURY 'AZ' DEF REFUND FORMULA
      *  112095 JTG CORRECTED SP-RBEARLY = 11, SO TRCD'S OTHER THAN
      *             'PO, PB, RN, SC' WOULD WORK
      *  112195 JTG ADDED SP-RBEARLY = 13, MERCURY NEVADA ACTION SPFRC=8
      *             FOR CUSTOMER PAYOFF
      *  111295 JTG ADDED SP-RBOPT1 = 13, ACTUARIAL 'F' NEAREST
      *             DUE DATE OPTION MERCURY ARIZONA ACTION 24
      *  112495 JTG ADDED SP-RBEARLY = 13 MERCURY 'NV' PP REFUND ACTION 8
      *  112795 JTG ADDED SP-RBREDUC = 9999.02, 'NV' MERCURY SPRDER = 09
      *  122195 BAH CHANGED FRMLA "U" TO USE IR RATE TABLE (MERCURY-CA)
      *             CALLS CL/IRREBT TO GET THE RATE
      *  011096 JTG ADDED SP-RBFRMLA2 = 1, MERCURY 'AZ' FIRE REFUND (PP)
      *  021296 JTG ADDED SP-RBSPOPT1 = 15, MERCURY 'OREGON' RULE OF
      *             ANTICIPATION
      *  050796 JTG CORRECTED REBATE-ACTUARIAL-F, SP-RBSPOPT1-FIRST-30,
      *             TO SET INTDATE TO NO LESS THAN ORIGINAL INTDATE
      *  051496 JF  INTEGRATED LIBGB/RBACT INTO REBATE-ACTUARIAL-F
      *  060596 JTG CORRECTED BUG IN ACTUARIAL-F WHICH DIDN'T PASS
      *             REB-TOTCHG TO RBACT-TOTCHG
      *  082996 BAH ADDED RBEARLY = 14, SUN, LA FOR ALABAMA
      *  020397 JTG CORRECTED RBFRMLA = 'V' & 'W' TO CORRECTLY COMPUTE
      *             DEFERMENT REFUND WHEN PAYOFF IS AFTER ORIGINAL LOAN
      *             MATURITY, REBATE-MERC-DAILY-PRORATA
      *  020497 JTG SP-RBEARLY = 10, CHANGED TO TEST FOR PAYDATE PRIOR
      *             TO INTEREST START DATE AND SET FULL REFUND
      *  021197 BAH SP-RBEARLY = 15, ALABAMA, SAME AS "MS" 12, EXCEPT 120 DAYS
      *             #L043
      *  021497 BAH ADDED A01-PAYDATE TO PERFORM REBATES INSIDE OF REBA01
      *  051397 JTG ADDED RBEARLY = 16, SUN, ALABAMA, JUST LIKE "MS" 3,
      *             EXCEPT 120 DAYS, PR#443
      *  051397 JTG ADDED SP-RBEARLY = 17, CLAY FINANCIAL SINGLE PAY LOANS ONLY
      *  062597 BAH ADDED SP-RBEARLY 18, 19 AND 20 FOR WORLD, A90-9,10 AND 11
      *  072997 MJD ADDED EARLY PROVISION FOR ACQUISITION CHARGES. REQ#163
      *  092697 JTG CHANGED TO ALLOW ADDON INTEREST REBATE TO WORK RBFRMLA = F
      *  100197 JTG ADDED LOGIC FOR SP-RBMINRET 9999.02 SC, WORLD
      *  020898 JTG CHANGED TO PASS REB-SKIPDEF-FG TO RBACT-SKIPDEF-FG
      *             IN REBATE-ACTUARIAL-F
      *  021098 JTG ADDED TEST OF REB-EARN-FULL-MONTH, TFC #637
      *             RE: SP-EARNACCRL-FG() = 'B'
      *  042798  CS ADDED TEST OF RENEWAL CODES "RB" AND "RO"
      *  102198 BAH ADDED SP-RBEARLY 22 FOR FREEDOM, AL #775, WITHIN 90 DAYS
      *             DO PRORATA, OTHERWISE, NO REFUND
      *  122198 JTG ADDED SP-RBSPOPT1 = 16 OHIO PREPAY PENALTY, REGENCY #001
      *  021299 BAH REMOVED THE CLEARING OF REB-MS-PENALTY ON THE FIRST
      *             LINE OF CODE, DOING A "PO", THE "RS" WIPED OUT THE
      *             PENALTY COMPUTED IN THE "RI", LOANS INC.
      *  021799 BAH ADDED DECIMAL TO REB-HALF-ORGTERM FOR DEDCHG 9999.02
      *  022699 BAH ADDED REB-HALF-ORGTERM-DEC AND DEDCHG 9999.04, AND PUT
      *             9999.02 BACK TO HOW IT WAS PRIOR TO 2/17/99 CHANGE
      *  JTG 990519 ADDED SP-RBFRMLA2 = '3', MEAN RULE 78THS       REGACC   #N/A
      *  BAH 990603 ADDED SP-RBEARLY = 23, WORLD (MO) #206
      *  BAH 990603 ADDED SP-RBSPOPT1 = 17, REGACC PR#875
      *  BAH 991021 ADDED SP-RBDEDCHG = 9999.05, HOME CREDIT, EXACTLY LIKE
      *             9999.03 EXCEPT $30.00 INSTEAD OF $25.00 PR#987
      *  JTG 000104 ADDED SET OF RBACT-PENALTY-FG RE: SP-RBREDUC = 9999.04
      *             WISCONSIN IB REAL ESTATE PREPAY PENALTY, WISCONSIN #1031
      *  JTG 000119 ADDED SP-RBFRMLA2 '4', TN PROPERTY REDUND (SHORT RATE
      *             METHOD) CONSUMER FINANCIAL #1062
      *  JTG 000317 CHANGED LOGIC FOR EP10 TO CARRY INITIAL CALC OF DAILY FACTOR
      *             TO MORE DECIMALS THAN 2 RE: WORLD BR 588 #01-006564
      *             WRITTEN UP BY AUDITORS
      *  JTG 000322 ALLOWED LOGIC FOR EP10 TO WORK FOR SERVICE CHARGE REFUNDS
      *             ALSO, RE: WORLD BR 588 #01-006564 WRITTEN UP BY AUDITORS
      *  BAH 000407 ADDED RBSPOPT1 = 18 MULLEN PR# 1118, IF REPO, RENEWED OR
      *             <= 60 DAYS, REBATE PRORATA
      *  KEC 000523 {PR#00000} MERCURY FINANCE [CINDY SHAFFER];
      *              ADDED 'B02-DEF-STOP' FOR THE PURPOSE OF NOT
      *              ALLOWING THE 'SEND-MESS' WITHIN CL/REBB02.C FROM
      *              HAPPENING SO THAT MERCURY CAN HAVE THEIR CUSTOMER
      *              STATEMENTS TO WORK (NOT STOPPING IN BATCH).
      *  JTG 000626 CORRECTED LOGIC FOR EP10 TO CARRY INITIAL CALC OF DAILY
      *             FACTOR TO MORE DECIMALS THAN 2 RE: WORLD BR 588 #01-006564
      *             WRITTEN UP BY AUDITORS, NO SERCHG REFUND FOR LOANS <= $100
      *  BAH 010413 ADDED SPOPT2 = 9 FOR FULL REFUND UP TO "SAME AS CASH" DATE
      *             LENDMARK PR# 1443
      *  JTG 010503 NEW CODE 'B' FOR SP-RBUNITPER-CD(OCCURS 16) PAACO PR#1466
      *  JTG 010706 ADDED SET OF REB-EARN-FULL-MONTH             REGENCY PR#1456
      *             RE: SP-ERNACCRL-FG() 'C'
      *  BAH 010910 ADDED RBSPOPT2 = 10, FULL REFUND WITHIN 60 DAYS, REGACC
      *             PR# 1550
      *  BAH 010928 RBREDUC = 9999.01, "MS" STATE BANK DEPT SAID WHENEVER
      *             THERE ARE REMAINING DAYS, BUMP INTO THE NEXT MONTH,
      *             WHICH COULD CAUSE A DIFFERENT PERCENT !!, PL# 334 LOANS
      *  JTG 011026 CHANGED RBACT-APRATE TO RBACT-RATE FOR NEW REFUND FORMULA
      *             '0 6'  AND CHANGED TO SET REB-LN-SMPRATE    REGENCY PR#1631
      *  JTG 011102 NEW DEFERMENT REFUND SP-RBFRMLA2 = 5 SMALL LOANS
      *                                                           CITIZENS #1622
      *  JTG 011107 PLUS NEW SP-RBFRMLA (0 7) CL & AH REFUNDS     MULLEN PR#1641
      *  JTG 020219 ADDED "CT" COUNTY TAX CODE KENTUCKY AND PERCENTS
      *             ADDED "CY" CITY   TAX CODE KENTUCKY AND PERCENTS
      *             ADDED LOGIC FOR INS-TAX                           WORLD #288
      *  JTG 020402 CHANGED INS-TAX TO INS-TAX-RB                     WORLD #288
      *  JTG 020930 ADDED NEW SHORT RATE FORMULA SP-RBEARLY = 24 (PP)    TFC #96
      *  JTG 030305 ALLOW RBFRMLA "8" TO WORK FOR A&H AND ALL INSURANCES
      *                                                               PACE #2012
      *  JTG 030513 ADDED SP-RBFRMLA2 (0 8) WISCONSIN CPI REFUND FORMULA
      *                                                           CITIZENS #2065
      *  JTG 030709 NEW INTEREST REFUND FORMULA (0 9) STATE OF TEXAS BASED
      *             ON DAILY INTEREST USING 365 DAYS AND SIMPLE      EXCEL #2111
      *  JTG 030717 CORRECTED INTEREST REFUND FORMULA (0 9) STATE OF TEXAS BASED
      *             ON DAILY INTEREST USING 365 DAYS AND SIMPLE
      *             STOPPED AMTFIN FROM GOING NEGITIVE               EXCEL #2111
      *  JTG 030728 NEW INSURANCE REFUND 2ND FORMULAS:
      *             (0 A) MEAN OF PRORATA & RULE 78
      *             (0 B) RULE 78 FOR NET + 1                        WORLD #326
      *  JTG 040603 MODIFY SP-RBSPOPT2 = 8, DO NOT SWING IF INS. COMPANY
      *             = 75, AND ROUND REFUND TO WHOLE DOLLARS         REGACC #0054
      *  JTG 040608 ADDED NEW SP-RBFRMLA (0 C) SHORT RATE FORMULA   MULLEN #2302
      *  BAH 040623 ADDED RBSPOPT1 = 21, THRIFT INVESTMENT PR# 2334
      *  JTG 040624 CHANGED 'REBATE-RBEARLY-21' TO CONSIDER A DEDUCT FROM CHARGE
      *             FOR COLORADO SERVICE CHARGE                       WORLD #353
      *  BAH 040630 REMOVED THE REGACC #0054 CHANGE DONE ON 040603 BY JTG
      *  JTG 040917 ADDED NEW FIELDS FOR SP-RBFRMLA (0 D)
      *             RULE 78THS USING DAYS IN TERM AND ELAPSED DAYS INSTEAD
      *             OF UNIT PERIODS IN TERM AND ELAPSE UNIT PERIODS
      *             ADDED REB-LN-INSEFF & REB-LN-INSEXP
      *             ALSO ADDED SP-RBSPOPT2 = 11 NEW ROUNDING OPTION TO
      *             TAKE COMPUTED REBATE AND ROUNDED UP OR DOWN TO
      *             WHOLE DOLLARS   ADDED 'REB-REBATE-CENTS'        REGACC #0074
      *  JTG 041102 ADDED ALAMAMA REFUND FRMLA ACTUARIAL (0 E)      WORLD  #J050
      *  JTG 041201 ALLOWED FORMULA (0 D) TO TEST FOR SWING OF 1 MONTH
      *             VIA ADD DAYS SP-RBDAYS(REB-SUB,1) FOR PRIOR TO
      *             FIRST PAYMENT                             REGACC #0074 #0454
      *  JTG 041220 ALLOWED FORMULA (0 D) TO TEST FOR SWING OF 1 MONTH
      *             VIA ADD DAYS SP-RBDAYS(REB-SUB,1) FOR PRIOR TO
      *             FIRST PAYMENT, THAN DO A FULL FULL REFUND REGACC #0074 #0454
      *             WHEN SPR FIELD SP-RBDAYS(REB-SUB,1) IS CODED FOR SWING
      *             AND ELAPSED DAYS PRIOR TO 1ST PAYMENT
      *             IS LESS THAN OR EQUAL TO SP-RBDAYS(REB-SUB,1) (NO SWING)
      *             FORCE A FLAT CANCEL, FULL REFUND
      *             NOTE: IF A SWING TAKES PLACE REB-ELAPSED-MONTHS WILL = 1
      *
      *             ---- TO  N O T  CAUSE A SWING LIKE IN THE FOLLOWING EX:
      *                  SPR 1STPAY YEAR TYPE SHOULD BE '360' NOT '365' ETC
      *
      *                                                360      365
      *                                 ------  ---      ---
      *                EFFECTIVE     08/29/04  DAYS     29       30
      *                REBATE DATE   09/28/04  MONTHS    0        1
      *                                        REM      29        0
      *                                               NO SWING  WILL SWING
      *                                                       REGACC #0074 #0454
      *  JTG 050210 CORRECTED LOGIC FOR REBATES AFTER INSURANCE EXPIRATION
      *             WHICH WAS CAUSING NEGATIVE DAYS-IN-FORCE AND
      *             YIELDED A POSITIVE REFUND   ---- WRONG -----
      *             FORMULA (0 D)                             REGACC #0074 #0454
      *  CS  050323 CHANGED REBATE-TX-INTEREST 'RI' TO 1ST PAYMENT DUE DATE
      *             WE WERE COMPUTING DAILY INTEREST FOR ELAPSED DAYS & THEN
      *             MULTIPLY BY -1 (?).  WHEN COMPUTING THE REBATE...
      *             (1.50 + LN-INTCHG + LN-EXTCHG - CALCULATED INTEREST) CALC
      *             INTEREST IS NEGATIVE SO IT ENDED UP ADDING TO REB-REBATE
      *FOR EX:      LOAN DATE   11/13/01
      *             1ST DUE     12/20/01
      *             'RI' DATE   12/20/01
      *             INTEREST    437.38
      *             EXTCHG         8.03
      *             CALCULATED INT  38.87
      *    WAS:
      *         (1.50) + 437.38 + 8.03 - (-38.87) SO REBATE 485.78 IS > INTCHG
      *    IT NOW DOES:
      *             (1.50) + 437.38 + 8.03 - 38.87 ( CALC INTEREST)= 408.04
      *                                                   DAVE SINCLAIR
      *  BAH 050405 ADDED SP-RBSPOPT1 = 22, SUN "LA" PR# 2504
      *             IF PAID BY "PB" OR "RN" REFUND RULE OF 78TH'S, IF PAID
      *             OFF WITH "PO", NO REFUND
      *  JTG 050418 NEW REFUND FORMULA '0 F' TRUE DAILY PRORATA REFUND FORMULA
      *             FOR STATE OF COLORADO                            WORLD #0401
      *  BAH 050518 STORE THE PENALTY IN REB-MS-PENALTY FOR THRIFT, MESSING
      *             UP DEFERMENT AMOUNT WITHOUT IT..., PL# 493
      *   CS 050523 NEW REBATE FORUMULA 2 (SP-RBFRMLA2 = H) SAME AS TEXAS 0 9
      *             EXCEPT USES THE SPLIT RATE TABLE ON PG 4 OF SPR #2527 REGMGT
      *             HARD CODED TO USE THE 1ST SPR TABLE(PG 4) PER JOHN WHEN
      *             I ASKED HIM HOW TO GET THE RIGHT TABLE
      *  JTG 050519 ADDED NEW FORMULAS FOR MEXICO
      *             SP-MFFRMLA-05 SP-RBFRMLA2 (G) SP-ERNFRMLA (24)   WORLD #0408
      *  JTG 050708 ADDED REB-LN-INT-REBATE REB-LN-SC-REBATE REB-LN-MF-REBATE
      *             REB-LN-DF-REBATE
      *             ADDED NEW FORMULAS FOR MEXICO
      *             SP-MFFRMLA-05 SP-RBFRMLA2 (G) SP-ERNFRMLA (24)   WORLD #0408
      *  JTG 050818 CHANGED SP-RBFRMLA2 (G) TO USE THE LN-ANTICERN(4)
      *             MAINTENANCE FEE ANTICIPATED INSTEAD OF THE STORED
      *             LN-MAINTFEE * LN-ORGTERM
      *             ADDED NEW FORMULAS FOR MEXICO
      *             SP-MFFRMLA-05 SP-RBFRMLA2 (G) SP-ERNFRMLA (24)   WORLD #0408
      *  BAH 050913 ALLOW SP-RBFRMLA2 (0 F) TO WORK WITH INSURANCES, PFSC
      *                SECURITY PR# 2600, LIBERAL PR# 2559
      *  CS  060203 ADD SP-RBSPOPT1 23 DONT ROUND TO WHOLE DOLLARS WHEN REFUND        *             IS REBATE-SHORT-RATE-0C [PEOPLE 2670]
      *  BAH 060213 WHEN THE CLEAR OF REB-MS-PENALTY WAS REMOVED FROM THE FIRST
      *             LINE(2/12/99), AND THE REFUND WAS 0, FULLY EARNED ALREADY,
      *             YOU WOULDNT GET INTO THE RBREDUC SECTION TO CLEAR THE
      *             PENALTY, SO IT WAS COMING OUT WITH JUNK LIKE 989762.88 IN
      *             IT ON MANUAL REBATES, SO I MOVED IT BACK UP TOP BUT ONLY
      *             CLEAR IT ON "RI"....LOANS INC PL# 529
      *   CS 060216 ADD SP-RBSPOPT1 = 24 USED IN CONJUNCTION WITH SP-RBFRMLA 0F
      *             REFUNDS 100% IF REFUNDED WITHIN 30 DAYS USES A 360 YR TYPE
      *                                                        PEOPLES 2680
      *   CS 060622 EARLY PROVISION 10 NEEDS TO WORK WITH ADDONS... IT WAS
      *             REBATING BACK ENTIRE LN-INTCHG IF REFUNDED WITH 10 DAYS
      *             & WASNT LOOKING AT THE ODDON CHARGE PR#534 CENTRAL ILL
      *             USE LT-ANTICERN(1)
      *   CS 060630 ADDED LT TO REBATE-CALL-SUBPROG SECTION CENTILL PR#534
      *  MJD 061207 ADDED SP-RBSPOPT1 = 25 OHIO PREPAY PENALTY, REGENCY #2878
      *  BAH 070814 ADDED SP-RBSPOPT1 = 26 TO USE APR INSTEAD OF SMPRATE
      *             ONLY IN RBFRMLA 'Q', LENDMARK PR# 3026
      *  JTG 071030 NEW INSURANCE REFUND FORMULA (0 I) FOR CPI POLICYS
      *             WHICH ARE ALWAYS 1 (ONE) YEAR TERMS          CITIZENS #3097
      *  MJD 080111 NEW INSURANCE REBATE FORMULA (0 J) FOR MX CL WORLD #563
      *   CS 080529 CHANGED  TEST FOR REBATE 78'S WITH FIRST PAY EXTENSION.
      *             WAS ADDING LN-EXTCHG TO REB-TOTCHG FOR ADDONS, THE LOAN
      *             EXT CHARGE SHOULD NOT AFFECT THE ADDON-REBATE SO...
      *             CHANGED TO :
      *             IF REB-ADDON-FG NOT = "Y"
      *                ADD LN-EXTCHG TO REB-TOTCHG.
      *             THIS WAS ALSO CAUSING CANCELS OF ADDON INTEREST > THAN
      *             ORIGINAL CHARGE
      *   CS 080603 DONT PERFORM EARLY PROVISION 10 (EP10) WHEN REFUNDING
      *             AN ADDON PR#630  CENTRAL ILL
      *  BAH 080910 ADDED RBFRMLA2 "K" FOR MULLEN PR# 3250
      *   CS 100304 ADDED RBFRMLA2 "L" FOR UNITED PR#3548, ACTS LIKE 0 9 EXCEPT
      *  BAH 100415 ADDED RBSPOPT2 12, FULL REFUND WITHIN 3 DAYS,
      *                                                         REGMGM PR# 3570
      *   CS 100805 CHANGED REBATE-ACTUARIAL-7-B IF REBATE-FRMLA 'B', TO COMPUTE
      *             THE RATE TO AMORTIZE THE LOAN USING UNITPER-PER-YEAR INSTEAD
      *             OF A MONTHLY RATE.  FOR THIS TO HAPPEN, SP-ORGST MUST BE
      *             'MX', MUST BE INTEREST REFUND, SP-RBUNITPER-CD = 'B'
      *                                                  WORLD PL#692
      *  100810 BAH ADDED SP-RBEARLY 25 FOR REGMGM, AL, WITHIN 90 DAYS DO
      *             PRORATA, OTHERWISE, DO SPR REFUND TABLE REGMGM PR# 3614
      *  BAH 100908 ADDED RBFRMLA2 "M" FOR WORLD TX ANTICIPATED PR# 711
      *             JUST LIKE RBFRMLA 8 WITHOUT TRUNCATING SPR RATE
      *   CS 110106 ADDED SP-RBEARLY 26 FOR WORLD, IL, IF PAID OFF BY "PO"
      *             WITHIN 60 DAYS, COMPUTE PRO-RATA REFUND, THIS IS THE SAME
      *             AS SP-RBEARLY 21 EXCEPT THAT IS ONLY CALCULATES A REFUND
      *             IF A 'PO'. ONLY FOR SERVICE CHARGE  WORLD PR#733
      *  110907  CS ADDED SP-RBEARLY = 98, FLAT CANCEL IF WITHIN 10 DAYS, IF
      *             > 10 DAYS, NO REFUND, DONT PUT A REBATE FORMULA IN SPR
      *             COMPONENT  REGMGM PR#3865
      *  110908  CS PER DAVID, FORCE 365 AS YEAR TYPE WITH SP-RBEARLY =98
      *                                                      REGMGM PR#3865
      *  111031 BAH ADDED SP-RBSPOPT2 = 13 FOR COLTN, PR# 3900
      *  120223 MJD ADDED REBATE FORMULA (0 N) FOR INDIANA,
      *             RULE OF ANTICIPATION LIFE                      CFSC #3795
      *  120502 MJD ADDED REBATE FORMULA (0 O) FOR TEXAS,
      *             RULE OF ANTICIPATION LIFE                      REGMGT #3938
      *  120517 MJD INSURANCE REBATE FORMULA 0-N TO USE A CONSTANTS
      *             RATE OF 3.7% AS THE STATE DISCOUNT RATE FOR THE
      *             CALCULATION OD LIFE INSURANCE REFUNDS.  AS PER RICHARD
      *             RODRIGUEZ AT ASSURANT.                         CFSC #4037
      *  120821 MJD CORRECTED RBFRMLA "O" (3938) TO ALWAYS USE FIXED
      *             RATE OF .035 RATHER THAN CALC BASED ON SPR DONE PER EMAIL
      *             FROM JIM R.
      *  130709 MJD ADDED EARLY PROVISION AND REBATE FOR GA 4% CLOSING FEE (54)
      *             THIS EARLY PROVISION IS ONLY UTILIZED BY SCFRMLA 54 AND
      *             RBFRMLA "I".                                    WORLD PR#920
      *             MODIFIED RBFRMLA "I" TO CALCULATE EARLY PROVISION AND
      *             THEN CALCULTE REBATE BASED ON THE REMAINING AMOUNT OF SERCHG
      *             4% CLOSING FEE STORED IN LN-POINTS              WORLD PR#876
      *  130913 BAH INSURANCE REBATE FORMULA 0-P TO USE A CONSTANT RATE OF
      *             1.42% AS THE STATE DISCOUNT RATE FOR THE CALCULATION OF
      *             LIFE INSURANCE REFUNDS.                      CFSC PR# 4367
      *  140117 MJD ADDED LOGIC FOR MX VYBA PREPAYMENT PENALTY      WORLD PR#954
      *  141010 MJD ADDED LOGIC FOR MX VYBA PREPYMNT PENALTY "PB"  WORLD PR#1030
      *  150611 BAH BOGUS JUNK CODE (:Q:F) SOMEHOW INSERTED IN THE
      *             REBATE-TX-INTEREST SECTION, ALONG WITH AN EXTRA GO TO
      *             STATEMENT WHICH CAUSED REBATE FORMULA 0 9 TO NOT WORK
      *                                                        SUPPORT PL#885
      *  160122 MJD WITHIN REBATE-EP54 CHANGED TO USE REB-EP54-AMT INSTEAD OF
      *             REB-WORK2 AS THE WORKER.  THIS VALUE NEED TO BE PART OF
      *             THE REBATE AND PASSED BACK TO BE STORED AND USED AT A
      *              LATER DATE.    REGMGT #4915
      *  160421 BAH CHANGED RBFRMLA2 "K" TO ALWAYS USE INSURANCE EFFECTE DATE
      *             AND INSURANCE TERM, NOT LOANDATE AND ORGTERM.
      *                                                  MULLEN/KSTREET PR#5049
      *  160511 MJD ADDED SP-RBDTE = "D"                             71105#0918
      *  CS 191204 ADD SP-RBEARLY 27 INDIANA EARLY PROVISION, NO REFUND OF
      *            EXTENSION CHARGE, FIRST MONTH INTEREST REBATED PRORATA 78'S
      *                                         WORLD PR#1406
      *            UNUSED DAYS FROM PAYDATE TO REFDATE(USED DAYS) COUNTED
      *            BY 365 YEAR TYPE AND SUBTRACTED FROM 30 TO GIVE US THE
      *            UNEARED PORTION OF THE SLICE OF EARNINGS
      *  2020.01.28 BAH ADDED SP-RBEARLY = 28, "IL" #1417
      *  2021.02.16 BAH CHANGE SPOPT1 = 3, IF B4 FIRST PAY, DO NOT INCLUDE THE
      *                 EXTENSION CHG IN THE REBATE CALC, IT SHOULD BE FULLY
      *                 REBATED INSTEAD. #1489
      *  2021.03.12 BAH SPOPT1 = 3, WAS INCLUDING "ON" THE FIRST PAY, SHOULD
      *                 ONLY BE PRIOR TO FIRST PAY. #1489A
      *  2021.0420 CS SP-RBFRMLA =0, SP-RBFRMLA2 = "H", PROBLEMS WITH INDEX
      *            OUT OF BALANCE IN REBATE-CALC-INT-0H. IT TESTS IF REBTX-
      *            STEP > 5, GO TO END BUT IF WAS = 5, IT ADDED 1 AND 
      *            CONTINUED TO TRY AND PROCESS RESULTING IN BAD REFUNDS, ERRORS
      *            ON INDEX THAT YOU DIDNT SEE UNLESS YOU WERE IN DEBUG. CHANGED
      *            INCREMENT OF REBTX-STEP TO HAPPEN PRIOR TO TEST AGAINST
      *            > 5.
      *  2021.0617 BAH ADDED SP-RBEARLY = 97, "TN" COMBINES RBEARLY 99 WITH
      *                AN EARLY PROVISION FOR NEW CLOSING FEE FROM SCFRMLA 60
      *                                                         #1500
      *  2021.0924 CS MOVED PERFORM OF EP11 BELOW THE SELECT-SP-ELAPSED-TIMTBL
      *               EP11 NEEDS TO KNOW IF PRIOR TO FIRST PAYMENT DUE DATE,
      *               (POFF-B4-1STPYDATE) WHICH IS REB-SUB2
      *               AS EP11 NEEDS TO KNOW IF PRIOR TO FIRST PAYMENT DUE DATE
      *               AS EP11 NEEDS TO KNOW IF PRIOR TO FIRST PAYMENT DUE DATE
      *  2022.0103 CS REMOVED BELOW, IT WILL NEVER HIT IT.  SP-RBFRMLA = "0" AND
      *               SP-RBFRMLA2 = "5", IS TESTED AS A DEFERMENT FORMULA AND 
      *               EXITS REBATE. YOU CANNOT ENTER ANYTHING OTHER THAN A 0 IN
      *               SP-RBFRMLA TO GET TO SP-RBFRMLA2. LOOKS LIKE WAS JUST
      *               COPIED FROM SP-RBFRMLA = "0" AND SP-RBFRMLA2 = "4"
      *               *** REMOVED*** WI SMALL LOAN DEFERMENT REFUND #1622
      *               IF SP-RBFRMLA2(REB-SUB) = "5"
      *                   PERFORM TN-PP-SHORT-RATE
      *  BAH 2023.0801 ADDED SP-RBFRMLA2 "Q" FOR "AL" CLOSING FEE #1619
      *   CS 2023.1012 IF WORLD DECIDES TO USE ADDONS, THE EARLY PROVISIONS
      *                NEED WORKED ON.  FOR EXAMPLE: RBEARLY(7) =19
      *                A REBATE IS CALCULATED ON THE ADDON PORTION AND SETS
      *                REB-SUB TO 0 WHICH IS A TRIGGER TO EXIT REBATE.
      *                PROBLEM WITH THAT IS WHEN YOU GET BACK TO FAPOFF:
      *                WITHIN PAYOFF-INT-ADDONS-NEXT, AFTER THE PERFORM OF
      *                ADDONRB-GET-IT-REBATE....
      *                    IF REB-SUB NOT = 0    ITS 0
      *                        MULTIPLY POFF-ONE BY REB-REBATE
      *                        ADD REB-REBATE TO POFF-ADDON-INT-REBATE
      *                    ELSE
      *                        MOVE 0 TO REB-REBATE. <----------
      *      IN THE CASE OF SP-RBEARLY(7) = 16 A NEW CALCULATION WOULD NEED
      *      TO BE ADDEDED IF REB-ADDON-FG = "Y" (SET IN FAPOFF) AS THIS
      *      RBEARLY FORMULA IS USING THE LN-LNAMT,LN-INTCHG,LN-EXTCHG BUT
      *      IS USING THE ELAPSED-DAYS FOR THE TIME PERIOD THE ADDON WAS IN
      *      EFFECT.
      **************************************************************************
       REBATE SECTION.
           MOVE 0 TO REB-REBATE REB-PRES-VALUE INS-TAX-RB REB-EP54-AMT.

      * ONLY CLEAR FOR INTEREST REBATE, OTHERWISE PAYOFF WILL WIPE IT OUT
      * WITH OTHER REBATES
           IF REB-SUB = 7
              MOVE 0 TO REB-MS-PENALTY.

           IF SP-RBFRMLA(REB-SUB) = "9" OR
              ( SP-RBFRMLA(REB-SUB) = " " AND
                SP-RBEARLY(REB-SUB) NOT = 99 AND NOT = 98 AND NOT = 97 )
              GO TO REBATE-EXIT.

      *    -------------------------------------------------------------
      *    EARLY PROVISION FOR ACQUISITION CHARGES
      *    -------------------------------------------------------------
           IF SP-RBEARLY(REB-SUB) = 21
              PERFORM REBATE-RBEARLY-21
              GO TO REBATE-EXIT.

           IF SP-RBEARLY(REB-SUB) = 26
              PERFORM REBATE-RBEARLY-26
              GO TO REBATE-EXIT.

           IF BR-NOT-OURS-REFUND NOT = "Y"
      *          TEST CL:
              IF (REB-SUB > 0 AND < 5) AND
                 (LN-INSOURS(1) = "N")
                 GO TO REBATE-EXIT
              ELSE
      *          TEST AH:
              IF ( REB-SUB = 5         ) AND
                 ( LN-INSOURS(2) = "N" )
                 GO TO REBATE-EXIT
              ELSE
      *          TEST PP/PP2:
              IF ( REB-SUB = 6 OR 11   ) AND
                 ( LN-INSOURS(3) = "N" )
                 GO TO REBATE-EXIT
              ELSE
      *          TEST O1 TO O7:
              IF ( REB-SUB > 10 AND < 19         ) AND
                 ( LN-INSOURS(REB-SUB - 8) = "N" )
                 GO TO REBATE-EXIT.

      * ---------------------------------------------------------
      * TEST FOR WINCONSIN DEFERMENT FORMULA 'X:
      * WHEN PAYOFF IS PRIOR TO ORIGINAL MATURITY
      * REFUND ALL DEFERMENT CHARGES:
      * ---------------------------------------------------------
              IF REB-SUB = 10
                 IF SP-RBFRMLA(REB-SUB) = "X"
      * REBATE ON ALL RENEWALS AND PAYOFF QUOTES 1/4/96 BAH
      *             IF REB-LPTRCD = "PO"
                       MOVE "Y" TO MDTE-FLAG
                       PERFORM MATURITY-DATE-CALCULATION
                       IF MDTE-DATE > REB-PAYDATE
                          MOVE LN-TOTDEF TO REB-REBATE
                          GO TO REBATE-EXIT.

      * IF SP-DBDTE = "D" AND ADDON-FG = "Y" CHECK TO SEE IF THE INS IN
      * QUESTION IS AN ADDON OR WAS PART OF THE ORIGINAL LOAN. THE
      * ADDON-FG IS A GENERAL FLAG AND NOT INSURANCE SPECIFIC, IF THE
      * INSURANCE IN QUESTION WAS NOT AN ADDON THEN DO NOT TREAT IT LIKE
      * ON REGARDLESS OF WHETHER OR NOT OTHER INSURANCES WERE ADDON
      * ON/ADJUSTED/CANCELLED.
      * THE TEST FOR SUB 1 AND 2 = D SEEMS WEIRD BUT THERE IS NO POINT
      * IN SEARCHING LT IF THERE IS NO POSSIBILITY OF EITHER BEING "D"
      * IF PAYOFF LPPOFF ALREADY SETS UP THE REB-LN FIELDS, WE JUST DO
      * NOT WANT TO USE THEM ID THE INS IN QUESTION REALLY IS NOT AN ADDON.
      *  ALSO IF IT IS A PAYOFF, THE REB-LN-ORIG-1STPYDATE IS ALREADY
      * CHANGED TO INS EFF DATE BY THE TIME IT GETS HERE.
      *                                                  71105#0918
           IF (REB-ADDON-FG = "Y") AND
                 ((SP-RBDTE(REB-SUB, 1) = "D") OR
                  (SP-RBDTE(REB-SUB, 2) = "D"))
      *  DO ANY LOAN INS TRAILERS EXIST FOR THIS INSURANCE?
              PERFORM REBATE-CHECK-ADDON-TRAILER
              IF REB-LTI-ADDON-NOT-FOUND
      * WANT TO TEST USING LN-ORIG-1STPYDATE THIS INS IS NOT AN ADDON.
                 MOVE REB-LN-ORIG-1STPYDATE TO REB-HOLD-LNORIG1STPAY
                 MOVE LN-ORIG-1STPYDATE TO REB-LN-ORIG-1STPYDATE
      * SETUP REB-SUB2
                 PERFORM SELECT-SP-ELAPSED-TIMTBL
                 IF JULIAN-DATE = 0
                    GO TO REBATE-EXIT
                 ELSE
      * IF CONDITIONS ARE MET CONTINUE AS THOUGH ADDON-FG = "N"
                    IF SP-RBDTE(REB-SUB, REB-SUB2) = "D"
                       MOVE "N" TO REB-ADDON-FG
                    END-IF
                 END-IF
                 MOVE REB-HOLD-LNORIG1STPAY TO REB-LN-ORIG-1STPYDATE
              END-IF
           END-IF.

      *^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
      * THIS SECTION OF CODE IS FOR ESTABLISHMENT OF ORIG LOAN FIELDS.
      *  NOTE: WHEN REB-ADDON-FG = "Y", IT IS ASSUMED THAT THESE FIELDS
      *        HAVE BEEN PRESET TO THE CORRECT VALUES FOR THE ADDON.
      *

           IF REB-ADDON-FG NOT = "Y"
              MOVE LN-ORIG-1STPYDATE TO REB-LN-ORIG-1STPYDATE
              MOVE LN-PURDATE     TO REB-LN-PURDATE
              MOVE LN-LOANDATE    TO REB-LN-LOANDATE
              MOVE LN-INTDATE     TO REB-LN-INTDATE
              MOVE LN-APRATE      TO REB-LN-APRATE
              MOVE LN-TOTNODEF    TO REB-LN-TOTNODEF
              IF REB-SUB < 5
                 MOVE LN-INSCOVR(1) TO REB-LN-INSCOVR
                 MOVE LN-INSEFF-DATE(1)  TO REB-LN-INSEFF
                 MOVE LN-INSEXP-DATE(1)  TO REB-LN-INSEXP
              ELSE
                IF REB-SUB = 6
                   MOVE LN-INSCOVR(3) TO REB-LN-INSCOVR
                   MOVE LN-INSEFF-DATE(3)  TO REB-LN-INSEFF
                   MOVE LN-INSEXP-DATE(3)  TO REB-LN-INSEXP
                ELSE
                  IF REB-SUB > 11 AND < 17
                     MOVE LN-INSCOVR(REB-SUB - 8) TO REB-LN-INSCOVR
                     MOVE LN-INSEFF-DATE(REB-SUB - 8)  TO REB-LN-INSEFF
                     MOVE LN-INSEXP-DATE(REB-SUB - 8)  TO REB-LN-INSEXP
                  ELSE
                     MOVE LN-INSCOVR(2) TO REB-LN-INSCOVR
                     MOVE LN-INSEFF-DATE(2)  TO REB-LN-INSEFF
                     MOVE LN-INSEXP-DATE(2)  TO REB-LN-INSEXP
                  END-IF
                END-IF
              END-IF
              MOVE LN-SALEFIN     TO REB-LN-SALEFIN

              MOVE LN-SMPRATE     TO REB-LN-SMPRATE
              MOVE LN-ORGTERM     TO REB-LN-ORGTERM
              MOVE LN-1STPYAMT    TO REB-LN-1STPYAMT
              MOVE LN-REGPYAMT    TO REB-LN-REGPYAMT
              MOVE LN-LASTPYAMT   TO REB-LN-LASTPYAMT.

           IF REB-SUB = 7 AND LN-CONVERCD = "X"
              IF LN-ADDON-FG NOT = "Y"
                      AND LN-FINCHG = LN-SUM-FINCHG
                 MOVE LN-ANTICERN(1) TO REB-TOTCHG
              ELSE
                 MOVE LN-OLDACCTNO TO REB-LN-PURDATE
                                      REB-LN-INTDATE
                                      REB-LN-LOANDATE
                                      NDTE-DATE
                 MOVE LN-PRLNNO TO REB-ORGTERM
                 MOVE 1 TO NDTE-HOLD
                 PERFORM INCREMENT-MONTHS
                 MOVE NDTE-DATE TO REB-LN-ORIG-1STPYDATE
                 COMPUTE REB-TOTCHG =
                    LN-ANTICERN(1) + LN-ANTICADJ(1) - LN-UFERN(1).

      *
      ***********   SET UNIT PERIOD INFO   **************************
            MOVE LN-UNITPER-CD      TO UPER-UNITPER-CD.
            MOVE LN-UNITPER-FREQ    TO UPER-UNITPER-FREQ.
            MOVE REB-LN-INTDATE     TO UPER-INTDATE.
            MOVE REB-LN-ORIG-1STPYDATE TO UPER-1STPYDATE.
            MOVE REB-ORGTERM        TO UPER-TERM.
            PERFORM UPER-CALCULATION.
      *^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
      * IF SP-RBUNITPER-CD(REB-SUB) = "A"
      *    IF OTHER THAN MONTHLY PAY CODE OR PAYFREQ CONVERT BACK TO MONTHLY

            IF SP-RBUNITPER-CD(REB-SUB) = "A"
               IF NOT (
                       LN-UNITPER-CD = "M"
                           AND LN-UNITPER-FREQ = 1
                      )
                  COMPUTE REB-ORGTERM =
                       REB-ORGTERM / UPER-UNITPER-PER-YEAR * 12
                  IF REB-LN-TOTNODEF NOT = 0
                     MOVE LN-UNITPER-CD   TO UPWK-UNITPER-CD
                     MOVE LN-UNITPER-FREQ TO UPWK-UNITPER-FREQ
                     PERFORM UPWK-DEF-CALC
                     DIVIDE UPWK-UNITPER-WK INTO REB-LN-TOTNODEF.

      *^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
      * IF SP-RBUNITPER-CD(REB-SUB) = "B"
      *    IF OTHER THAN MONTHLY PAY CODE OR PAYFREQ
      *       COMPUTATION OF ELAPSED-MONTHS, ETC WILL BE DONE ON
      *       UNIT PERIOD BASES
      *^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^

      * TEST FOR TENN, CALIF, INDIANA, S. CAROLINA DEFERMENT REBATE:
           IF SP-RBFRMLA(REB-SUB) = "E" OR "O" OR "K"
                                 OR "M" OR "Y" OR "Z"
              PERFORM REBATE-DEFERMENT-EOKMYZ05
              GO TO REBATE-EXIT.

      * TEST FOR WISCONSIN DEFERMENT REBATE:
           IF SP-RBFRMLA(REB-SUB)  = "0" AND
              SP-RBFRMLA2(REB-SUB) = "5"
              PERFORM LOAN-CALCULATIONS
              IF (FINANCED-AMOUNT > 4999.99) OR
                 (LN-ORGTERM      > 36     )
                 MOVE LN-TOTDEF TO REB-REBATE
              ELSE
                 PERFORM REBATE-DEFERMENT-EOKMYZ05
              END-IF
              GO TO REBATE-EXIT.

      * TEST FOR REBATE 78THS WITH 1ST PAYMENT EXTENDED:
           IF SP-RBFRMLA(REB-SUB) = "3" OR "N"
              IF SP-RBEARLY(REB-SUB) NOT = 7
                 IF SP-RBSPOPT1(REB-SUB) NOT = 3
                    IF REB-ADDON-FG NOT = "Y"
                       ADD LN-EXTCHG TO REB-TOTCHG.

      * TEST FOR REBATE '0 9' THAT WILL INCLUDE 1ST PAYMENT EXTENSION: 'EXCEL'
           IF SP-RBFRMLA2(REB-SUB) = "9" OR
               SP-RBFRMLA2(REB-SUB) = "G"
              ADD LN-EXTCHG TO REB-TOTCHG.

      * TEST FOR REBATE 78THS W/1ST PMT EXT + DEFERMENTS:
           IF SP-RBFRMLA(REB-SUB) = "A"
              ADD LN-EXTCHG LN-TOTDEF TO REB-TOTCHG.

      * TEST FOR ACTUARIAL REBATE W/1ST PMT EXT + SERVICE CHGI
      * BUT NOT IF CALCULATED FOR EARNINGS LPCERN OR LPAERN:
           IF REB-SKIPDEF-FG NOT = "Y"
              IF SP-RBFRMLA(REB-SUB) = "D" OR "F"
                 ADD LN-EXTCHG LN-SERCHG TO REB-TOTCHG.

      * TEST FOR EARLY PAYOFF EXCEPTIONS:
           IF SP-RBEARLY(REB-SUB) NOT = 0
      *    TEST FOR MISSISSIPPI COMPANY PAYOFF PRORATA REBATE:
      *    TEST FOR ALABAMA COMPANY PAYOFF PRORATA REBATE( 90DAYS):
      *    TEST FOR ALABAMA COMPANY PAYOFF PRORATA REBATE(120DAYS):
              IF SP-RBEARLY(REB-SUB) = 3 OR 12 OR 15 OR 16
                 PERFORM REBATE-EP3
                 IF REB-SUB = 0
                    GO TO REBATE-EXIT
                 ELSE
                    NEXT SENTENCE
              ELSE
      * WORLD SOUTH CAROLINA COMPANY PAYOFF PRORATA INTEREST REBATE:
              IF SP-RBEARLY(REB-SUB) = 19
                 PERFORM REBATE-EP19
                 IF REB-SUB = 0
                    GO TO REBATE-EXIT
                 ELSE
                    NEXT SENTENCE
              ELSE
      * WORLD SOUTH CAROLINA SERVICE CHARGE REBATE:
              IF SP-RBEARLY(REB-SUB) = 20
                 PERFORM REBATE-EP20
                 IF REB-SUB = 0
                    GO TO REBATE-EXIT
                 ELSE
                    NEXT SENTENCE
              ELSE
      * TEST FOR ALABAMA PRIOR TO 90 DAY REBATE (SUN) :
              IF SP-RBEARLY(REB-SUB) = 14
                 PERFORM REBATE-EP14
                 GO TO REBATE-EXIT
              ELSE
      * TEST FOR ALABAMA PRIOR TO 90 DAY REBATE (FREEDOM) :
              IF SP-RBEARLY(REB-SUB) = 22
                 PERFORM REBATE-EP22
                 GO TO REBATE-EXIT
              ELSE
      * REGMGM ALABAMA, LIKE 22 EXCEPT AFTER 90 DAYS, USE SPR TABLE
              IF SP-RBEARLY(REB-SUB) = 25
                 PERFORM REBATE-EP22
                 IF REB-SUB = 0
                    GO TO REBATE-EXIT
                 ELSE
                    NEXT SENTENCE
              ELSE
                 IF SP-RBEARLY(REB-SUB) = 99
                    MOVE REB-SUB TO REB-SUB2
                    PERFORM REBATE-EARLY-99
                    IF REB-SUB2 = 0
                      GO TO REBATE-EXIT
                    ELSE
                      NEXT SENTENCE
                 ELSE
                  IF SP-RBEARLY(REB-SUB) = 97
                    MOVE REB-SUB TO REB-SUB2
                    PERFORM REBATE-EARLY-97
                    IF REB-SUB2 = 0
                      GO TO REBATE-EXIT
                    ELSE
                      NEXT SENTENCE
                  ELSE
                  IF SP-RBEARLY(REB-SUB) = 98
                     MOVE REB-SUB TO REB-SUB2
                     PERFORM REBATE-EARLY-98
                     IF REB-SUB2 = 0
                       GO TO REBATE-EXIT.



      * IF ON OR BEFORE START DATE, FULL REFUND (MERCURY),
      * WHERE START DATE IS 1 MONTH FROM 1ST PAY DATE:
           IF SP-RBEARLY(REB-SUB) = 9
              MOVE REB-PAYDATE TO NUM-DATE
              MOVE LN-ORIG-1STPYDATE TO SYS-DATE
              PERFORM TIM360
              IF ELAPSED-MONTHS > 0
                 MOVE REB-TOTCHG TO REB-REBATE
                 GO TO REBATE-EXIT.

      * REBATE FORMULA2 "Q" "AL" CLOSING FEE
      *  THEY ALSO USE RBEARLY 99 FOR A FULL REFUND OF SERVICE CHARGE SO
      *  THIS CHECK MUST STAY BELOW TESTING FOR THAT.
      * IF RBFRMLA2 = "Q", AND IT ISNT WITHIN THE EARLY PROVISION OF 99,
      *  THEN YOU ARE ONLY REFUNDING THE CLOSING FEE (LN-POINTS)
           IF SP-RBFRMLA2(REB-SUB) = "Q"
              MOVE LN-POINTS TO REB-TOTCHG.

      *************************************************************
      *NORMAL:
      *  ESTABLISH REB-SUB2 PER REBATE DONE [BEFORE (1)]
      *  OR [ON OR AFTER (2)] 1STPAYDATE:
      *MISSISSIPPI OPT1=4:
      *  ESTABLISH REB-SUB2 PER REBATE DONE [ON OR BEFORE (2)]
      *  OR [AFTER (2)] 1STPAYDATE:
      *************************************************************
           PERFORM SELECT-SP-ELAPSED-TIMTBL.
           IF JULIAN-DATE = 0
              GO TO REBATE-EXIT.

      *THIS WAS BEING PERFORMED ABOVE THE SELECT-SP-ELAPSED-TIMTBL.
      *IT NEEDS TO COME AFTER THAT AS WE NEED TO KNOW IF PAYING PRIOR TO
      *1ST PAYMENT DUE(POFF-B4-1STPYDATE) WHICH IS SET IN 
      *SELECT-SP-ELAPSED-TIMTBL
      * TEST FOR MERCURY MISSISSIPPI PRORATA REBATE:
           IF SP-RBEARLY(REB-SUB) = 11
              PERFORM REBATE-EP11
              IF REB-SUB = 0
                 GO TO REBATE-EXIT.

      *   TEST FOR CLAY FINANCIAL, SINGLE PAY 78TH:
           IF SP-RBEARLY(REB-SUB) = 17
              PERFORM REBATE-EP17
              IF REB-SUB = 0
                 GO TO REBATE-EXIT.

      *  TEST FOR MISSISSIPPI 90 DAY RULE;
      *  AND FOR MERCURY ALABAMA INTEREST REFUND
      * IF SO WHEN PAYOFF IS ON OR BEFORE 1ST PAYDATE
      * INCLUDE 1ST PAY EXTENSION CHARGE:
           IF PODATE-B4-1STPYDATE
              IF SP-RBSPOPT1(REB-SUB) = 4 OR 12
                 ADD LN-EXTCHG TO REB-TOTCHG.

      * GET REBATE REFERENCE DATE:
           PERFORM REBATE-REFDATE.
           IF REB-REFDATE = 0
              MOVE 0 TO REB-SUB
              GO TO REBATE-EXIT.


      * CALCULATE ELAPSED TIME FROM REF THRU PAYDATE:
           PERFORM CALC-ELAPSED-REF-THRU-PODATE.
           IF JULIAN-DATE = 0
              MOVE 0 TO REB-SUB
              GO TO REBATE-EXIT.

      * TEST TO SEE IF PAYOFF IS PRIOR TO REF-DATE:
           IF REB-ELAPSED-DAYS NOT > 0
              IF NOT (REB-HOLD-RBDTE = "C" OR "D")
                            OR SP-RBEARLY(REB-SUB) = 7
                 MOVE 0 TO REB-ELAPSED-REM
                           REB-ELAPSED-MONTHS
              ELSE
                 IF REB-PAYDATE NOT = REB-LN-INTDATE
                    MOVE 0 TO REB-ELAPSED-MONTHS
                    MOVE 1 TO REB-ELAPSED-REM.

      * TEST FOR HOME CREDIT (NC), IF ELAPSED DAYS < 31
      * FULL REFUND:
           IF SP-RBDEDCHG(REB-SUB) = 9999.03 OR 9999.05
              IF REB-ELAPSED-DAYS < 31
                 MOVE REB-TOTCHG TO REB-REBATE
                 GO TO REBATE-EXIT.

           IF SP-RBSPOPT2(REB-SUB) = 12
              IF REB-ELAPSED-DAYS NOT > 3
                 MOVE REB-TOTCHG TO REB-REBATE
                 GO TO REBATE-EXIT.
           IF SP-RBSPOPT2(REB-SUB) = 4
              IF REB-ELAPSED-DAYS NOT > 10
                 MOVE REB-TOTCHG TO REB-REBATE
                 GO TO REBATE-EXIT.
           IF SP-RBSPOPT2(REB-SUB) = 5
              IF REB-ELAPSED-DAYS NOT > 15
                 MOVE REB-TOTCHG TO REB-REBATE
                 GO TO REBATE-EXIT.
      * TEST FOR LIBERAL/ARROW FULL REFUND IF PAYOFF WITHIN 30 DAYS:
           IF SP-RBSPOPT2(REB-SUB) = 6
              IF REB-ELAPSED-DAYS NOT > 30
                 MOVE REB-TOTCHG TO REB-REBATE
                 GO TO REBATE-EXIT.
           IF SP-RBSPOPT2(REB-SUB) = 10
              IF REB-ELAPSED-DAYS NOT > 60
                 MOVE REB-TOTCHG TO REB-REBATE
                 GO TO REBATE-EXIT.

      * LENDMARK * FULL REFUND UP TO "SAME AS CASH" DATE
           IF SP-RBSPOPT2(REB-SUB) = 9
              IF REB-PAYDATE NOT > LN-CASHDATE
                 MOVE REB-TOTCHG TO REB-REBATE
                 GO TO REBATE-EXIT.

      * PEOPLES * FULL REFUND IF PAID WITHIN 30 DAYS
      *         * USES 360 YEAR IN TIME CALCULATIONS
           IF SP-RBSPOPT1(REB-SUB) = 24
              IF ELAPSED-DAYS NOT > 30
                 MOVE REB-TOTCHG TO REB-REBATE
                 GO TO REBATE-EXIT.

           PERFORM REBATE-TEST-SWING.

           PERFORM REBATE-DEDUCT-CHARGE.

           IF SP-RBEARLY(REB-SUB) = 2
              IF PODATE-B4-1STPYDATE
                 PERFORM REBATE-EP2
                 GO TO REBATE-EXIT.

      * PR1406 "IN"
           IF SP-RBEARLY(REB-SUB) = 27
              IF LN-EXTCHG NOT = 0
                 IF PODATE-B4-1STPYDATE
                    PERFORM REBATE-EP27
                    GO TO REBATE-EXIT.


           IF SP-RBEARLY(REB-SUB) = 18
              IF (LN-LNAMT - LN-INTCHG - LN-SERCHG - LN-EXTCHG)
                                                         < 100.01
                 MOVE 0 TO REB-REBATE
                 GO TO REBATE-EXIT
              ELSE
                 IF PODATE-B4-1STPYDATE
                    PERFORM REBATE-EP18
                    GO TO REBATE-EXIT.

           IF SP-RBEARLY(REB-SUB) = 10
              IF REB-SUB = 7
                 IF REB-ADDON-FG = "Y"
                    MOVE LTI-ANTICERN(1)   TO REB-TOTCHG
                 ELSE
                    MOVE LN-INTCHG TO REB-TOTCHG
                 END-IF
              END-IF
              IF REB-SUB = 8
                 IF (LN-LNAMT - LN-INTCHG - LN-SERCHG - LN-EXTCHG)
                                                         < 100.01
                    MOVE 0 TO REB-REBATE
                    GO TO REBATE-EXIT
                 END-IF
              END-IF
              IF PODATE-B4-1STPYDATE
                 IF REB-ADDON-FG NOT = "Y"
                 PERFORM REBATE-EP10
                 GO TO REBATE-EXIT.

      * WORLD MISSOURI 6/03/1999 23
      * WORLD ILLINOIS 1/28/2020 28
           IF SP-RBEARLY(REB-SUB) = 23 OR 28
              MOVE LN-INTCHG TO REB-TOTCHG
              IF PODATE-B4-1STPYDATE
                 PERFORM REBATE-EP23-28
                 GO TO REBATE-EXIT.


      * TEST FOR MERCURY NEVADA PRORATA REBATE:
           IF SP-RBEARLY(REB-SUB) = 13
              PERFORM REBATE-EP13
              IF REB-SUB = 0
                 GO TO REBATE-EXIT.

      * TEST FOR TFC PHYSICAL DAMAGE COVERAGE SHORT RATE REBATE:
           IF SP-RBEARLY(REB-SUB) = 24
              PERFORM REBATE-EP24
              IF REB-SUB = 0
                 GO TO REBATE-EXIT.


      * INTSUB IS INT LEVEL OF INS/INT ARRAY; MUST COME LAST
           IF REB-SUB < 1 OR > 18 OR REB-PAYDATE = 0
              MOVE 0 TO REB-SUB
              GO TO REBATE-EXIT.

           IF REB-TOTCHG NOT > 0 OR SP-RBFRMLA(REB-SUB) = " "
              GO TO REBATE-EXIT.
           IF REB-ADDON-FG = "Y" AND REB-SUB = 7
              NEXT SENTENCE
           ELSE
              IF REB-DEDCHG > REB-TOTCHG
                 GO TO REBATE-EXIT.

      * TEST FOR ACTUARIAL-IB-REBATE:
           IF SP-RBFRMLA(REB-SUB) = "6"
              PERFORM REBATE-ACTUARIAL-IB
              PERFORM REBATE-ADJUST-REB-REBATE
              GO TO REBATE-EXIT.

      * SKIP THIS TEST FOR (0 I)
           IF NOT ( SP-RBFRMLA(REB-SUB)  = "0" AND
                    SP-RBFRMLA2(REB-SUB) = "I"
                  )
      * BELOW THIS FLOOR CONSIDERED FULLY EARNED
              IF REB-REMTERM < SP-RBNOMON(REB-SUB)
                 IF NOT (SP-RBFRMLA(REB-SUB) = "D" OR "F")
                    GO TO REBATE-EXIT.

           IF REB-RECAST = "Y" AND SP-RBRECAST(REB-SUB) = 1
              MOVE "C" TO REB-FORM-PROG-TYPE
              MOVE 01 TO REB-FORM-PROG-NO
              PERFORM REBATE-CALL-SUBPROG
              IF REB-COMMON-RTNCD = " "
                 MOVE REB-COMMON-REBATE TO REB-REBATE
                 GO TO REBATE-END-ADJ
              ELSE
                 MOVE 0 TO REB-REBATE REB-SUB
                 GO TO REBATE-EXIT.

      * CHOOSE REBATE METHOD:
           IF SP-RBSPOPT2(REB-SUB) = 1
              IF REB-LPTRCD = "PB" OR "RN" OR "SC"
                                OR "RB" OR "RO"
                 PERFORM REBATE-PRORATA
                 GO TO REBATE-END-ADJ.

           IF SP-RBSPOPT2(REB-SUB) = 3
              IF REB-LPTRCD = "RI"
                 PERFORM REBATE-PRORATA
                 GO TO REBATE-END-ADJ.

      * WDT 1-18-96
           IF SP-RBSPOPT2(REB-SUB) = 7
              IF REB-LPTRCD = "PB" OR "RN" OR "RB" OR "RO"
                 PERFORM REBATE-ACTUARIAL-F-06
                 GO TO REBATE-END-ADJ.

      * COLTN 10/31/2011 PR# 3900
      *  IF RENEWAL, USE FMLA "F", IF PAYOFF WITHIN 24 MONTHS, USE SPR,
      *   OTHERWISE USE "F"
           IF SP-RBSPOPT2(REB-SUB) = 13
              IF REB-LPTRCD = "PB" OR "RN" OR "RB" OR "RO" OR "SC"
                 PERFORM REBATE-ACTUARIAL-F-06
                 GO TO REBATE-END-ADJ
              ELSE
              IF REB-LPTRCD = "PO"
      * IF PAYOFF WITHIN 24 MONTHS, FALL THRU HERE AND USE SPR
      * IF PAYOFF > 24 MONTHS, USE FORMULA "F"
                 IF REB-ELAPSED-MONTHS > 24
                    PERFORM REBATE-ACTUARIAL-F-06
                    GO TO REBATE-END-ADJ.

      * SUN, LA IF PAID BY "PB" OR "RN" REFUND RULE OF 78TH'S, IF PAID
      *         OFF WITH "PO", NO REFUND
           IF SP-RBSPOPT1(REB-SUB) = 22
               IF REB-LPTRCD = "PO"
                 MOVE 0 TO REB-REBATE
                 GO TO REBATE-END-ADJ.

      * MULLEN, CA IF REPO REBATE PRORATA, EXACT DAILY:
           IF SP-RBSPOPT1(REB-SUB) = 7
              IF (LN-REPOCD = "X" OR "S")
                         OR (REB-LPTRCD = "PB" OR "RN"
                             OR "RB" OR "RO")
                 PERFORM REBATE-PRORATA-EXACT
                 GO TO REBATE-END-ADJ.

      * MULLEN, CA IF REPO, RENEWED OR <= 60 DAYS, REBATE PRORATA,
      *            EXACT DAILY:
           IF SP-RBSPOPT1(REB-SUB) = 18
              IF (LN-REPOCD = "X" OR "S") OR
                   (REB-LPTRCD = "PB" OR "RN" OR "RB" OR "RO") OR
                     (REB-ELAPSED-DAYS NOT > 60)
                 PERFORM REBATE-PRORATA-EXACT
                 GO TO REBATE-END-ADJ.

      * REGIONAL ACCPT, IF REPO, REBATE PRORATA, MONTHLY:
           IF SP-RBSPOPT1(REB-SUB) = 17
              IF (LN-REPOCD = "X" OR "S")
                 PERFORM REBATE-PRORATA
                 GO TO REBATE-END-ADJ.

      **** BEGINNING OF RBFRMLA PERFORMS: ***

           IF SP-RBFRMLA(REB-SUB) = "1" OR SP-RBFRMLA2(REB-SUB) = "Q"
              PERFORM REBATE-78
           ELSE
           IF SP-RBFRMLA(REB-SUB) = "2"
              PERFORM REBATE-ACTUARIAL-2
           ELSE
      * EXT. CHARGE HAS ALREADY BEEN ADDED IN:
           IF SP-RBFRMLA(REB-SUB) = "3" OR "N"
              PERFORM REBATE-78
           ELSE
           IF SP-RBFRMLA(REB-SUB) = "4"
              PERFORM REBATE-ANTICIPATED
           ELSE
           IF SP-RBFRMLA(REB-SUB) = "5"
              PERFORM REBATE-PRORATA
           ELSE
      * ACTUARIAL USING WHOLE MONTHS ONLY:
           IF SP-RBFRMLA(REB-SUB) = "7" OR "B"
              PERFORM REBATE-ACTUARIAL-7-B
           ELSE
      * INSURANCE ANTICIPATED:
           IF SP-RBFRMLA(REB-SUB) = "8"
              PERFORM REBATE-INS-ANTICIPATED
           ELSE
      * NOTE:  EXT. CHARGE AND DEFERMENTS HAVE ALREADY
      *        BEEN ADDED IN IF SP-RBFRMLA(REB-SUB) = "A"
           IF SP-RBFRMLA(REB-SUB) = "A"
              PERFORM REBATE-78
           ELSE
           IF SP-RBFRMLA(REB-SUB) = "C"
              PERFORM REBATE-ACTUARIAL-C
           ELSE
           IF SP-RBFRMLA(REB-SUB) = "D"
              PERFORM REBATE-ACTUARIAL-D
           ELSE
           IF SP-RBFRMLA(REB-SUB) = "F"
              PERFORM REBATE-ACTUARIAL-F-06
           ELSE
      * MASS. A&H ANTICIPATED (IDEAL):
           IF SP-RBFRMLA(REB-SUB) = "G"
              PERFORM REBATE-MA-AH-ANTICIPATED
           ELSE
      * "H" (GA) SERCHG REBATE (ROGER WILLIAMS)
      * "I" (GA)   "      "    (JOE DOUGLAS SR):
           IF SP-RBFRMLA(REB-SUB) = "H" OR "I"
              PERFORM REBATE-GA-SC
           ELSE
      * MULLEN, CA INSURANCE:
           IF SP-RBFRMLA(REB-SUB) = "J"
              PERFORM REBATE-PRORATA-EXACT
           ELSE
      * MULLEN, CA A&H ANTICIPATED:
           IF SP-RBFRMLA(REB-SUB) = "L"
              PERFORM REBATE-CA-AH-ANTICIPATED
           ELSE
      * UVALDE, TX A&H ANTICIPATED:
           IF SP-RBFRMLA(REB-SUB) = "P"
              PERFORM REBATE-TX-ANTICIPATED
           ELSE
      * INSURANCE ACTUARIAL (VA) USING WHOLE MONTHS ONLY:
           IF SP-RBFRMLA(REB-SUB) = "Q"
              PERFORM REBATE-ACTUARIAL-Q
           ELSE
      * TIME LOAN, CA A&H ANTICIPATED:
           IF SP-RBFRMLA(REB-SUB) = "R"
              PERFORM REBATE-CA-ANTICIPATED
           ELSE
      * TIME LOAN, CA 'CL' ANTICIPATED REFUND:
           IF SP-RBFRMLA(REB-SUB) = "S"
              PERFORM REBATE-CA-CL-ANTICIPATED
           ELSE
      * LIBERAL NJ TABLE FORMULA:
           IF SP-RBFRMLA(REB-SUB) = "T"
              PERFORM REBATE-TABLE-FORMULA
           ELSE
      * MERCURY A&H ANTICIPATED :
           IF SP-RBFRMLA(REB-SUB) = "U"
              PERFORM REBATE-MERC-AH-ANTIC
           ELSE
      * MERCURY DAILY PRORATA REFUND OF DEFERMENT CHARGES
           IF SP-RBFRMLA(REB-SUB) = "V" OR "W"
              PERFORM REBATE-MERC-DAILY-PRORATA
           ELSE
           IF SP-RBFRMLA(REB-SUB) = "0"
      *************************************************
      *    SP-RBFRMLA2 FORMULAS   (SP-RBFRMLA ='0')
      *************************************************
      * MERCURY DAILY PRORATA REFUND 'AZ' (RP)
              IF SP-RBFRMLA2(REB-SUB) = "1"
                 PERFORM REBATE-MERC-TO-THE-DAY
              ELSE
      * MULLEN CA, CL NET PAYOFF BALBOA INSURANCE
              IF SP-RBFRMLA2(REB-SUB) = "2"
                 PERFORM REBATE-NET-POFF-MULL
              ELSE
      * MEAN RULE 78THS, REGACC #NA
              IF SP-RBFRMLA2(REB-SUB) = "3"
                 PERFORM RULE-78THS-MEAN
              ELSE
      * TN SHORT RATE, CONSUMERS #1062
              IF SP-RBFRMLA2(REB-SUB) = "4"
                 PERFORM TN-PP-SHORT-RATE
              ELSE
      * TN ACTUARIAL REFUND LIKE 'F' USING EQUIVALENT SIMPLE RATE
              IF SP-RBFRMLA2(REB-SUB) = "6"
                 PERFORM REBATE-ACTUARIAL-F-06
              ELSE
      * CA 'CL' TRUNCATED NET AND 'AH' 14 DAY NON RETRO PROTECTIVE LIFE
              IF SP-RBFRMLA2(REB-SUB) = "7"
                 PERFORM REBATE-MULLEN-CL-AH-07
              ELSE
      * WI CPI REFUND FORMULA
              IF SP-RBFRMLA2(REB-SUB) = "8"
                 PERFORM REBATE-WI-CPI
              ELSE
      * TX INTEREST REFUND FORMULA .....EXCEL
              IF SP-RBFRMLA2(REB-SUB) = "9"
                 PERFORM REBATE-TX-INTEREST
              ELSE
              IF SP-RBFRMLA2(REB-SUB) = "L"
                 PERFORM REBATE-TX-INT-UNITED
              ELSE
              IF SP-RBFRMLA2(REB-SUB) = "H"
                 PERFORM REBATE-TX-INT-0H
              ELSE
              IF SP-RBFRMLA2(REB-SUB) = "I"
                 PERFORM REBATE-TRIST-CPI-0I
              ELSE
      * MEAN OF PRORATE & RULE 78THS ...WORLD
              IF SP-RBFRMLA2(REB-SUB) = "A"
                 PERFORM REBATE-MEAN-OF-PRORATA-78
              ELSE
      * RULE 78THS FOR NET + 1 LIFE REFUND
              IF SP-RBFRMLA2(REB-SUB) = "B"
                 PERFORM REBATE-78-NET-1
              ELSE
      * CA COLLATERAL PROTECTION INSURANCE (SHORT RATE REBATE)
              IF SP-RBFRMLA2(REB-SUB) = "C" OR "K"
                 PERFORM REBATE-SHORT-RATE-0C
              ELSE
      * RULE 78THS USING DAYS IN TERM .........REGACC
              IF SP-RBFRMLA2(REB-SUB) = "D"
                 PERFORM REBATE-78-DAYS-0D
              ELSE
      * ACTUARIAL NET PAYOFF REBATE   .........WORLD
              IF SP-RBFRMLA2(REB-SUB) = "E"
                 PERFORM REBATE-ACTUARIAL-0E
              ELSE
      * TRUE DAILY PRORATE REBATE     .........WORLD
              IF SP-RBFRMLA2(REB-SUB) = "F"
                 PERFORM REBATE-TRUE-PRORATA-0F
              ELSE
      * MF REBATE SAME PROPORTION AS INT & SCHG  ....MEXICO WORLD
              IF SP-RBFRMLA2(REB-SUB) = "G"
                 PERFORM REBATE-SAME-PROP-INT-SC-0G
              ELSE
              IF SP-RBFRMLA2(REB-SUB) = "J"
                 PERFORM REBATE-MX-CL-ANTICIPATED
              ELSE
              IF SP-RBFRMLA2(REB-SUB) = "M"
                 PERFORM REBATE-INS-ANTICIPATED
              ELSE
              IF SP-RBFRMLA2(REB-SUB) = "N" OR "P"
                 PERFORM REBATE-ANTICIPATED-N-P
              ELSE
              IF SP-RBFRMLA2(REB-SUB) = "O"
                 PERFORM REBATE-ANTICIPATED-O.

       REBATE-END-ADJ.
           PERFORM REBATE-ADJUST-REB-REBATE.

       REBATE-EXIT.
           IF BR-FORCE-VALID-CITY-COUNTY = "Y"
      *      CREDIT LIFE SALES TAX:
              IF REB-SUB < 5
                 COMPUTE INS-TAX-RB ROUNDED =
                    LN-FEEAMT(5) * REB-REBATE / REB-TOTCHG
                 ADD INS-TAX-RB TO REB-REBATE
              ELSE
      *      A&H SALES TAX:
              IF REB-SUB = 5
                 COMPUTE INS-TAX-RB ROUNDED =
                    LN-FEEAMT(6) * REB-REBATE / REB-TOTCHG
                 ADD INS-TAX-RB TO REB-REBATE
              ELSE
      *      PP & PP2 SALES TAX:
              IF REB-SUB = 6 OR 11
                 COMPUTE INS-TAX-RB ROUNDED =
                    LN-FEEAMT(7) * REB-REBATE / REB-TOTCHG
                 ADD INS-TAX-RB TO REB-REBATE
              ELSE
      *      O1 OR O2 OR O3 OR O4 OR O5 SALES TAX:
              IF REB-SUB > 11 AND < 17
                 COMPUTE INS-TAX-RB ROUNDED =
                    LN-FEEAMT(REB-SUB - 4) * REB-REBATE / REB-TOTCHG
                 ADD INS-TAX-RB TO REB-REBATE.

           MOVE 0 TO REB-STATEWK.
           MOVE SPACES TO REB-SKIPDEF-FG
                          REB-ADDON-FG.


      *********************************
      *    RULE OF 78'S ROUTINE   (1)
      *********************************
       REBATE-78 SECTION.
            IF SP-RBEARLY(REB-SUB) NOT = 4
               PERFORM REBATE-78-ROUTINE
               GO TO REBATE-78-EXIT.

      * TEST FOR GEORGIA NORMAL REBATE FOR INTEREST:
            IF NOT (REB-LPTRCD = "PB" OR "RN" OR "SC"
                                    OR "RB" OR "RO")
               PERFORM REBATE-78-ROUTINE
               GO TO REBATE-78-EXIT.

      * TEST FOR GEORGIA RENEWAL PRORATION:
            PERFORM REBATE-78-ROUTINE.
            MOVE REB-REBATE TO REB-STATEWK.
            SUBTRACT 1 FROM REB-REMTERM.
            PERFORM REBATE-78-ROUTINE.

            COMPUTE REB-REBATE ROUNDED = REB-REBATE +
                (REB-STATEWK - REB-REBATE ) / 30 * REB-ELAPSED-REM.
            GO TO REBATE-78-EXIT.


       REBATE-78-EXIT.
           EXIT.

      ******************************************
      *    ACTUARIAL REBATE ROUTINE  2
      ******************************************
       REBATE-ACTUARIAL-2 SECTION.
           MOVE 0 TO REB-REBATE.
           IF REB-LN-APRATE = 0
              GO TO REBATE-ACTUARIAL-2-EXIT.

      * TEST FOR INCREASE OF REMAINING TERM BY DEFERMENTS:
           IF REB-SKIPDEF-FG = " "
              ADD REB-LN-TOTNODEF TO REB-REMTERM.

      * SET FULL REBATE IF NO TIME ELAPSED:
           IF REB-REMTERM > REB-ORGTERM
              MOVE REB-TOTCHG TO REB-REBATE
              GO TO REBATE-ACTUARIAL-2-EXIT.

      * TEST FOR PAST MATURITY:
           IF REB-REMTERM < 0
              GO TO REBATE-ACTUARIAL-2-EXIT.

      * APR PER PMT INTERVAL:
           COMPUTE REB-PRATE =
              (REB-LN-APRATE / 100) / 12.

      * COMPOUND INTEREST FACTOR:
           COMPUTE REB-FACTOR ROUNDED =
              1 / ((1 + REB-PRATE) ** REB-REMTERM).

      * COMPUTE BALANCE OWING EXCLUDING PARTIAL MONTHS:
           COMPUTE REB-BAL ROUNDED =
              REB-LN-REGPYAMT * ((1 - REB-FACTOR) / REB-PRATE).

      * INTEREST FOR PARTIAL MONTHS (SP-YEARTYPE OK HERE):
           MOVE 360 TO ELAPSED-YRTYPE.
           IF SP-RBYRTYPE(REB-SUB REB-SUB2) = 365
              MOVE 365 TO ELAPSED-YRTYPE.
           IF REB-ELAPSED-REM NOT = 0
              COMPUTE REB-BAL ROUNDED = REB-BAL *
                 (1 + REB-ELAPSED-REM * (REB-PRATE * 12)
                                           / ELAPSED-YRTYPE).
      * COMPUTE REBATE AMOUNT:
           COMPUTE REB-REBATE =
                       (REB-LN-REGPYAMT * REB-REMTERM) - REB-BAL.

           IF SP-RBSPOPT1(REB-SUB) = 20
      * (A) 10 % OF AMT FINANCED. IF > $75, THEN $75, IF < $75,
      *     THEN 10% AMT FINANCED.
              PERFORM LOAN-CALCULATIONS
              COMPUTE REB-WORKER ROUNDED = FINANCED-AMOUNT *
                                            .10
              IF REB-WORKER > 75.00
                 MOVE 75.00 TO REB-WORKER
              END-IF
      * (B) COMPUTE INTEREST EARNED
      * (C) TAKE THE LARGER FIGURE
              IF REB-WORKER > (REB-TOTCHG - REB-REBATE)
                 COMPUTE REB-REBATE = REB-TOTCHG - REB-WORKER.


       REBATE-ACTUARIAL-2-EXIT.
           EXIT.

      *********************************
      *   ANTICIPATED 78 REBATE  (4)
      *********************************
       REBATE-ANTICIPATED SECTION.
      * UNIT CHARGE
           IF LN-OVRDRATE = 999.999
              MOVE SP-CAL-RATE(1,1) TO REB-PRATE
           ELSE
              MOVE LN-OVRDRATE TO REB-PRATE.
           COMPUTE REB-SUMORG =
                         (REB-PRATE * REB-LN-REGPYAMT) / 12.
           PERFORM REBATE-78-REBATE.
       REBATE-ANTICIPATED-EXIT.
           EXIT.

      *********************************
      *    PRO RATA REBATE METHOD  (5)
      *********************************
       REBATE-PRORATA SECTION.
           IF ( NOT (SP-RBEARLY(REB-SUB) = 5 OR 6) )
            OR
              ( ( SP-RBEARLY(REB-SUB) = 5 OR 6 ) AND
                               ( REB-ELAPSED-REM =  0 ) )
              PERFORM REBATE-PRORATA-ROUTINE
              GO TO REBATE-PRORATA-EXIT.

           IF SP-RBEARLY(REB-SUB) = 5
              PERFORM REBATE-PRORATA-GA-SWING
           ELSE
              PERFORM REBATE-PRORATA-GA-NO-SWING.
           GO TO REBATE-PRORATA-EXIT.

      * GEORGIA PRORATA / RULE 78 SLICE W/SWING FOR MAINT. FEES:
       REBATE-PRORATA-GA-SWING.
           PERFORM REBATE-PRORATA-ROUTINE.
           MOVE REB-REBATE TO REB-STATEWK.
           MOVE REB-TOTCHG TO REB-STATEWK2.
           MOVE LN-MAINTFEE TO REB-TOTCHG.
           PERFORM REBATE-78.
           MOVE REB-STATEWK2 TO REB-TOTCHG.
           COMPUTE REB-REBATE = REB-STATEWK
                          + (LN-MAINTFEE - REB-REBATE).

      * GEORGIA PRORATA / RULE 78 SLICE W/NO SWING FOR MAINT. FEES:
       REBATE-PRORATA-GA-NO-SWING.
           PERFORM REBATE-PRORATA-ROUTINE.
           IF REB-ELAPSED-REM NOT = 30
              COMPUTE REB-REBATE ROUNDED = REB-REBATE -
                  ( LN-MAINTFEE - (30 - REB-ELAPSED-REM)
                          * (30 - REB-ELAPSED-REM + 1)
                                        / 930 * LN-MAINTFEE ).

       REBATE-PRORATA-ROUTINE.
           IF REB-ORGTERM NOT = 0
              COMPUTE REB-REBATE ROUNDED =
                  (REB-TOTCHG - REB-DEDCHG) *
                         (REB-REMTERM / REB-ORGTERM).


       REBATE-PRORATA-EXIT.
           EXIT.

      *********************************
      *    ACTUARIAL (IB)     (6)
      *
      *    (INTEREST BEARING METHOD)
      *********************************
       REBATE-ACTUARIAL-IB SECTION.
           PERFORM LOAN-CALCULATIONS.
           IF PRINCIPLE < TOTAL-NOTE
              COMPUTE REB-REBATE ROUNDED =
                   (TOTAL-NOTE - PRINCIPLE - LN-TOTINT)
                   / (TOTAL-NOTE - PRINCIPLE) * REB-TOTCHG.
       REBATE-ACTUARIAL-IB-EXIT.
           EXIT.

      ****************************************************************
      *    SECURITY OF AMERICA   ACTUARIAL  (7 OR B)
      *
      *    NOTE:
      *          7 - USES A.P.R.
      *          B - USES EQUIVALENT SIMPLE RATE
      *
      *        1
      *    ------------   + IT - 1
      *     (1 + I)**T
      *   --------------------------  *  REB-TOTCHG
      *        1
      *    ------------   + IN - 1
      *     (1 + I)**N
      *
      * WHERE:   T = REMAINING TERM OF INSURANCE, IN MONTHS
      *          N = ORIGINAL TERM OF INSURANCE, IN MONTHS
      *          I = APR OR SMP RATE OF LOAN DIVIDED BY 1200
      ****************************************************************
       REBATE-ACTUARIAL-7-B SECTION.
           IF SP-RBFRMLA(REB-SUB) = "7"
              MOVE REB-LN-APRATE TO REB-MPRATE
           ELSE
              MOVE LN-SMPRATE TO REB-MPRATE.

           COMPUTE REB-MPRATE ROUNDED = REB-MPRATE / 1200.

      *MEXICO SEMI-MONTHLY ACTUARIAL REBATE USING UNIT PERIODS PER YEAR
      *TO DETERMINE RATE INSTEAD OF A MONTHLY RATE USING FORMULA 'B'
           IF SP-ORGST = "MX"
              IF REB-SUB = 7
                 IF SP-RBUNITPER-CD(REB-SUB) = "B"
                    IF SP-RBFRMLA(REB-SUB) = "B"
                       MOVE LN-SMPRATE TO REB-MPRATE
                        COMPUTE REB-MPRATE = REB-MPRATE * .01
                                 / UPER-UNITPER-PER-YEAR.


           COMPUTE REB-REBATE ROUNDED =
                 (1 / ((1 + REB-MPRATE) ** REB-REMTERM) +
                               (REB-MPRATE * REB-REMTERM) - 1
                 )
                 /
                 (1 / ((1 + REB-MPRATE) ** REB-ORGTERM) +
                             (REB-MPRATE * REB-ORGTERM) - 1
                 )
                 * REB-TOTCHG.

      ****************************************************************
      *    INSURANCE REFUND   ACTUARIAL  (Q)
      *        VIRGINIA
      *
      ****************************************************************
       REBATE-ACTUARIAL-Q SECTION.
           IF SP-RBSPOPT1(REB-SUB) = 26
              COMPUTE REBI ROUNDED = LN-APRATE / 1200
           ELSE
              COMPUTE REBI ROUNDED = LN-SMPRATE / 1200.
           COMPUTE REB-REBATE ROUNDED =
               REB-TOTCHG
               *
               ( REB-REMTERM
                  -
                    (
                     (1 - 1 / (1 + REBI) ** (LN-ORGTERM
                                              - REB-ORGTERM
                                                + REB-REMTERM)
                     ) / REBI
                    )
                  +
                    (
                     (1 - 1 / (1 + REBI) ** (LN-ORGTERM
                                              - REB-ORGTERM)
                     ) / REBI
                    )
               )
               /
               ( REB-ORGTERM
                  -
                    (
                     (1 - 1 / (1 + REBI) ** (LN-ORGTERM)) / REBI
                    )
                  +
                    (
                     (1 - 1 / (1 + REBI) ** (LN-ORGTERM
                                              - REB-ORGTERM)
                     ) / REBI
                    )
               ).

      *****************************************************
      *    INSURANCE ANTICIPATED
      *      PACESETTER "TX" (8)
      *      WORLD "TX"      RBFRMLA2 = "M"   JUST LIKE 8 EXCEPT DOES
      *                                       NOT TRUNCATE RATE FROM TABLE
      *
      *    THIS ROUTINE CALCULATES TO INSURANCE  PREMIUM
      *    TO COVER THE REMAINING TERM ON THE COVERAGE
      *    REMAINING, AND REBATES THAT AMOUNT.
      *****************************************************
       REBATE-INS-ANTICIPATED SECTION.
           MOVE REB-SUB TO REB-INSSUB.
           PERFORM REBATE-FIND-INS-RATE.

      *    CREDIT LIFE
           IF REB-SUB < 5
              MOVE 1 TO ITRM-SUB
           ELSE
      *       A&H & PP
              IF REB-SUB < 7
                 SUBTRACT 3 FROM REB-SUB GIVING ITRM-SUB
              ELSE
      *          O1, O2, O3, O4, O5
                 SUBTRACT 8 FROM REB-SUB GIVING ITRM-SUB.

           PERFORM INS-TERM-CALCULATION.

      * DO NOT TRUNCATE THE RATE FROM THE SPR TABLE !
           IF SP-RBFRMLA2(REB-SUB) = "M"
              COMPUTE REB-REBATE ROUNDED =
                  REB-LN-INSCOVR / ITRM-INSTERM
                      * REB-REMTERM * REB-MANYDECIMALS / 100
           ELSE
              COMPUTE REB-REBATE ROUNDED =
                  REB-LN-INSCOVR / ITRM-INSTERM
                      * REB-REMTERM * REB-HUNDREDS / 100.

       REBATE-INS-ANTICIPATED-EXIT.
           EXIT.

      *****************************************************
      *    INSURANCE ANTICIPATED
      *      CFSC - ASSURANT - STATE OF IN
      *
      * N - DISCOUNT RATE .037
      * P - DISCOUNT RATE .0142
      *
      *    THIS ROUTINE CALCULATES TO INSURANCE  PREMIUM
      *    TO COVER THE REMAINING TERM ON THE COVERAGE
      *    REMAINING, AND REBATES THAT AMOUNT.
      *
      *****************************************************
       REBATE-ANTICIPATED-N-P SECTION.

      *--->MOVE REB-SUB TO REB-INSSUB.
      *--->PERFORM REBATE-FIND-INS-RATE.

           COMPUTE REBM = ITRM-INSTERM - REB-ELAPSED-MONTHS.
           COMPUTE REBR = LN-ORGTERM - ITRM-INSTERM.

           IF SP-RBFRMLA2(REB-SUB) = "P"
              COMPUTE REBJ = (( 1 + 0.0142)**(1/12)) - 1
           ELSE
              COMPUTE REBJ = (( 1 + 0.037)**(1/12)) - 1.
           COMPUTE REBWK1 = 1 / (( 1 + REBJ ) ** ITRM-INSTERM).
           COMPUTE REBWK2 = 1 / (( 1 + REBJ ) ** REBM).
           COMPUTE REBAN-M-1 = (1 - (1/(1+REBJ)) ** ITRM-INSTERM) / REBJ.
           COMPUTE REBAN-M-1-T = (1 - (1/(1+REBJ))** REBM) / REBJ.

           COMPUTE REB-REBATE ROUNDED =
                REB-TOTCHG *
                ((REBM - (REBR * REBWK2) - REBAN-M-1-T) /
                 (LN-ORGTERM - (REBR * REBWK1)  - REBAN-M-1)).

       REBATE-ANTICIPATED-N-EXIT.
           EXIT.

      **********************************************************************
      *    "O": RULE OF ANTICIPATION FOR LIFE REFUND - STATE OF TEXAS
      *
      *      GROSS LIFE COVERAGE, SINGLE PREMIUM PER $100 PER YEAR STATES,
      *                               WITH DISCOUNT
      *
      * DEFINITIONS:
      *
      * DS-RATE= .035   FIXED
      * OP     = ORIGINAL PREMIUM (REB-TOTCHG)
      * M      = ORIG INS TERM IN MONTHS (ITRM-INSTERM)
      * T      = ELAPSED MONTHS FROM INCEPTION TO REFUND (REB-ELAPSED-MONTHS)
      * DSRATE = DISCOUNT RATE (REB-MANYDECIMALS)
      *
      *                      1 + (DSRATE * M)/24     (M-T)*(M-T)
      * REFUND = ORIG PREM * --------------------- * -----------
      *                      1 + (DSRATE * M-T)/24     (M * M)
      *
      **********************************************************************
       REBATE-ANTICIPATED-O SECTION.

           MOVE REB-SUB TO REB-INSSUB.
           PERFORM REBATE-FIND-INS-RATE.

           COMPUTE REBM = ITRM-INSTERM - REB-ELAPSED-MONTHS.

           COMPUTE REBWK1 = (1 + (.035 * ITRM-INSTERM)/24)/
                            (1 + (.035 * REBM)/24).

           COMPUTE REBWK2 = (REBM*REBM)/(ITRM-INSTERM*ITRM-INSTERM).

           COMPUTE REB-REBATE ROUNDED = REB-TOTCHG * REBWK1 * REBWK2.

       REBATE-ANTICIPATED-O-EXIT.
           EXIT.

      ******************************************
      *    ACTUARIAL REBATE ROUTINE   C
      ******************************************
      * ROUTINE IS EXCLUSIVELY FOR INTEREST:

       REBATE-ACTUARIAL-C SECTION.
           MOVE 0 TO REB-REBATE.

      * GET EQUIVALENT SIMPLE NUMBERS:
           PERFORM APRS-RATE.
           IF APRS-SMPRATE = 0
              GO TO REBATE-ACTUARIAL-C-EXIT.

      * TEST FOR INCREASE OF REMAINING TERM BY DEFERMENTS:
           IF REB-SKIPDEF-FG = " "
              ADD REB-LN-TOTNODEF TO REB-REMTERM.

      * SET FULL REBATE IF NO TIME HAS ELAPSED:
           IF REB-REMTERM > REB-ORGTERM
              MOVE REB-TOTCHG TO REB-REBATE
              GO TO REBATE-ACTUARIAL-C-EXIT.

      * TEST FOR PAST MATURITY:
           IF REB-REMTERM < 0
              GO TO REBATE-ACTUARIAL-C-EXIT.

      * APR PER PMT INTERVAL:
           COMPUTE REB-PRATE = (APRS-SMPRATE / 100) / 12.

      * COMPOUND INTEREST FACTOR:
           COMPUTE REB-FACTOR ROUNDED =
              1 / (1 + REB-PRATE) ** REB-REMTERM.


      * COMPUTE BALANCE OWING EXCLUDING PARTIAL MONTHS:
           COMPUTE REB-BAL ROUNDED =
                  APRS-REGPYAMT * ((1 - REB-FACTOR) / REB-PRATE).

      * INTEREST FOR PARTIAL MONTHS (SP-YEARTYPE OK HERE):
           MOVE 360 TO ELAPSED-YRTYPE.
           IF SP-RBYRTYPE(REB-SUB REB-SUB2) = 365
              MOVE 365 TO ELAPSED-YRTYPE.
           IF REB-ELAPSED-REM NOT = 0
              COMPUTE REB-BAL ROUNDED = REB-BAL *
                 (1 + REB-ELAPSED-REM * (REB-PRATE * 12)
                                           / ELAPSED-YRTYPE).

      * COMPUTE REBATE AMOUNT:
           COMPUTE REB-REBATE =
                       (APRS-REGPYAMT * REB-REMTERM) - REB-BAL.
       REBATE-ACTUARIAL-C-EXIT.
           EXIT.

      ******************************************
      *    ACTUARIAL REBATE ROUTINE  D
      *               IOWA
      ******************************************
       REBATE-ACTUARIAL-D SECTION.
           MOVE 0 TO REB-REBATE.
           IF REB-LN-APRATE = 0
              GO TO REBATE-ACTUARIAL-D-EXIT.

      * TEST FOR INCREASE OF REMAINING TERM BY DEFERMENTS:
           IF REB-SKIPDEF-FG = " "
              ADD REB-LN-TOTNODEF TO REB-REMTERM.

      * SET FULL REBATE IF NO TIME ELAPSED:
           IF REB-REMTERM > REB-ORGTERM
              MOVE REB-TOTCHG TO REB-REBATE
              GO TO REBATE-ACTUARIAL-D-EXIT.

      * TEST FOR PAST MATURITY:
           IF REB-REMTERM < 0
              GO TO REBATE-ACTUARIAL-D-EXIT.

      * SET ELAPSED-YRTYPE:
           MOVE 360 TO ELAPSED-YRTYPE.
           IF SP-RBYRTYPE(REB-SUB REB-SUB2) = 365
              MOVE 365 TO ELAPSED-YRTYPE.

      * DETERMINE NO OF DAYS BASED ON PAYDATE THRU NEXT PAY:
           IF REB-ELAPSED-REM NOT = 0
              MOVE REB-LN-INTDATE TO NUM-DATE
              MOVE REB-PAYDATE TO NDTE-DATE
              IF NDTE-DD < NUM-DA
                 MOVE NUM-DA TO NDTE-DD
              ELSE
                 MOVE NUM-DA TO NDTE-DD
                 MOVE 1 TO NDTE-HOLD
                 PERFORM INCREMENT-MONTHS.
           IF REB-ELAPSED-REM NOT = 0
              MOVE REB-PAYDATE TO NUM-DATE
              MOVE NDTE-DATE TO SYS-DATE
              PERFORM TIMALL
              COMPUTE REB-ELAPSED-REM =
                       (ELAPSED-YRTYPE / 12) - ELAPSED-DAYS.

      * APR PER PMT INTERVAL:
           COMPUTE REB-PRATE = (REB-LN-APRATE / 100) / 12.

      * COMPOUND INTEREST FACTOR:
           COMPUTE REB-FACTOR ROUNDED =
              1 / ((1 + REB-PRATE) ** REB-REMTERM).

      * COMPUTE BALANCE OWING EXCLUDING PARTIAL MONTHS:
           COMPUTE REB-BAL ROUNDED =
              REB-LN-REGPYAMT * ((1 - REB-FACTOR) / REB-PRATE).

      * INTEREST FOR PARTIAL MONTHS (SP-YEARTYPE OK HERE):
           IF REB-ELAPSED-REM NOT = 0
              COMPUTE REB-BAL ROUNDED = REB-BAL *
                 (1 + REB-ELAPSED-REM * (REB-PRATE * 12)
                                           / ELAPSED-YRTYPE).
      * COMPUTE REBATE AMOUNT:
           COMPUTE REB-REBATE =
                       (REB-LN-REGPYAMT * REB-REMTERM) - REB-BAL.
       REBATE-ACTUARIAL-D-EXIT.
           EXIT.

      ****************************************************************
      *    DEFERMENT REBATE ROUTINE  E   (01)
      *              TENNESSEE
      *    DEFERMENT REBATE ROUTINE  O   (01)
      *              TENNESSEE                   (WITH PAY SCHEDULES)
      *    DEFERMENT REBATE ROUTINE  K   (02)
      *              INDIANA
      *    DEFERMENT REBATE ROUTINE  M   (03)
      *              CALIFORNIA
      *    DEFERMENT REBATE ROUTINE  Y   (04)
      *              S. CAROLINA                 (MERCURY)
      *    DEFERMENT REBATE ROUTINE  Z   (05)
      *              ARIZONA                     (MERCURY)
      *    DEFERMENT REBATE ROUTINE  0 5 (06)
      *              WISCONSIN                   (CITIZENS)
      ****************************************************************
       REBATE-DEFERMENT-EOKMYZ05 SECTION.
           IF REB-LN-TOTNODEF = 0
              GO TO REBATE-DEFERMENT-EOKMYZ05-EXIT.

      * CALL SUBPROGRAM TO DETERMINE REDUCTION:
           MOVE "B" TO REB-FORM-PROG-TYPE.
           IF SP-RBFRMLA(REB-SUB) = "E" OR "O"
              MOVE 01 TO REB-FORM-PROG-NO
              MOVE SP-RBFRMLA(REB-SUB) TO B01-FRMLA
              MOVE LN-PAY-SCHLD-FG TO B01-PAY-SCHLD-FG
              MOVE REB-DEF-STOP TO B01-DEF-STOP
           ELSE
           IF SP-RBFRMLA(REB-SUB) = "K"
              MOVE 02 TO REB-FORM-PROG-NO
              MOVE REB-DEF-STOP TO B02-DEF-STOP
           ELSE
           IF SP-RBFRMLA(REB-SUB) = "M"
              MOVE 03 TO REB-FORM-PROG-NO
              MOVE REB-DEF-STOP TO B01-DEF-STOP
           ELSE
           IF SP-RBFRMLA(REB-SUB) = "Y"
              MOVE 04 TO REB-FORM-PROG-NO
           ELSE
           IF SP-RBFRMLA(REB-SUB) = "Z"
              MOVE 05 TO REB-FORM-PROG-NO
           ELSE
           IF SP-RBFRMLA(REB-SUB)  = "0" AND
              SP-RBFRMLA2(REB-SUB) = "5"
              MOVE 06 TO REB-FORM-PROG-NO.

           MOVE REB-PAYDATE TO B01-PAYDATE.
           PERFORM REBATE-CALL-SUBPROG.
           IF REB-COMMON-RTNCD = " " OR "E"
              MOVE REB-COMMON-REBATE TO REB-REBATE.

       REBATE-DEFERMENT-EOKMYZ05-EXIT.
           EXIT.

      *********************************************************************
      *    ACTUARIAL REBATE ROUTINE  F-06
      *    LOGIC FOR RBFRMLA = 'F' AND '0 6'
      *
      *         TRUE ACTUARIAL REBATING ROUTINE:
      *
      * THIS ROUTINES USING THE APR OR ESR RATE
      * AND TAKES INTO ACCOUNT 1ST PAYMENT EXTENSION DAYS (+ OR - NO LIMIT)
      * 1ST PAYMENT CHARGES (ODD 1ST PAYMENTS)
      *********************************************************************
       REBATE-ACTUARIAL-F-06 SECTION.
           MOVE REB-LN-ORIG-1STPYDATE  TO RBACT-ORIG-1STPAY.
           MOVE REB-LN-INTDATE      TO RBACT-INTDATE.
           IF SP-RBFRMLA(REB-SUB) = "F"
              MOVE REB-LN-APRATE    TO RBACT-RATE
           ELSE
              MOVE REB-LN-SMPRATE   TO RBACT-RATE.
           MOVE REB-LN-TOTNODEF     TO RBACT-TOTNODEF.
           MOVE REB-LN-ORGTERM      TO RBACT-ORGTERM.
           MOVE REB-LN-1STPYAMT     TO RBACT-1STPYAMT.
           MOVE REB-LN-REGPYAMT     TO RBACT-REGPYAMT.
           MOVE REB-LN-LASTPYAMT    TO RBACT-LASTPYAMT.
           MOVE LN-UNITPER-CD       TO RBACT-UNITPER-CD.
           MOVE LN-UNITPER-FREQ     TO RBACT-UNITPER-FREQ.
           MOVE REB-TOTCHG          TO RBACT-TOTCHG.
           MOVE REB-PAYDATE         TO RBACT-RBDATE.
           MOVE REB-ELAPSED-MONTHS  TO RBACT-ELAPSED-MONTHS.
           MOVE REB-ELAPSED-REM     TO RBACT-ELAPSED-REM.
           MOVE REB-ELAPSED-DAYS    TO RBACT-ELAPSED-DAYS.
           MOVE REB-SKIPDEF-FG      TO RBACT-SKIPDEF-FG.
           MOVE REB-EARN-FULL-MONTH TO RBACT-EARN-FULL-MONTH.
           MOVE REB-SUB             TO RBACT-SUB.
           MOVE REB-SUB2            TO RBACT-SUB2.
           MOVE " "                 TO RBACT-PENALTY-FG.

           PERFORM RBACT-ACTUARIAL.

           MOVE RBACT-REBATE     TO REB-REBATE.
           MOVE RBACT-PRES-VALUE TO REB-PRES-VALUE.

       REBATE-ACTUARIAL-F-EXIT.
           EXIT.

      ***************************************************
      *      O L D    R O U T I N E
      *
      *    ACTUARIAL REBATE ROUTINE  F
      *
      *         TRUE ACTUARIAL REBATING ROUTINE:
      *
      * THIS ROUTINE USINGS THE APR AND TAKES INTO ACCOUNT
      * 1ST PAYMENT EXTENSION DAYS (+ OR - NO LIMIT)
      * 1ST PAYMENT CHARGES (ODD 1ST PAYMENTS)
      * DEFERMENTS HAVE NO IMPACT ON CALCULATIONS
      ***************************************************
      *REBATE-ACTUARIAL-F SECTION.
      *    IF REB-LN-APRATE = 0
      *       GO TO REBATE-ACTUARIAL-F-EXIT.

      ********************************************************
      *    DETERMINE IF FIRST PAYMENT EXTENSIONS
      *    ARE NOT CHARGED INTEREST AND DISCLOSURE IS
      *    IS DONE FOR SALES FINANCE VIA: 'B', IF SO SET
      *    INTEREST DATE , ONE MONTH PRIOR TO ORIG 1ST PAY
      *    AND ESTABLISH REBF-ADJ FOR DAILY INTEREST
      ********************************************************
      *    MOVE 1 TO REBF-ADJ.
      *    IF SP-RBSPOPT1-FIRST-30(REB-SUB)
      *       MOVE REB-LN-ORIG-1STPYDATE TO NDTE-DATE
      *       MOVE -1 TO NDTE-HOLD
      *       PERFORM INCREMENT-MONTHS
      *       MOVE REB-LN-INTDATE TO NUM-DATE
      *       MOVE NDTE-DATE TO SYS-DATE
      *       PERFORM CMPDAT
      *       IF SYS-IS-GT
      *          MOVE NDTE-DATE TO REB-LN-INTDATE
      *       END-IF
      *       IF SP-INTMETHOD = "E"
      *          COMPUTE REBF-ADJ ROUNDED = 360 / 365.

      ***************************************************
      *    DETERMINE IF PAYDATE IS BEFORE INTDATE,
      *    IF SO FULL REBATE
      ***************************************************
      *    MOVE REB-LN-INTDATE TO NUM-DATE.
      *    MOVE REB-PAYDATE TO SYS-DATE.
      *    PERFORM CMPDAT.
      *    IF SYS-IS-LE
      *       MOVE REB-TOTCHG TO REB-REBATE
      *       GO TO REBATE-ACTUARIAL-F-EXIT.

      ***************************************************
      *    TEST FOR SWING RULE
      ***************************************************
      *    MOVE REB-PAYDATE TO REB-REFDATE.
      *    IF REB-ELAPSED-REM = 0
      *       MOVE REB-LN-INTDATE TO NDTE-DATE
      *       MOVE REB-ELAPSED-MONTHS TO NDTE-HOLD
      *       PERFORM INCREMENT-MONTHS
      *       MOVE NDTE-DATE TO REB-REFDATE.

      ***************************************************
      *    DETERMINE ELAPSED TIME REMAINING BETWEEN
      *    PAYDATE AND ORIGINAL MATURITY:
      ***************************************************
      *    MOVE REB-LN-ORIG-1STPYDATE TO NDTE-DATE.
      *    SUBTRACT 1 FROM REB-LN-ORGTERM GIVING NDTE-HOLD.
      *    IF REB-SKIPDEF-FG = " "
      *       IF SP-RBDEFER(REB-SUB) = 1
      *          ADD REB-LN-TOTNODEF TO NDTE-HOLD.
      *    PERFORM INCREMENT-MONTHS.
      *    MOVE REB-REFDATE TO NUM-DATE.
      *    MOVE NDTE-DATE TO SYS-DATE.
      *    MOVE 999 TO ELAPSED-YRTYPE.
      *    PERFORM TIMALL.
      *    IF ELAPSED-DAYS NOT > 0
      *       GO TO REBATE-ACTUARIAL-F-EXIT.

      ***************************************************
      *    SET FRACTIONAL PERIOD FROM PAYDATE TO NEXT
      *    PAYMENT DUE DATE:
      ***************************************************
      *    COMPUTE REBF = ELAPSED-REM / 30 * REBF-ADJ.

      ***************************************************
      *    SET WHOLE PERIOD(S) FROM PAYDATE TO NEXT
      *    PAYMENT DUE DATE:
      ***************************************************
      *    IF ELAPSED-MONTHS NOT < REB-LN-ORGTERM
      *       COMPUTE REBT = ELAPSED-MONTHS
      *                      - REB-LN-ORGTERM + 1
      *    ELSE
      *       IF REBF = 0
      *          MOVE 1 TO REBT
      *       ELSE
      *          MOVE 0 TO REBT.

      ***************************************************
      *    DETERMINE 1ST PAYMENT REQUIREMENTS:
      ***************************************************
      * ESTABLISH 1ST PAY AMOUNT:
      *    IF REB-LN-ORGTERM = 1
      *       IF REB-LN-1STPYAMT NOT = 0
      *          MOVE REB-LN-1STPYAMT TO REBP1
      *       ELSE
      *          MOVE REB-LN-REGPYAMT TO REBP1.

      *    IF REB-LN-ORGTERM NOT = 1
      *       IF REB-SUB2 = 2
      * ON OR AFTER 1ST PAYDATE:
      *          MOVE REB-LN-REGPYAMT TO REBP1
      *       ELSE
      * BEFORE 1ST PAYDATE:
      *          IF REB-LN-1STPYAMT NOT = 0
      *             MOVE REB-LN-1STPYAMT TO REBP1
      *          ELSE
      *             MOVE REB-LN-REGPYAMT TO REBP1.

      ***************************************************
      *    ESTABLISH ANNUITY PAYMENT AMT AND TERM:
      ***************************************************
      *    IF REB-LN-ORGTERM = 1
      *       MOVE 0 TO REBN-1 REBP
      *    ELSE
      *       IF ELAPSED-MONTHS > 0
      *          MOVE REB-LN-REGPYAMT TO REBP
      *          SUBTRACT REBT FROM ELAPSED-MONTHS
      *                                  GIVING REBN-1
      *       ELSE
      *          MOVE 0 TO REBN-1 REBP.

      *******************************************************
      *    DETERMINE IF SWING TO NEAREST DUE DATE REQUIRED
      *    (MERCURY ARIZONA ACTION 24)
      *******************************************************
      *    IF SP-RBSPOPT1(REB-SUB) = 13
      *       MOVE REB-PAYDATE TO NUM-DATE
      *       MOVE REB-LN-ORIG-1STPYDATE TO SYS-DATE
      *       PERFORM CMPDAT
      *       IF SYS-IS-GE
      *          MOVE REB-LN-INTDATE TO NUM-DATE
      *          PERFORM TIM365
      *          COMPUTE REB-FACTOR ROUNDED =
      *             (REBF + REBT) * 30 / ELAPSED-DAYS
      *          IF REB-FACTOR NOT = 0
      *             IF REB-FACTOR > 0.5
      *                MOVE 0 TO REBF
      *                MOVE 1 TO REBT
      *             ELSE
      *                MOVE 0 TO REBF
      *                          REBT
      *          ELSE
      *             NEXT SENTENCE
      *       ELSE
      *          IF REBF NOT = 0
      *             IF REBF > 0.5
      *                MOVE 0 TO REBF
      *                MOVE 1 TO REBT
      *             ELSE
      *                MOVE 0 TO REBF
      *                          REBT.

      ***************************************************
      *    COMPUTE INT. RATE PER PMT/COMPOUND INTERVAL:
      ***************************************************
      *    COMPUTE REBI ROUNDED =
      *       (REB-LN-APRATE / 100) / 12.

      ***************************************************
      *    COMPUTE ADJUSTMENT FACTOR TO 1ST DUE PAYMENT:
      ***************************************************
      * SINGLE PAY LOANS AND US RULE:
      *    IF REB-LN-ORGTERM < 2 OR SP-DISFRMLA = "C"
      *       COMPUTE REBADJ ROUNDED =
      *          1 / (1 + (REBT + REBF) * REBI)
      *    ELSE
      * ACTUARIAL:
      *       COMPUTE REBADJ ROUNDED =
      *          1 / ((1 + REBF * REBI) * (1 + REBI) ** REBT).

      ***************************************************
      *    COMPUTE PRESENT WORTH OF $1.00 PER PERIOD:
      ***************************************************
      *    COMPUTE REBPW ROUNDED =
      *       (1 - 1 / (1 + REBI) ** REBN-1) / REBI.

      ***************************************************
      *    COMPUTE PRESENT VALUE OF REMAINING PAYMENTS
      *    AT THE TIME OF REBATE:
      ***************************************************
      *    COMPUTE REBPV ROUNDED =
      *       REBADJ * (REBP1 + REBP * REBPW).
      *    MOVE REBPV TO REB-PRES-VALUE.

      ***************************************************
      * COMPUTE REBATE AMOUNT:
      ***************************************************
      *    COMPUTE REB-REBATE ROUNDED =
      *       (REBP1 + REBP * REBN-1) - REBPV.
      *REBATE-ACTUARIAL-F-EXIT.
      *    EXIT.

      *****************************************************
      *    MASS. A & H ANTICIPATED     (G)
      *
      *    THIS ROUTINE CALCULATES THE INSURANCE PREMIUM
      *    TO COVER THE REMAINING TERM ON THE COVERAGE
      *    REMAINING, AND REBATES THAT AMOUNT.
      *****************************************************
       REBATE-MA-AH-ANTICIPATED SECTION.
           MOVE REB-AHSUB TO REB-INSSUB.
           PERFORM REBATE-FIND-INS-RATE.

           COMPUTE REB-REBATE ROUNDED = REB-LN-REGPYAMT *
                          REB-HUNDREDS * REB-REMTERM / 100.

       REBATE-MA-AH-ANTICIPATED-EXIT.
           EXIT.

      *****************************************************
      *                     (GA)
      *    SERCHG REBATE FRMLA   H -   ROGER WILLIAMS
      *                          I -   JOE DOUGLAS SR
      *
      *    THIS ROUTINE CALCULATES THE SERVICE CHARGE
      *    REBATE, INSURING INTEREST AND SERVICE CHG
      *    EARNINGS DON'T EXCEED 5% PER MONTH.
      *****************************************************
       REBATE-GA-SC SECTION.
      * COMPUTE ORIGINAL BALANCE:
           IF SP-RBFRMLA(REB-SUB) = "H"
              COMPUTE REB-REBATE = LN-LNAMT - LN-INTCHG
                        - LN-SERCHG - (LN-MAINTFEE * REB-LN-ORGTERM)
           ELSE
              COMPUTE REB-REBATE = LN-LNAMT
                         - (LN-MAINTFEE * REB-LN-ORGTERM).

      * IF SCFRMLA 54 DO NOT INCLUDE 4% CLOSING FEE WHEN CALCULATING
      *  REBATE FOR REMAINING PART OF THE SC.
           IF SP-RBFRMLA(REB-SUB) = "I" AND SP-SCFRMLA = 54
              SUBTRACT LN-POINTS FROM REB-REBATE.

      * COMPUTE MONTHLY PAYMENT:
           COMPUTE REB-WORKER = REB-LN-REGPYAMT - LN-MAINTFEE.

      * COMPUTE MAXIMUM EARNINGS (INT + SER):
           COMPUTE REB-REBATE ROUNDED = .05 *
             ( (REB-ELAPSED-MONTHS * REB-REBATE) -
                (REB-ELAPSED-MONTHS - 1) * REB-ELAPSED-MONTHS / 2
                    * REB-WORKER).

      * COMPUTE REBATE OF SERCHG TAKING MAXIMUM (SC) INCOME:
           IF LN-ANTICADJ(1) NOT = 0
              MOVE LN-ANTICADJ(1) TO REB-STATEWK.

           COMPUTE REB-REBATE = (LN-SERCHG - LN-POINTS)
                    - ( REB-REBATE - (LN-INTCHG - REB-STATEWK) ).

           IF REB-REBATE < 0
              MOVE 0 TO REB-REBATE.

      *  IF SERVICE CHARGE FORMULA 54 - GA 4% CLOSING FEE,
      *  ADD IN THE AMOUNT OF REBATE CALCULATED BY THE EARLY PROVISION.
           IF SP-RBFRMLA(REB-SUB) = "I" AND SP-SCFRMLA = 54
              PERFORM REBATE-EP54
      *       ADD REB-WORK2 TO REB-REBATE
              ADD REB-EP54-AMT TO REB-REBATE.

       REBATE-GA-SC-EXIT.
           EXIT.

      *****************************************************
      *    CALIFORNIA A & H ANTICIPATED     (L)
      *    MULLEN FINANCE
      *
      *****************************************************
       REBATE-CA-AH-ANTICIPATED SECTION.
           IF REB-ELAPSED-DAYS < 11
              MOVE REB-TOTCHG TO REB-REBATE
              GO TO REBATE-CA-AH-ANTICIPATED-EXIT.
           MOVE REB-AHSUB TO REB-INSSUB.
           PERFORM REBATE-FIND-INS-RATE.

      * COMPUTE NEW COVERAGE ON REMAINING TERM:
           IF SP-RBSPOPT1(REB-SUB) = 11
              COMPUTE REB-BAL ROUNDED =
                 REB-TOTCHG * REB-REMTERM * REB-HUNDREDS
                       / REB-ORGTERM
              MOVE REB-REMTERM TO REB-WORKER
              MOVE REB-ORGTERM TO REB-REMTERM
              PERFORM REBATE-FIND-INS-RATE
              MOVE REB-WORKER TO REB-REMTERM
              COMPUTE REB-UPR ROUNDED =
                            REB-BAL / REB-HUNDREDS
           ELSE
           IF SP-RBSPOPT1(REB-SUB) = 10
              COMPUTE REB-UPR ROUNDED =
                           (REB-LN-INSCOVR / REB-ORGTERM)
                                   * REB-REMTERM / 100
              COMPUTE REB-UPR = REB-UPR * REB-HUNDREDS
           ELSE
              COMPUTE REB-UPR-WHOLE ROUNDED =
                           (REB-LN-INSCOVR / REB-ORGTERM)
                                   * REB-REMTERM / 100 + 0.99
              MOVE REB-UPR-WHOLE TO REB-UPR
              COMPUTE REB-UPR = REB-UPR * REB-HUNDREDS.

           COMPUTE REB-WFAC = (.0000045573 * (REB-ORGTERM ** 2))
                            + (-.001125 * REB-ORGTERM) + .099375.

      * COMPUTE REFUND CHARGE FACTOR:
           IF REB-ORGTERM < 14
              MOVE 0 TO REB-RCF.

           IF REB-ORGTERM > 13 AND < 49
              COMPUTE REB-RCF1 ROUNDED =
                 (1 - (REB-ORGTERM - REB-REMTERM - 1) /
                                              (REB-ORGTERM - 13))
              IF REB-RCF1 < 0
                 MOVE 0 TO REB-RCF
              ELSE
                 COMPUTE REB-RCF ROUNDED = REB-WFAC * REB-RCF1.

           IF REB-ORGTERM > 48
              COMPUTE REB-RCF1 ROUNDED =
                 1 - (REB-ORGTERM - REB-REMTERM - 1) / 35
              IF REB-RCF1 < 0
                 MOVE 0 TO REB-RCF
              ELSE
                 COMPUTE REB-RCF ROUNDED = REB-WFAC * REB-RCF1.


           COMPUTE REB-REBATE ROUNDED =
                                   REB-UPR * (1 - REB-RCF).

       REBATE-CA-AH-ANTICIPATED-EXIT.
           EXIT.

      ***********************************************
      *    PRO RATA, EXACT DAYS REBATE METHOD  (J)
      ***********************************************
       REBATE-PRORATA-EXACT SECTION.
           IF REB-ORGTERM NOT = 0
              COMPUTE REB-REBATE ROUNDED =
                  (REB-TOTCHG - REB-DEDCHG)
                  * ( REB-REMTERM / REB-ORGTERM
                           - REB-ELAPSED-REM
                               / (REB-ORGTERM * 30.41666667)
                    ).

      *****************************************************
      *    TEXAS A & H ANTICIPATED     (P)
      *    UVALDE FINANCE
      *
      *****************************************************
       REBATE-TX-ANTICIPATED SECTION.
           COMPUTE REB-REBATE ROUNDED =
              (REB-TOTCHG - REB-DEDCHG) / 2
               * (
                  (REB-REMTERM * (REB-REMTERM + 1))
                   / (REB-ORGTERM * (REB-ORGTERM + 1))
                      + (REB-REMTERM / REB-ORGTERM)
                 ).

      *****************************************************
      *    CALIFORNIA A & H ANTICIPATED     (R)
      *      NREW FORMULA EFFECTIVE: 05-14-94
      *
      *    THIS ROUTINE CALCULATES TO INSURANCE PREMIUM
      *    TO COVER THE REMAINING TERM ON THE COVERAGE
      *    REMAINING, AND REBATES THAT AMOUNT.
      *****************************************************
       REBATE-CA-ANTICIPATED SECTION.
           MOVE 2 TO ITRM-SUB.
           PERFORM INS-TERM-CALCULATION.

           MOVE REB-REMTERM TO REB-WORKER.
           MOVE ITRM-INSTERM TO REB-REMTERM.
           MOVE REB-AHSUB TO REB-INSSUB.
           PERFORM REBATE-FIND-INS-RATE.
           MOVE REB-WORKER TO REB-REMTERM.

           COMPUTE REBF ROUNDED =
               REB-TOTCHG / (ITRM-INSTERM * REB-MANYDECIMALS).

           MOVE REB-AHSUB TO REB-INSSUB.
           PERFORM REBATE-FIND-INS-RATE.

           COMPUTE REB-REBATE ROUNDED =
               REBF * REB-REMTERM * REB-MANYDECIMALS.

       REBATE-CA-ANTICIPATED-EXIT.
           EXIT.

      *****************************************************
      *    CALIFORNIA 'CL' ANTICIPATED     (S)
      *    COSMO
      *
      *****************************************************
       REBATE-CA-CL-ANTICIPATED SECTION.
           MOVE LN-ORGTERM TO REBN.
           MOVE REB-ORGTERM TO REBM.
           SUBTRACT 1 FROM REBM GIVING REBM-1.
           SUBTRACT REB-REMTERM FROM REB-ORGTERM GIVING REBT.
           SUBTRACT REBT FROM REBM GIVING REBM-T.

      ***************************************************
      *    COMPUTE INT. RATE PER PMT/COMPOUND INTERVAL:
      ***************************************************
           COMPUTE REBI ROUNDED = REB-LN-APRATE / 1200.
           ADD 1 REBI GIVING REBI1.
      ***************************************************
      *    COMPUTE CA DISCOUNT RATE PER PMT/COMPOUND INTERVAL:
      ***************************************************
           COMPUTE REBJ ROUNDED = 4.2 / 1200.
           ADD 1 REBJ GIVING REBJ1.

      ***************************************************
      *    COMPUTE ANNUITY FACTOR  (M - 1)
      ***************************************************
           COMPUTE REBAN-M-1 ROUNDED =
              (1 - 1 / REBJ1 ** REBM-1) / REBJ.

      ***************************************************
      *    COMPUTE ANNUITY FACTOR  (M - 1 -T)
      ***************************************************
           COMPUTE REBAN-M-1-T ROUNDED =
              (1 - 1 / REBJ1 ** (REBM-T - 1)) / REBJ.

      * COMPUTE REBATE AMOUNT:
           COMPUTE REBWK1 ROUNDED =
               REBJ1 * (REBI1 ** REBM-T - REBJ1 ** REBM-T)
                   / (
                       REBI1 ** (REBN - REBT) * REBJ1 ** REBM-T
                                                  * (REBI - REBJ)
                     ).
           COMPUTE REBWK2 ROUNDED =
               REBJ1 * ( REBI1 ** REBM - REBJ1 ** REBM)
                   / (
                       REBI1 ** REBN * REBJ1 ** REBM
                                         * (REBI - REBJ)
                     ).
           COMPUTE REB-REBATE ROUNDED =
               REB-TOTCHG * ( 1 + REBAN-M-1-T - REBWK1)
                                     / ( 1 + REBAN-M-1 - REBWK2).

       REBATE-CA-CL-ANTICIPATED-EXIT.
           EXIT.

      ******************************************
      *    REBATE TABLE FORMULA (T)
      *              ADDONS
      ******************************************
       REBATE-TABLE-FORMULA SECTION.
           IF SP-RBSPOPT1(REB-SUB) = 0
              GO TO REBATE-TABLE-FORMULA-EXIT.
      * CALL SUBPROGRAM TO DETERMINE REDUCTION:
           MOVE "T" TO REB-FORM-PROG-TYPE.
           MOVE 01 TO REB-FORM-PROG-NO.
           MOVE SP-RBSPOPT1(REB-SUB) TO T01-TBL-NO.
           MOVE REB-ORGTERM TO T01-ORGTERM.
           MOVE REB-ELAPSED-MONTHS TO T01-ELAPSED-MONTHS.
           MOVE REB-ELAPSED-DAYS TO T01-ELAPSED-DAYS.
           MOVE 100 TO T01-REFUND-RATE.
           PERFORM REBATE-CALL-SUBPROG.
           COMPUTE REB-REBATE ROUNDED =
                  REB-TOTCHG * T01-REFUND-RATE / 100.

       REBATE-TABLE-FORMULA-EXIT.
           EXIT.

      *******************************************************************
      *                     FORMULA (0, 2)
      *    MULLEN'S CALIFORNIA CL NET PAYOFF   BALBOA LIFE & CASUALTY
      *******************************************************************
       REBATE-NET-POFF-MULL  SECTION.
           COMPUTE REBJ ROUNDED = 4.2 / 1200.
      * SET LOAN TERM
           SUBTRACT REB-ELAPSED-MONTHS FROM REB-LN-ORGTERM
                                                 GIVING REBN.
           IF REBN NOT > 1
              GO TO REBATE-NET-POFF-MULL-EXIT.

      * SET INSURANCE REMAINING TERM
           MOVE REB-REMTERM TO REBP.

           COMPUTE REB-A--P-J ROUNDED =
                  (1 - 1 / (1 + REBJ) ** REBP) / REBJ.

           COMPUTE REBI ROUNDED = LN-SMPRATE / 1200.
           COMPUTE REB-A--P-I ROUNDED =
               (1 - 1 / (1 + REBI) ** REBP) / REBI.

           COMPUTE REB-WK ROUNDED =
              ( 1 / REBI * (1 + REBJ)
                     * (REB-A--P-J
                           + (REBJ * REB-A--P-J - REBI * REB-A--P-I)
                              / (1 + REBI) ** (REBN - REBP)
                              / (REBI - REBJ)
                       )
              ).

           COMPUTE REB-REBATE ROUNDED =
                 REB-LN-REGPYAMT * SP-INSRATE(REB-SUB 1)
                                           * .001 * REB-WK.
       REBATE-NET-POFF-MULL-EXIT.
           EXIT.

      *******************************************************************
      *                     FORMULA (0, 3)
      *    MEAN RULE OF 78THS REFUND (MEAN)                  REGACC #
      *******************************************************************
       RULE-78THS-MEAN  SECTION.

           IF REB-ORGTERM = REB-REMTERM
              COMPUTE REB-REBATE = REB-TOTCHG - REB-DEDCHG
           ELSE
              COMPUTE REB-SUMREM =
                   REB-REMTERM * (REB-REMTERM + REB-LN-ORGTERM + 2)
              COMPUTE REB-SUMORG =
                   (REB-LN-ORGTERM + 1) * REB-LN-ORGTERM  * 2
              IF REB-SUMORG = 0
                 MOVE 0 TO REB-REBATE
              ELSE
                 COMPUTE REB-REBATE ROUNDED =
                     (REB-TOTCHG - REB-DEDCHG)
                              * REB-SUMREM / REB-SUMORG.
       EXIT-RULE-78THS-MEAN.
           EXIT.

      *******************************************************************
      *                     FORMULA (0, 4)
      *    SHORT RATE 'TN' PP REFUND METHOD            TN, CONSUMER #1067
      *******************************************************************
       TN-PP-SHORT-RATE  SECTION.
           IF REB-ELAPSED-DAYS <= 0
              MOVE REB-TOTCHG TO REB-REBATE
              GO TO EXIT-TN-PP-SHORT-RATE.

           IF REB-LPTRCD = "PB" OR "RN" OR "SC" OR "RB" OR "RO"
              PERFORM REBATE-PRORATA-ROUTINE
              GO TO EXIT-TN-PP-SHORT-RATE.

      * COMPUTE TERM OF POLICY IN YEARS (T):
           COMPUTE REBJ ROUNDED = REB-ORGTERM / 12.

      * COMPUTE EARNED PREMIUM FACTOR (E):
           COMPUTE REB-WK ROUNDED =
              (REB-ELAPSED-DAYS / (REBJ * 365)) * 100.

      * DETERMINE INITIAL POLICY WRITING EXPENSE (I):
           IF REB-ELAPSED-DAYS <= (22 * REBJ)
              COMPUTE REB-UPR ROUNDED =
                  0.226 * (REB-ELAPSED-DAYS / REBJ) + 5
           ELSE
           IF REB-ELAPSED-DAYS <= (182 * REBJ)
              MOVE 10.1195 TO REB-UPR
           ELSE
              COMPUTE REB-UPR ROUNDED =
                   -0.054 * (REB-ELAPSED-DAYS / REBJ) + 20.1006.

      * COMPUTE REFUND = TOTCHG - (TOTCHG * REFUND PERCENT(E + I)):
           COMPUTE REB-REBATE ROUNDED =
               REB-TOTCHG - (REB-TOTCHG * (REB-WK + REB-UPR) * 0.01).

       EXIT-TN-PP-SHORT-RATE.
           EXIT.

      *****************************************************
      *                     (U)
      *    MERC. A & H ANTICIPATED
      *          AND 'CL' FOR OREGON RE: SP-RBSBOPT1 = '15'
      *
      *    THIS ROUTINE CALCULATES THE INSURANCE PREMIUM
      *    TO COVER THE REMAINING TERM ON THE COVERAGE
      *    REMAINING, AND REBATES THAT AMOUNT. BASED ON
      *    TOTAL NOTE.
      *****************************************************
       REBATE-MERC-AH-ANTIC SECTION.
           MOVE REB-AHSUB TO REB-INSSUB.
           PERFORM REBATE-FIND-INS-RATE.
           PERFORM LOAN-CALCULATIONS.

           COMPUTE REB-REBATE ROUNDED = (TOTAL-NOTE / LN-ORGTERM) *
                          REB-MANYDECIMALS * REB-REMTERM / 100.
      * OREGON OPTION:
           IF SP-RBSPOPT1(REB-SUB) = 15
              COMPUTE REB-WORKER ROUNDED = REB-TOTCHG * 0.9
              COMPUTE REB-WORK2 ROUNDED = REB-TOTCHG - 75.00
              IF REB-WORK2 > REB-WORKER
                 MOVE REB-WORK2 TO REB-WORKER
              END-IF
              IF REB-WORKER < REB-REBATE
                 MOVE REB-WORKER TO REB-REBATE.

       REBATE-MERC-AH-ANTIC-EXIT.
           EXIT.

      *****************************************************
      *                     (0 7)
      *    MULLEN 'CL' AND 'AH' REFUNDS PROTECTIVE LIFE
      *           TRUNCATED NET LIFE COVERAGE
      *           NON RETRO 14 DAY AH COVERAGE
      *
      *****************************************************
       REBATE-MULLEN-CL-AH-07 SECTION.
           IF REB-SUB < 5
              PERFORM REBATE-MULLEN-CL-07
           ELSE
              PERFORM REBATE-MULLEN-AH-07.

       REBATE-MULLEN-CL-AH-07-EXIT.
           EXIT.

      *****************************************************
      *                     (0 7)
      *    MULLEN 'CL' REFUND PROTECTIVE LIFE
      *           TRUNCATED NET LIFE COVERAGE
      *****************************************************
       REBATE-MULLEN-CL-07 SECTION.

      *   GET CREDIT LIFE MOB RATE:
           IF LN-INSTYPES-DL
              IF LN-INS-SINGLE
                 MOVE SP-INSRATE(1 1) TO REB-MANYDECIMALS
              ELSE
              IF LN-INS-JOINT
                 MOVE SP-INSRATE(2 1) TO REB-MANYDECIMALS
              ELSE
                 MOVE REB-TOTCHG TO REB-REBATE
                 GO TO REBATE-MULLEN-CL-07-EXIT.
           IF LN-INSTYPES-LL
              IF LN-INS-SINGLE
                 MOVE SP-INSRATE(3 1) TO REB-MANYDECIMALS
              ELSE
              IF LN-INS-JOINT
                 MOVE SP-INSRATE(4 1) TO REB-MANYDECIMALS
              ELSE
                 MOVE REB-TOTCHG TO REB-REBATE
                 GO TO REBATE-MULLEN-CL-07-EXIT.

           COMPUTE REBI ROUNDED = REB-LN-APRATE / 1200.
           COMPUTE REB-A--P-I ROUNDED =
              (1 - (1 / (1 + REBI) **
                 (REB-LN-ORGTERM - (REB-ORGTERM - REB-REMTERM)))
              ) / REBI.
           COMPUTE REB-A--P-J ROUNDED =
              (1 - (1 / (1 + 0.0035) ** REB-REMTERM)) / 0.0035.
           COMPUTE REB-WK ROUNDED =
              (1 - (1 / (1 + REBI) ** (REB-LN-ORGTERM - REB-ORGTERM))
              ) / REBI.
           COMPUTE REB-WK ROUNDED =
              REB-A--P-J - REB-A--P-I + REB-WK
                - 0.0035 * REB-A--P-J * REB-WK.
           COMPUTE REB-WK ROUNDED =
              REB-MANYDECIMALS * .001
                 * (1.0035 * REB-WK) / (REBI - 0.0035).

           COMPUTE REB-REBATE ROUNDED = REB-LN-REGPYAMT * REB-WK.

       REBATE-MULLEN-CL-07-EXIT.
           EXIT.

      ***********************************************************
      *                     (0 7)
      *    MULLEN 'AH' REFUND PROTECTIVE LIFE
      *           NON RETRO 14 DAY AH COVERAGE
      *
      *    REFUND = PREMIUM * REMAIN-TERM / ORIG-TERM
      *                * REMAIN-TERM-RATE / ORIG-TERM-RATE - 10
      ***********************************************************
       REBATE-MULLEN-AH-07 SECTION.

      *   GET SINGLE PREMIUM AH RATE FOR INITIAL COVERED TERM:
      *   WILL STORE IN REB-WK UNTIL CALCULATION IS DONE
      *   AND WILL USE ITRM-INSTERM IN CALCULATION
           MOVE REB-REMTERM TO REB-WORK2.
           MOVE 2 TO ITRM-SUB.
           PERFORM INS-TERM-CALCULATION.
           MOVE ITRM-INSTERM TO REB-REMTERM.
           MOVE REB-AHSUB TO REB-INSSUB.
           PERFORM REBATE-FIND-INS-RATE.
           MOVE REB-MANYDECIMALS TO REB-WK.
           MOVE REB-WORK2 TO REB-REMTERM.

      *   GET SINGLE PREMIUM AH RATE FOR REMAINING TERM:
           MOVE REB-AHSUB TO REB-INSSUB.
           PERFORM REBATE-FIND-INS-RATE.

      *   DETERMINE AH REFUND:
           COMPUTE REB-REBATE ROUNDED =
              REB-TOTCHG * REB-REMTERM / ITRM-INSTERM
               * REB-MANYDECIMALS / REB-WK - 10.

       REBATE-MULLEN-AH-07-EXIT.
           EXIT.

      *****************************************************
      *                     (0 C)  NEWPORT  90%
      *                     (0 K)  MULLEN   95% AND USES INSEFF DATE
      *     COLLATERAL PROTECTION INSURANCE
      *     SHORT RATE REBATE CANCELLATION FORMULA
      *
      *****************************************************
       REBATE-SHORT-RATE-0C SECTION.

      * "K" NEEDS TO WORK FOR INSURANCES WRITTEN FOR DIFFERENT TERMS
           IF SP-RBFRMLA2(REB-SUB) = "K"
              MOVE REB-LN-INSEFF TO NDTE-DATE
              MOVE REB-ORGTERM   TO NDTE-HOLD
           ELSE
              MOVE REB-LN-LOANDATE TO NDTE-DATE
              MOVE REB-LN-ORGTERM  TO NDTE-HOLD.
           PERFORM INCREMENT-UNITPER.

      *   DETERMINE DAYS IN TERM:
           MOVE NDTE-DATE       TO SYS-DATE.
           IF SP-RBFRMLA2(REB-SUB) = "K"
              MOVE REB-LN-INSEFF TO NUM-DATE
           ELSE
              MOVE REB-LN-LOANDATE TO NUM-DATE.
           PERFORM TIM365.
           MOVE ELAPSED-DAYS    TO REBX-DAYS-IN-TERM.

           IF REB-SUB < 5
              MOVE 1 TO REB-INSSUB
           ELSE
           IF REB-SUB = 5
              MOVE 2 TO REB-INSSUB
           ELSE
           IF REB-SUB = 6 OR 11
              MOVE 3 TO REB-INSSUB
           ELSE
           IF REB-SUB > 10 AND < 17
              SUBTRACT 6 FROM REB-SUB GIVING REB-INSSUB.

      *   DETERMINE DAYS IN FORCE:
           IF SP-RBFRMLA2(REB-SUB) = "K"
              MOVE REB-LN-INSEFF TO NUM-DATE
           ELSE
              MOVE REB-LN-LOANDATE TO NUM-DATE.
           MOVE REB-PAYDATE     TO SYS-DATE.
           PERFORM TIM365.
           MOVE ELAPSED-DAYS    TO REBX-DAYS-IN-FORCE.

      * POLICY FEE IS NOT INCLUDED IN LN-INSPREM OR REB-TOTCHG
      * NET CONTRACT COST:
           MOVE REB-TOTCHG TO REBX-NET-CONTRACT-COST.

      * DETERMINE DAILY COST:
           IF SP-RBSPOPT1(REB-SUB) = 23
              COMPUTE REBX-DAILY-COST-NO-ROUND ROUNDED =
                 REBX-NET-CONTRACT-COST / REBX-DAYS-IN-TERM
           ELSE
              COMPUTE REBX-DAILY-COST ROUNDED =
                  REBX-NET-CONTRACT-COST / REBX-DAYS-IN-TERM.

      * DETERMINE EARNED COST:

           IF SP-RBSPOPT1(REB-SUB) = 23
              COMPUTE REBX-EARNED-COST-NO-ROUND ROUNDED =
               REBX-DAYS-IN-FORCE * REBX-DAILY-COST-NO-ROUND
           ELSE
              COMPUTE REBX-EARNED-COST ROUNDED =
               REBX-DAYS-IN-FORCE * REBX-DAILY-COST.

      * DETERMINE PRO-RATA REFUND:
           IF SP-RBSPOPT1(REB-SUB) = 23
              COMPUTE REBX-PRO-RATA-REFUND-NO-ROUND ROUNDED =
                REBX-NET-CONTRACT-COST - REBX-EARNED-COST-NO-ROUND
           ELSE
              COMPUTE REBX-PRO-RATA-REFUND =
                REBX-NET-CONTRACT-COST - REBX-EARNED-COST.

      * DETERMINE SHORT RATE REFUND 90% OF PRORATA:
           IF SP-RBFRMLA2(REB-SUB) = "K"
              IF SP-RBSPOPT1(REB-SUB) = 23
                 COMPUTE REBX-SHORT-RATE-REFUND-NO-ROUND ROUNDED =
                  REBX-PRO-RATA-REFUND-NO-ROUND * 0.95
              ELSE
                 COMPUTE REBX-SHORT-RATE-REFUND ROUNDED =
                  REBX-PRO-RATA-REFUND * 0.95
           ELSE
              IF SP-RBSPOPT1(REB-SUB) = 23
                 COMPUTE REBX-SHORT-RATE-REFUND-NO-ROUND ROUNDED =
                  REBX-PRO-RATA-REFUND-NO-ROUND * 0.90
              ELSE
                 COMPUTE REBX-SHORT-RATE-REFUND ROUNDED =
                  REBX-PRO-RATA-REFUND * 0.90.

           IF SP-RBSPOPT1(REB-SUB) = 23
              MOVE REBX-SHORT-RATE-REFUND-NO-ROUND TO REB-REBATE
           ELSE
              MOVE REBX-SHORT-RATE-REFUND TO REB-REBATE.

       REBATE-SHORT-RATE-0C-EXIT.
           EXIT.

      ****************************************************************
      *                     (0 D)
      *    REGIONAL ACCEPTANCE
      *    RULE 78THS USING DAYS IN TERM AND ELAPSED DAYS
      *    INSTEAD OF UNIT PERIODS IN TERM AND ELAPSED UNIT PERIODS
      *
      ****************************************************************
       REBATE-78-DAYS-0D SECTION.

      *   WHEN SPR FIELD SP-RBDAYS(REB-SUB,1) IS CODED FOR SWING
      *   AND ELAPSED DAYS PRIOR TO 1ST PAYMENT
      *   IS LESS THAN OR EQUAL TO SP-RBDAYS(REB-SUB,1) (NO SWING)
      *   FORCE A FLAT CANCEL, FULL REFUND
      *   NOTE: IF A SWING TAKES PLACE REB-ELAPSED-MONTHS WILL = 1
      *
      *          ---- TO  N O T  CAUSE A SWING LIKE IN THE FOLLOWING EX:
      *                   SPR 1STPAY YEAR TYPE SHOULD BE '360' NOT '365' ETC
      *
      *                                         360      365
      *                                 ------  ---      ---
      *         EFFECTIVE     08/29/04  DAYS     29       30
      *         REBATE DATE   09/28/04  MONTHS    0        1
      *                                 REM      29        0
      *                                        NO SWING  WILL SWING

           IF REB-SWING = "Y" AND REB-ELAPSED-MONTHS = 0
              MOVE REB-TOTCHG TO REB-REBATE
              GO TO REBATE-78-DAYS-0D-EXIT
           ELSE
      * DETERMINE DAYS IN IN FORCE
              MOVE REB-PAYDATE   TO NUM-DATE
              MOVE REB-LN-INSEXP TO SYS-DATE
              PERFORM TIM365
              MOVE ELAPSED-DAYS TO REBX-DAYS-IN-FORCE
              IF REBX-DAYS-IN-FORCE < 0
                 MOVE 0 TO REB-REBATE
                 GO TO REBATE-78-DAYS-0D-EXIT
              END-IF
      * DETERMINE DAYS IN INSURANCE TERM
              MOVE REB-LN-INSEFF TO NUM-DATE
              MOVE REB-LN-INSEXP TO SYS-DATE
              PERFORM TIM365
              MOVE ELAPSED-DAYS TO REBX-DAYS-IN-TERM.

      * DETERMINE RULE 78 REBATE:
           COMPUTE REB-REBATE ROUNDED = REB-TOTCHG
              * (REBX-DAYS-IN-FORCE * (REBX-DAYS-IN-FORCE + 1))
              / (REBX-DAYS-IN-TERM * (REBX-DAYS-IN-TERM + 1)).

       REBATE-78-DAYS-0D-EXIT.
           EXIT.

      ****************************************************************
      *                     (0 E)
      *    WORLD
      *    ALABAMA ACTUARIAL REFUND FORMULA
      *    SINGLE PREMIUM RATE MEMORANDUM
      *
      ****************************************************************
       REBATE-ACTUARIAL-0E SECTION.

           MOVE LN-ORGTERM   TO REBN.
           MOVE REB-ORGTERM  TO REBM.
           MOVE REB-REMTERM  TO REBR.
           SUBTRACT REBM FROM REBN GIVING REBN-M.

      * COMPUTE MONTHLY RATE OF INTEREST:
           COMPUTE REBI ROUNDED = REB-LN-APRATE / 1200.

      * COMPUTE ANNUITY FACTORS:
           COMPUTE REBAN ROUNDED =
               (1 - 1 / (1 + REBI) ** REBN) / REBI.

           COMPUTE REBAN-M ROUNDED =
               (1 - 1 / (1 + REBI) ** (REBN - REBM)) / REBI.

           COMPUTE REBAN-MPLUSR ROUNDED =
               (1 - 1 / (1 + REBI) ** (REBN - REBM + REBR)) / REBI.

      * COMPUTE REBATE:
           COMPUTE REB-REBATE ROUNDED =
               REB-TOTCHG *
                 ( ((REBR - REBAN-MPLUSR + REBAN-M) / REBI) + REBR )
                 /
                 ( ((REBM - REBAN + REBAN-M) / REBI ) + REBM ).

       REBATE-ACTUARIAL-0E-EXIT.
           EXIT.

      ****************************************************************
      *                     (0 F)
      *    WORLD
      *    COLORADO TRUE DAILY PRORATA REFUND FORMULA
      *
      ****************************************************************
       REBATE-TRUE-PRORATA-0F SECTION.
           PERFORM MATURITY-DATE-CALCULATION.



      * DETERMINE DAYS IN LOAN TERM:
           IF REB-SUB = 7 OR 8 OR 9 OR 10
              MOVE REB-LN-LOANDATE TO NUM-DATE
              MOVE MDTE-DATE       TO SYS-DATE
           ELSE
      * INSURANCES
              MOVE REB-LN-INSEFF TO NUM-DATE
              MOVE REB-LN-INSEXP TO SYS-DATE MDTE-DATE.

           IF SP-RBSPOPT1(REB-SUB) = 24
               PERFORM TIM360
           ELSE
               PERFORM TIM365.
           MOVE ELAPSED-DAYS    TO REB-WORKER.

      * DETERMINE DAYS REMAINING FROM PAYDATE TO MATURITY:
           MOVE REB-PAYDATE     TO NUM-DATE.
           MOVE MDTE-DATE       TO SYS-DATE.

           IF SP-RBSPOPT1(REB-SUB) = 24
              PERFORM TIM360
           ELSE
              PERFORM TIM365.

      * COMPUTE REBATE:
           COMPUTE REB-REBATE ROUNDED =
               REB-TOTCHG * ELAPSED-DAYS / REB-WORKER.

       REBATE-TRUE-PRORATA-0F-EXIT.
           EXIT.

      ****************************************************************
      *                     (0 G)
      *    WORLD
      *    MEXICO MAINTENANCE FEE REFUND
      *
      *    THE REFUND IS IN THE SAME PROPORTION OF THE INTEREST & SERVICE
      *    CHARGE REFUNDS
      *
      ****************************************************************
       REBATE-SAME-PROP-INT-SC-0G SECTION.

      * COMPUTE REBATE:
           COMPUTE REB-REBATE ROUNDED =
               (REB-LN-INT-REBATE + REB-LN-SC-REBATE)
               * LN-ANTICERN(4) / (LN-INTCHG + LN-SERCHG).

       RB-SAME-PROP-INT-SC-0G-EXIT.
           EXIT.

      *****************************************************
      *    MEXICO CL ANTICIPATED     (0, J)
      *
      *    THIS ROUTINE CALCULATES THE INSURANCE PREMIUM
      *    TO COVER THE REMAINING TERM ON THE COVERAGE
      *    REMAINING, AND REBATES THAT AMOUNT.
      *****************************************************
       REBATE-MX-CL-ANTICIPATED SECTION.
           IF LN-INSTYPES-DL
              IF LN-INS-SINGLE
                 COMPUTE REB-REBATE ROUNDED =
                         ((SP-INSRATE(1 1) / 12 * REB-REMTERM) / 100) *
                                   REB-REMTERM * REB-LN-REGPYAMT
              ELSE
                 COMPUTE REB-REBATE ROUNDED =
                         ((SP-INSRATE(2 1) / 12 * REB-REMTERM) / 100) *
                                   REB-REMTERM * REB-LN-REGPYAMT.

       REBATE-MX-CL-ANTICIPATED-EXIT.
           EXIT.
      *****************************************************
      *    MERC. DAILY PRORATA         (V) & (W)
      *
      *****************************************************
       REBATE-MERC-DAILY-PRORATA SECTION.

           IF SP-RBFRMLA(REB-SUB) = "W"
              MOVE REB-LN-ORIG-1STPYDATE TO NDTE-DATE
              MOVE -1 TO NDTE-HOLD
              PERFORM INCREMENT-MONTHS
              MOVE NDTE-DATE TO SYS-DATE
           ELSE
              MOVE REB-LN-LOANDATE TO NDTE-DATE.
           MOVE REB-LN-ORGTERM TO NDTE-HOLD.
           PERFORM INCREMENT-MONTHS.
           MOVE NDTE-DATE TO NUM-DATE.
           IF SP-RBFRMLA(REB-SUB) = "W"
              MOVE SYS-DATE TO NDTE-DATE
           ELSE
              MOVE REB-LN-LOANDATE TO NDTE-DATE.
           COMPUTE NDTE-HOLD = REB-LN-ORGTERM + REB-LN-TOTNODEF.
           PERFORM INCREMENT-MONTHS.
           MOVE REB-PAYDATE TO SYS-DATE.
           MOVE 360 TO ELAPSED-YRTYPE.
           PERFORM TIMALL.
           IF ELAPSED-DAYS <= 0
              COMPUTE REB-REMTERM = (ELAPSED-DAYS * -1) *
                                     REB-LN-TOTNODEF
              COMPUTE ELAPSED-DAYS = ((REB-LN-TOTNODEF *
                                     (REB-LN-TOTNODEF + 1)) / 2) * 30
              COMPUTE REB-REBATE =
                   (REB-REMTERM + ELAPSED-DAYS)
                         * REB-LN-APRATE * REB-LN-REGPYAMT / 36000
           ELSE
              MOVE NDTE-DATE TO NUM-DATE
              MOVE REB-PAYDATE TO SYS-DATE
              MOVE 360 TO ELAPSED-YRTYPE
              PERFORM TIMALL
              IF ELAPSED-DAYS <= 0
                 COMPUTE ELAPSED-DAYS = (ELAPSED-DAYS * -1)
                 MOVE 0 TO REB-WORKER
                 PERFORM UNTIL ELAPSED-DAYS < 0
                    ADD ELAPSED-DAYS TO REB-WORKER
                    SUBTRACT 30 FROM ELAPSED-DAYS
                 END-PERFORM
      *JTG020397 DIVIDE ELAPSED-DAYS BY 30 GIVING REB-REMTERM
      *CORRECTED COMPUTE ELAPSED-DAYS = ELAPSED-DAYS *
      *SLC ERROR      (REB-REMTERM + 1)
                 COMPUTE REB-REBATE =
                     REB-WORKER * REB-LN-APRATE * REB-LN-REGPYAMT
                                    / 36000.

           IF REB-REBATE > REB-TOTCHG
              MOVE REB-TOTCHG TO REB-REBATE.

       REBATE-MERC-DAILY-PRORATA-EXIT.
           EXIT.


      ************************************************
      *               (RBFRMLA2 = 1)
      *    PRO RATA, TO THE DAY REBATE METHOD
      *    MECURY ACTION: FORMULA 9
      ************************************************
       REBATE-MERC-TO-THE-DAY SECTION.
           IF REB-ORGTERM NOT = 0
              MOVE REB-LN-LOANDATE TO NUM-DATE
              MOVE LN-INSEXP-DATE(3) TO SYS-DATE
              PERFORM TIM365
              COMPUTE REB-REBATE ROUNDED =
                  (REB-TOTCHG - REB-DEDCHG)
                        - (REB-TOTCHG - REB-DEDCHG)
                                * REB-ELAPSED-DAYS / ELAPSED-DAYS.

      *********************************
      *    REBATE REF DATE ROUTINE
      *********************************
       REBATE-REFDATE SECTION.
      * = 0 SIGNALS ERROR FOR SP-RBDATE / DATE CALC:
           MOVE 0 TO REB-REFDATE.
           MOVE SP-RBDTE(REB-SUB REB-SUB2) TO REB-HOLD-RBDTE.

      * WHEN NON MONTHLY PAPER AMD SP-RBUNITPER-CD = 'B'
      
           IF SP-RBUNITPER-CD(REB-SUB) = "B" AND
              (NOT (LN-UNITPER-CD = "M" AND LN-UNITPER-FREQ = 1 ))
              MOVE LN-UNITPER-CD       TO DATER-UNITPER-CD
              MOVE LN-UNITPER-FREQ     TO DATER-UNITPER-FREQ
              MOVE REB-LN-ORIG-1STPYDATE  TO NDTE-DATE
              MOVE -1                  TO NDTE-HOLD
              PERFORM INCREMENT-UNITPER
              MOVE NDTE-DATE           TO REB-REFDATE
              GO TO REBATE-REFDATE-EXIT.

           IF SP-RBEARLY(REB-SUB) = 1
              PERFORM REBATE-EP1.

           IF REB-HOLD-RBDTE = "A"
              MOVE REB-LN-LOANDATE TO REB-REFDATE.
           IF REB-HOLD-RBDTE = "B"
              MOVE REB-LN-INTDATE  TO REB-REFDATE.

      * LOGIC FOR EARNINGS CALCULATIONS:
      * WHEN 'X' USE REB-LN-INTDATE UNLESS SALES FIN. USE PURCHDATE
           IF REB-HOLD-RBDTE = "X"
              IF REB-LN-SALEFIN = "Y"
                 MOVE REB-LN-PURDATE TO REB-REFDATE
              ELSE
                 MOVE REB-LN-INTDATE TO REB-REFDATE.

           IF NOT (REB-HOLD-RBDTE = "C" OR "D")
              GO TO REBATE-REFDATE-EXIT.

      * ONE PMT INTERVAL PRIOR TO (TRIGGER) FIRST PMT DATE:
           MOVE REB-TRIGGER-DATE TO NDTE-DATE.
           MULTIPLY -1 BY 1 GIVING NDTE-HOLD.
           PERFORM INCREMENT-MONTHS.
           MOVE NDTE-DATE TO REB-REFDATE.
       REBATE-REFDATE-EXIT.
           EXIT.

      ***************************
      * 78'S FOR ORIGINAL TERM:
      ***************************
       REBATE-78-SUMORG SECTION.
           IF (SP-RBFRMLA(REB-SUB) = "N")
               COMPUTE REB-SUMORG =
                    (REB-HOLD-ORGTERM * (REB-HOLD-ORGTERM + 1)) / 2
               COMPUTE REB-SUMORG = REB-SUMORG +
                    (REB-ORGTERM - REB-HOLD-ORGTERM) * REB-HOLD-ORGTERM
           ELSE
               COMPUTE REB-SUMORG =
                      (REB-ORGTERM * (REB-ORGTERM + 1)) / 2.

      ***************************
      * 78'S ROUTINE
      ***************************
       REBATE-78-ROUTINE SECTION.
           IF REB-ORGTERM = REB-REMTERM
              COMPUTE REB-REBATE = REB-TOTCHG - REB-DEDCHG
           ELSE
              PERFORM REBATE-78-SUMORG
              IF REB-SUMORG NOT = 0
                 COMPUTE REB-SUMORG ROUNDED =
                              (REB-TOTCHG - REB-DEDCHG) / REB-SUMORG
                 PERFORM REBATE-78-REBATE
              ELSE
                 PERFORM REBATE-78-REBATE.

      ******************************************
      * UNIT CHARGE * 78'S FOR REMAINING TERM
      ******************************************
       REBATE-78-REBATE SECTION.
           IF SP-RBFRMLA(REB-SUB) = "N"
                         AND (REB-REMTERM > REB-HOLD-ORGTERM)
              COMPUTE REB-SUMREM =
                  (REB-HOLD-ORGTERM * (REB-HOLD-ORGTERM + 1)) / 2
              COMPUTE REB-SUMREM = REB-SUMREM +
                  (REB-REMTERM - REB-HOLD-ORGTERM) * REB-HOLD-ORGTERM
           ELSE
              COMPUTE REB-SUMREM =
                  (REB-REMTERM * (REB-REMTERM + 1)) / 2.
           COMPUTE REB-REBATE ROUNDED = REB-SUMORG * REB-SUMREM.

      *****************************************************************
      *                  ESTABLISH REB-SUB2
      * DETERMINE WHICH ADD DAYS, START DATE, YRTYPE APPLIES:
      *
      * NORMAL:
      *   WHEN REBATE IS DONE BEFORE TRIGGER DATE, REBSUB2 = (1)
      *   AND ON OR AFTER TRIGGER DATE, REBSUB2 = (2):
      * MISSISSIPPI OPT1 (4):
      * INDIANA:
      *   WHEN REBATE IS DONE ON OR BEFORE TRIGGER DATE, REBSUB2  = (1)
      *   AND AFTER TRIGGER DATE, REBSUB2 = (2):
      *
      *   NOTE:
      *        IF SP-RBEARLY() NOT = 8
      *           TRIGGER-DATE = 1STPAYDATE
      *        ELSE
      *           TRIGGER-DATE = 1 MONTH AFTER INTDATE
      *****************************************************************
       SELECT-SP-ELAPSED-TIMTBL SECTION.
           PERFORM REBATE-TRIGGER-DATE.
           MOVE REB-PAYDATE TO NUM-DATE.
           MOVE REB-TRIGGER-DATE TO SYS-DATE.
           MOVE 365 TO ELAPSED-YRTYPE.
           PERFORM TIMALL.

           IF (NOT (SP-RBSPOPT1(REB-SUB) = 4)) AND
                                  (SP-RBEARLY(REB-SUB) NOT = 8)
              MOVE 1 TO REB-SUB2
           ELSE
              MOVE 0 TO REB-SUB2.

           IF JULIAN-DATE = 0
              MOVE 0 TO REB-SUB
           ELSE
              IF ELAPSED-DAYS NOT < REB-SUB2
                 MOVE 1 TO REB-SUB2
              ELSE
                 MOVE 2 TO REB-SUB2.

      * EXAMPLE FOR MS OPTION1 = 3
      * ON FIRST PAYDATE, WILL MOVE 1 TO REB-SUB2, THEN NEXT IF, ELAPSED
      * IS 0, SO IS 0 >= 1 ? NO, SO MOVE 2 TO REB-SUB2 WHICH IS NOT
      * CONSIDERED PODATE-B4-1STPYDATE AND THAT IS WHAT THEY WANT!

      * AFTER FIRST PAYDATE, WILL MOVE 1 TO REB-SUB2, THEN NEXT IF, ELAPSED
      * IS -1, SO IS -1 >= 1 ? NO, SO MOVE 2 TO REB-SUB2 WHICH IS NOT
      * CONSIDERED PODATE-B4-1STPYDATE AND THAT IS WHAT THEY WANT!

      * PRIOR FIRST PAYDATE, WILL MOVE 1 TO REB-SUB2, THEN NEXT IF, ELAPSED
      * IS 3, SO IS 3 >= 1 ? YES, SO MOVE 1 TO REB-SUB2 WHICH IS
      * CONSIDERED PODATE-B4-1STPYDATE AND THAT IS WHAT THEY WANT!

      ***************************************************
      *    DETERMINE TRIGGER DATE FOR CALC OF REB-SUB2
      *      AND IF SP-RBEARLY(8), CHANGE REB-ORGTERM
      ***************************************************
       REBATE-TRIGGER-DATE SECTION.
           MOVE REB-LN-ORIG-1STPYDATE TO REB-TRIGGER-DATE.

           IF SP-RBEARLY(REB-SUB) = 8
              MOVE REB-ORGTERM TO REB-HOLD-ORGTERM
              MOVE REB-LN-INTDATE TO NDTE-DATE
              MOVE 1 TO NDTE-HOLD
              PERFORM INCREMENT-MONTHS
              MOVE NDTE-DATE TO REB-TRIGGER-DATE
              IF REB-SUB = 7 OR 8 OR 9
                 PERFORM REBATE-GET-EXTDAYS
                 IF REB-TOTAL-EXTDAYS NOT < 30
                    COMPUTE REB-ORGTERM = REB-ORGTERM +
                                          (REB-TOTAL-EXTDAYS / 30).

      ****************************************************
      * FIND ELAPSED DD MM & REM ON REFERENCE THRU PAYOFF
      ****************************************************
       CALC-ELAPSED-REF-THRU-PODATE SECTION.
           MOVE REB-PAYDATE TO SYS-DATE.
      * TEST FOR MISSISSIPPI REBATE 20 OR 90 DAY OPTION,
      * IF SO ADD 20 OR 90 DAYS TO PAYDATE:
           IF SP-RBSPOPT1(REB-SUB) = 3
              MOVE 20 TO REB-ELAPSED-REM
           ELSE
              IF SP-RBSPOPT1(REB-SUB) = 4
                 MOVE 90 TO REB-ELAPSED-REM.
           IF SP-RBSPOPT1(REB-SUB) = 3 OR 4
              MOVE REB-PAYDATE TO NUM-DATE
              PERFORM JUL
              ADD REB-ELAPSED-REM TO JULIAN-DATE
              PERFORM CJUL
              MOVE NUM-DATE TO SYS-DATE.
           MOVE REB-REFDATE TO NUM-DATE.

      * WHEN SP-RBUNITPER-CD(REB-SUB) = "B"
      *    IF OTHER THAN MONTHLY PAY CODE OR PAYFREQ
      *       COMPUTATION OF ELAPSED-MONTHS, ETC WILL BE DONE ON
      *       UNIT PERIOD BASES

           IF SP-RBUNITPER-CD(REB-SUB) = "B" AND
              (NOT (LN-UNITPER-CD = "M" AND LN-UNITPER-FREQ = 1 ))
              MOVE LN-UNITPER-CD       TO DATER-UNITPER-CD
              MOVE LN-UNITPER-FREQ     TO DATER-UNITPER-FREQ
              PERFORM TIMUPER
              MOVE ELAPSED-UNITPER     TO REB-ELAPSED-MONTHS
              MOVE ELAPSED-UNITPER-REM TO REB-ELAPSED-REM
              MOVE ELAPSED-DAYS        TO REB-ELAPSED-DAYS
           ELSE
              MOVE SP-RBYRTYPE(REB-SUB, REB-SUB2) TO ELAPSED-YRTYPE
              PERFORM TIMALL
              MOVE ELAPSED-MONTHS TO REB-ELAPSED-MONTHS
              MOVE ELAPSED-REM    TO REB-ELAPSED-REM
              MOVE ELAPSED-DAYS   TO REB-ELAPSED-DAYS.

      ********************************************
      *    APPLICATION OF 'SWING RULE':
      *    ESTABLISH REM-TERM
      *    ADJUST TERM BY DEFERMENTS
      ********************************************
       REBATE-TEST-SWING SECTION.
           MOVE " " TO REB-SWING.
           IF (NOT
                  ( SP-RBSPOPT1(REB-SUB) = 7
                          AND ( (LN-REPOCD = "X" OR "S")
                                   OR (REB-LPTRCD = "PB" OR "RN"
                                       OR "RB" OR "RO")
                              )
                  ) OR
                  ( SP-RBSPOPT1(REB-SUB) = 18
                          AND ( (LN-REPOCD = "X" OR "S") OR
                                 (REB-LPTRCD = "PB" OR "RN" OR
                                               "RB" OR "RO") OR
                                  (REB-ELAPSED-DAYS NOT > 60)
                              )
                  )
              )
              IF (
                   (SP-RBDAYS(REB-SUB REB-SUB2) NOT = 0) AND
                                     (SP-RBEARLY(REB-SUB) NOT = 4)
                 )
               OR
                 (
                   (SP-RBEARLY(REB-SUB) = 4) AND
                         (NOT (REB-LPTRCD = "PB" OR "RN" OR "SC"
                                                OR "RB" OR "RO"))
                 )
               OR
                 (
                   (REB-EARN-FULL-MONTH = "B" OR "C")
                 )
                 MOVE "Y" TO REB-SWING
                 IF (REB-ELAPSED-REM NOT < SP-RBDAYS(REB-SUB REB-SUB2))
                           OR (REB-EARN-FULL-MONTH = "B" OR "C")
                    ADD 1 TO REB-ELAPSED-MONTHS
                    IF SP-RBEARLY(REB-SUB) NOT = 5
                       MOVE 0 TO REB-ELAPSED-REM
                    END-IF
                 ELSE
                    MOVE 0 TO REB-ELAPSED-REM.

      * SETUP REMAINING TERM:
           COMPUTE REB-REMTERM = REB-ORGTERM - REB-ELAPSED-MONTHS.

      * ADJUST TERMS BASED ON SP-RBDEFER CODE:
      * DEFERMENTS NOT CONSIDERED FOR INSURANCE:
           IF REB-SUB = REB-INTSUB
            OR (REB-SUB = REB-DEFSUB AND SP-RBFRMLA(REB-SUB) =
                1 OR "V" OR "W")
              IF SP-RBFRMLA(REB-SUB) NOT = "2"
                                  AND NOT = "C"
                                   AND NOT = "D"
                                    AND NOT = "F"
                                     AND NOT = "H"
                                      AND NOT = "I"
                                       AND NOT = "J"
                 IF SP-RBDEFER(REB-SUB) NOT = 2
                    ADD REB-LN-TOTNODEF TO REB-REMTERM
                    IF SP-RBDEFER(REB-SUB) = 0
                       ADD REB-LN-TOTNODEF TO REB-ORGTERM.

      *************************************************
      * REBATE AFTER REDUCTION AND %AGE COULD BE < 0
      *************************************************
       REBATE-ADJUST-REB-REBATE SECTION.
      * TEST FOR EARLY PROV 7, PRORATA EXTENSION CHARGE:
           IF SP-RBEARLY(REB-SUB) = 7
              IF LN-EXTCHG NOT = 0
                 IF PODATE-B4-1STPYDATE
                    PERFORM REBATE-EP7
      * ADD BACK EXTCHG FOR MIN REB TEST:
                    ADD LN-EXTCHG TO REB-TOTCHG.

      * TEST FOR MISSISSIPPI 20 DAY RULE;
      *  IF PREPAYMENT OCCURS BEFORE THE FIRST PAYMENT DATE, FULLY
      *  REBATE THE EXTENSION CHARGE, USED TO INCLUDE IT IN CALC
           IF SP-RBSPOPT1(REB-SUB) = 3
              IF PODATE-B4-1STPYDATE
                 ADD LN-EXTCHG TO REB-REBATE.

      * REDUCE COMPUTED REBATE BY REDUCTION AMOUNT:
           IF SP-RBREDUC(REB-SUB) > 9999
              PERFORM REBATE-SPEC-RBREDUC
           ELSE
           IF SP-RBSPOPT1(REB-SUB) = 5
              PERFORM REBATE-RBREDUC-NJ
           ELSE
           IF SP-RBSPOPT1(REB-SUB) = 16
              PERFORM REBATE-RBREDUC-OH
           ELSE
           IF SP-RBSPOPT1(REB-SUB) = 25
              PERFORM REBATE-RBREDUC-OH-25
           ELSE
           IF SP-RBSPOPT1(REB-SUB) = 21
              PERFORM REBATE-RBREDUC-NJ-21
           ELSE
           IF SP-RBSPOPT1(REB-SUB) = 28
              PERFORM REBATE-RBREDUC-VYBA-28
           ELSE
              COMPUTE REB-REBATE ROUNDED =
                          REB-REBATE - SP-RBREDUC(REB-SUB).

      * TAKE A PERCENT OF COMPUTED REBATE:
           COMPUTE REB-REBATE ROUNDED =
                         REB-REBATE * (SP-RBPCT(REB-SUB) / 100).

      * WE MUST EARN AT LEAST RBMINRET
           MOVE SP-RBMINRET(REB-SUB) TO REB-WORKER.
      * TEST FOR IOWA MINIMUM RETENTION:
           IF REB-WORKER = 9999.01
              IF REB-LPTRCD = "PB" OR "RN" OR "SC"
                                OR "RB" OR "RO"
                 MOVE 0 TO REB-WORKER
              ELSE
                 ADD LN-SERCHG TO REB-TOTCHG
                 PERFORM LOAN-CALCULATIONS
                 IF DISCLOSED-FINAMT > 75.00
                    MOVE 7.50 TO REB-WORKER
                 ELSE
                    MOVE 5.00 TO REB-WORKER
                 END-IF
              END-IF
           ELSE
      * TEST FOR SC, WORLD MINIMUM RETENTION:
           IF REB-WORKER = 9999.02
              IF REB-LPTRCD = "PB" OR "RN" OR "SC"
                                 OR "RB" OR "RO"
                 MOVE 0 TO REB-WORKER
              ELSE
                 MOVE 15.00 TO REB-WORKER.

           IF REB-ADDON-FG = "Y" AND REB-SUB = 7
              GO TO REBATE-ADJUST-REB-REBATE-FLOOR.

           COMPUTE REB-WORK2 = REB-TOTCHG - REB-REBATE.
           IF REB-ADDON-FG NOT = "Y" AND REB-SUB = 7
              IF LN-ADDON-FG = "Y"
                 IF LN-CONVERCD = "X"
                    COMPUTE REB-WORK2 = LN-ANTICERN(1) - REB-REBATE.

           IF REB-WORK2 < REB-WORKER
              COMPUTE REB-REBATE =
                        REB-WORK2 + REB-REBATE - REB-WORKER.

       REBATE-ADJUST-REB-REBATE-FLOOR.
      * NEXT STMT NECESSARY, AS < SP-RBMIN CHECK IS CONDITIONAL
           IF REB-REBATE < 0
              MOVE 0 TO REB-REBATE.

      * NO NEED TO REBATE WHEN BELOW THIS FLOOR
           IF SP-RBSPOPT1(REB-SUB) = 2 AND
              (REB-LPTRCD = "PB" OR "RN" OR "SC"
                              OR "RB" OR "RO")
              NEXT SENTENCE
           ELSE
              IF SP-RBMIN(REB-SUB) NOT = 999.01
                 IF SP-RBMIN(REB-SUB) NOT = 999.02
                    IF REB-REBATE < SP-RBMIN(REB-SUB)
                       MOVE 0 TO REB-REBATE
                    ELSE
                       NEXT SENTENCE
                 ELSE
      * 999.02 MERCURY "OK" PERSONAL PROPERTY
      * IF RENEWED, MINIMUM IS $1.00, IF PAID OUT, MINIMUM IS $5.00
                    IF (REB-LPTRCD = "PB" OR "RN" OR "SC"
                                        OR "RB" OR "RO")
                       IF REB-REBATE < 1.00
                          MOVE 0 TO REB-REBATE
                       ELSE
                          NEXT SENTENCE
                    ELSE
                       IF REB-REBATE < 5.00
                          MOVE 0 TO REB-REBATE.

      * SPECIAL OPTION 2 = 11:
      * THE FINAL REFUND IS ROUNDED UP OR DOWN TO WHOLE DOLLARS
           IF SP-RBSPOPT2(REB-SUB) = 11
              IF REB-REBATE-CENTS < 51
                 MOVE 0 TO REB-REBATE-CENTS
              ELSE
                 COMPUTE REB-UPR-WHOLE ROUNDED = REB-REBATE + 0.50
                 MOVE REB-UPR-WHOLE TO REB-REBATE.

      **************************************
      *   SPECIAL REBATE REDUCTION ROUTINES
      **************************************
       REBATE-SPEC-RBREDUC SECTION.
           IF SP-RBREDUC(REB-SUB) NOT = 9999.01
              GO TO REBATE-SPEC-RBREDUC-NV.

           MOVE 0 TO A01-MS-PENALTY REB-MS-PENALTY.
           MOVE "A" TO REB-FORM-PROG-TYPE.
      * MISSISSIPPI ROUTINE:

      * TEST FOR NORMAL PAYOFF:
      *    IF REB-LPTRCD NOT = "PO"
           IF REB-LPTRCD = "PB" OR "RN" OR "SC"
                              OR "RB" OR "RO"
              GO TO REBATE-SPEC-RBREDUC-EXIT.

      * TEST FOR PAYOFF WITHIN 1ST 5 YEARS:
           MOVE 360 TO ELAPSED-YRTYPE.
           MOVE REB-LN-LOANDATE TO NUM-DATE.
           MOVE REB-PAYDATE TO SYS-DATE.
           PERFORM TIMALL.
           IF ELAPSED-MONTHS < 0 OR > 60
              GO TO REBATE-SPEC-RBREDUC-EXIT.

      * CALL SUBPROGRAM TO DETERMINE REDUCTION:
           MOVE 01 TO REB-FORM-PROG-NO.
      * 9/13/01 MS STATE BANKING DEPT SAID WHENEVER THERE ARE REMAINING
      *         DAYS, YOU SHOULD BUMP INTO THE NEXT MONTH, WHICH COULD
      *         CAUSE A DIFFERENT PERCENT !!
      * EXAMPLE:  IF 36 MONTHS AND 5 DAYS, THIS USED TO CHARGE 3%, BUT
      *           IT SHOULD CHARGE 2% BECAUSE YOU ARE INTO THE 4TH YR.
           IF ELAPSED-REM > 0
              ADD 1 TO ELAPSED-MONTHS.
           MOVE ELAPSED-MONTHS TO A01-ELAPSED-MO.
           MOVE REB-PAYDATE TO A01-PAYDATE.
           MOVE REB-REBATE TO REB-COMMON-REBATE.
           PERFORM REBATE-CALL-SUBPROG.
           IF REB-SUB = 0 OR REB-COMMON-RTNCD NOT = " "
              GO TO REBATE-SPEC-RBREDUC-EXIT.
           MOVE REB-COMMON-REBATE TO REB-REBATE.
           MOVE A01-MS-PENALTY TO REB-MS-PENALTY.

           GO TO REBATE-SPEC-RBREDUC-EXIT.

      * ------------------------------------------------------
      * NEVADA ROUTINE:  9999.02 MERCURY ACTION SPRDER = 9
      * TEST FOR CUSTOMER PAYOFF PRIOR TO ORIGINAL MATURITY
      *      IF SO DEDUCT $25.00 FROM REBATE:
      * ------------------------------------------------------
       REBATE-SPEC-RBREDUC-NV.
           IF SP-RBREDUC(REB-SUB) NOT = 9999.02
              GO TO REBATE-SPEC-RBREDUC-EXIT.

           IF NOT (REB-LPTRCD = "PB" OR "RN" OR "SC"
                                   OR "RB" OR "RO")
              MOVE "Y" TO MDTE-ORGTERM-FG
                          MDTE-FLAG
              PERFORM MATURITY-DATE-CALCULATION
              IF REB-PAYDATE < MDTE-DATE
                 SUBTRACT 25.00 FROM REB-REBATE
                 IF REB-REBATE < 0
                    MOVE 0 TO REB-REBATE.

       REBATE-SPEC-RBREDUC-EXIT.
           EXIT.

      *****************************************************
      *    SPRING FINANCE (NJ) PRE-PAYOFF PENALTY
      *
      *    IF PAYOFF(PO) IS PRIOR TO MATURITY TAKE PENALTY
      *****************************************************
       REBATE-RBREDUC-NJ SECTION.

           MOVE 0 TO REB-MS-PENALTY.

           IF REB-LPTRCD NOT = "PO"
              GO TO REBATE-RBREDUC-NJ-EXIT.

           PERFORM MATURITY-DATE-CALCULATION.
           MOVE REB-PAYDATE TO NUM-DATE.
           MOVE MDTE-DATE TO SYS-DATE.
           PERFORM TIM.
           IF ELAPSED-DAYS NOT > 0
              GO TO REBATE-RBREDUC-NJ-EXIT.

           PERFORM LOAN-CALCULATIONS.
           IF FINANCED-AMOUNT < 2000.01
              MOVE 20.00 TO REB-MS-PENALTY
              SUBTRACT 20.00 FROM REB-REBATE
           ELSE
              IF FINANCED-AMOUNT < 5000.01
                 COMPUTE REB-MS-PENALTY ROUNDED =
                                 (FINANCED-AMOUNT * .01)
                 COMPUTE REB-REBATE ROUNDED =
                     REB-REBATE - FINANCED-AMOUNT * .01
              ELSE
                 MOVE 100.00 TO REB-MS-PENALTY
                 SUBTRACT 100.00 FROM REB-REBATE.

       REBATE-RBREDUC-NJ-EXIT.
           EXIT.

      *****************************************************
      *    THRIFT INVESTMENT CORP (NJ) PREPAID PENALTY
      *
      * IF PREPAID WITHIN 12 MONTHS AFTER THE FIRST PAY IS
      * IS DUE, YOU CAN CHARGE A PRE-PAYMENT PENALTY.
      *****************************************************
       REBATE-RBREDUC-NJ-21 SECTION.

           MOVE 0 TO REB-MS-PENALTY.

           IF REB-LPTRCD NOT = "PO"
              GO TO REBATE-RBREDUC-NJ-21-EXIT.

           MOVE LN-ORIG-1STPYDATE TO NUM-DATE.
           MOVE REB-PAYDATE TO SYS-DATE.
           PERFORM TIM365.
           IF ELAPSED-MONTHS NOT < 12
              GO TO REBATE-RBREDUC-NJ-21-EXIT.

      *    0 - 2000 = $20.00
      * 2000 - 5000 = 1% OF AMTFIN
      * 5000 - ?    = $100.00

           PERFORM LOAN-CALCULATIONS.
           IF FINANCED-AMOUNT < 2000.01
              SUBTRACT 20.00 FROM REB-REBATE
              MOVE 20.00 TO REB-MS-PENALTY
           ELSE
              IF FINANCED-AMOUNT < 5000.01
                 COMPUTE REB-MS-PENALTY ROUNDED =
                                             (FINANCED-AMOUNT * .01)
                 COMPUTE REB-REBATE ROUNDED =
                     REB-REBATE - FINANCED-AMOUNT * .01
              ELSE
                 MOVE 100.00 TO REB-MS-PENALTY
                 SUBTRACT 100.00 FROM REB-REBATE.

       REBATE-RBREDUC-NJ-21-EXIT.
           EXIT.

      ********************************************************************
      *    REGENCY FINANCE (OH RBSPOPT1 = 25) PRE-PAYOFF PENALTY
      *
      *    IF PAYOFF IS WITH IN 5 YEARS (360 * 5 = 1800 DAYS) OF LOAN DATE
      *       REDUCE COMPUTED REBATE BY 1% OF ORIGINAL PRINCIPAL
      ********************************************************************
       REBATE-RBREDUC-OH SECTION.

           MOVE 0 TO REB-MS-PENALTY.
           IF REB-LPTRCD = "PO"
              MOVE LN-LOANDATE TO NUM-DATE
              MOVE REB-PAYDATE TO SYS-DATE
              PERFORM TIM360
              IF ELAPSED-DAYS NOT > 1800
                 PERFORM LOAN-CALCULATIONS
                 COMPUTE REB-MS-PENALTY =
                                 ( FINANCED-AMOUNT * 0.01 )
                 COMPUTE REB-REBATE =
                    REB-REBATE - ( FINANCED-AMOUNT * 0.01 )
                 IF REB-REBATE < 0
                    MOVE 0 TO REB-REBATE.

      ********************************************************************
      *    REGENCY FINANCE (OH RBSPOPT1 = 25) PRE-PAYOFF PENALTY
      *
      *    IF THE ORIGINAL PRINCIPLE AMOUNT IS $75,000 OR LESS
      *       IF PAYOFF IS WITH IN 5 YEARS (360 * 5 = 1800 DAYS) OF LOAN DATE
      *          REDUCE COMPUTED REBATE BY 1% OF THE LOANS NET BALANCE.
      *
      *    IF THE ORIGINAL PRINCIPLE AMOUNT IS OVER $75,000
      *       IF PAYOFF IS WITHIN 1 YEAR  (360 * 1 = 360  DAYS) OF LOAN DATE
      *          REDUCE COMPUTED REBATE BY 2% OF THE LOANS NET BALANCE.
      *       IF PAYOFF IS 1 - 2 YEARS (360 TO 720 DAYS) OF LOAN DATE
      *          REDUCE COMPUTED REBATE BY 1% OF THE LOANS NET BALANCE.
      *       IF PAYOFF IS OVER 5 YEARS (360 * 5 = 1800 DAYS) OF LOAN DATE
      *          THERE IS NO PENALTY.
      ********************************************************************
       REBATE-RBREDUC-OH-25 SECTION.

           MOVE 0 TO REB-MS-PENALTY.
           IF REB-LPTRCD = "PO"
              PERFORM LOAN-CALCULATIONS
              IF FINANCED-AMOUNT <= 75000
                 MOVE LN-LOANDATE TO NUM-DATE
                 MOVE REB-PAYDATE TO SYS-DATE
                 PERFORM TIM360
                 IF ELAPSED-DAYS NOT > 1800
                    COMPUTE REB-MS-PENALTY = ( LN-CURBAL * 0.01 )
                    COMPUTE REB-REBATE = REB-REBATE - REB-MS-PENALTY
                    IF REB-REBATE < 0
                       MOVE 0 TO REB-REBATE
                    END-IF
                 END-IF
              ELSE
                 MOVE LN-LOANDATE TO NUM-DATE
                 MOVE REB-PAYDATE TO SYS-DATE
                 PERFORM TIM360
                 IF ELAPSED-DAYS NOT > 360
                    COMPUTE REB-MS-PENALTY = ( LN-CURBAL * 0.02 )
                    COMPUTE REB-REBATE = REB-REBATE - REB-MS-PENALTY
                    IF REB-REBATE < 0
                       MOVE 0 TO REB-REBATE
                    END-IF
                 ELSE IF ELAPSED-DAYS > 360 AND NOT > 720
                    COMPUTE REB-MS-PENALTY = ( LN-CURBAL * 0.01 )
                    COMPUTE REB-REBATE = REB-REBATE - REB-MS-PENALTY
                    IF REB-REBATE < 0
                       MOVE 0 TO REB-REBATE
                    END-IF
                 ELSE
                    MOVE 0 TO REB-MS-PENALTY.

      ******************************************************************
      * PREPAYMENT PENALTY FOR MX VYBA LOANS (WORLD PR#0954)
      *
      * THE FOLLOWING DESCRIPTION IS COPIED DIRECTLY FROM THE REQEST:
      * -------------------------------------------------------------
      *  THE CALCULATION WOULD BE THE AMOUNT FINANCED TIMES THE EFFECTIVE
      *  RATE, DIVIDED BY 365 TIMES THE NUMBER OF DAYS FROM DATE OF LOAN
      *  UNTIL FIRST PAY DATE, OR PAYOFF DATE, WHICHEVER IS EARLIER.
      *
      *  SO, IF THE LOAN PAYS OFF BEFORE THE FIRST PAY DATE, IT WILL BE FOR
      *  THE NUMBER OF DAYS LOAN WAS OPEN.  IF IT IS AFTER THE FIRST PAY
      *  DATE BUT IS IN THE FIRST 12 MONTHS, IT WILL BE THE INTEREST THAT
      *  SHOULD HAVE BEEN CHARGED FOR THE EXTENDED FIRST PAY DATE.
      *
      *  EXAMPLE, DATE OF LOAN 09/01/13.  DATE OF FIRST PAYMENT 11/07/13.
      *  AMOUNT FINANCED $20,000. RATE IS EFFECTIVE 75%.
      *
      *  CUSTOMER PAYS OFF ON 10/25/13.  THIS IS PRIOR TO THE FIRST PAY DATE,
      *  SO THE CHARGE WILL BE FOR ONLY THE NUMBER OF DAYS FROM DATE OF
      *  LOAN TO PAYOFF DATE, WHICH ON A 365 DAY YEAR TYPE IS 54 DAYS.
      *  SO WE WILL TAKE $20,000.00 TIMES .75 DIVIDED BY 365 TIMES 54,
      *  OR 20000 X .75 = 15000/365=41.09589 X 54 = $2,219.18 EARLY
      *  PAYOFF PENALTY.
      *
      *  CUSTOMER PAYS OFF ON 12/25/13.  THIS IS AFTER THE FIRST PAY DATE,
      *  SO THE CHARGE WILL BE FOR ONLY THE NUMBER OF DAYS FROM DATE OF
      *  LOAN TO THE FIRST PAY DATE, WHICH ON A 365 DAY YEAR TYPE IS 67 DAYS.
      *  SO WE WILL TAKE $20,000.00 TIMES .75 DIVIDED BY 365 TIMES 67,
      *  OR 20000 X .75 = 15000/365=41.09589 X 67 = $2,753.42 EARLY PAYOFF
      *  PENALTY. THE EARLY PAYOFF AMOUNT IS SUBTRACTED FROM THE NORMAL
      *  REFUND AMOUNT, THUS INCREASING THE PAYOFF BY THE AMOUNT OF THE
      *  EARLY PENALTY.
      *
      *  CUSTOMER PAYS OFF ON 11/24/14 NO PENALTY SINCE IT IS MORE THAN 12
      *  MONTHS FROM DATE OF LOAN.
      *
      ******************************************************************
       REBATE-RBREDUC-VYBA-28 SECTION.
           MOVE 0 TO REB-MS-PENALTY.
           IF REB-LPTRCD = "PO" OR "PB"
              IF REB-PAYDATE < LN-ORIG-1STPYDATE
                 PERFORM LOAN-CALCULATIONS
                 MOVE LN-LOANDATE TO NUM-DATE
                 MOVE REB-PAYDATE TO SYS-DATE
                 PERFORM TIM365
                 COMPUTE REB-MS-PENALTY ROUNDED =
                     ( FINANCED-AMOUNT * LN-EFFRATE * ELAPSED-DAYS ) /
                            (365 * 100)
              ELSE
                 MOVE LN-LOANDATE TO NUM-DATE
                 MOVE REB-PAYDATE TO SYS-DATE
                 PERFORM TIM365
                 IF ELAPSED-MONTHS NOT < 12
                    GO TO REBATE-RBEARLY-VYBA-28-EXIT
                 END-IF
                 MOVE LN-LOANDATE TO NUM-DATE
                 MOVE LN-ORIG-1STPYDATE TO SYS-DATE
                 PERFORM TIM365
                 PERFORM LOAN-CALCULATIONS
                 COMPUTE REB-MS-PENALTY ROUNDED =
                     ( FINANCED-AMOUNT * LN-EFFRATE * ELAPSED-DAYS ) /
                            (365 * 100)
              ELSE
                 MOVE 0 TO REB-MS-PENALTY.

           COMPUTE REB-REBATE = REB-REBATE - REB-MS-PENALTY
           IF REB-REBATE < 0
              MOVE 0 TO REB-REBATE
           END-IF.

       REBATE-RBEARLY-VYBA-28-EXIT.
           EXIT.

      *********************************************
      *   ROUTINE TO CALL SUBPROGRAM:
      *
      *   MOVE FIELDS TO PASS TO REB-COMMON
      *   MOVE PROGRAM NAME TO REB-FORM-PROG
      *   IF NO CANCEL OF SUBPROG IS REQUESTED
      *      MOVE " " TO REB-CANCEL-FG.
      *********************************************
       REBATE-CALL-SUBPROG SECTION.
           MOVE REB-FORM-PATH TO FORM-NAM.
       REBATE-CALL-SUBPROG-RETRY.
           IF REB-RECAST = "Y" AND SP-RBRECAST(REB-SUB) = 1
              MOVE REB-PAYDATE TO REB-COMMON-PAYDATE
              CALL FORM-PROGX USING FORM-PATH REB-COMMON LN-REC SP-REC
                                                         LTI-REC
                  ON OVERFLOW
                  GO TO REBATE-CALL-SUBPROG-RETRY
           ELSE
              CALL FORM-PROGX USING FORM-PATH REB-COMMON LN-REC LTI-REC
                  ON OVERFLOW
                  GO TO REBATE-CALL-SUBPROG-RETRY.

           IF REB-CANCEL-FG = "Y"
              CANCEL FORM-PROGX.
           MOVE "Y" TO REB-CANCEL-FG.

      *********************************
      *   EARLY PROVISION ROUTINES
      *********************************
       REBATE-EP1 SECTION.
           IF LN-EXTCHG = 0
              MOVE "B" TO REB-HOLD-RBDTE
           ELSE
              IF REB-HOLD-RBDTE = "D"
                 MOVE "D" TO REB-HOLD-RBDTE
              ELSE
                 MOVE "C" TO REB-HOLD-RBDTE.
      * ^^^^^^^^^^
      * THE ABOVE LOGIS IS CONFUSING TO ME, PRIOR TO ADDING "D" THE ELSE
      * JUST SET THE VALUE TO "C" TO USE A LATER DATE DUE TO EXTENSION.
      * FROM THIS POINT FORWARD 'C' AND 'D' ARE THE SAME SO THE ONLY ONE
      * AFFECTED IS 'A' WHICH WILL CONTINUE TO BE FORCED TO A 'C AS IT
      * DID BEFORE.

       REBATE-EP2 SECTION.
           COMPUTE REB-EARN-FOR-1MON ROUNDED =
                         (REB-ORGTERM * LN-INTCHG) /
                             (REB-ORGTERM * (REB-ORGTERM + 1) / 2).
           IF REB-REBATE NOT < 0
              COMPUTE REB-REBATE = LN-INTCHG - REB-EARN-FOR-1MON
              MOVE REB-PAYDATE TO NUM-DATE
              MOVE REB-LN-ORIG-1STPYDATE TO SYS-DATE
              MOVE SP-RBYRTYPE(REB-SUB,REB-SUB2) TO ELAPSED-YRTYPE
              PERFORM TIMALL
              IF ELAPSED-DAYS > 30
                 SUBTRACT 30 FROM ELAPSED-DAYS GIVING
                                                 REB-UNUSED-EXTDAYS
                 PERFORM REBATE-EP2-REBATE-EXTCHG
                 GO TO REBATE-EP2-EXIT
              ELSE
                 PERFORM REBATE-EP2-REBATE-CHG
                 GO TO REBATE-EP2-EXIT.

       REBATE-EP2-REBATE-CHG.
           IF REB-REBATE NOT < 0
              IF ELAPSED-DAYS > 0
                 COMPUTE REB-PRORATA-30DAY-PCT =
                                         (ELAPSED-DAYS / 30) + .0009
                 COMPUTE REB-REBATE ROUNDED = REB-REBATE +
                           REB-EARN-FOR-1MON * REB-PRORATA-30DAY-PCT.

       REBATE-EP2-REBATE-EXTCHG.
           PERFORM REBATE-GET-EXTDAYS.
           IF REB-TOTAL-EXTDAYS > 0
              COMPUTE REB-REBATE ROUNDED = LN-INTCHG +
                       REB-UNUSED-EXTDAYS / REB-TOTAL-EXTDAYS *
                                                           LN-EXTCHG.

       REBATE-EP2-EXIT.
           EXIT.

      *****************************************************
      *    EARN EXTENSION CHARGE PRORATA
      *****************************************************
       REBATE-EP7 SECTION.
           MOVE 0 TO REB-EARN-FOR-1MON.
           PERFORM REBATE-GET-EXTDAYS.
           MOVE REB-LN-INTDATE TO NUM-DATE.
           MOVE REB-PAYDATE TO SYS-DATE.
           PERFORM TIM.
           IF REB-TOTAL-EXTDAYS > 0
              COMPUTE REB-EARN-FOR-1MON ROUNDED = LN-EXTCHG -
                 (LN-EXTCHG / REB-TOTAL-EXTDAYS) * ELAPSED-DAYS
              IF REB-EARN-FOR-1MON < 0
                 MOVE 0 TO REB-EARN-FOR-1MON.
           ADD REB-EARN-FOR-1MON TO REB-REBATE.

      **************************************************************************
      *    1/30TH 78THS IF BF 1STPYDATE
      * REV:
      *  JTG 020497 CHANGED TO TEST FOR PAYDATE PRIOR TO
      *             INTEREST START DATE AND SET FULL REFUND
      *  JTG 000317 CHANGED TO CARRY INITIAL CALC OF DAILY FACTOR
      *             TO MORE DECIMALS THAN 2 RE: WORLD BR 588 #01-006564
      *             WRITTEN UP BY AUDITORS
      *  JTG 000322 ALLOWED LOGIC FOR EP10 TO WORK FOR SERVICE CHARGE REFUNDS
      *             ALSO, RE: WORLD BR 588 #01-006564 WRITTEN UP BY AUDITORS
      **************************************************************************
       REBATE-EP10 SECTION.
           MOVE REB-LN-INTDATE TO NUM-DATE.
           MOVE REB-PAYDATE TO SYS-DATE.
           PERFORM TIM365.
           IF REB-SUB = 7
              IF ELAPSED-DAYS < 0
                 IF REB-ADDON-FG = "Y"
                    MOVE LTI-ANTICERN(1)        TO REB-REBATE
                 ELSE
                    ADD LN-INTCHG LN-EXTCHG GIVING REB-REBATE
              ELSE
                 IF REB-ADDON-FG = "Y"
                    COMPUTE REB-WK ROUNDED =
                    ( LTI-ANTICERN(1)
                        - (LTI-ANTICERN(1)
                             * (REB-ORGTERM - 1) * (REB-ORGTERM)
                                / (REB-ORGTERM * (REB-ORGTERM + 1))
                          )
                    ) / 30
                 COMPUTE REB-REBATE ROUNDED =
                     LTI-ANTICERN(1)  - ELAPSED-DAYS * REB-WK
              ELSE
                 COMPUTE REB-WK ROUNDED =
                    ( LN-INTCHG
                        - (LN-INTCHG
                                * (REB-ORGTERM - 1) * (REB-ORGTERM)
                                   / (REB-ORGTERM * (REB-ORGTERM + 1))
                          )
                    ) / 30
                 COMPUTE REB-REBATE ROUNDED =
                     LN-INTCHG + LN-EXTCHG - ELAPSED-DAYS * REB-WK
              END-IF
             END-IF
           ELSE
           IF REB-SUB = 8
              IF ELAPSED-DAYS < 0
                 MOVE LN-SERCHG TO REB-REBATE
              ELSE
                 COMPUTE REB-WK ROUNDED =
                    ( LN-SERCHG
                        - (LN-SERCHG
                                * (REB-ORGTERM - 1) * (REB-ORGTERM)
                                   / (REB-ORGTERM * (REB-ORGTERM + 1))
                          )
                    ) / 30
                 COMPUTE REB-REBATE ROUNDED =
                   LN-SERCHG - ELAPSED-DAYS * REB-WK.

      *****************************************************
      * MISSOURI
      * 23-  IF BEFORE 1STPYDATE, REFUND THE DIFF BETWEEN THE
      *       ORIG INTCHG AND INT PRECOMPUTED ON THE ACTUAL
      *       UNPAID PRINCIPAL BALANCE 1/360TH OF THE EFFRATE
      *       FOR EACH DAY FROM LOANDATE TO POFFDATE
      * ILLINOIS
      * 28-  IF BEFORE 1STPYDATE, REFUND THE DIFF BETWEEN THE
      *       (ORIG INTCHG + ORIG EXTCHG) AND INT PRECOMPUTED
      *       ON THE ACTUAL UNPAID PRINCIPAL BALANCE 1/360TH
      *       OF THE EFFRATE FOR EACH DAY FROM LOANDATE TO POFFDATE
      *****************************************************
       REBATE-EP23-28 SECTION.

           MOVE REB-LN-LOANDATE TO NUM-DATE.
           MOVE REB-PAYDATE     TO SYS-DATE.
           PERFORM TIM365.
           IF ELAPSED-DAYS < 0
              ADD LN-INTCHG LN-EXTCHG GIVING REB-REBATE
           ELSE
              PERFORM LOAN-CALCULATIONS
              IF SP-RBEARLY(REB-SUB) = 23
                 COMPUTE REB-REBATE ROUNDED = LN-INTCHG -
                 ( DISCLOSED-FINAMT * (LN-EFFRATE / 36000)
                    * ELAPSED-DAYS )
              ELSE
                 COMPUTE REB-REBATE ROUNDED =
                        (LN-INTCHG + LN-EXTCHG) -
                 ( DISCLOSED-FINAMT * (LN-EFFRATE / 36000)
                        * ELAPSED-DAYS ).


      ******************************************************
      *    INDIANA EARLY PROVISION 27
      *    EXTENSION CHARGE IS NEVER REFUNDED
      *    1ST MONTH IS PRORATED, 30 DAYS EARNS THE ENTIRE
      *    MONTHS EARNINGS EVEN IF 31 DAYS IN THE PERIOD
      *                                     PR#1406
      *****************************************************
       REBATE-EP27 SECTION.
      * IF PAYDATE PRIOR TO 30DAY PRIOR DATE REFUND ALL INTEREST
           IF REB-PAYDATE < REB-REFDATE
               MOVE REB-TOTCHG TO REB-REBATE
               GO TO REBATE-EP27-EXIT.

      * SLICE FOR THE FIRST MONTH
           COMPUTE REB-EARN-FOR-1MON ROUNDED =
                    (REB-ORGTERM * LN-INTCHG) /
                        (REB-ORGTERM * (REB-ORGTERM + 1) / 2).

      * THIS WILL GIVE US REBATE IF 30 DAYS IS USED
           IF REB-REBATE NOT < 0
              COMPUTE REB-REBATE = LN-INTCHG - REB-EARN-FOR-1MON.

      * THIS WILL GIVE US ACTUAL USED DAYS USING 365 YEAR TYPE
              MOVE REB-REFDATE TO NUM-DATE.
              MOVE REB-PAYDATE TO SYS-DATE.
              MOVE 365  TO ELAPSED-YRTYPE.
              PERFORM TIMALL.

      * WHAT WE ARE REALLY COMPUTING IS REMAINING DAYS IN THE
      * BEGINNING START POINT OF 30 DAYS.
              COMPUTE ELAPSED-DAYS = 30 - ELAPSED-DAYS.

              IF ELAPSED-DAYS > 30
                  MOVE 30 TO ELAPSED-DAYS.

              PERFORM REBATE-EP27-REBATE-CHG.

              GO TO REBATE-EP27-EXIT.

       REBATE-EP27-REBATE-CHG.
           IF REB-REBATE NOT < 0
              IF ELAPSED-DAYS > 0
                 COMPUTE REB-PRORATA-30DAY-PCT =
                                         (ELAPSED-DAYS / 30) + .0009
                 COMPUTE REB-REBATE ROUNDED = REB-REBATE +
                          REB-EARN-FOR-1MON * REB-PRORATA-30DAY-PCT.
       REBATE-EP27-EXIT.
           EXIT.

      *******************************************************
      * TENNEESSEE EARLY REFUND WITHIN 3 DAYS - AND -
      * EARLY PROVISION FOR THE 5% CLOSING FEE PORTION THAT
      * IS STORED IN LN-POINTS AND ALSO INCLUDED IN LN-SERCHG
      *******************************************************
       REBATE-EARLY-97 SECTION.

      * TEST FOR PAYOFF WITHIN 1ST 3 DAYS; INCLUDES SERCHG & CLOSING FEE
           MOVE 365 TO ELAPSED-YRTYPE.
           MOVE REB-LN-INTDATE TO NUM-DATE.
           MOVE REB-PAYDATE    TO SYS-DATE.
           PERFORM TIMALL.
           IF ELAPSED-DAYS < 4
              MOVE REB-TOTCHG TO REB-REBATE
              MOVE 0 TO REB-SUB2
              GO TO REBATE-EARLY-97-EXIT.

      * EARLY PROVISION FOR THE CLOSING FEE PORTION (LN-POINTS).
      * USING A DAILY PRO-RATA FORMULA DURING THE FIRST 90 DAYS OF
      * THE LOAN, USING 365 YEAR TYPE, SUBJECT TO $25 MINIMUM RET.

      * TOOK CODE FROM SCFRMLA 54 SAME LOGIC
           MOVE 0 TO REB-EP54-AMT.

      * GET ELAPSED DAYS FROM LOAN DATE TO REFUND DATE.
           MOVE 365 TO ELAPSED-YRTYPE.
           MOVE REB-LN-LOANDATE TO NUM-DATE.
           MOVE REB-PAYDATE     TO SYS-DATE.
           PERFORM TIMALL.
           MOVE ELAPSED-DAYS TO REBX-DAYS-REMAINING.

           IF ELAPSED-DAYS > 90
              MOVE 0 TO REB-SUB2
              GO TO REBATE-EARLY-97-EXIT.

      * DETERMINE DAYS IN ORG TERM:
      *    MOVE "Y" TO MDTE-FLAG
      *                MDTE-ORGTERM-FG.
      *    PERFORM MATURITY-DATE-CALCULATION.
      *    MOVE MDTE-DATE      TO SYS-DATE.
      *    MOVE REB-LN-INTDATE TO NUM-DATE.
      *    PERFORM TIM365.
      *    MOVE ELAPSED-DAYS TO REBX-DAYS-IN-TERM.
 
      * THEY WANT DAYS IN TERM FORCED TO THE 90 DAYS
           MOVE 90 TO REBX-DAYS-IN-TERM.

      * COMPUTE EARNED FOR PRORATA:
           COMPUTE REB-EP54-AMT ROUNDED =
                  (REBX-DAYS-REMAINING * LN-POINTS)/
                        REBX-DAYS-IN-TERM

           IF REB-EP54-AMT < 0
              MOVE 0 TO REB-EP54-AMT.

      * TEST FOR MINIMUM EARNED OF $25.00
           IF REB-EP54-AMT < 25.00
              MOVE 25.00 TO REB-EP54-AMT.

      * COMPUTE REBATE:
           COMPUTE REB-EP54-AMT ROUNDED =  LN-POINTS - REB-EP54-AMT.

           IF REB-EP54-AMT < 0
              MOVE 0 TO REB-EP54-AMT.

           ADD REB-EP54-AMT TO REB-REBATE.
      * THIS SIGNALS TO EXIT AFTER PERFORMING THIS SECTION. NOTHING
      * ELSE REBATES WHEN THIS PROVISION IS BEING USED.
           MOVE 0 TO REB-SUB2.

       REBATE-EARLY-97-EXIT.
           EXIT.

      ********************************************
      * TENNEESSEE EARLY REFUND WITHIN 3 DAYS
      ********************************************
       REBATE-EARLY-99 SECTION.

      * TEST FOR PAYOFF WITHIN 1ST 3 DAYS:
           MOVE 365 TO ELAPSED-YRTYPE.
           MOVE REB-LN-INTDATE TO NUM-DATE.
           MOVE REB-PAYDATE TO SYS-DATE.
           PERFORM TIMALL.
           IF ELAPSED-DAYS < 4
              MOVE REB-TOTCHG TO REB-REBATE
              MOVE 0 TO REB-SUB2.

       REBATE-EARLY-99-EXIT.
           EXIT.

      *****************************************************
      * REGIONAL MGMT FULL CANCEL IF PAIDOFF WITHIN 10 DAYS
      *****************************************************
       REBATE-EARLY-98 SECTION.
      * TEST FOR PAYOFF WITHIN 1ST 10 DAYS:
      *    MOVE SP-RBYRTYPE(REB-SUB,1) TO ELAPSED-YRTYPE.
           MOVE 365 TO ELAPSED-YRTYPE.
           MOVE REB-LN-INTDATE TO NUM-DATE.
           MOVE REB-PAYDATE TO SYS-DATE.
           PERFORM TIMALL.
           IF ELAPSED-DAYS < 11
              MOVE REB-TOTCHG TO REB-REBATE
              MOVE 0 TO REB-SUB2.

       REBATE-EARLY-98-EXIT.
           EXIT.





      *****************************************************************
      * MISSISSIPPI COMPANY PAYOFF WITHIN 90 DAYS - 3
      * ALABAMA COMPANY PAYOFF WITHIN 120 DAYS - 16  (SAME AS 3)
      *
      * ALABAMA COMPANY PAYOFF WITHIN 90 DAYS - 12
      * ALABAMA COMPANY PAYOFF WITHIN 120 DAYS - 15
      *    IF PREPAID WITHIN 90 DAYS BY COMPANY PAYOFF, REFUND PRORATA
      *    TO THE DAY. A 365-DAY YEAR IS USED TO COMPUTE THE 90DAY TEST
      *    AND SP-YEARTYPE IS USED TO COMPUTE THE REBATE. (SPREAR=1)
      *****************************************************************
       REBATE-EP3 SECTION.
      * TEST FOR COMPANY PAYOFF:
           IF NOT (REB-LPTRCD = "PB" OR "RN" OR "SC"
                                   OR "RB" OR "RO")
              GO TO REBATE-EP3-EXIT.

      * TEST FOR PAYOFF WITHIN 1ST 90 DAYS:
           MOVE 367 TO ELAPSED-YRTYPE.
      * MERCURY - "AL"
           IF SP-RBEARLY(REB-SUB) = 12 OR 15
              MOVE 365 TO ELAPSED-YRTYPE.
           MOVE REB-LN-INTDATE TO NUM-DATE.
           MOVE REB-PAYDATE TO SYS-DATE.
           PERFORM TIMALL.
           IF SP-RBEARLY(REB-SUB) = 15 OR 16
              IF ELAPSED-DAYS > 120
                 GO TO REBATE-EP3-EXIT
              ELSE
                 NEXT SENTENCE
           ELSE
              IF ELAPSED-DAYS > 90
                 GO TO REBATE-EP3-EXIT.

           IF SP-RBEARLY(REB-SUB) = 12 OR 15
              MOVE REB-LN-INTDATE TO NDTE-DATE
              MOVE REB-LN-ORGTERM TO NDTE-HOLD
              PERFORM INCREMENT-MONTHS
              MOVE REB-LN-LOANDATE TO NUM-DATE
              MOVE NDTE-DATE TO SYS-DATE
              MOVE SP-YEARTYPE TO ELAPSED-YRTYPE
              PERFORM TIMALL
              MOVE ELAPSED-DAYS TO REB-ELAPSED-DAYS
              MOVE REB-LN-INTDATE TO NUM-DATE
              MOVE REB-PAYDATE TO SYS-DATE
              MOVE SP-YEARTYPE TO ELAPSED-YRTYPE
              PERFORM TIMALL
      * ACTION TRUNCATES TO 3 DECIMALS
              COMPUTE REB-PRORATA-30DAY-PCT ROUNDED =
                   (REB-ELAPSED-DAYS - ELAPSED-DAYS) / REB-ELAPSED-DAYS
              COMPUTE REB-REBATE ROUNDED = REB-PRORATA-30DAY-PCT
                                         *  REB-TOTCHG
           ELSE
              COMPUTE REB-REBATE ROUNDED =
                (LN-INTCHG + LN-EXTCHG) - (LN-EFFRATE / 360 / 100 *
                (LN-LNAMT - LN-INTCHG - LN-EXTCHG) * ELAPSED-DAYS).

           IF REB-REBATE < 0
              MOVE 0 TO REB-REBATE.
           MOVE 0 TO REB-SUB.

       REBATE-EP3-EXIT.
           EXIT.

      ********************************************
      * MERCURY MISSISSIPPI EARLY REFUND PROVISION
      * IF PREPAID WITHIN 90 DAYS FROM DATE INT STARTS
      * BY COMPANY PAYOFF OR BY THE CUSTOMER BEFORE THE FIRST
      * PAYMENT COMES DUE, REFUND IS PRO RATA TO THE DAY
      * BASED ON THE FOLLOWING FORMULA:
      *   1.   APR
      *        ---   X  AF  X D = EA
      *        360
      *
      *   2. FC - EA = R
      *
      *      APR = WITHOUT SERVICE CHARGE
      *      AF  = TOTAL NOTE - INTCHG - EXTCHG
      *      D   = NUMBER OF DAYS LOAN IN EFFECT
      *      EA  = EARNED AMOUNT
      *      FC  = ORIGINAL FINANCE CHARGE + EXTCHG
      *      R   = REFUND
      ********************************************
       REBATE-EP11 SECTION.
      * TEST FOR COMPANY PAYOFF WITHIN 90 DAYS FROM DATE INT STARTS
           IF (REB-LPTRCD = "PB" OR "RN" OR "SC"
                               OR "RB" OR "RO")
              MOVE 367 TO ELAPSED-YRTYPE
              MOVE REB-LN-INTDATE TO NUM-DATE
              MOVE REB-PAYDATE TO SYS-DATE
              PERFORM TIMALL
              IF ELAPSED-DAYS > 90
                 GO TO REBATE-EP11-EXIT
              ELSE
                 NEXT SENTENCE
           ELSE
      * TEST FOR CUSTOMER PAYOFF BEFORE THE FIRST PAYMENT COMES DUE
              IF REB-SUB NOT = 7
                 GO TO REBATE-EP11-EXIT
              ELSE
                 IF NOT PODATE-B4-1STPYDATE
                    GO TO REBATE-EP11-EXIT
                 ELSE
                    MOVE 367 TO ELAPSED-YRTYPE
                    MOVE REB-LN-INTDATE TO NUM-DATE
                    MOVE REB-PAYDATE TO SYS-DATE
                    PERFORM TIMALL.

           COMPUTE REB-REBATE ROUNDED =
              (LN-INTCHG + LN-EXTCHG) - (LN-EFFRATE / 360 / 100 *
              (LN-LNAMT - LN-INTCHG - LN-EXTCHG) * ELAPSED-DAYS).

           IF REB-REBATE < 0
              MOVE 0 TO REB-REBATE.
           MOVE 0 TO REB-SUB.

       REBATE-EP11-EXIT.
           EXIT.

      *******************************************************
      * MERCURY, NEVADA EARLY REFUND PROVISION
      * WHEN PAYOFF IN DONE BY CUSTOMER REFUND IS
      * BASED ON THE ACTION DATA SHORT RATE TABLE:
      *******************************************************
       REBATE-EP13 SECTION.
           IF REB-LPTRCD = "PB" OR "RN" OR "SC"
                              OR "RB" OR "RO"
              GO TO REBATE-EP13-EXIT.

           MOVE "T" TO REB-FORM-PROG-TYPE.
           MOVE 02 TO REB-FORM-PROG-NO.
           MOVE REB-REFDATE TO NUM-DATE.
           MOVE REB-PAYDATE TO SYS-DATE.
           PERFORM TIM365.
           MOVE ELAPSED-DAYS TO T02-ELAPSED-DAYS.
           MOVE 3 TO ITRM-SUB.
           PERFORM INS-TERM-CALCULATION.
           MOVE ITRM-INSTERM TO T02-INSTERM.
           PERFORM REBATE-CALL-SUBPROG.
           IF REB-COMMON-RTNCD = " "
              MOVE REB-COMMON-REBATE TO REB-REBATE
           ELSE
              MOVE 0 TO REB-REBATE REB-SUB.

           IF REB-REBATE < 0
              MOVE 0 TO REB-REBATE.
           MOVE 0 TO REB-SUB.

       REBATE-EP13-EXIT.
           EXIT.

      ********************************************
      * ALABAMA ANY PAYOFF WITHIN 90 DAYS (14)
      ********************************************
       REBATE-EP14 SECTION.

           MOVE 365 TO ELAPSED-YRTYPE.
           MOVE REB-LN-INTDATE TO NUM-DATE.
           MOVE REB-PAYDATE TO SYS-DATE.
           PERFORM TIMALL.
      * SUN - "AL", AFTER 90 DAYS, SERVICE CHARGE IS FULLY EARNED
      * AND NO REBATES ARE DONE
           IF ELAPSED-DAYS > 90
              MOVE 0 TO REB-REBATE
              GO TO REBATE-EP14-EXIT.

           MOVE REB-LN-INTDATE TO NDTE-DATE.
           MOVE REB-LN-ORGTERM TO NDTE-HOLD.
           PERFORM INCREMENT-MONTHS.
           MOVE REB-LN-LOANDATE TO NUM-DATE.
           MOVE NDTE-DATE TO SYS-DATE.
           MOVE SP-YEARTYPE TO ELAPSED-YRTYPE.
           PERFORM TIMALL.
           MOVE ELAPSED-DAYS TO REB-ELAPSED-DAYS.
           MOVE REB-LN-INTDATE TO NUM-DATE.
           MOVE REB-PAYDATE TO SYS-DATE.
           MOVE SP-YEARTYPE TO ELAPSED-YRTYPE.
           PERFORM TIMALL.
           COMPUTE REB-PRORATA-30DAY-PCT ROUNDED =
                (REB-ELAPSED-DAYS - ELAPSED-DAYS) / REB-ELAPSED-DAYS.
           COMPUTE REB-REBATE ROUNDED = REB-PRORATA-30DAY-PCT
                                      *  REB-TOTCHG.

           IF REB-REBATE < 0
              MOVE 0 TO REB-REBATE.

           IF REB-REBATE < SP-RBMIN(REB-SUB)
              MOVE 0 TO REB-REBATE.

       REBATE-EP14-EXIT.
           EXIT.

      *****************************************************************
      * CLAY FINANCIAL SINGLE PAY LOANS ONLY
      *    IF PREPAID BEFORE 1ST AND ONLY DUE DATE, REBATE
      *       IS RULE OF 78THS, WITH NO OF MONTHS TO 1ST
      *       PAYMENT EQUAL TO ORIGINAL TERM
      *
      *  NOTE: THIS WILL NOT WORK WITH REBATE FORMULA 'N'
      *****************************************************************
       REBATE-EP17 SECTION.
      * TEST FOR COMPANY PAYOFF:
           IF REB-LN-ORGTERM NOT = 1
              GO TO REBATE-EP17-EXIT.

           MOVE REB-LN-INTDATE TO NUM-DATE
                                  REB-REFDATE.
           MOVE REB-LN-ORIG-1STPYDATE TO SYS-DATE.
           PERFORM TIM360.
           MOVE ELAPSED-MONTHS TO REB-LN-ORGTERM
                                  REB-ORGTERM.

           PERFORM CALC-ELAPSED-REF-THRU-PODATE.
           IF JULIAN-DATE = 0
              MOVE 0 TO REB-SUB
              GO TO REBATE-EP17-EXIT.

           PERFORM REBATE-TEST-SWING.
           PERFORM REBATE-DEDUCT-CHARGE.

           PERFORM REBATE-78.
           IF REB-REBATE < 0
              MOVE 0 TO REB-REBATE.
           MOVE 0 TO REB-SUB.

       REBATE-EP17-EXIT.
           EXIT.

      * TEXAS SERVICE CHARGE EARLY PROVISION:
       REBATE-EP18 SECTION.
           COMPUTE REB-EARN-FOR-1MON ROUNDED =
                         (REB-ORGTERM * LN-SERCHG) /
                             (REB-ORGTERM * (REB-ORGTERM + 1) / 2).
           IF REB-REBATE NOT < 0
              COMPUTE REB-REBATE = LN-SERCHG - REB-EARN-FOR-1MON
              MOVE REB-PAYDATE TO NUM-DATE
              MOVE REB-LN-ORIG-1STPYDATE TO SYS-DATE
              MOVE SP-RBYRTYPE(REB-SUB,REB-SUB2) TO ELAPSED-YRTYPE
              PERFORM TIMALL
              IF ELAPSED-DAYS > 30
                 MOVE LN-SERCHG TO REB-REBATE
                 GO TO REBATE-EP18-EXIT
              ELSE
                 PERFORM REBATE-EP18-REBATE-CHG
                 GO TO REBATE-EP18-EXIT.

       REBATE-EP18-REBATE-CHG.
           IF REB-REBATE NOT < 0
              IF ELAPSED-DAYS > 0
                 COMPUTE REB-PRORATA-30DAY-PCT =
                                         (ELAPSED-DAYS / 30) + .0009
                 COMPUTE REB-REBATE ROUNDED = REB-REBATE +
                           REB-EARN-FOR-1MON * REB-PRORATA-30DAY-PCT.

       REBATE-EP18-EXIT.
           EXIT.
      *************************************************
      * SOUTH CAROLINA COMPANY PAYOFF WITHIN 90 DAYS
      * INTEREST CHARGE REFUND
      *************************************************
       REBATE-EP19 SECTION.
      * TEST FOR COMPANY PAYOFF:
           IF NOT (REB-LPTRCD = "PB" OR "RN" OR "SC"
                                   OR "RB" OR "RO")
              GO TO REBATE-EP19-EXIT.

      * TEST FOR PAYOFF WITHIN 1ST 90 DAYS:
           MOVE 365 TO ELAPSED-YRTYPE.
           MOVE REB-LN-INTDATE TO NUM-DATE.
           MOVE REB-PAYDATE TO SYS-DATE.
           PERFORM TIMALL.
           IF ELAPSED-DAYS > 90
              GO TO REBATE-EP19-EXIT.

           COMPUTE REB-REBATE ROUNDED =
              REB-TOTCHG
                - REB-TOTCHG / (REB-ORGTERM * 30) * ELAPSED-DAYS.
           IF REB-REBATE < 0
              MOVE 0 TO REB-REBATE.
           MOVE 0 TO REB-SUB.

       REBATE-EP19-EXIT.
           EXIT.

      *************************************************
      * SOUTH CAROLINA SERVICE CHARGE REFUND,
      * IF CASH ADVANCE < $150.01, NO REFUND
      *************************************************
       REBATE-EP20 SECTION.
           PERFORM LOAN-CALCULATIONS.
           IF CASH-ADVANCE < 150.01
              MOVE 0 TO REB-REBATE.
           MOVE 0 TO REB-SUB.

       REBATE-EP20-EXIT.
           EXIT.

      ********************************************
      * FREEDOM, AL ANY PAYOFF WITHIN 90 DAYS IS PRO RATA
      *             AFTER 90 DAYS, NO REBATE.
      * REGMGM, AL (25) ANY PAYOFF WITHIN 90 DAYS IS PRO RATA
      *             AFTER 90 DAYS, USE SPR TABLE FORMULA'S
      ********************************************
       REBATE-EP22 SECTION.

           MOVE 365 TO ELAPSED-YRTYPE.
           MOVE REB-LN-INTDATE TO NUM-DATE.
           MOVE REB-PAYDATE TO SYS-DATE.
           PERFORM TIMALL.
      * AFTER 90 DAYS, SERVICE CHARGE IS FULLY EARNED, NO REBATE
           IF ELAPSED-DAYS > 90
              MOVE 0 TO REB-REBATE
              GO TO REBATE-EP22-EXIT.

      * EARNED
           COMPUTE REB-REBATE ROUNDED =
                            ( ELAPSED-DAYS * REB-TOTCHG / 90 ).

      * MUST EARN AT LEAST THE RBMINRET ($25.00)
      * REBATE
           IF REB-REBATE <  SP-RBMINRET(REB-SUB)
              COMPUTE REB-REBATE = REB-TOTCHG - SP-RBMINRET(REB-SUB)
           ELSE
               COMPUTE REB-REBATE = REB-TOTCHG - REB-REBATE.

           IF REB-REBATE < 0
              MOVE 0 TO REB-REBATE.

           IF REB-REBATE < SP-RBMIN(REB-SUB)
              MOVE 0 TO REB-REBATE.

      * TRIGGER THAT YOU WERE < 90 DAYS AND ALREADY COMPUTED
           MOVE 0 TO REB-SUB.

       REBATE-EP22-EXIT.
           EXIT.

      *******************************************************
      *    TFC SHORT RATE CANCELLATION TABLE REFUND
      *         CL/REBT04.C                               #96
      *******************************************************
       REBATE-EP24 SECTION.
           MOVE "T" TO REB-FORM-PROG-TYPE.
           MOVE 04  TO REB-FORM-PROG-NO.
           MOVE REB-REFDATE TO NUM-DATE.
           MOVE REB-PAYDATE TO SYS-DATE.
           PERFORM TIM365.
           MOVE ELAPSED-DAYS TO T04-ELAPSED-DAYS.
           PERFORM REBATE-CALL-SUBPROG.
           IF REB-COMMON-RTNCD = " "
              MOVE REB-COMMON-REBATE TO REB-REBATE
           ELSE
              MOVE 0 TO REB-REBATE REB-SUB.
           IF REB-REBATE < 0
              MOVE 0 TO REB-REBATE.
           MOVE 0 TO REB-SUB.
   
       REBATE-EP24-EXIT.
           EXIT.

      ********************************************
      * GEORGIA 4% CLOSING FEE EARLY PROVISION
      *
      * NOTE: THE AMOUNT OF THE CLOSING FEE IS STORED IN THE
      *       LN-POINTS FIELD AND ALSO INCLUDED IN THE SERVICE CHARGE
      *
      *  ***
      *  ** THIS EARLY PROVISION IS NOT STAND ALONE, IT IS CALLED FROM
      *  **  REBATE FORMULA "I" WHEN SERVICE CHARGE FORMULA = "54"
      *  ***
      *
      * REBATE AMOUNT IS PASSED BACK IN THE REB-WORK2 FIELD AND WILL BE
      *  ADDED TO REB-REBATE IN THE CALLING SECTION.
      *
      * IF THE LOAN PAYS  OFF IN THE FIRST 90 DAYS FROM DATE OF LOAN,
      * THE CUSTOMER IS ENTITLED TO A DAILY PRORATA REFUND OF THE CLOSING
      * FEE WITH A MINIMUM RETENTION OF $25.00.  THEREFORE, IF THE SERVICE
      * CHARGE IS GREATER THAN $25.00, MEANING $25.01 OR GREATER, THEN
      * THE CUSTOMER IS ENTITLED TO A REFUND OF THE DAILY PRORATA AMOUNT
      * GREATER THAN $25.00.  IF THE SERVICE CHARGE IS $25.00 OR LESS,
      * NO REFUND IS REQUIRED.
      *
      * SCENARIO ONE
      *    LOAN AMOUNT: $1250, CLOSING FEE OF $50
      *    LOAN TERM: 12 MONTHS, 360 DAYS
      *    LOAN PAID IN FULL ON 90TH DAY
      *
      *    $1250 X 4% = $50, $50 / 360 = .13888, .13888 X 90 = 12.4999,
      *    $12.50 IS EARNED,
      *    $37.50 IS UNEARNED,
      *    THE LENDER MAY RETAIN $25.00,
      *    THE REFUND TO THE BORROWER IS $25.00
      *
      * SCENARIO TWO
      *    LOAN AMOUNT: $1250, CLOSING FEE OF $50
      *    LOAN TERM: 4 MONTHS, 120 DAYS
      *    LOAN PAID IN FULL ON 90TH DAY

      *    $1250 X 4% = $50, $50 / 120 = .41666 X 90 = 37.4994,
      *    $37.50 IS EARNED,
      *    $12.50 IS UNEARNED,
      *    THE LENDER MAY RETAIN $37.50,
      *    THE REFUND OR CREDIT TO THE BORROWER IS $12.50
      *
      * SCENARIO THREE
      *    LOAN AMOUNT: $500, CLOSING FEE OF $20
      *    LOAN TERM: 6 MONTHS, 180 DAYS
      *    LOAN PAID IN FULL ON 90TH DAY

      *    $500 X 4% = $20, $20 / 180 = .11111 X 90 = 9.99999,
      *    $10.00 IS EARNED,
      *    $10.00 IS UNEARNED,
      *    THE LENDER MAY RETAIN $20.00,
      *    THE REFUND TO THE BORROWER IS $00.00
      *
      * SCENARIO FOUR
      *    LOAN AMOUNT: $1000, CLOSING FEE OF $40
      *    LOAN TERM: 6 MONTHS, 180 DAYS
      *    LOAN PAID IN FULL ON 90TH DAY

      *    $1000 X 4% = $40, $40 / 180 = .22222 X 90 = 19.99999,
      *    $20.00 IS EARNED,
      *    $20.00 IS UNEARNED,
      *    THE LENDER MAY RETAIN $25.00,
      *    THE REFUND OR CREDIT TO THE BORROWER IS $15.00
      ********************************************
       REBATE-EP54 SECTION.
           MOVE 0 TO REB-EP54-AMT.

           IF SP-SCFRMLA NOT = 54
              GO TO REBATE-EP54-EXIT.

      * GET ELAPSED DAYS FROM LOAN DATE TO REFUND DATE.
           MOVE 360 TO ELAPSED-YRTYPE
           MOVE REB-LN-LOANDATE TO NUM-DATE
           MOVE REB-PAYDATE TO SYS-DATE
           PERFORM TIMALL
           MOVE ELAPSED-DAYS TO REBX-DAYS-REMAINING

      * IF COMPANY PAYOFF AND ELAPSED > 90, NO EARLY PROVISION
           IF (REB-LPTRCD = "PB" OR "RN" OR "SC"
                               OR "RB" OR "RO")
              PERFORM TIMALL
              IF ELAPSED-DAYS > 90
                 GO TO REBATE-EP54-EXIT
              END-IF
           ELSE
      * IF
              IF REB-SUB NOT = 8
                 GO TO REBATE-EP54-EXIT
              ELSE
      * IF CUSTOMER PAYOFF AND ELAPSED > 90, NO EARLY PROVISION
                 IF ELAPSED-DAYS > 90
                    GO TO REBATE-EP54-EXIT.

      * DETERMINE DAYS IN ORG TERM:
           MOVE "Y" TO MDTE-FLAG
                       MDTE-ORGTERM-FG.
           PERFORM MATURITY-DATE-CALCULATION.
           MOVE MDTE-DATE TO SYS-DATE.
           MOVE REB-LN-INTDATE TO NUM-DATE.
           PERFORM TIM360.
           MOVE ELAPSED-DAYS TO REBX-DAYS-IN-TERM.

      * COMPUTE EARNED:
           COMPUTE REB-EP54-AMT ROUNDED =
                  (REBX-DAYS-REMAINING * LN-POINTS)/
                        REBX-DAYS-IN-TERM

           IF REB-EP54-AMT < 0
              MOVE 0 TO REB-EP54-AMT
           END-IF.

      * TEST FOR MINIMUM EARNED OF $25.00
           IF REB-EP54-AMT < 25.00
              MOVE 25.00 TO REB-EP54-AMT
           END-IF.

      * COMPUTE REBATE:
           COMPUTE REB-EP54-AMT ROUNDED =  LN-POINTS - REB-EP54-AMT.

           IF REB-EP54-AMT < 0
              MOVE 0 TO REB-EP54-AMT
           END-IF.

       REBATE-EP54-EXIT.
           EXIT.


      ***********************************************
      *              ( 0 8 )
      *    WISCONSIN CPI INSURANCE REFUND FORMULA
      ***********************************************
       REBATE-WI-CPI SECTION.
    
           COMPUTE REB-UPR-WHOLE ROUNDED =
                REB-TOTCHG * (365 - REB-ELAPSED-DAYS) / 366.
           MOVE REB-UPR-WHOLE TO REB-REBATE.
    
           IF REB-REBATE < 0
              MOVE 0 TO REB-REBATE.
    
       REBATE-WI-CPI-EXIT.
           EXIT.

      ***********************************************
      *              ( 0 9 )
      *    TEXAS INTEREST REFUND FORMULA
      ***********************************************
       REBATE-TX-INTEREST SECTION.
           MOVE 0 TO REBTX-CALC-INT-TOT.
    
      * IF REBATE DATE IS >= MATURITY, FORCE A ZERO REBATE:
           PERFORM MATURITY-DATE-CALCULATION.
           IF REB-PAYDATE NOT < MDTE-DATE
              MOVE 0 TO REB-REBATE
              GO TO REBATE-TX-INTEREST-EXIT.
    
           PERFORM LOAN-CALCULATIONS.
           MOVE DISCLOSED-FINAMT TO REBTX-AMTFIN.
    
           COMPUTE REBI ROUNDED = REB-LN-SMPRATE * 0.01.
           COMPUTE REBTX-SERCHG-SLICE ROUNDED =
                          LN-SERCHG / REB-LN-ORGTERM.
    
      * SEE IF REBATE DATE IS ON OR PRIOR TO 1STPAY,
      * IF SO DETERMINE INTEREST THRU REBATE DATE
      
           IF REB-PAYDATE NOT > REB-LN-ORIG-1STPYDATE
              MOVE REB-LN-INTDATE  TO NUM-DATE
              MOVE REB-PAYDATE     TO SYS-DATE
              PERFORM TIM365
      * WHY ARE WE MULTIPLYING THE INTEREST BY -1, THIS ALWAYS YIELDS
      * A ZERO REBATE
      *       COMPUTE REBTX-CALC-INT ROUNDED =
      *           REBTX-AMTFIN * REBI / 365 * ELAPSED-DAYS * -1
              COMPUTE REBTX-CALC-INT ROUNDED =
                  REBTX-AMTFIN * REBI / 365 * ELAPSED-DAYS
              ADD REBTX-CALC-INT TO REBTX-CALC-INT-TOT
              GO TO REBATE-TX-INTEREST-END.
    
           MOVE REB-LN-1STPYAMT    TO REBTX-PYAMT.
           MOVE REB-LN-INTDATE     TO REBTX-FROM-DATE.
           MOVE REB-LN-ORIG-1STPYDATE TO REBTX-TO-DATE.
    
       REBATE-TX-INTEREST-LOOP.
           MOVE REBTX-FROM-DATE TO NUM-DATE.
           MOVE REBTX-TO-DATE   TO SYS-DATE.
           PERFORM TIM365.
           COMPUTE REBTX-CALC-INT ROUNDED =
               REBTX-AMTFIN * REBI / 365 * ELAPSED-DAYS.
           ADD REBTX-CALC-INT TO REBTX-CALC-INT-TOT.
           COMPUTE REBTX-AMTFIN =
              REBTX-AMTFIN - REBTX-PYAMT
               + REBTX-CALC-INT + REBTX-SERCHG-SLICE.
           IF REBTX-AMTFIN < 0
              MOVE 0 TO REBTX-AMTFIN.
    
           MOVE REBTX-TO-DATE   TO REBTX-FROM-DATE
                                   NDTE-DATE.
           MOVE 1               TO NDTE-HOLD.
           PERFORM INCREMENT-UNITPER.
           MOVE NDTE-DATE       TO REBTX-TO-DATE.
           MOVE REB-LN-REGPYAMT TO REBTX-PYAMT.
    
           IF REB-PAYDATE > REBTX-TO-DATE
              GO TO REBATE-TX-INTEREST-LOOP.
    
           MOVE REBTX-FROM-DATE TO NUM-DATE.
           MOVE REB-PAYDATE     TO SYS-DATE.
           PERFORM TIM365.
           COMPUTE REBTX-CALC-INT ROUNDED =
               REBTX-AMTFIN * REBI / 365 * ELAPSED-DAYS.
           ADD REBTX-CALC-INT TO REBTX-CALC-INT-TOT.
    
       REBATE-TX-INTEREST-END.
           COMPUTE REB-REBATE =
              1.50 + LN-INTCHG + LN-EXTCHG - REBTX-CALC-INT-TOT.
           IF REB-REBATE < 0
              MOVE 0 TO REB-REBATE.
           IF REB-REBATE > (LN-INTCHG + LN-EXTCHG)
              COMPUTE REB-REBATE = LN-INTCHG + LN-EXTCHG.
    
       REBATE-TX-INTEREST-EXIT.
           EXIT.


      ***********************************************
      *              ( 0 L )
      *    TEXAS INTEREST REFUND FORMULA FOR UNITED
      ***********************************************
       REBATE-TX-INT-UNITED SECTION.
           MOVE 0 TO REBTX-CALC-INT-TOT.

      * IF REBATE DATE IS >= MATURITY, FORCE A ZERO REBATE:
           PERFORM MATURITY-DATE-CALCULATION.
           IF REB-PAYDATE NOT < MDTE-DATE
              MOVE 0 TO REB-REBATE
              GO TO REBATE-TX-INT-UNITED-EXIT.

           PERFORM LOAN-CALCULATIONS.
           MOVE DISCLOSED-FINAMT TO REBTX-AMTFIN.

           COMPUTE REBI ROUNDED = REB-LN-APRATE * 0.01.
           COMPUTE REBTX-SERCHG-SLICE ROUNDED =
                          LN-SERCHG / REB-LN-ORGTERM.

      * SEE IF REBATE DATE IS ON OR PRIOR TO 1STPAY,
      * IF SO DETERMINE INTEREST THRU REBATE DATE
      
           IF REB-PAYDATE NOT > REB-LN-ORIG-1STPYDATE
              MOVE REB-LN-INTDATE  TO NUM-DATE
              MOVE REB-PAYDATE     TO SYS-DATE
              PERFORM TIM365
      * WHY ARE WE MULTIPLYING THE INTEREST BY -1, THIS ALWAYS YIELDS
      * A ZERO REBATE
      *       COMPUTE REBTX-CALC-INT ROUNDED =
      *           REBTX-AMTFIN * REBI / 365 * ELAPSED-DAYS * -1
              COMPUTE REBTX-CALC-INT ROUNDED =
                  REBTX-AMTFIN * REBI / 365 * ELAPSED-DAYS

              ADD REBTX-CALC-INT TO REBTX-CALC-INT-TOT
              GO TO REBATE-TX-INT-UNITED-END.

           MOVE REB-LN-1STPYAMT    TO REBTX-PYAMT.
           MOVE REB-LN-INTDATE     TO REBTX-FROM-DATE.
           MOVE REB-LN-ORIG-1STPYDATE TO REBTX-TO-DATE.

       REBATE-TX-INT-UNITED-LOOP.
           MOVE REBTX-FROM-DATE TO NUM-DATE.
           MOVE REBTX-TO-DATE   TO SYS-DATE.
           PERFORM TIM365.
           COMPUTE REBTX-CALC-INT ROUNDED =
               REBTX-AMTFIN * REBI / 365 * ELAPSED-DAYS.
           ADD REBTX-CALC-INT TO REBTX-CALC-INT-TOT.
           COMPUTE REBTX-AMTFIN =
              REBTX-AMTFIN - REBTX-PYAMT
               + REBTX-CALC-INT + REBTX-SERCHG-SLICE.
           IF REBTX-AMTFIN < 0
              MOVE 0 TO REBTX-AMTFIN.

           MOVE REBTX-TO-DATE   TO REBTX-FROM-DATE
                                   NDTE-DATE.
           MOVE 1               TO NDTE-HOLD.
           PERFORM INCREMENT-UNITPER.
           MOVE NDTE-DATE       TO REBTX-TO-DATE.
           MOVE REB-LN-REGPYAMT TO REBTX-PYAMT.

           IF REB-PAYDATE > REBTX-TO-DATE
              GO TO REBATE-TX-INT-UNITED-LOOP.

           MOVE REBTX-FROM-DATE TO NUM-DATE.
           MOVE REB-PAYDATE     TO SYS-DATE.
           PERFORM TIM365.
           COMPUTE REBTX-CALC-INT ROUNDED =
               REBTX-AMTFIN * REBI / 365 * ELAPSED-DAYS.
           ADD REBTX-CALC-INT TO REBTX-CALC-INT-TOT.

       REBATE-TX-INT-UNITED-END.
           COMPUTE REB-REBATE =
              1.50 + LN-INTCHG + LN-EXTCHG - REBTX-CALC-INT-TOT.
           IF REB-REBATE < 0
              MOVE 0 TO REB-REBATE.
           IF REB-REBATE > (LN-INTCHG + LN-EXTCHG)
              COMPUTE REB-REBATE = LN-INTCHG + LN-EXTCHG.

       REBATE-TX-INT-UNITED-EXIT.
           EXIT.



      *******************************************
      *          ( 0 H )
      *TEXAS INTEREST REFUND FORMULA
      *******************************************
       REBATE-TX-INT-0H SECTION.
           MOVE 0 TO REBTX-CALC-INT-TOT.

      *IF REBATE DATE IS >= MATURITY, FORCE A ZERO REBATE:
           PERFORM MATURITY-DATE-CALCULATION.
           IF REB-PAYDATE NOT < MDTE-DATE
              MOVE 0 TO REB-REBATE
              GO TO REBATE-TX-INT-0H-EXIT.

           PERFORM LOAN-CALCULATIONS.
           MOVE DISCLOSED-FINAMT TO REBTX-AMTFIN.

           COMPUTE REBTX-SERCHG-SLICE ROUNDED =
                         LN-SERCHG / REB-LN-ORGTERM.

      * SEE IF REBATE DATE IS ON OR PRIOR TO 1STPAY,
      * IF SO DETERMINE INTEREST THRU REBATE DATE
      
           IF REB-PAYDATE NOT > REB-LN-ORIG-1STPYDATE
              MOVE REB-LN-INTDATE  TO NUM-DATE
              MOVE REB-PAYDATE     TO SYS-DATE
              PERFORM TIM365
              PERFORM REBTX-CALC-INT-0H
              ADD REBTX-CALC-INT TO REBTX-CALC-INT-TOT
              GO TO REBATE-TX-INT-0H-END.


           MOVE REB-LN-1STPYAMT    TO REBTX-PYAMT.
           MOVE REB-LN-INTDATE     TO REBTX-FROM-DATE.
           MOVE REB-LN-ORIG-1STPYDATE TO REBTX-TO-DATE.

       REBATE-TX-INT-0H-LOOP.
           MOVE REBTX-FROM-DATE TO NUM-DATE.
           MOVE REBTX-TO-DATE   TO SYS-DATE.
           PERFORM TIM365.
           PERFORM REBTX-CALC-INT-0H
           ADD REBTX-CALC-INT TO REBTX-CALC-INT-TOT.
           COMPUTE REBTX-AMTFIN =
              REBTX-AMTFIN - REBTX-PYAMT
               + REBTX-CALC-INT + REBTX-SERCHG-SLICE.

           IF REBTX-AMTFIN < 0
              MOVE 0 TO REBTX-AMTFIN.

           MOVE REBTX-TO-DATE   TO REBTX-FROM-DATE
                                  NDTE-DATE.
           MOVE 1               TO NDTE-HOLD.
           PERFORM INCREMENT-UNITPER.
           MOVE NDTE-DATE       TO REBTX-TO-DATE.
           MOVE REB-LN-REGPYAMT TO REBTX-PYAMT.

           IF REB-PAYDATE > REBTX-TO-DATE
              GO TO  REBATE-TX-INT-0H-LOOP.

           MOVE REBTX-FROM-DATE TO NUM-DATE.
           MOVE REB-PAYDATE     TO SYS-DATE.
           PERFORM TIM365.
           PERFORM REBTX-CALC-INT-0H.
           ADD REBTX-CALC-INT TO REBTX-CALC-INT-TOT.


       REBATE-TX-INT-0H-END.
      *    MOVE REBTX-CALC-INT-TOT TO
      *                    REBTX-CALC-INT-X.
      *    MOVE REBTX-CALC-INT-X TO MESS.
      *    PERFORM SEND-MESS.
           COMPUTE REB-REBATE =
              1.50 + LN-INTCHG + LN-EXTCHG - REBTX-CALC-INT-TOT.
           IF REB-REBATE < 0
              MOVE 0 TO REB-REBATE.
           IF REB-REBATE > (LN-INTCHG + LN-EXTCHG)
              COMPUTE REB-REBATE = LN-INTCHG + LN-EXTCHG.
           GO TO REBATE-TX-INT-0H-EXIT.

       REBATE-TX-INT-0H-EXIT.
           EXIT.

       REBTX-CALC-INT-0H SECTION.
           MOVE ZEROS TO REBTX-STEP.
           MOVE ZEROS TO REBTX-CALC-INT.
           MOVE ZEROS TO REBTX-RATE-AMT.
           MOVE REBTX-AMTFIN TO REBTX-RATE-BAL.
       REBTX-CALC-INT-0H-LOOP.
           IF REBTX-RATE-BAL = ZEROS
              GO TO REBTX-CALC-INT-0H-END.

           ADD 1 TO REBTX-STEP.

           IF REBTX-STEP > 5
              GO TO REBTX-CALC-INT-0H-END.


           IF SP-CAL-BREAK(1,REBTX-STEP) = ZEROS
              MOVE REBTX-RATE-BAL TO REBTX-RATE-AMT
           ELSE
           IF REBTX-STEP = 1
              MOVE SP-CAL-BREAK(1,REBTX-STEP) TO REBTX-RATE-AMT
           ELSE
              SUBTRACT SP-CAL-BREAK(1,REBTX-STEP - 1) FROM
                        SP-CAL-BREAK(1,REBTX-STEP) GIVING
                                              REBTX-RATE-AMT.
           IF REBTX-RATE-BAL > REBTX-RATE-AMT
              SUBTRACT REBTX-RATE-AMT FROM REBTX-RATE-BAL
           ELSE
              MOVE REBTX-RATE-BAL TO REBTX-RATE-AMT
              MOVE ZERO TO REBTX-RATE-BAL.

           COMPUTE REBTX-CALC-INT = REBTX-CALC-INT +
              REBTX-RATE-AMT *
                 (SP-CAL-RATE(1,REBTX-STEP) * .01).


      *    COMPUTE REBTX-NEW-INT ROUNDED =
      *          REBTX-RATE-AMT *
      *          (SP-CAL-RATE(1,REBTX-STEP) * .01).
      *    COMPUTE REBTX-CALC-INT = REBTX-CALC-INT + REBTX-NEW-INT.


      *    MOVE REBTX-CALC-INT TO REBTX-CALC-INT-X.
      *    MOVE REBTX-CALC-INT-X TO MESS.
      *    PERFORM SEND-MESS.

           GO TO REBTX-CALC-INT-0H-LOOP.

       REBTX-CALC-INT-0H-END.
           COMPUTE REBTX-CALC-INT =
                   REBTX-CALC-INT / 365 * ELAPSED-DAYS.

       REBTX-CALC-INT-0H-EXIT.
           EXIT.

      ****************************************************************
      *                     (0 I)
      *    CITIZENS FINANCE COMPANY
      *    FORMULA FOR TRIST CPI SINGLE PAYMENT LOANS
      *
      ****************************************************************
       REBATE-TRIST-CPI-0I SECTION.

      * REBATE = ORIGINAL PREMIUM / DAYS IN TERM * DAYS REMAINING

      * DETERMINE DAYS IN INSURANCE TERM:
           MOVE REB-LN-INSEFF TO NUM-DATE.
           MOVE REB-LN-INSEXP TO SYS-DATE.
           PERFORM TIM365.
           MOVE ELAPSED-DAYS TO REBX-DAYS-IN-TERM.

      * DETERMINE DAYS REMAINING:
           MOVE REB-PAYDATE   TO NUM-DATE.
           MOVE REB-LN-INSEXP TO SYS-DATE.
           PERFORM TIM365.
           MOVE ELAPSED-DAYS TO REBX-DAYS-REMAINING.

           IF REBX-DAYS-REMAINING < 0
              MOVE 0 TO REB-REBATE
              GO TO REBATE-TRIST-CPI-0I-EXIT.

      * DETERMINE REBATE:
           COMPUTE REB-REBATE =
              REB-TOTCHG / REBX-DAYS-IN-TERM.

           COMPUTE REB-REBATE = REB-REBATE * REBX-DAYS-REMAINING.

      * ROUND REFUND TO THE NEAREST DOLLAR:
           IF REB-REBATE-CENTS > 49
              MOVE 0 TO REB-REBATE-CENTS
              ADD 1 TO REB-REBATE-WHOLE
           ELSE
              MOVE 0 TO REB-REBATE-CENTS.

       REBATE-TRIST-CPI-0I-EXIT.
           EXIT.

      ***********************************************
      *              ( 0 A )
      *    MEAN OF PRORATA AND RULE 78THS
      ***********************************************
       REBATE-MEAN-OF-PRORATA-78 SECTION.
   
           COMPUTE REB-REBATE ROUNDED = REB-TOTCHG
                * ( (REB-ORGTERM - REB-ELAPSED-MONTHS)
                    * (2 * REB-ORGTERM - REB-ELAPSED-MONTHS + 2)
                  )
                  / ((2 * REB-ORGTERM) * (REB-ORGTERM + 1))
   
           IF REB-REBATE < 0
              MOVE 0 TO REB-REBATE.
   
       REBATE-MEAN-OF-PRORATA-78-EXIT.
           EXIT.

      ***********************************************
      *              ( 0 B )
      *    RULE 78THS FOR NET + 1 LIFE REFUND
      ***********************************************
       REBATE-78-NET-1 SECTION.
   
           COMPUTE REB-REBATE ROUNDED = REB-TOTCHG
                * ( (REB-ORGTERM - REB-ELAPSED-MONTHS)
                    * (REB-ORGTERM - REB-ELAPSED-MONTHS + 1)
                    + (REB-ORGTERM - REB-ELAPSED-MONTHS)
                  )
                  / (REB-ORGTERM * (REB-ORGTERM + 1) + REB-ORGTERM)
   
           IF REB-REBATE < 0
              MOVE 0 TO REB-REBATE.
   
       REBATE-78-NET-1-EXIT.
           EXIT.

      ***********************************************
      *    GET EXTENSION DAYS
      ***********************************************
       REBATE-GET-EXTDAYS SECTION.
           MOVE SP-PYEXYEARTP TO ELAPSED-YRTYPE.
           MOVE REB-LN-ORIG-1STPYDATE TO SYS-DATE.
           IF SP-PYEXFMDATE-LOAN-DT
              MOVE REB-LN-LOANDATE TO NUM-DATE
           ELSE
              MOVE REB-LN-INTDATE TO NUM-DATE.
           PERFORM TIMALL.
           SUBTRACT 30 FROM ELAPSED-DAYS GIVING REB-TOTAL-EXTDAYS.

      ***********************************************************
      *    FIND INSURANCE RATE FROM SPR RATE TABLE
      *              USING INTERPOLATION / OR NEXT HIGH
      *                       (A&H)
      *
      *    IN  - REB-INSSUB       = SPR INSURANCE LEVEL
      *    OUT - REB-HUNDREDS
      *                AND        = RATE FOR REMAINING INS. TERM
      *          REB-MANYDECIMALS
      * REV:
      *  JTG 113094 CHANGED TO PASS RATE BACK WITH 4 DECIMALS
      *             SP-RBFRMLA = 'R' FOR TIME LOAN, CA AH REFUND
      *  JTG 070595 CHANGED TO USE INTERPOLATION OR NEXT HIGH
      *             RE: SPR
      *  JTG 071295 CHANGED TO HANDLE 7 OTHER INSURANCES
      ***********************************************************
       REBATE-FIND-INS-RATE SECTION.
           MOVE 0 TO REB-HUNDREDS.
           SET ISTP TO 1.

           IF SP-INS-RATE-FILE(REB-INSSUB)
              GO TO REBATE-FIND-INS-TABLE.

           IF NOT SP-TABLE-ROUND-UP(REB-INSSUB)
              GO TO REBATE-FIND-INS-RATE-INTERPO.
      *--------------------------------------
      *    FIND INSURANCE RE: NEXT HIGH
      *--------------------------------------
           SEARCH SP-INSRATETBL
                  END GO TO REBATE-FIND-INS-RATE-EXIT
             WHEN SP-INSTERM (REB-INSSUB,ISTP) NOT < REB-REMTERM
                  NEXT SENTENCE.

             MOVE SP-INSRATE(REB-INSSUB ISTP) TO REB-HUNDREDS
                                                 REB-MANYDECIMALS.
             GO TO REBATE-FIND-INS-RATE-EXIT.

      *--------------------------------------
      *    FIND INSURANCE RE: INTERPOLATION
      *--------------------------------------
       REBATE-FIND-INS-RATE-INTERPO.
           SEARCH SP-INSRATETBL
                 END GO TO REBATE-FIND-INS-RATE-EXIT
              WHEN SP-INSTERM(REB-INSSUB ISTP) = REB-REMTERM
                 MOVE SP-INSRATE(REB-INSSUB ISTP) TO REB-HUNDREDS
                                                     REB-MANYDECIMALS
              WHEN SP-INSTERM(REB-INSSUB ISTP) > REB-REMTERM
                 PERFORM REBATE-FIND-INS-RATE-COMPUTE.
           GO TO REBATE-FIND-INS-RATE-EXIT.

       REBATE-FIND-INS-TABLE.

           MOVE REB-REMTERM TO IRREBT-REMTERM.
           MOVE REB-INSSUB TO IRREBT-INSSUB.
           MOVE 0 TO IRREBT-MANYDECIMALS.
           MOVE "CL/IRREBT" TO FORM-NAM.
           CALL FORM-PROGX USING FORM-PATH REB-COMMON LN-REC SP-REC.
           CANCEL FORM-PROGX.

           MOVE IRREBT-MANYDECIMALS TO REB-MANYDECIMALS REB-HUNDREDS.
           GO TO REBATE-FIND-INS-RATE-EXIT.

       REBATE-FIND-INS-RATE-COMPUTE.
           IF ISTP = 1
              COMPUTE REB-MANYDECIMALS ROUNDED =
                 SP-INSRATE(REB-INSSUB 1) / SP-INSTERM(REB-INSSUB 1)
                                                      * REB-REMTERM
           ELSE
              COMPUTE REB-MANYDECIMALS ROUNDED =
                 (SP-INSRATE(REB-INSSUB ISTP) -
                                  SP-INSRATE(REB-INSSUB ISTP - 1)) /
                 (SP-INSTERM(REB-INSSUB ISTP) -
                                  SP-INSTERM(REB-INSSUB ISTP - 1)) *
                 (REB-REMTERM - SP-INSTERM(REB-INSSUB ISTP - 1))
                                + SP-INSRATE(REB-INSSUB ISTP - 1).
           MOVE REB-MANYDECIMALS TO REB-HUNDREDS.

       REBATE-FIND-INS-RATE-EXIT.
           EXIT.

       REBATE-DEDUCT-CHARGE SECTION.
           IF REB-ADDON-FG = "Y" AND REB-SUB = 7
              MOVE 0 TO REB-DEDCHG
              GO TO REBATE-DEDUCT-CHARGE-EXIT.

           MOVE SP-RBDEDCHG(REB-SUB) TO REB-DEDCHG.

           IF REB-DEDCHG = 9999.01
              COMPUTE REB-DEDCHG ROUNDED =
                            (LN-CURBAL - LN-OT2BAL) * 0.1
              IF REB-DEDCHG > 25.00
                 MOVE 25.00 TO REB-DEDCHG
              ELSE
                 MOVE 0 TO REB-DEDCHG
           ELSE
              IF REB-DEDCHG = 9999.02
                 COMPUTE REB-HALF-ORGTERM = REB-ORGTERM / 2
                 IF REB-REMTERM NOT < REB-HALF-ORGTERM
                    MOVE 25.00 TO REB-DEDCHG
                 ELSE
                    MOVE 0 TO REB-DEDCHG
              ELSE
                 IF REB-DEDCHG = 9999.03
                    MOVE 25.00 TO REB-DEDCHG
                 ELSE
      * 9999.04  IF MORE THAN HALF THE TERM HAS ELAPSED, THE REBATE SHALL
      *          BE COMPUTED WITHOUT DEDUCTING THE PRECHARGE. (WORLD)
      * JUST LIKE 9999.02 EXCEPT NEEDS A DECIMAL ON HALF-ORGTERM
      * 2/17/99 ADDED DECIMAL TO REB-HALF-ORGTERM-DEC FOR THE FOLLOWING EXAMPLE
      * TERM = 9     REMAINING = 4  ELAPSED = 5  HALF = 4.5
      * WITHOUT THE DECIMAL, THIS EXAMPLE TOOK THE 25.00 AND SHOULDN'T
      * TERM = 8     REMAINING = 4  ELAPSED = 4  HALF = 4
      * THIS EXAMPLE TAKES THE 25.00 LIKE IT SHOULD
                   IF REB-DEDCHG = 9999.04
                      COMPUTE REB-HALF-ORGTERM-DEC = REB-ORGTERM / 2
                      IF REB-REMTERM NOT < REB-HALF-ORGTERM-DEC
                         MOVE 25.00 TO REB-DEDCHG
                      ELSE
                         MOVE 0 TO REB-DEDCHG
                   ELSE
                      IF REB-DEDCHG = 9999.05
                         MOVE 30.00 TO REB-DEDCHG.


       REBATE-DEDUCT-CHARGE-EXIT.
           EXIT.

      ******************************************************************
      * EARLY PROVISION FOR ACQUISITION CHARGES - OKLAHOMA    (REQ #163)
      *
      *   1/60TH FOR EACH DAY FROM DATE OF PREPAYMENT TO THE SIXTIETH
      *   DAY OF LOAN (PRO RATA)
      ******************************************************************
       REBATE-RBEARLY-21 SECTION.
           MOVE LN-LOANDATE TO NUM-DATE.
           MOVE REB-PAYDATE TO SYS-DATE.
           PERFORM TIM365.
           IF ELAPSED-DAYS < 60
      *       ----------------------------------------------------------
      *       REBATE = UNELAPSED DAYS
      *                * ( SERVICE CHARGE - DEDUCT PRECHARGE ) / 60
      *       ----------------------------------------------------------
              PERFORM REBATE-DEDUCT-CHARGE
              COMPUTE REB-REBATE ROUNDED =
                      ( 60 - ELAPSED-DAYS )
                        * ( REB-TOTCHG - REB-DEDCHG ) / 60
           ELSE
      *       ----------------------------------------------------------
      *       IF MORE THAN 60 DAYS HAVE ELAPSED THEN THERE IS NO REFUND
      *       ----------------------------------------------------------
              MOVE 0 TO REB-REBATE.

       REBATE-RBEARLY-21-EXIT.
           EXIT.

      ******************************************************************
      * EARLY PROVISION FOR SERVICE CHARGE ILLINOIS WORLD PR#733
      *
      *   1/60TH FOR EACH DAY FROM DATE OF PREPAYMENT TO THE SIXTIETH
      *   DAY OF LOAN (PRO RATA)
      ******************************************************************
       REBATE-RBEARLY-26 SECTION.
           MOVE LN-LOANDATE TO NUM-DATE.
           MOVE REB-PAYDATE TO SYS-DATE.
           PERFORM TIM365.
           IF REB-LPTRCD = "PO"
              IF ELAPSED-DAYS < 60
      *       ----------------------------------------------------------
      *       REBATE = UNELAPSED DAYS
      *                * ( SERVICE CHARGE - DEDUCT PRECHARGE ) / 60
      *       ----------------------------------------------------------
                   PERFORM REBATE-DEDUCT-CHARGE
                   COMPUTE REB-REBATE ROUNDED =
                        ( 60 - ELAPSED-DAYS )
                         * ( REB-TOTCHG - REB-DEDCHG ) / 60
              ELSE
      *       ----------------------------------------------------------
      *       IF MORE THAN 60 DAYS HAVE ELAPSED THEN THERE IS NO REFUND
      *       ----------------------------------------------------------
                   MOVE 0 TO REB-REBATE
           ELSE
               MOVE 0 TO REB-REBATE.

       REBATE-RBEARLY-26-EXIT.
           EXIT.

      ********************************************************************
      * CHECK TO SEE IF THE THE INSURANCE IN QUESTION WAS PUT ON AS AN
      * ADDON OR AS PART OF THE ORIGINAL LOAN.
      * IF NO LT INS RECORDS EXIST THEN IT WAS PUT ON WITH THE LOAN ELSE IT
      * WAS ADDED, ADJUSTED, CANCELLED AFTER THE LOAN WAS BOOKED.
      * 71105#0918
      ********************************************************************
       REBATE-CHECK-ADDON-TRAILER SECTION.
      * OTHER PARTS OF REBATE EXPECT LT-REC TO BE INTACT, SAVE HERE TO NOT
      * DISRUPT OTHER USES.
           MOVE LTI-REC TO REB-HOLD-LTI-REC.
           PERFORM OPEN-LTI1-FILE.
           MOVE 0 TO LTI-SEQNO.
           MOVE "N" TO REB-LTI-ADDON-FG.
           EVALUATE REB-SUB
      * CL
               WHEN 1 MOVE 1 TO REB-TEST-LTI-TYPE
               WHEN 2 MOVE 1 TO REB-TEST-LTI-TYPE
               WHEN 3 MOVE 1 TO REB-TEST-LTI-TYPE
               WHEN 4 MOVE 1 TO REB-TEST-LTI-TYPE
      * AH
               WHEN 5 MOVE 2 TO REB-TEST-LTI-TYPE
      * PP
               WHEN 6 MOVE 3 TO REB-TEST-LTI-TYPE
               WHEN 11 MOVE 3 TO REB-TEST-LTI-TYPE
      * 01-05
               WHEN 12 MOVE 4 TO REB-TEST-LTI-TYPE
               WHEN 13 MOVE 5 TO REB-TEST-LTI-TYPE
               WHEN 14 MOVE 6 TO REB-TEST-LTI-TYPE
               WHEN 15 MOVE 7 TO REB-TEST-LTI-TYPE
               WHEN 16 MOVE 8 TO REB-TEST-LTI-TYPE
               WHEN OTHER GO TO REBATE-CHECK-ADDON-TRAILER-EXIT
           END-EVALUATE.
       REBATE-CHECK-ADDON-TRAILER-NEXT.
           MOVE LN-ACCTNO TO LTI-ACCTNO.
           ADD 1 TO LTI-SEQNO.
           PERFORM READ-LTI1-FILE.
           IF IO-BAD
              GO TO REBATE-CHECK-ADDON-TRAILER-EXIT
           ELSE
              IF LTI-INS-TYPE = REB-TEST-LTI-TYPE
                 MOVE "Y" TO REB-LTI-ADDON-FG
              END-IF
              GO TO REBATE-CHECK-ADDON-TRAILER-NEXT
           END-IF.
       REBATE-CHECK-ADDON-TRAILER-EXIT.
           PERFORM CLOSE-LTI1-FILE.
           MOVE REB-HOLD-LTI-REC TO LTI-REC.

      *-----------------------------------------------------------------------
      *    DUMMY SECTION TO INCLUDE IN COPY MEMBERS THAT NEED TO BE
      *    REFLECTED IN 'WHAT' COMMANDS FOR SUPPORT
      *-----------------------------------------------------------------------
       SCCS-COPY-ROUTINE SECTION. MOVE
           "@(#)./LIBGB/REBATE.CPY S35 >"
              TO MESS.

      *-----------------------------------------------------------------------
