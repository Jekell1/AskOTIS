      * COPYMEMBER: LIBUP/DECLARE
      *
      * *** ATTENTION *** ATTENTION *** ATTENTION *** ATTENTION 
      * *** ATTENTION *** ATTENTION *** ATTENTION *** ATTENTION 
      * *** ATTENTION *** ATTENTION *** ATTENTION *** ATTENTION 
      *
      * JKC 2020-0123 THIS IS A EXACT COPY OF LIBGB/DECLARE.CPY BUT
      *               IT IS MODIFIED FOR ADDITIONAL ITEMS TO GET
      *               ACU4GL ERRORS IN ORDER TO DISPLAY THEM.
      *
      ***********************************************************************
      * REV:
      *  JTG 000508 ADDED LOGIC FOR SECOND ERROR MESSAGE TO INFORM
      *             OPERATOR TO STOP AND CALL SUPPORT          PARADATA #1171
      *  BLV 000619 SET PIGGY-BACK-FLAG & EXIT IF COMING FROM LONPMC - SO
      *             LONPMC CAN HANDLE FILE ERRORS WITHOUT LOCKING UP
      *  BLV 021023 ADD CHECK ON FILE-STAT 99, IF ERROR-LOCK-FG = "Y"
      *             DON'T LOOP THROUGH 10 TIMES & GIVE RECORD LOCKED WINDOW.
      *             THIS IS FOR SPECIAL SITUATIONS IN WHICH YOU WANT TO
      *             HANDLE THE LOCKING IN YOUR PROGRAM.  FOR EXAMPLE, YOU
      *             JUST WANT TO GIVE A MESSAGE THAT THE RECORD IS BUSY &
      *             TO SELECT ANOTHER INSTEAD OF TRYING 10 TIMES & STAYING
      *             IN THE RECORD LOCKED LOOP.  TO USE THIS, YOU MUST CHANGE
      *             THE IO-ROUTINE - WHERE IS LOOPS TO THE READ ON AN IO-FG
      *             OF 8, CHANGE TO DO THIS ONLY "IF ERROR-LOCK-FG NOT = Y".
      *             THEN ON THE READ IN QUESTION, YOU MUST MOVE "Y" TO THE
      *             FLAG BEFORE THE READ AND BE SURE TO SET IT BACK TO "N"
      *             OR BLANK AFTER THE READ.  I'M TRYING THIS IN GL/JRENT...
      *  BLF 051012 ADDED - MOVE "E" TO ERRCD - IN FILE-ERRORS-TERMINATE
      *             RIGHT BEFORE PERFORM OF CLOSE-FILES.  WHEN YOU WERE
      *             GETTING A BAD ERROR IN LONPF2, FOR EXAMPLE, AN OPEN
      *             ERROR OF 98, AFTER LOGGING ERROR IN LG & DISPLAYING
      *             ERROR WINDOW, THE LONP WOULD JUST SEND YOU BACK INTO
      *             LONPF2 AGAIN.  THIS WAS BECAUSE ERRCD WAS STILL A
      *             "P" AND PROGRAM DIDN'T CHANGE.  SO YOU GO BACK INTO
      *             LONPF2 & IT SAYS "LOAN HAS BEEN CHANGED, RE-INPUT
      *             CHANGES" WHICH ENCOURAGES USERS TO KEEP POSTING
      *             OVER & OVER & MAKING A MESS!!!  BETH & I TESTED,
      *             IT EVEN PERFORMED THIS WAY IN A60, NOT SURE WHEN
      *             IT STOPPED GOING TO "ERROR IN LOAN PROCESSING".
      *  BLF 051013 IF FILE STATUS IS 98 & USER IS IN LOAN PROCESSING,
      *             MOVE "Z" TO ERRCD, WHICH WILL CAUSE LONP TO LOG
      *             USER OFF
      *  BLM 120726 BROUGHT DECLARE OVER FROM A15 & REDID ALL CHANGES FOR
      *             NEW RELEASE
      *  MJD 130117 COMMENTED OUT CALL-TMMSG AND CALL-TMRSP PARAGRAPHS.
      *             THEY WILL INSTEAD BE PUT INTO COPYBOOKS.
      *  BAH 130312 CHANGED CALL SUSP TO CALL "C$SLEEP"
      *  BAH 130320 UNCOMMENTED ALARM, SHOULD WORK USING DISPLAY BEEP
      *  BLM 130530 FIXED RESET-MESS, WILL RESET LIST OF FIELDS IN MESS
      *             TO SPACES.
      *  BLM 130605 REMOVED RESET-MESS, PUT IN LIBGB/SCREEN
      *  BLM 131031 STARRED OFF TMRSP
      *  BLM 140507 CHANGED TO DISPLAY MESSAGE BOX FOR SEND-MESS INSTEAD OF
      *             CALLING GB/SENDME, VERYANT
      *  BAH 140521 COMMENTED OUT ALARM, CAUSING SCREEN SHIFTS
      *   CS 140826 GOT AN ERROR READING THE SPR & GOT THE NORMAL MESSAGE
      *             EXCEPT THE KEY, I ADDED MOVE ERROR-MSG-KEY TO ERRMSG3-MSG3
      *  BLM 161216 REMOVE MESSAGE ON FILE STAT 93 IF LONPR0, WILL NOW
      *             DISPLAY MESSAGE IN THAT PROGRAM
      *  BLM 161220 REMOVE REFERENCE TO TEST-FORM-1ST4, JUST REFERENCE AS
      *             POSTITION (16:4) OF FORM-PATHNAME
      * BAH 190207 ADDED IO-MULTI-FG FOR USE WITH FILES THAT HAVE BEEN
      *            SPLIT SO WE CAN IDENTIFY WHICH FILE HAD BAD I/O
      * JKC 2023-0929 ADDED COUNT-IT PARAGRAPH FOR SQL ROW COUNT IO
      * JKC 2024-0304 REMOVED ACU4GL ERROR DISPLAY LOGIC
      ***********************************************************************
       CHECK-ERROR-TYPE.
           IF E-MSG4 = "STAR"
              GO TO END-DECLARE.

           MOVE SPACES TO IO-MULTI-FG.
           MOVE 9      TO IO-FG.

           IF FILE-STAT = "93"
              IF LOCK-STATUS-93-RETRIES NOT = ZERO
                 MOVE 8 TO IO-FG
                 PERFORM SUSPEND
                 SUBTRACT 1 FROM LOCK-STATUS-93-RETRIES
                 GO TO END-DECLARE
              ELSE
                 IF FORM-PROGX = "LONPA0"
                    MOVE "  WARNING" TO ERRMSG-HEAD
                    MOVE "      SOMEONE IN YOUR BRANCH IS "
                       TO ERRMSG-MSG1
                    MOVE "      CURRENTLY UPDATING THIS DAY!"
                       TO ERRMSG-MSG2
                    MOVE SPACES         TO ERRMSG-MSG3
                    PERFORM CALL-ERRMSG
                    PERFORM CLOSE-FILES
                    GO TO EXIT-PROG
              ELSE
                 IF FORM-PROGX = "LONPR0"
                    GO TO END-DECLARE
                 ELSE
                    MOVE FILE-STAT TO E-STATUS-ERROR
                    MOVE "FILE(S) EXCLUSIVELY RESERVED" TO MESS
                    MOVE E-FILE TO E-FILEX
                    MOVE FORM-PATHNAME TO E-PROG
                    MOVE ERROR-MSG-HEAD TO ERRMSG-HEAD
                    MOVE ERROR-MSG-STAT TO ERRMSG-MSG1
                    MOVE ERROR-MSG-PATH TO ERRMSG-MSG2
                    MOVE SPACES         TO ERRMSG-MSG3
                    PERFORM CALL-ERRMSG
                    PERFORM CLOSE-FILES
                    GO TO EXIT-PROG.
           IF FILE-STAT = "99"
              MOVE 8 TO IO-FG
              IF ERROR-LOCK-FG = "Y"
                 GO TO END-DECLARE
              END-IF
              PERFORM SUSPEND
              ADD 1 TO LOCK-STATUS-99-RETRIES
              IF LOCK-STATUS-99-RETRIES = 10
                 MOVE 0 TO LOCK-STATUS-99-RETRIES
                 MOVE FILE-STAT TO E-STATUS-ERROR
                 MOVE "RECORD LOCKED BY ANOTHER USER" TO MESS
                 MOVE E-FILE TO E-FILEX
                 MOVE FORM-PATHNAME TO E-PROG
                 MOVE ERROR-MSG-HEAD TO ERRMSG-HEAD
                 MOVE ERROR-MSG-STAT TO ERRMSG-MSG1
                 MOVE ERROR-MSG-PATH TO ERRMSG-MSG2
                 MOVE "PRESS ENTER TO CONTINUE" TO ERRMSG-MSG3
                 PERFORM CALL-ERRMSG.

           CALL "C$RERR" USING EXTENDED-FILE-STAT.
           MOVE FILE-STAT TO FILE-STAT-DISP.
           IF SECONDARY-FILE-STAT NOT = SPACES
              MOVE "," TO FILE-STATC
              MOVE SECONDARY-FILE-STAT TO FILE-STATD2.
           MOVE FILE-STAT-DISP TO E-STATUS-ERROR.
           IF FILE-STAT = "41"
              MOVE 7 TO IO-FG.
       CHECK-IO9.
           IF IO-FG NOT = 9
              GO TO END-DECLARE.
           IF E-MSG4 = "OPEN" OR E-MSG4 = "REWR" OR
              E-MSG4 = "DELE" OR FILE-STAT = "30"
              GO TO FILE-ERRORS.
           GO TO END-DECLARE.
       OPEN-IT.
           MOVE "OPEN ERROR" TO E-MSG.
           MOVE SPACES TO E-KEYX.
           IF IO-FG NOT = 8 MOVE 0 TO LOCK-STATUS-99-RETRIES.
           MOVE 0 TO IO-FG IO-STATUS.
       COUNT-IT.
           MOVE "ROW COUNT ERROR" TO E-MSG.
           IF IO-FG NOT = 8 MOVE 0 TO LOCK-STATUS-99-RETRIES.
           MOVE 0 TO IO-FG IO-STATUS.
       READ-IT.
           MOVE "READ ERROR" TO E-MSG.
           IF IO-FG NOT = 8 MOVE 0 TO LOCK-STATUS-99-RETRIES.
           MOVE 0 TO IO-FG IO-STATUS.
       WRITE-IT.
           MOVE "WRITE ERROR" TO E-MSG.
           IF IO-FG NOT = 8 MOVE 0 TO LOCK-STATUS-99-RETRIES.
           MOVE 0 TO IO-FG IO-STATUS.
       REWRITE-IT.
           MOVE "REWRITE ERROR" TO E-MSG.
           IF IO-FG NOT = 8 MOVE 0 TO LOCK-STATUS-99-RETRIES.
           MOVE 0 TO IO-FG IO-STATUS.
       DELETE-IT.
           MOVE "DELETE ERROR" TO E-MSG.
           IF IO-FG NOT = 8 MOVE 0 TO LOCK-STATUS-99-RETRIES.
           MOVE 0 TO IO-FG IO-STATUS.
       START-IT.
           MOVE "START ERROR" TO E-MSG.
           IF IO-FG NOT = 8 MOVE 0 TO LOCK-STATUS-99-RETRIES.
           MOVE 0 TO IO-FG IO-STATUS.
       CLOSE-IT.
           MOVE "CLOSE ERROR" TO E-MSG.
       FORM-IT.
           MOVE "FORM ERROR" TO E-MSG.
           MOVE STAT TO E-STATUS-ERROR.
           MOVE SPACES TO E-KEYX.
       SUSPEND.
      *    CALL SUSP.
           CALL "C$SLEEP" USING 1.
       ALARM.
      *    DISPLAY OMITTED WITH BEEP.
       FILE-ERRORS.
           MOVE E-FILE TO E-FILEX ERRLOGW-FILE.
           MOVE FORM-PATHNAME TO E-PROG ERRLOGW-PROGX.
           MOVE E-MSG TO ERRLOGW-TYPE.
           MOVE E-CODE TO ERRLOGW-CODE.
           MOVE E-SUBCODE TO ERRLOGW-SUBCODE.
           MOVE E-KEYX TO ERRLOGW-KEY.
           MOVE "E" TO ERRLOGW-ACTION.
           MOVE FORM-PATHNAME TO FORM-PATH.
           MOVE "CL/ERRLOG" TO FORM-NAM.
           CALL FORM-PROGX USING FORM-PATH ERRLOGW-AREA EXIT-PATHNAME.
           CANCEL FORM-PROGX.
           MOVE FORM-PATHNAME TO FORM-PATH.
           IF PIGGY-BACK-FLAG = "PIGGY"
              MOVE "SNDERR" TO PIGGY-BACK-FLAG
              GO TO FILE-ERRORS-TERMINATE.
           PERFORM ALARM.
           IF E-MSG4 = "OPEN" OR "WRIT" OR "REWR"
              MOVE "**** STOP ****"       TO ERRMSG-HEAD
              STRING "                    CALL HOME "
                     "OFFICE OR SUPPORT             "
                     DELIMITED BY SIZE INTO ERRMSG-MSG1
              STRING "AN ERROR HAS OCCURRED         "
                     "                DO NOT PROCEED"
                     DELIMITED BY SIZE INTO ERRMSG-MSG2
              MOVE "PRESS ENTER TO CONTINUE" TO ERRMSG-MSG3
              PERFORM CALL-ERRMSG.
           MOVE SPACES TO MESS.
      * THIS MUST STAY LIKE THIS, E-MSG HAS "OPEN ERROR" OR WHATEVER
      * IN IT, IT MUST GET MOVED AS BELOW
           MOVE ERROR-MSG-HEAD TO ERRMSG-HEAD.
           MOVE ERROR-MSG-STAT TO ERRMSG-MSG1.
           MOVE ERROR-MSG-PATH TO ERRMSG-MSG2.
      *    MOVE SPACES         TO ERRMSG-MSG3.
           MOVE ERROR-MSG-KEY  TO ERRMSG-MSG3.
           PERFORM CALL-ERRMSG.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
           MOVE SPACES TO MESS.
       FILE-ERRORS-TERMINATE.
      *    A30, STARRED OFF, 7/26/12, BLM:
      *    IF WINDOWS-OPEN > ZERO
      *       CALL DELWIN
      *       SUBTRACT 1 FROM WINDOWS-OPEN
      *       GO TO FILE-ERRORS-TERMINATE.
           MOVE "E" TO ERRCD.
           IF FILE-STAT = "98"
              IF (FORM-PATHNAME(16:4) = "LONP" OR "XONP")
                 MOVE "Z" TO ERRCD.
           PERFORM CLOSE-FILES.
           GO TO EXIT-PROG.
       SEND-MESS.
           PERFORM ALARM.
           DISPLAY MESSAGE BOX MESS.
       COPY "LIBGB/TMMSG.CPY".
       COPY "LIBGB/ERRMSG.CPY".
       CLOSE-FILES.
           MOVE "CLOSE ERROR" TO E-MSG.
