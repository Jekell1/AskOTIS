       IDENTIFICATION DIVISION.
       PROGRAM-ID.      LONPN2.
       DATE-WRITTEN.    012484.
      ******************************************************************
      *
      *                 (LP)
      *  DESCRIPTION:   DIRECT LOAN (NON SALES FINANCE)
      *                 LOAN DELETE
      *
      *=================================================================
      * REV:
      * 062895 MJD ADDED COPY MEMBERS LIBLP/POSTDATE AND POSTDATEW.
      *            SET TC-USERID, POSTTIME, AND POSTDATE.
      *            SET TL-USERID, POSTTIME, AND POSTDATE.
      * 071995 JTG ADDED SET OF LN-PRPDUE-DAYS & LN-PRLNAMT
      * 072195 JTG ADDED LPSRCD LOGIC
      * MJD 081195 ADDED GP-FILE INFO FOR LIBLP/LPSRCD :
      *                  GP-THE-ONLY-SF-SRCD.
      * MJD 081695 ADDED GP-THE-ONLY-SF-SRCD LOGIC FOR DS UPDATES.
      * MJD 091895 ADDED DS-DATE LOGIC.
      * MJD 110995 ADDED LOGIC TO UPDATE TY-(DELINQ TABLE) BYPASSING
      *             VOLUME.
      * BAH 012496 ADDED SET OF PD-SALEFIN AND PD-SERVACCT ON PRIOR
      *            LOAN INFO, VOID OF "SC" WAS NOT GETTING SET PROPERLY
      * MJD 012596 ADDED LOGIC FOR UPDATE OF SEPARATE PL TABLE.
      * MJD 020896 ADDED UPDATE OF DEALER RESERVE FIELDS IN TY-REC.
      *             (NET GAIN/LOSS AND NEW VOLUME).
      * MJD 021396 ADDED UPDATE OF PL TABLES
      * KEC 031296 ON UPDATING PD-REC, MOVED ZEROES TO
      *            PD-APPROVAL-LIMIT.
      * JTG 042596 ADDED LOGIC TO REMOVE LN-MORELN FLAG IF NO OTHER
      *            SEQUENCE LOANS EXITS
      * JTG 061896 ADDED MA-RECORD LOGIC TO CHECK FOR LOAN APPLICATION
      * MJD 071696 ADDED LIBLP/LPUPWK FOR CHANGES IN LIBLP/AGEING
      * JTG 081396 CORRECTED NEVER ENDING LOOP IN UPDATE-MA1-FILE SECTION
      * JTG 093096 ADDED UPDATE OF DSFILE (DELINQUENCY)
      * KEC 112596 CHANGED LN-POCD FOR RESCISSION.
      * MJD 012297 REMOVED REFERENCE TO LN-INSTBL(9-10) AND TY-INS-TYPE(9-10)
      * KEC 012397 REVISED ACCESSING MA-FILE: NEW FORMAT FOR MA-FILE.
      * KEC 050197 REVISED FOR LOAN APPLICATIONS: ADDED UPDATE TO
      *            APPLICATION MEMOS.
      * GLF 050797 ADDED COPY MEMBERS: SETCTBLW AND SETCTBL
      * BLV 051997 UPDATE TY & DS FOR APPLICATION STATS
      * MJD 070297 ADDED DS-DLDISC-VOL AND TY-DLDISC
      * JTG 081497 ADDED UPDATE OF TL-GIFTNET
      * GLF 090897 REPLACED 'IO' ON GP-FILE WITH GET 'GPENV'
      * MJD 091797 REMOVED DELSUB-SET LOGIC; ADDED LPPDUE(W) COPY MEMBERS
      * GLF 091997 ADDED LOGIC FOR BW-PRIOR-RN-FOR10 AND BW-LAST-RN-FOR10
      * BLV 020698 DELETE BI RECORD WHEN LOAN IS VOIDED
      *  CS 031798 ADD TEST OF LC-APP-REQ-FOR-LOAN IF BR-APP-REQUIRED-FOR
      *            INPUT = Y TO DECIDE IF APP STATS SHOULD BE UPDATED.
      * MJD 032098 ADDED GETENV(W) FOR USE IN LIBLP/POSTDATE
      * BLV 042098 FIXED LOGIC FOR BW-PRIOR-RN-FOR10 - HAD LOGIC FOR
      *            BOOKING LOAN, NOT DELETING LOAN
      *  CS 042898 ADDED "RB" AND "RO" TO RENEWAL CODES
      * KEC 051498 ADDED 'SET-TRACK-STATUS'.  WAS NOT UPDATING THE
      *             MA-STATUS-TRACK.
      * JTG 061598 ADDED LOGIC TO UPDATE BANKRUPT DELINQUENT STATISTICS
      * JTG 061798 ADDED SET OF PD-PO-TRCD SO XONPC0.C COULD TEST RENEWAL TRCD
      * JTG 063098 EXPANDED DS-APP-TYPE-TBL TO 3 LEVELS
      * JTG 081898 ALLOWED 'PB' ON BANKRUPT ACCOUNTS IF GP-I-AM-WORLD <JGW
      * KEC 090498 ADDED 'MOVE LN-LOANTYPE TO PD-LOANTYPE';
      *            FOR REGENCY REVOLVING.
      * JTG 102398 CHANGED DS..APP..UPDATE FOR RENEWALS TO LEVEL 2 OTHER
      * GLF 111298 MADE MEMO DATE 2000 COMPATIBLE  0 AND BW-LAST-RN-FOR10
      * JTG 990322 ADDED UPDATE OF TL-SP-LNCOST-FRMLA RE: TFC #008
      * MJD 990420 CHANGED TO INCLUDE/EXCLUDE O2 IN DELINQ BASED
      *            ON GP-DELINQ-WITH-O2
      * BLV 990521 WHEN VOIDING CHECKS, SKIP IF CK-CLASS NOT = LN-CLASS:
      *            WORLD HAS PURGED LOANS WITH SAME ACCT# AS NEW LOAN
      *            IN OTHER CLASS, PROGRAM WAS VOIDING OLD CHECK FROM
      *            PURGED LOAN!!!
      * JTG 990611 ADDED LOGIC FOR UPDATE OF REVOLVING LOAN CONTRACTS, REGENCY
      * MJD 990720 CHANGED TO UPDATE BI FILE LAST AFTER ALL OTHER FILES
      *            HAVE BEEN UPDATED.
      * JTG 990721 STOPPED UPDATE OF BW-FILE FOR REVOLVING   REGENCY #45
      * JTG 990726 ADDED LOGIC FOR UPDATE OF REVOLVING LOAN APPLICATION STATS
      * MJD 990806 ADDED GP-INCL-OTH-IN-EARNED FLAG. IF "Y" THEN INCLUDE
      *            EARNINGS BUCKET 6 (OTHER) IN ACCUMULATED TOTALS
      *            OTHERWISE EXCLUDE BUCKET 6.
      * MJD 990910 CHANGED TO SKIP INSURANCE BUCKETS 7 & 8 (O4 & O5) WHEN
      *            UPDATING TY. CHANGED TO UPDATE INS-ELIG FOR O1 IF
      *            APPLICABLE. ADDED LOGIC TO UPDATE INS-NOT-WRITTEN (9 & 10)
      *            IF AD&D OR ROADGARD ARE NOT WRITTEN (WORLD ONLY)
      *            (WORLD PR# AS PER JOHN)
      * MJD 991012 CHANGE TO ONLY UPDATE BW-(PRIOR/LAST)-RN-FOR10 IF
      *            GP-RN-FOR10-LN NOT = "Y". LN-(PRIOR/LAST)-RN-FOR10
      *            DOES NOT NEED TO BE UPDATED ON VOID.  (REGMGM PR#942)
      * KEC 991217 {PR#0000} WAS A PROBLEM THAT THE MA-TRACK-STATUS
      *            WAS NOT BEING PROPERLY SETUP.
      *                CHANGED THE FOLLOWING:
      *                  1.  ORIGINAL-TRACK-STATUS WAS X(1) NOT X(6).
      *                  2.  WAS MOVING MA-STATUS NOT MA-TRACK-STATUS
      *                       TO ORIGINAL-TRACK-STATUS.
      * JTG 000324 MODIFIED TO UPDATE UNEARNED STATISTICS, MERCURY #223
      * MJD 000406 ADDED UPDATE OF TY-PROCEEDS AND DS-PROCEEDS (MERC PR#223)
      * MJD 000814 CHANGED TO COMPUTE EARNINGS BEFORE SETTING POCD TO "**".
      * BAH 020305 ADDED TEST OF CK-VOID = "R", REPLACEMENT VOID, WAS VOIDING
      *            AN ALREADY VOIDED/REPLACEMENT CHECK AGAIN !!!!
      * BAH 020403 DONT DELETE THE PAYMENT SCHEDULE LT, ITS NEEDED FOR THE
      *            CREDIT BUREAU (TRWTAP), CREATIVE PL#359
      * BLV 020910 MOVE LN-MIP-FG TO TL-MIP-FG ON VOID, REGWAN PR# 1801
      * CS  021129 ADDED UPDATE OF LXX FIELDS [WORLD PR#288]
      * CS  021218 NEED THE TAX CODES FROM THE LXX-FILE FOR THE TR FILE[BARB]
      *            [WORLD PR#288]
      * BAH 030722 WHEN VOIDING A "PB" LOAN, WE REBUILD THE PD SCRATCH PAD
      *            RECORD TO BE LIKE THE ORIGINAL PB PAYOFF RECORD THAT IS
      *            NEEDED TO BOOK THE NEW LOAN, AND WE KNEW TO SET THE
      *            LOAN TYPE FLAGS BACK TO THE ORIGINAL LOANS (THE ONE THAT
      *            WAS PAID OFF) SETTINGS, BUT WE FORGOT ABOUT ORGST, CLASS,
      *            AND SUBCLASS WHICH IS IMPORTANT TO MANY SERVICE CHARGE
      *            FORMULAS FOR RENEWAL RESTRICTIONS....LENDMARK SCFRMLA 46
      *
      * KEC 2003.0819 {PR#00000} PARADATA <A15> [KEN CISZEWSKI]
      *               ADDED NEW ROUTINE TO CREATE LOAN APPLICATION
      *               STATUS CHANGE MEMO (LIBLA/MEMOSTATUS)
      * BAH 031120 ADDED GP-NON-FILE-FEECODE TO UPDATE TY/BY FILE WITH
      *            NON FILE INSURANCE FOR WORLD SMITH REPLACEMENT
      * JTG 040520 ADDED LOGIC TO CAUSE THE UNEARNED LOAN COST TO ROLL TO
      *            THE NEW LOAN AFTER 'RENEWAL'. NOTE: THIS COST IS REFLECTIVE
      *            OF THE DISCOUNT WHEN PURCHASING LOAN PAPER NOT CARRIED AS
      *            SALES PAPER WITH DEALER DISCOUNT.
      *            ADDED SP-LNCOST-FRMLA = 'R'                    REGENCY #2287
      * BLF 070801 STARRED OFF "IF I-AM-WORLD" SO ROADGARD, AD&D & NONFILE
      *            INSURANCE WILL UPDATE TO STATS IF CODES ARE SET UP IN
      *            BRANCH OR GLOBAL PARAMTER RECORDS, CONSUMER PR# 3025
      * BAH 100429 DO NOT MOVE ANYTHING TO TL-ANTICERN(6) AND TL-EARNINGS(6)
      *            FOR GIFTNET, WORLD PR# 672
      *  CS 100603 CHANGED VOID CHECK TO CHECK FOR CK-PGM = "D0", REGENCY HAD
      *            AN 'OV' CHECK FROM 5 YEARS BACK WITH THE SAME ACCT# AS LOAN
      *            THAT WAS BEING VOIDED & THE 'OV' CHECK WAS ALSO VOIDED
      *                                                    PL#687 REGENCY
      * BLM 110829 ADD DELETE OF CUSTOMER INFO REC (CIFILE) FOR VOIDED
      *            LOAN, IF I-AM-WORLD, WORLD PR#439 ADDENDUM
      * KEC 2013.0206 {PR#04196} REGENCY <A15> [LORI MCBEE]
      *         MADE CHANGES TO LOAN APPS - ADDED MX-FILE.
      * KEC 2013.0625 {PR#04285} SOUTHERN AUTO <A15> [DAVID SINCLAIR]
      *            MADE IT TO RESTORE APPROVED STATUS ITEMS.
      * BAH 130801 ADDED SET OF TL-POINTS FOR EMLNRG, WORLD PR# 898
      *  CS 151221 ADD VOID OF ELECTRONIC PROCESSING (EP) ON VOID
      *                                              CITIZENS FIN PL912
      * BAH 160125 SPLIT OUT LTFILE, PD0003
      *
      * KEC 2016.0211 {PD#00003} PARADATA <A30> [JAY CISZEWSKI]
      *         FIXED KEYS & RECS FOR FD-REC.
      *         CHANGED FMNO/FORMNO/NEXTFM/SETFM FROM 3 TO 4 BYTES.
      *
      * BAH 160218 EXPANDED LM-REC ADDED LM-BRNO, PD0003
      *
      * KEC 2016.0219 {PD#00003} PARADATA <A30> [JAY CISZEWSKI]
      *         FIXED KEYS & RECS FOR FD-REC.
      *         CHANGED FMNO/FORMNO/NEXTFM/SETFM FROM 3 TO 4 BYTES.
      * KEC 2016.0310 {PD#00003} PARADATA <A30> [JAY CISZEWSKI]
      *         CHANGED CD-GROUP, CD-SECURED & CD-TRW TO BE
      *         CD-CO-GROUP, CD-CO-SECURED & CD-CO-TRW.
      * BLM 160310 FIXED FOR NEW TY DATE FORMAT, PD0003
      * BAH 160304 CHANGED TILLNO TO NUMERIC
      * BAH 160422 ADDED BI-LN-SMALLOAN FOR DUAL TEST, REGENCY PR#5030
      *
      * KEC 2016.0505 {PD#00003} PARADATA <A30> [JAY CISZEWSKI]
      *         CHANGED MA-APPNO TO MA-APP-NUMBER TO MATCH MA1-KEY.
      *         CHANGED MEMOSTATUS-APPNO TO MEMOSTATUS-APP-NUMBER
      *          AND INCREASE FROM 6 TO 9 BYTES.
      *         CHANGED MEMOSTATUS-NUMBER TO MEMOSTATUS-ACCTNO 
      *          AND INCREASE FROM 6 TO 8 BYTES.
      *         CHANGED MEMOSTATUS-STATUS-DATE FROM 6 TO 8 BYTES.
      *         CHANGED LN-APPNO TO USE LN-APP-NUMBER FOR MA1-KEY.
      *         CHANGED MA-STATUS-CD TO MA-STATUS-REASON.
      * CS 160509 REMOVED REDEF OF LE-FLAG/LE-SSNO AND MADE SEPARATE FIELDS
      *           CHANGED LE-FLAG FROM PIC X(4) TO PIC X.
      * BLM 160513 FIX FOR NEW DS-DATE FORMAT, FIX DS-UNEARNED, PD0003
      *
      * KEC 2016.0524 {PD#00003} PARADATA <A30> [JAY CISZEWSKI]
      *         CHANGED USE OF MA-APPDATE & MA-STATUS-DATE
      *          TO USE CCYYMMDD.
      *
      * KEC 2016.0525 {PD#00003} PARADATA <A30> [JAY CISZEWSKI]
      *         CHANGED USE OF MA-TRACK-STATUS TO BE X(10).
      *
      * KEC 2016.0621 {PD#00003} PARADATA <A30> [JAY CISZEWSKI]
      *         REMOVED USE OF THE MX-FILE [LIBLA/LAFDMX].
      *         REMOVED USE OF OLD STATUS ROUTINES [LIBLA/MASTATUS(W)].
      *         ADDED USE OF NEW MA-STATUS-INFO FIELDS; AS WELL AS
      *         ADDED USE OF NEW ROUTINES [LIBLA/LAFRMA & LAWSMA].
      *
      * KEC 2017.0105 {PR#00000} PARADATA <A30> [JAY CISZEWSKI]
      *          CHANGED DS-DATE TO DS-DATE-CCYYMM FOR CONSISTENCY
      *          WITH THE DATE FIELDS IN BY & UD FILE.
      *  CS 170531 EP-FILE MISSING FROM DECLARE
      * BAH 170629 ACTIVATE 8 DIGIT DATES, PD0006
      * BAH 180215 UPDATE DEBIT CARD TRAILER FOR VOID DATE AND
      *            TL-DEBIT-CARD-AMT, WORLD #1306
      * BLM 180228 FIX TO UPDATE TY-FEE TABLE INSTEAD OF TY-INS TABLE 
      *            FOR NONFILE, AD&D AND ROADGARD
      * BAH 180316 ADDED LN-PLDATE TO BICREA CALL, WORLD #1308
      * KEC 180824 LOAN APP MODULE REMOVAL FROM A30 [LARM30]
      * BAH 181008 GLOBAL CKFILE
      * BAH 181010 GLOBAL CXFILE
      * BAH 181030 GLOBAL LEFILE
      * BAH 181115 GLOBAL FMFILE
      *  CS 181121 GLOBAL LTAFILE
      *  CS 181121 GLOBAL LTBFILE
      * BAH 181128 GLOBAL LMFILE
      *  CS 120518 GLOBAL LTCFILE
      * BAH 181211 GLOBAL LNFILE
      *  CS 181227 GLOBAL LTFFILE
      *  CS 190103 GLOBAL LTIFILE
      *  CS 190107 GLOBAL LTLFILE
      * BAH 190109 GLOBAL NOFILE
      * BAH 190116 GLOBAL LXFILE, SPLIT
      *  CS 190206 COMMENT AWFILE, REPLACE WITH LIBLP/LPCPAW
      * BAH 190313 MOVED EDIT-FM-INITKEY TO LPWSFM-EDIT-FM-INITKEY 
      * BAH 20190625 REMOVED LN-REVOLVING, PT0153
      * BAH 20220422 UPDATE TL-ACTIONCD2, SECURITIZATION #1551
      * BAH 20220830 ADDED UPDATE FOR REPAY VOIDS SO AUTO PAY KNOWS THIS
      *              ACCOUNT WAS VOIDED. LOAN UPDATE CREATES IN NEWLOANS
      *              AND VOIDS WILL BE IN VOIDS FOLDER. #1589
      * BAH 20231011 REMOVED EPFILE
      * BAH 2024.0130 CALL CRNOR2 AND WRITE NOTICE RECORD
      * BAH 2025.0106 ADDED BW-UMC-EXPIRE-DATE AND BW-UMC-PR-EXPIRE-DATE #1687
      *                                                      S35Q-146
      * BAH 2025.0319 UPDATE-BI-FILE WAS MISSING! S35Q-186
      ******************************************************************
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       SOURCE-COMPUTER. 3B.
       OBJECT-COMPUTER. 3B.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

       COPY "LIBLP/LPFSWK.CPY".
      * REPAY VOID FILE
           SELECT WK-REPAY-LN-FILE ASSIGN OUTPUT WK-REPAY-LN-PATH
                  ORGANIZATION LINE SEQUENTIAL
                  LOCK MODE AUTOMATIC WITH LOCK ON RECORD
                  FILE STATUS FILE-STAT.
       I-O-CONTROL.
       DATA DIVISION.
       FILE SECTION.

       COPY "LIBLP/LPFDWK.CPY".
      **************************************************
      * REPAY LOAN ASCII FILE    RSZ =  (INCLUDES 0D0A)
      **************************************************
       FD  WK-REPAY-LN-FILE
           RECORD IS VARYING IN SIZE FROM 5 TO 200 CHARACTERS
           LABEL RECORDS ARE STANDARD.
       01  WK-REPAY-LN-REC.
           03  WK-REPAY-LN-BYTE         PIC X OCCURS 200 TIMES.


       WORKING-STORAGE SECTION.
       77  SCCS-IDENTIFIER PIC X(37) VALUE
           "@(#)LP/LONPN2 S35 03/19/25-15:07:52 >".
      **********************************
      *       F I L E   I F N          *
      **********************************
       COPY "LIBLP/LP01TRC.CPY".
       COPY "LIBLP/LP01TRC_SQL.CPY".
       COPY "LIBLP/LPWSTRC.CPY".
       COPY "LIBLP/LP01TRL.CPY".
       COPY "LIBLP/LP01TRL_SQL.CPY".
       COPY "LIBLP/LPWSTRL.CPY".
       COPY "LIBLP/LP01LC.CPY".
       COPY "LIBLP/LP01LC_SQL.CPY".
       COPY "LIBLP/LPWSLC.CPY".
       COPY "LIBLP/LP01LE.CPY".
       COPY "LIBLP/LP01LE_SQL.CPY".
       COPY "LIBLP/LPWSLE.CPY".
       COPY "LIBLP/LP01LM.CPY".
       COPY "LIBLP/LP01LM_SQL.CPY".
       COPY "LIBLP/LPWSLM.CPY".
       COPY "LIBLP/LP01SP.CPY".
       COPY "LIBLP/LP01SPA.CPY".
       COPY "LIBLP/LP01SPB.CPY".
       COPY "LIBLP/LP01SPC.CPY".
       COPY "LIBLP/LP01SP_SQL.CPY".
       COPY "LIBLP/LPWSSP.CPY".
       COPY "LIBLP/LP01LN.CPY".
       COPY "LIBLP/LP01LN_SQL.CPY".
       COPY "LIBLP/LPWSLN.CPY".
       COPY "LIBLP/LP01LTA.CPY".
       COPY "LIBLP/LP01LTA_SQL.CPY".
       COPY "LIBLP/LPWSLTA.CPY".
       COPY "LIBLP/LP01LTB.CPY".
       COPY "LIBLP/LP01LTB_SQL.CPY".
       COPY "LIBLP/LPWSLTB.CPY".
       COPY "LIBLP/LP01LTC.CPY".
       COPY "LIBLP/LP01LTC_SQL.CPY".
       COPY "LIBLP/LPWSLTC.CPY".
       COPY "LIBLP/LP01LTD.CPY".
       COPY "LIBLP/LP01LTD_SQL.CPY".
       COPY "LIBLP/LPWSLTD.CPY".
       COPY "LIBLP/LP01LTF.CPY".
       COPY "LIBLP/LP01LTF_SQL.CPY".
       COPY "LIBLP/LPWSLTF.CPY".
       COPY "LIBLP/LP01LTI.CPY".
       COPY "LIBLP/LP01LTI_SQL.CPY".
       COPY "LIBLP/LPWSLTI.CPY".
       COPY "LIBLP/LP01LTL.CPY".
       COPY "LIBLP/LP01LTL_SQL.CPY".
       COPY "LIBLP/LPWSLTL.CPY".
       COPY "LIBLP/LP01LTP.CPY".
       COPY "LIBLP/LP01LTP_SQL.CPY".
       COPY "LIBLP/LPWSLTP.CPY".
       COPY "LIBLP/LP01LX.CPY".
       COPY "LIBLP/LP01LX_SQL.CPY".
       COPY "LIBLP/LPWSLX.CPY".
       COPY "LIBLP/LP01BW.CPY".
       COPY "LIBLP/LP01BW_SQL.CPY".
       COPY "LIBLP/LPWSBW.CPY".
       COPY "LIBLP/LP01CA.CPY".
       COPY "LIBLP/LP01CA_SQL.CPY".
       COPY "LIBLP/LPWSCA.CPY".
       COPY "LIBLP/LP01TY.CPY".
       COPY "LIBLP/LP01TY_SQL.CPY".
       COPY "LIBLP/LPWSTY.CPY".
       COPY "LIBLP/LP01DS.CPY".
       COPY "LIBLP/LP01DS_SQL.CPY".
       COPY "LIBLP/LPWSDS.CPY".
       COPY "LIBLP/LP01PD.CPY".
       COPY "LIBLP/LP01PD_SQL.CPY".
       COPY "LIBLP/LPWSPD.CPY".
       COPY "LIBLP/LP01FM.CPY".
       COPY "LIBLP/LP01FM_SQL.CPY".
       COPY "LIBLP/LPWSFM.CPY".
       COPY "LIBLP/LP01NO.CPY".
       COPY "LIBLP/LP01NO_SQL.CPY".
       COPY "LIBLP/LPWSNO.CPY".
       COPY "LIBLP/LP01CD.CPY".
       COPY "LIBLP/LP01CD_SQL.CPY".
       COPY "LIBLP/LPWSCD.CPY".
       COPY "LIBLP/LP01CK.CPY".
       COPY "LIBLP/LP01CK_SQL.CPY".
       COPY "LIBLP/LPWSCK.CPY".
       COPY "LIBLP/LP01CX.CPY".
       COPY "LIBLP/LP01CX_SQL.CPY".
       COPY "LIBLP/LPWSCX.CPY".
       COPY "LIBGB/GB01BR.CPY".
       COPY "LIBGB/GB01BR_SQL.CPY".
       COPY "LIBGB/GBWSBR.CPY".
       COPY "LIBLP/LP01CI.CPY".
       COPY "LIBLP/LP01CI_SQL.CPY".
       COPY "LIBLP/LPWSCI.CPY".

      * REPAY VOIDED LOANS
      * /USR/DATA/R1/FTP/REPAY/VOIDS/V.BRNO.NUMBER
      *                                1234.123456
       01  WK-REPAY-LN-PATH.
           03  FILLER                    PIC X      VALUE "/".
           03  WK-REPAY-LN-PATH-BASE     PIC X(9).
           03  FILLER                    PIC X      VALUE "/".
           03  WK-REPAY-LN-PATH-MACHINE  PIC X(2).
           03  WK-REPAY-LN-PATH-DIR      PIC X(11)  VALUE "/FTP/REPAY/".
           03  WK-REPAY-LN-FILENAME      PIC X(8)   VALUE "VOIDS/V.".
           03  WK-REPAY-LN-PATH-BRNO     PIC 9999.
           03  FILLER                    PIC X VALUE ".".
           03  WK-REPAY-LN-PATH-NUMBER   PIC 9(6).

       01  WK-IFN          PIC X(2) VALUE "WK".
       01  WK-REPAY-LN-IFN PIC X(7) VALUE "REPAYLN".

       01  WK-KEY          PIC 9(2).

       01  MSEL-LIST       PIC X(5)    VALUE "MSEL.".
       01  SUB             PIC 9(6)    COMP.
       01  B-SUB           PIC 9(6)    COMP.
       01  REC-EXISTS      PIC X       VALUE "N".

      * REPAY RECORD
       01  WS-REPAY-LN-REC.
           03  WS-REPAY-VOID-FLAG                PIC X.
           03  WS-REPAY-LN-BRACCTNO              PIC Z(12).
           03  WS-REPAY-LN-NUMBER                PIC Z(8).
           03  WS-REPAY-LN-OWNBR                 PIC Z(6).
           03  WS-REPAY-BW-FNAME                 PIC X(16).
           03  WS-REPAY-BW-LNAME                 PIC X(16).

      * REPAY WORKERS
       01  CR-1             PIC X VALUE X"0D".
       01  JUSTIFY-TYPE     PIC X VALUE "L".
       01  WS-BRACCT.
           03  WS-OWNBR     PIC Z(6).
           03  WS-NUMBER    PIC 9(6).
      * REPAY WORKERS

       01  HOLD-LN-ACCTNO         PIC 9(8)    COMP.
       01  HOLD-DEBIT-CARD-AMT    PIC S9(7)V99 COMP-3 VALUE 0.

       01  UMC-TERM               PIC 9(3)V99.
       01  UMC-TERMX REDEFINES UMC-TERM.
           03  UMC-WHOLE          PIC 9(3).
           03  UMC-DEC1           PIC 9.
           03  UMC-DEC2           PIC 9.
       01  FEE-SUB             PIC 9(6) COMP-3.

       01  MISC-INS-WORKERS.
           03  ADD-WRITTEN         PIC X(01)   VALUE "N".
           03  ROADGARD-WRITTEN    PIC X(01)   VALUE "N".
           03  NON-FILE-WRITTEN    PIC X(01)   VALUE "N".

       01  SAVE-TL-LINE    PIC 9(3).
       01  SAVE-OUTBAL     PIC S9(7)V99 COMP VALUE 0.

       01  SAVE-LN-ACCTNO      PIC 9(8).
       01  SAVE-LM-SEQ         PIC 9(3).
       01  WK-MMEMO.
           03  FILLER          PIC X(3) VALUE "ON ".
           03  WK-DATE         PIC 99/99/99.
           03  FILLER          PIC X(5) VALUE
               " RE: ".
           03  WK-ACCTNO       PIC 999999/99.
           03  FILLER          PIC X(26) VALUE
               ", DELETED INTEREST REB ADJ".

       01  SAVE-TC-SEQ       PIC 9(5).

       COPY "LIBCL/CRNOR2W.CPY".
       COPY "LIBLP/LPPOFFW.CPY".
       COPY "LIBGB/DATERW.CPY".
       COPY "LIBGB/CONNECTW_SQL.CPY".
       COPY "LIBGB/FILEWK.CPY".
       COPY "LIBGB/MESSWK.CPY".
       COPY "LIBCL/CREAIXW.CPY".
       COPY "LIBLP/LPWKPATH.CPY".
       COPY "LIBLP/LPMENUW.CPY".
       COPY "LIBLP/LPSETTYW.CPY".
       COPY "LIBLP/LPDSCLW.CPY".
       COPY "LIBLP/POSTDATEW.CPY".
       COPY "LIBGB/GETENVW.CPY".
       COPY "LIBLP/LPSRCDW.CPY".
       COPY "LIBLP/LPPDUEW.CPY".
       COPY "LIBGB/AGEINGW.CPY".
       COPY "LIBGB/GPENVW.CPY".
       COPY "LIBLP/LPUPWKW.CPY".
       COPY "LIBLP/LPCEPPW.CPY".
       COPY "LIBGB/SPPDTHW.CPY".
       COPY "LIBLP/LPEARNW.CPY".
       COPY "LIBLP/LPMDTEW.CPY".
       COPY "LIBLP/RNFOR10W.CPY".

       COPY "LIBGB/CONAME_EXT.CPY".
       COPY "LIBGB/SCREENW.CPY".
       COPY "LIBGB/HELPLINK.CPY".

       COPY "LIBGB/GETFMW.CPY".
       COPY "LIBLP/LPLOANW.CPY".
       COPY "LIBLP/LPPFBUF.CPY".

       SCREEN SECTION.

       PROCEDURE DIVISION
             USING FORM-PATHNAME EXIT-PATHNAME PROG-BUF UPDATE-BUF.
       DECLARATIVES.
       DECLARE-ERROR SECTION.
           USE AFTER ERROR PROCEDURE ON
               WK-FILE.
       COPY "LIBGB/DECLARE.CPY".
           PERFORM CLOSE-TY1-FILE.
           PERFORM CLOSE-TRL1-FILE.
           PERFORM CLOSE-TRC1-FILE.
           PERFORM CLOSE-SP1-FILE.
           PERFORM CLOSE-PD1-FILE.
           PERFORM CLOSE-NO1-FILE.
           PERFORM CLOSE-LX1-FILE.
           PERFORM CLOSE-LTP1-FILE.
           PERFORM CLOSE-LTL1-FILE.
           PERFORM CLOSE-LTI1-FILE.
           PERFORM CLOSE-LTF1-FILE.
           PERFORM CLOSE-LTD1-FILE.
           PERFORM CLOSE-LTC1-FILE.
           PERFORM CLOSE-LTB1-FILE.
           PERFORM CLOSE-LTA1-FILE.
           PERFORM CLOSE-LN1-FILE.
           PERFORM CLOSE-LM1-FILE.
           PERFORM CLOSE-LE1-FILE.
           PERFORM CLOSE-LC1-FILE.
           PERFORM CLOSE-FM1-FILE.
           PERFORM CLOSE-DS1-FILE.
           PERFORM CLOSE-CX1-FILE.
           PERFORM CLOSE-CK1-FILE.
           PERFORM CLOSE-CI1-FILE.
           PERFORM CLOSE-CD1-FILE.
           PERFORM CLOSE-CA1-FILE.
           PERFORM CLOSE-BW1-FILE.
           PERFORM CLOSE-BR-FILE.
           CLOSE WK-FILE.
       COPY "LIBGB/DECLAR2.CPY".

      ***********************************
      *   C O N T R O L   M O D U L E   *
      ***********************************
       MAIN-PROGRAM SECTION.

           PERFORM SQL-CONNECT.

      D    STOP MESS.
           MOVE FORM-PATHNAME TO FORM-PATH.

           MOVE USER-PID TO TEMP-PID.

           MOVE 7 TO WK-KEY.
           PERFORM OPEN-WK-FILE.
           PERFORM READ-WK-FILE.
           IF IO-FG = 9
              GO TO IO-ERROR.

           MOVE WK-SAVE-REC TO LN-REC HOLD-LN-REC.
           MOVE LN-ACCTNO TO HOLD-LN-ACCTNO.

           MOVE LN-ORGST TO SP-ORGST.
           MOVE LN-SPRCLASS TO SP-SPRCLASS.
           MOVE LN-SUBCLASS TO SP-SUBCLASS.
           MOVE LN-LAWCODE TO SP-LAWCODE.
           PERFORM OPEN-SP1-FILE.
           PERFORM READ-SP1-FILE.
           PERFORM CLOSE-SP1-FILE.
           IF IO-FG = 9
              GO TO IO-ERROR.

           PERFORM GET-GPENV.

           PERFORM OPEN-BR-FILE.
           MOVE LN-OWNBR TO BR-NO.
           PERFORM READ-BR-FILE.
           PERFORM CLOSE-BR-FILE.
           IF IO-FG = 9
              GO TO IO-ERROR.

           PERFORM OPEN-LN1-FILE.
           PERFORM READ-LN1-FILE.
           IF IO-FG = 9
              GO TO IO-ERROR.

           IF ( LN-POCD-VOID )
              MOVE "ACCOUNT HAS BEEN DELETED" TO MESS
              PERFORM SEND-MESS
              GO TO FINISH-UPDATE.

           IF ( LN-POCD-INRESCISSION )
              MOVE "ACCOUNT IS IN RESCISSION" TO MESS
              PERFORM SEND-MESS
              GO TO FINISH-UPDATE.

           IF ( LN-POCD-RESCIND )
              MOVE "ACCOUNT HAS BEEN RESCINDED" TO MESS
              PERFORM SEND-MESS
              GO TO FINISH-UPDATE.

           MOVE LN-CLASS TO LC-CODE.
           PERFORM GET-LC1-RECORD.
           IF ( IO-FG NOT = ZEROES )
              MOVE "N" TO LC-APP-REQ-FOR-LOAN.

      * NEED TO COMPUTE EARNINGS BEFORE SETTING POCD TO "**"
      * (COMPUTE-EARNINGS WILL NOT COMPUTE EARNINGS FOR VOIDED ACCOUNTS
      *  WHICH IS NEEDED FOR STAT UPDATES OF UNEARNED)

           PERFORM COMPUTE-EARNINGS.

      * ADDED TO PREVENT TWO USERS FROM DELETING
           MOVE "**" TO LN-POCD.
           PERFORM REWRITE-LN1-FILE.

           PERFORM CLOSE-LN1-FILE.
      D    STOP MESS.

      * WANT THIS HERE SO WE GET DEBIT CARD AMT FROM THE TRAILER FILE
      * AND CAN UPDATE THE LOAN TRANS FILE WITH THAT AMOUNT!
      * IF OROCEEDS EQUAL ALL CASH, NO SENSE LOOKING FOR A DEBIT CARD
           MOVE 0 TO HOLD-DEBIT-CARD-AMT.
           IF LN-PROCASH NOT = LN-PROCEEDS
              PERFORM UPDATE-DEBIT-CARD.

           PERFORM UPDATE-TR1-FILE.

           IF LN-PROCASH NOT = 0
              PERFORM UPDATE-CA1-FILE.

           PERFORM UPDATE-REPAY-FILE.

           PERFORM UPDATE-BW1-FILE.

           PERFORM UPDATE-LM1-FILE.
           PERFORM UPDATE-LT1-FILE.
           PERFORM SET-DELINQUENCY.
           IF LN-PRLNNO NOT = 0
              PERFORM UPDATE-PD1-FILE.

      * NOTE: UPDATE-MA-FILE MUST BE DONE BEFORE DS & TY UPDATE:
           PERFORM UPDATE-DS1-FILE.
           PERFORM UPDATE-TY1-FILE.

           IF LN-SMALLOAN = "Y"
              PERFORM UPDATE-LE1-FILE.

           PERFORM UPDATE-FM1-FILE.

           PERFORM UPDATE-CI1-FILE.

           PERFORM UPDATE-LN1-FILE.

           PERFORM UPDATE-NOTICE-FILE.

           IF LN-PROCASH NOT = LN-PROCEEDS
              PERFORM VOID-CHECK-ROUTINE
              PERFORM REMOVE-TEMP-CHECKS.

           MOVE HOLD-LN-REC TO LN-REC.

           MOVE "UPDATING GLOBAL BORROWER INDEX....(BI)......." TO MESS.
           PERFORM SEND-LEGEND.
           MOVE 0 TO IO-FG.
           MOVE "D" TO STAT.
           PERFORM UPDATE-BI-FILE.

       FINISH-UPDATE.
           MOVE " " TO WK-REC LN-REC.
           MOVE 0 TO LN-ACCTNO LOAN-KEY.
           MOVE LN-REC TO WK-SAVE-REC.
           MOVE 0 TO WK-ELE.
           PERFORM REWRITE-WK-FILE.

           MOVE "N0N" TO NEW-PROG-FORM.
           MOVE "P" TO ERRCD.
           GO TO END-ROUTINE.

      ********************************
       UPDATE-DEBIT-CARD SECTION.
      ********************************
           PERFORM OPEN-LTD1-FILE.
           MOVE LTD-PATH-OWNBR  TO LTD-BRNO.
           MOVE LN-ACCTNO TO LTD-ACCTNO.
           MOVE 1         TO LTD-SEQNO.
           PERFORM READ-LTD1-FILE.
      * NO DEBIT CARD ENTRIES
           IF IO-FG NOT = 0
              GO TO EXIT-UPDATE-DEBIT-CARD.

           MOVE "UPDATING DEBIT CARD NUMBER.........." TO MESS.
           PERFORM SEND-LEGEND.

           MOVE LTD-DEBIT-CARD-AMT TO HOLD-DEBIT-CARD-AMT.
           MOVE TRANS-DATE         TO LTD-VOID-DATE.
           PERFORM REWRITE-LTD1-FILE.
           PERFORM CLOSE-LTD1-FILE.

       EXIT-UPDATE-DEBIT-CARD.
           EXIT.
          
      ************************
       UPDATE-TR1-FILE SECTION.
      ************************
           MOVE "UPDATING LOAN TRANSACTION FILE.........." TO MESS.
           PERFORM SEND-LEGEND.

           PERFORM OPEN-TRL1-FILE.
           MOVE 1 TO SAVE-TL-LINE.
       UPDATE-NEXT-TR1.
           PERFORM CLEAR-TRL-FILE.

           MOVE BRANCH       TO TL-BRNO.
           MOVE TRANS-DATE   TO TL-DATE.
           MOVE LN-CLASS     TO TL-CLASS.
           MOVE LN-ACCTNO    TO TL-ACCTNO.
           MOVE LN-LAWCODE   TO TL-LAWCODE.
           MOVE SAVE-TL-LINE TO TL-LINE.

           MOVE LN-LOANTYPE TO TL-LOANTYPE.
           MOVE LN-LOANDATE TO TL-LOANDATE.
           MOVE LN-INTDATE TO TL-INTDATE.
           MOVE LN-PURCD TO TL-PURCD.
           MOVE LN-SRCD TO TL-SRCD.
           MOVE LN-COLLCD TO TL-COLLCD.
           MOVE LN-ORGTERM TO TL-ORGTERM.
           SUBTRACT LN-PROCEEDS FROM TL-PROCEEDS.
           SUBTRACT LN-PROCASH FROM TL-PROCASH.
           SUBTRACT LN-LNAMT FROM TL-LNAMT.
           SUBTRACT LN-FINCHG FROM TL-FINCHG.
           SUBTRACT LN-1STPYAMT FROM TL-1STPYAMT.
           MOVE LN-NOREGPY TO TL-NOREGPY.
           SUBTRACT LN-REGPYAMT FROM TL-REGPYAMT.
           SUBTRACT LN-LASTPYAMT FROM TL-LASTPYAMT.
           MOVE LN-INSURED TO TL-INSURED.
           MOVE LN-DLNO TO TL-DLNO.
           MOVE LN-PRLNNO TO TL-PRLNNO.
           SUBTRACT LN-PRDLDISC-UNEARN FROM TL-PRDLDISC-UNEARN.
           SUBTRACT LN-PRBAL FROM TL-PRBAL.
           SUBTRACT LN-PRINTDUE FROM TL-PRINTDUE.
           SUBTRACT LN-PRFINCHGRB FROM TL-PRFINCHGRB.
           SUBTRACT LN-PRINSRB FROM TL-PRINSRB.
           MOVE LN-SERVACCT TO TL-SERVACCT.
           MOVE LN-TILLNO TO TL-TILLNO.
           MOVE LN-GIFTNET TO TL-GIFTNET.
           SUBTRACT LN-ACCERN(1) FROM TL-EARNINGS(1).
           SUBTRACT LN-ACCERN(2) FROM TL-EARNINGS(2).
           SUBTRACT LN-ACCERN(3) FROM TL-EARNINGS(3).
           SUBTRACT LN-ACCERN(4) FROM TL-EARNINGS(4).
           SUBTRACT LN-ACCERN(5) FROM TL-EARNINGS(5).
           SUBTRACT LN-MAINTFEE FROM TL-MAINTFEE.
           IF LN-ORGST = "GA"
              SUBTRACT LN-POINTS FROM TL-POINTS.
           SUBTRACT LN-SERCHG FROM TL-SERCHG.
           SUBTRACT LN-EXTCHG FROM TL-EXTCHG.
           MOVE LN-INSTYPES TO TL-INSTYPES.
           MOVE LN-INSELIGIBLE TO TL-INSELIGIBLE.
           SUBTRACT LN-INTCHG FROM TL-INTCHG.
           SUBTRACT LN-ANTICERN(1) FROM TL-ANTICERN(1).
           SUBTRACT LN-ANTICERN(2) FROM TL-ANTICERN(2).
           SUBTRACT LN-ANTICERN(3) FROM TL-ANTICERN(3).
           SUBTRACT LN-ANTICERN(4) FROM TL-ANTICERN(4).
           SUBTRACT LN-ANTICERN(5) FROM TL-ANTICERN(5).
           IF LN-GIFTNET NOT = "Y"
              SUBTRACT LN-ANTICERN(6) FROM TL-ANTICERN(6)
              SUBTRACT LN-ACCERN(6) FROM TL-EARNINGS(6).

           MOVE SP-LNCOST-FRMLA TO TL-SP-LNCOST-FRMLA.

           MOVE 1 TO SUB.
       NEXT-TL-BORRTBL.
           IF SUB NOT > 4
              MOVE LN-SSNO(SUB) TO TL-SSNO(SUB)
              MOVE LN-MAKERCD(SUB) TO TL-MAKERCD(SUB).
           IF SUB NOT > 8
              MOVE LN-INSCOMP(SUB) TO TL-INSCOMP(SUB)
              COMPUTE TL-INSPREM(SUB) = 0 - LN-INSPREM(SUB)
              MOVE LN-INSEXP-DATE(SUB) TO TL-INSEXP-DATE(SUB)
              MOVE LN-INSOURS(SUB) TO TL-INSOURS(SUB)
              MOVE LN-INSEFF-DATE(SUB) TO TL-INSEFF-DATE(SUB)
              COMPUTE TL-INSCOMM(SUB) = 0 - LN-INSCOMM(SUB).
           IF SUB NOT > 10
              MOVE LN-FEECODE(SUB) TO TL-FEECODE(SUB)
              COMPUTE TL-FEEAMT(SUB) = 0 - LN-FEEAMT(SUB).
           IF SUB < 10
              ADD 1 TO SUB
              GO TO NEXT-TL-BORRTBL.

      * MISSISSIPPI INT. REB. ADJUSTMENT:
           IF LN-STATE-FLAG = "A"
              COMPUTE TL-STATE-WORKER = 0 - LN-STATE-WORKER
              MOVE "A" TO TL-LPEOD5-FG.

           PERFORM SET-POSTDATE.
           MOVE POST-USERID TO TL-USERID.
           MOVE POST-TIME TO TL-POSTTIME.
           MOVE POST-DATE TO TL-POSTDATE.

           MOVE LN-MIP-FG TO TL-MIP-FG.
      *WHEN BR-FORCE-VALID-CITY-COUNTY IS 'Y'
           IF BR-FORCE-VALID-CITY-COUNTY = "Y"
              PERFORM GET-LXX-TAX
              MOVE LXX-INS-TAX-COUNTY-CD TO TL-INS-TAX-COUNTY-CD
              MOVE LXX-INS-TAX-CITY-CD   TO TL-INS-TAX-CITY-CD.

           SUBTRACT HOLD-DEBIT-CARD-AMT FROM TL-DEBIT-CARD-AMT.
           MOVE LN-ACTIONCD2 TO TL-ACTIONCD2.

           PERFORM WRITE-TRL1-FILE.
           IF IO-FG = 0
              GO TO EXIT-UPDATE-TR1.

           IF SAVE-TL-LINE < 999
              ADD 1 TO SAVE-TL-LINE
              GO TO UPDATE-NEXT-TR1.

           MOVE "TRANSACTION SEQ = 999, REC NOT CREATED !" TO MESS.
           PERFORM SEND-MESS.
       EXIT-UPDATE-TR1.
           PERFORM CLOSE-TRL1-FILE.

      ************************
       UPDATE-CA1-FILE SECTION.
      ************************
           MOVE "UPDATING CASH DRAWER.................." TO MESS.
           PERFORM SEND-LEGEND.

           MOVE BRANCH     TO CA-BRNO.
           MOVE TRANS-DATE TO CA-DATE.
           MOVE LN-TILLNO  TO CA-TILLNO.
           PERFORM OPEN-CA1-FILE.
           PERFORM READ-CA1-FILE.
           IF IO-FG = 0
              ADD LN-PROCASH TO CA-RUNNING
              PERFORM REWRITE-CA1-FILE.
           PERFORM CLOSE-CA1-FILE.

      ************************
       UPDATE-BW1-FILE SECTION.
      ************************
           MOVE "UPDATING BORROWER FILE.................." TO MESS.
           PERFORM SEND-LEGEND.

           PERFORM GET-COLLATERAL.
           MOVE 1 TO SUB.
           PERFORM OPEN-BW1-FILE.
       UPDATE-NEXT-BW.
           MOVE LN-SSNO(SUB) TO BW-SSNO.
           PERFORM READ-BW1-FILE.
           IF IO-FG = 9
              GO TO IO-ERROR.
           SUBTRACT 1 FROM BW-LOANS.
           IF SUB = 1
              IF CD-CO-SECURED = "R"
                 SUBTRACT LN-YTDINTPD FROM BW-RE-INTYTD
              ELSE
                 SUBTRACT LN-YTDINTPD FROM BW-INTYTD.
           IF LN-SMALLOAN = "Y"
              SUBTRACT LN-LNAMT FROM BW-LIASMLN
           ELSE
              SUBTRACT LN-LNAMT FROM BW-LIAOTH.

      * SET LAST RENEWAL DATE FOR RENEWALS LESS THAN 10%:
           IF GP-RN-FOR10-LN NOT = "Y"
              PERFORM COMPUTE-RNFOR10
              IF RNFOR10-FG = "Y"
                 MOVE BW-PRIOR-RN-FOR10-DATE TO BW-LAST-RN-FOR10-DATE
                 MOVE 0 TO BW-PRIOR-RN-FOR10-DATE.

      * UPDATE UMC EXPIRATION DATE, IF WRITTEN (MAKER ONLY)
            IF SUB = 1
               PERFORM VARYING FEE-SUB FROM 1 BY 1 UNTIL FEE-SUB > 10
               IF (LN-FEECODE(FEE-SUB) = BR-ROAD-G-SGL-FEECODE AND
                  BR-ROAD-G-SGL-FEECODE NOT = SPACES) OR
                     (LN-FEECODE(FEE-SUB) = BR-ROAD-G-FAM-FEECODE AND
                       BR-ROAD-G-FAM-FEECODE NOT = SPACES)
                  PERFORM UPDATE-BW-UMC-EXPIRE.


           PERFORM REWRITE-BW1-FILE.
           IF SUB < 4
              ADD 1 TO SUB
              IF LN-SSNO(SUB) NOT = 0
                 GO TO UPDATE-NEXT-BW.
           PERFORM CLOSE-BW1-FILE.

      ************************
       UPDATE-LM1-FILE SECTION.
      ************************
           MOVE "DELETING LOAN MEMO FILE................." TO MESS.
           PERFORM SEND-LEGEND.

           PERFORM OPEN-LM1-FILE.

           MOVE LM-PATH-OWNBR TO LM-BRNO
                                 QLM1-WBEG-BRNO
                                 QLM1-WEND-BRNO.
           MOVE LN-ACCTNO     TO LM-ACCTNO
                                 QLM1-WBEG-ACCTNO
                                 QLM1-WEND-ACCTNO.
           MOVE 0             TO LM-DATE
                                 QLM1-WBEG-DATE-REV.
           MOVE 1             TO LM-SEQ 
                                 QLM1-WBEG-SEQ.
           MOVE ALL "9"       TO QLM1-WEND-DATE-REV
                                 QLM1-WEND-SEQ.

           PERFORM START-LM1-FILE.
       NEXT-LM1-DELETE.
           PERFORM READ-LM1-FILE-NEXT.
           IF IO-FG = 0
              IF (LM-ACCTNO = LN-ACCTNO) AND (LM-BRNO = LM-PATH-OWNBR)
                 PERFORM DELETE-LM1-FILE
                 GO TO NEXT-LM1-DELETE.

           PERFORM CLOSE-LM1-FILE.

      ***********************
       UPDATE-LT1-FILE SECTION.
      ***********************

      * DELETE ALL THE TRAILERS EXCEPT PAYMENT SCHEDULES, NEED THAT FOR
      * REPORTING VOIDS SOMEWHERE ?

      * DO NOT DELETE DEBIT CARD TRAILERS EITHER. LOAN PURGE WILL DO THAT

           MOVE "DELETING LOAN TRAILER FILE............" TO MESS.
           PERFORM SEND-LEGEND.

           PERFORM DELETE-LTA-RECORDS.
           PERFORM DELETE-LTB-RECORDS.
           PERFORM DELETE-LTC-RECORDS.
           PERFORM DELETE-LTF-RECORDS.
           PERFORM DELETE-LTI-RECORDS.
           PERFORM DELETE-LTL-RECORDS.

       EXIT-UPDATE-LT1-FILE.
           EXIT.


      ****************************************
       DELETE-LTA-RECORDS SECTION.
      ****************************************
           PERFORM OPEN-LTA1-FILE.

           MOVE LTA-PATH-OWNBR TO LTA-BRNO
                                  QLTA1-WBEG-BRNO
                                  QLTA1-WEND-BRNO.
           MOVE LN-ACCTNO      TO LTA-ACCTNO
                                  QLTA1-WBEG-ACCTNO
                                  QLTA1-WEND-ACCTNO.
           MOVE 0              TO LTA-SEQNO
                                  QLTA1-WBEG-SEQNO.
           MOVE ALL "9"        TO QLTA1-WEND-SEQNO.

           PERFORM START-LTA1-FILE.
       NEXT-LTA-DELETE.
           PERFORM READ-LTA1-FILE-NEXT.
           IF IO-FG NOT = 0
              GO TO DELETE-LTA-RECORDS-EXIT.

           IF (LTA-BRNO = LTA-PATH-OWNBR) AND
               (LTA-ACCTNO = LN-ACCTNO)
              PERFORM DELETE-LTA1-FILE.

           GO TO NEXT-LTA-DELETE.

       DELETE-LTA-RECORDS-EXIT.
           PERFORM CLOSE-LTA1-FILE.

      ****************************************
       DELETE-LTB-RECORDS SECTION.
      ****************************************
           PERFORM OPEN-LTB1-FILE.

           MOVE LTB-PATH-OWNBR TO LTB-BRNO
                                  QLTB1-WBEG-BRNO
                                  QLTB1-WEND-BRNO.
           MOVE LN-ACCTNO      TO LTB-ACCTNO
                                  QLTB1-WBEG-ACCTNO
                                  QLTB1-WEND-ACCTNO.
           MOVE 0              TO LTB-SEQNO
                                  QLTB1-WBEG-SEQNO.
           MOVE ALL "9"        TO QLTB1-WEND-SEQNO.

           PERFORM START-LTB1-FILE.
       NEXT-LTB-DELETE.
           PERFORM READ-LTB1-FILE-NEXT.
           IF IO-FG NOT = 0
              GO TO DELETE-LTB-RECORDS-EXIT.

           IF (LTB-BRNO = LTB-PATH-OWNBR) AND
               (LTB-ACCTNO = LN-ACCTNO)
              PERFORM DELETE-LTB1-FILE.

           GO TO NEXT-LTB-DELETE.

       DELETE-LTB-RECORDS-EXIT.
           PERFORM CLOSE-LTB1-FILE.

      ****************************************
       DELETE-LTC-RECORDS SECTION.
      ****************************************
           PERFORM OPEN-LTC1-FILE.

           MOVE LTC-PATH-OWNBR TO LTC-BRNO
                                  QLTC1-WBEG-BRNO
                                  QLTC1-WEND-BRNO.
           MOVE LN-ACCTNO      TO LTC-ACCTNO
                                  QLTC1-WBEG-ACCTNO
                                  QLTC1-WEND-ACCTNO.
           MOVE 0              TO LTC-SEQNO
                                  QLTC1-WBEG-SEQNO.
           MOVE ALL "9"        TO QLTC1-WEND-SEQNO.
           PERFORM START-LTC1-FILE.
       NEXT-LTC-DELETE.
           PERFORM READ-LTC1-FILE-NEXT.
           IF IO-FG NOT = 0
              GO TO DELETE-LTC-RECORDS-EXIT.

           IF (LTC-BRNO = LTC-PATH-OWNBR) AND
               (LTC-ACCTNO = LN-ACCTNO)
              PERFORM DELETE-LTC1-FILE.

           GO TO NEXT-LTC-DELETE.

       DELETE-LTC-RECORDS-EXIT.
           PERFORM CLOSE-LTC1-FILE.

      ****************************************
       DELETE-LTF-RECORDS SECTION.
      ****************************************
           PERFORM OPEN-LTF1-FILE.

           MOVE LTF-PATH-OWNBR TO LTF-BRNO
                                  QLTF1-WBEG-BRNO
                                  QLTF1-WEND-BRNO.
           MOVE LN-ACCTNO      TO LTF-ACCTNO
                                  QLTF1-WBEG-ACCTNO
                                  QLTF1-WEND-ACCTNO.
           MOVE 0              TO LTF-SEQNO
                                  QLTF1-WBEG-SEQNO.
           MOVE ALL "9"        TO QLTF1-WEND-SEQNO.

           PERFORM START-LTF1-FILE.
       NEXT-LTF-DELETE.
           PERFORM READ-LTF1-FILE-NEXT.
           IF IO-FG NOT = 0
              GO TO DELETE-LTF-RECORDS-EXIT.

           IF (LTF-BRNO  = LTF-PATH-OWNBR) AND
               (LTF-ACCTNO = LN-ACCTNO)
              PERFORM DELETE-LTF1-FILE.

           GO TO NEXT-LTF-DELETE.

       DELETE-LTF-RECORDS-EXIT.
           PERFORM CLOSE-LTF1-FILE.

      ****************************************
       DELETE-LTI-RECORDS SECTION.
      ****************************************
           PERFORM OPEN-LTI1-FILE.
           MOVE LTI-PATH-OWNBR  TO LTI-BRNO.
           MOVE LN-ACCTNO TO LTI-ACCTNO.
           MOVE 0         TO LTI-SEQNO.

           MOVE LTI-PATH-OWNBR TO LTI-BRNO
                                  QLTI1-WBEG-BRNO
                                  QLTI1-WEND-BRNO.
           MOVE LN-ACCTNO      TO LTI-ACCTNO
                                  QLTI1-WBEG-ACCTNO
                                  QLTI1-WEND-ACCTNO.
           MOVE 0              TO LTI-SEQNO
                                  QLTI1-WBEG-SEQNO.
           MOVE ALL "9"        TO QLTI1-WEND-SEQNO.

           PERFORM START-LTI1-FILE.
       NEXT-LTI-DELETE.
           PERFORM READ-LTI1-FILE-NEXT.
           IF (IO-FG NOT = 0) OR (LTI-BRNO NOT = LTI-PATH-OWNBR)
              GO TO DELETE-LTI-RECORDS-EXIT.

           IF (LTI-ACCTNO = LN-ACCTNO)
              PERFORM DELETE-LTI1-FILE.

           GO TO NEXT-LTI-DELETE.

       DELETE-LTI-RECORDS-EXIT.
           PERFORM CLOSE-LTI1-FILE.

      ****************************************
       DELETE-LTL-RECORDS SECTION.
      ****************************************
           PERFORM OPEN-LTL1-FILE.
           MOVE LN-OWNBR  TO LTL-BRNO.
           MOVE LN-ACCTNO TO LTL-ACCTNO.
           MOVE 0         TO LTL-SEQNO.

           MOVE LTL-PATH-OWNBR TO LTL-BRNO
                                  QLTL1-WBEG-BRNO
                                  QLTL1-WEND-BRNO.
           MOVE LN-ACCTNO      TO LTL-ACCTNO
                                  QLTL1-WBEG-ACCTNO
                                  QLTL1-WEND-ACCTNO.
           MOVE 0              TO LTL-SEQNO
                                  QLTL1-WBEG-SEQNO.
           MOVE ALL "9"        TO QLTL1-WEND-SEQNO.

           PERFORM START-LTL1-FILE.
       NEXT-LTL-DELETE.
           PERFORM READ-LTL1-FILE-NEXT.
           IF (IO-FG NOT = 0) OR (LTL-BRNO NOT = LTL-PATH-OWNBR)
              GO TO DELETE-LTL-RECORDS-EXIT.

           IF LTL-ACCTNO = LN-ACCTNO
              PERFORM DELETE-LTL1-FILE.

           GO TO NEXT-LTL-DELETE.

       DELETE-LTL-RECORDS-EXIT.
           PERFORM CLOSE-LTL1-FILE.

      ************************
       UPDATE-DS1-FILE SECTION.
      ************************
           MOVE "UPDATING DEALER STATISTICS FILE........." TO MESS.
           PERFORM SEND-LEGEND.

           PERFORM OPEN-DS1-FILE.
           MOVE "N" TO REC-EXISTS.
       UPDATE-DS1-FILE-CHECK.
           PERFORM SET-DS1-KEY.
           PERFORM READ-DS1-FILE.
           IF IO-FG = 0
              GO TO UPDATE-DS1-FILE-UPD-REC.
           IF REC-EXISTS = "Y"
              GO TO IO-ERROR.
           MOVE "Y" TO REC-EXISTS.
           PERFORM CLEAR-DS-FILE.
           PERFORM SET-DS1-KEY.
           PERFORM WRITE-DS1-FILE.
           IF IO-FG = 9 AND E-CODE NOT = "22"
              GO TO IO-ERROR.
           GO TO UPDATE-DS1-FILE-CHECK.

       UPDATE-DS1-FILE-UPD-REC.

           IF LN-SRCD = "PB" OR "RN" OR "SC" OR "RB" OR "RO"
              SUBTRACT 1 FROM DS-RNEWUNIT(1) DS-RNEWUNIT(2)
              SUBTRACT LN-LNAMT FROM DS-RNEWAMT(1) DS-RNEWAMT(2)
           ELSE
           IF LN-SRCD = "NB"
              SUBTRACT 1 FROM DS-NPURUNIT(1) DS-NPURUNIT(2)
              SUBTRACT LN-LNAMT FROM DS-NPURAMT(1) DS-NPURAMT(2)
           ELSE
                 SUBTRACT 1 FROM DS-OPURUNIT(1) DS-OPURUNIT(2)
                 SUBTRACT LN-LNAMT FROM DS-OPURAMT(1) DS-OPURAMT(2).

      * UPDATE UNEARNED STATISTICS NET AND VOLUME
      
           IF GP-UNEARNED-STATS = "Y"
              PERFORM VARYING DSSUB3 FROM 1 BY 1 UNTIL DSSUB3 > 6
                        AFTER DSSUB2 FROM 1 BY 1 UNTIL DSSUB2 > 2
                  IF DSSUB2 = 1
                     SUBTRACT EARN-UNEARN(DSSUB3) FROM
                                 DS-UNEARNED-AMT(1 DSSUB2 DSSUB3)
                  ELSE
                     IF (DSSUB3 < 6) OR
                        (DSSUB3 = 6 AND NOT GP-INCL-OTH-IN-EARNED-N)
                        SUBTRACT LN-ANTICERN(DSSUB3)
                                 FROM DS-UNEARNED-AMT(1 DSSUB2 DSSUB3)
                                      DS-UNEARNED-AMT(2 DSSUB2 DSSUB3)
                     END-IF
                  END-IF
              END-PERFORM
      * UPDATE PROCEEDS: WE DO NOT CARE ABOUT ACCOUNT STATUS.
              SUBTRACT LN-PROCEEDS FROM DS-PROCEEDS(1)
                                        DS-PROCEEDS(2).

           SUBTRACT LN-ORGTERM FROM DS-SUMWGTTM.
           SUBTRACT LN-LNAMT FROM DS-SUMWGTAV.
           COMPUTE DS-SUMWGTTA =
              DS-SUMWGTTA - (LN-ORGTERM * LN-LNAMT).
           IF LN-SERVACCT NOT = "Y"
              SUBTRACT LN-DLDISC FROM DS-DLDISC
                                      DS-DLDISC-VOL(1)
                                      DS-DLDISC-VOL(2)
              SUBTRACT LN-DLRVPRIN(1) FROM DS-DLRVPRIN(1)
              SUBTRACT LN-DLRVPRIN(2) FROM DS-DLRVPRIN(2)
              SUBTRACT LN-DLRVPRIN(3) FROM DS-DLRVPRIN(3)
              SUBTRACT LN-DLRVCHGS(1) FROM DS-DLRVCHGS(1)
              SUBTRACT LN-DLRVCHGS(2) FROM DS-DLRVCHGS(2)
              SUBTRACT LN-DLRVCHGS(3) FROM DS-DLRVCHGS(3)
              SUBTRACT LN-DLPARVHELD FROM DS-DLPARVHELD
              SUBTRACT LN-DLPARVPAID FROM DS-DLPARVPAID
           ELSE
              MOVE 0 TO LN-DLDISC LN-DLRVPRIN(1) LN-DLRVPRIN(2)
                        LN-DLRVPRIN(3) LN-DLRVCHGS(1) LN-DLRVCHGS(2)
                        LN-DLRVCHGS(3) LN-DLPARVHELD LN-DLPARVPAID.
           SUBTRACT 1 FROM DS-OUTSUNIT(1).
           SUBTRACT LN-LNAMT FROM DS-OUTSAMT(1).

      * UPDATE DELINQUENCY NORMAL:
      * SETUP OUTBAL WORKER FOR UPDATE OF STATS

           IF GP-DELINQ-WITH-O2 = "Y"
              MOVE LN-CURBAL TO SAVE-OUTBAL
           ELSE
              SUBTRACT LN-OT2BAL FROM LN-CURBAL
                         GIVING SAVE-OUTBAL.

           SUBTRACT 1 FROM DS-UNITS(DSNOW DSREC RDSUB)
                           DS-UNITS(DSNOW DSCON CSUB).
           SUBTRACT SAVE-OUTBAL FROM DS-OUTBAL(DSNOW DSREC RDSUB)
                                     DS-OUTBAL(DSNOW DSCON CSUB).
      * UPDATE DELINQUENCY BANKRUPT:
      *<JGWIF LN-SRCD = "RB"
           IF LN-BNKRPTDATE NOT = 0
              SUBTRACT 1 FROM DS-UNITS-BNKRPT(DSNOW DSREC RDSUB)
                              DS-UNITS-BNKRPT(DSNOW DSCON CSUB)
              SUBTRACT SAVE-OUTBAL
                        FROM DS-OUTBAL-BNKRPT(DSNOW DSREC RDSUB)
                             DS-OUTBAL-BNKRPT(DSNOW DSCON CSUB).
       UPDATE-DS1-FILE-EXIT.
           PERFORM REWRITE-DS1-FILE.
           PERFORM CLOSE-DS1-FILE.

      **************************************************
       UPDATE-TY1-FILE SECTION.
      **************************************************
           MOVE "UPDATING TODAY'S TOTAL FILE............." TO MESS.
           PERFORM SEND-LEGEND.

           PERFORM OPEN-TY1-FILE.
           MOVE "N" TO REC-EXISTS.
       UPDATE-TY1-FILE-CHECK.
           PERFORM SET-TY-KEY.
           PERFORM READ-TY1-FILE.
           IF IO-FG = 0
              GO TO UPDATE-TY1-FILE-UPD-REC.
           IF REC-EXISTS = "Y"
              GO TO IO-ERROR.

           MOVE "Y" TO REC-EXISTS.
           PERFORM CLEAR-TY-FILE.
           PERFORM SET-TY-KEY.
           PERFORM WRITE-TY1-FILE.
           IF IO-FG = 9 AND E-CODE NOT = "22"
              GO TO IO-ERROR.
           GO TO UPDATE-TY1-FILE-CHECK.

       UPDATE-TY1-FILE-UPD-REC.

           PERFORM GET-SRCD-SUB.

      * UPDATE UNEARNED STATISTICS NET AND VOLUME
      
           IF GP-UNEARNED-STATS = "Y"
              PERFORM VARYING TYSUB3 FROM 1 BY 1 UNTIL TYSUB3 > 6
                        AFTER TYSUB2 FROM 1 BY 1 UNTIL TYSUB2 > 2
                  IF TYSUB2 = 1
                     SUBTRACT EARN-UNEARN(TYSUB3) FROM
                                 TY-UNEARNED-AMT(TYSUB2 TYSUB3)
                  ELSE
                     IF (TYSUB3 < 6) OR
                        (TYSUB3 = 6 AND NOT GP-INCL-OTH-IN-EARNED-N)
                        SUBTRACT LN-ANTICERN(TYSUB3)
                                 FROM TY-UNEARNED-AMT(TYSUB2 TYSUB3)
                     END-IF
                  END-IF
              END-PERFORM
      * UPDATE PROCEEDS: WE DO NOT CARE ABOUT ACCOUNT STATUS.
              SUBTRACT LN-PROCEEDS FROM TY-PROCEEDS.

           SUBTRACT LN-ORGTERM FROM TY-SUMWGTTM.
           SUBTRACT LN-LNAMT FROM TY-SUMWGTAV.
           COMPUTE TY-SUMWGTTA =
              TY-SUMWGTTA - (LN-ORGTERM * LN-LNAMT).

      * UPDATE DEALER RESERVES:
      
           IF LN-SERVACCT NOT = "Y"
              SUBTRACT LN-DLDISC      FROM TY-DLDISC
              SUBTRACT LN-DLRVPRIN(1) FROM TY-DLRVPRIN(1 1)
                                           TY-DLRVPRIN(2 1)
              SUBTRACT LN-DLRVPRIN(2) FROM TY-DLRVPRIN(1 2)
                                           TY-DLRVPRIN(2 2)
              SUBTRACT LN-DLRVPRIN(3) FROM TY-DLRVPRIN(1 3)
                                           TY-DLRVPRIN(2 3)
              SUBTRACT LN-DLRVCHGS(1) FROM TY-DLRVCHGS(1 1)
                                           TY-DLRVCHGS(2 1)
              SUBTRACT LN-DLRVCHGS(2) FROM TY-DLRVCHGS(1 2)
                                           TY-DLRVCHGS(2 2)
              SUBTRACT LN-DLRVCHGS(3) FROM TY-DLRVCHGS(1 3)
                                           TY-DLRVCHGS(2 3)
              SUBTRACT LN-DLPARVHELD  FROM TY-DLPARVHELD(1)
                                           TY-DLPARVHELD(2).

           IF SRCD-NOT-PL
              SUBTRACT 1 FROM TY-NO(1 SRCD)
                              TY-NO(2 SRCD)
              SUBTRACT LN-LNAMT FROM TY-AMT(1 SRCD)
                                     TY-AMT(2 SRCD)
              SUBTRACT LN-OT2BAL FROM TY-O2(1 SRCD)
                                      TY-O2(2 SRCD)
              SUBTRACT LN-INTCHG FROM TY-INT(1 SRCD)
                                      TY-INT(2 SRCD)
              SUBTRACT LN-EXTCHG FROM TY-INT(1 SRCD)
                                      TY-INT(2 SRCD)
           ELSE
              SUBTRACT 1 FROM TY-NO-PL(1 SRCD)
                              TY-NO-PL(2 SRCD)
              SUBTRACT LN-LNAMT FROM TY-AMT-PL(1 SRCD)
                                     TY-AMT-PL(2 SRCD)
              SUBTRACT LN-OT2BAL FROM TY-O2-PL(1 SRCD)
                                      TY-O2-PL(2 SRCD)
              SUBTRACT LN-INTCHG FROM TY-INT-PL(1 SRCD)
                                      TY-INT-PL(2 SRCD)
              SUBTRACT LN-EXTCHG FROM TY-INT-PL(1 SRCD)
                                      TY-INT-PL(2 SRCD).

      * UPDATE DELINQUENCY NORMAL:

      * SETUP OUTBAL WORKER FOR UPDATE OF STATS
      
           IF GP-DELINQ-WITH-O2 = "Y"
              MOVE LN-CURBAL TO SAVE-OUTBAL
           ELSE
              SUBTRACT LN-OT2BAL FROM LN-CURBAL
                         GIVING SAVE-OUTBAL.

           SUBTRACT 1 FROM TY-UNITS(1 RDSUB)
                           TY-UNITS(2 CSUB).
           SUBTRACT SAVE-OUTBAL FROM TY-OUTBAL(1 RDSUB)
                                     TY-OUTBAL(2 CSUB).
      * UPDATE DELINQUENCY BANKRUPT:
      *<JGWIF LN-SRCD = "RB"
           IF LN-BNKRPTDATE NOT = 0
              SUBTRACT 1 FROM TY-UNITS-BNKRPT(1 RDSUB)
                              TY-UNITS-BNKRPT(2 CSUB)
              SUBTRACT SAVE-OUTBAL FROM TY-OUTBAL-BNKRPT(1 RDSUB)
                                        TY-OUTBAL-BNKRPT(2 CSUB).

      * UPDATE INSURANCES
      *  DO NOT INCLUDE 7&8 HERE THEY ARE NOT BEING USED YET
      *  AND WE DO NOT WANT TO COUND THEN AS NOT WRITTEN IN STATS
      *                                         (MJD 09/10/1999).
           PERFORM UPDATE-TY1-FILE-INS
              VARYING SUB FROM 1 BY 1 UNTIL SUB > 6.

           IF LN-INSELIGIBLE-DL
                           OR LN-INSELIGIBLE-LL
              SUBTRACT 1 FROM TY-INS-NO-ELIG(1).

           IF LN-INSELIGIBLE-AH
              SUBTRACT 1 FROM TY-INS-NO-ELIG(2).

           IF LN-INSELIGIBLE-PP
                           OR LN-INSELIGIBLE-PP2
              SUBTRACT 1 FROM TY-INS-NO-ELIG(3).

           IF LN-INSELIGIBLE-O1
              SUBTRACT 1 FROM TY-INS-NO-ELIG(4).

      * TEST FOR ROADGARD INSURANCE AND AD&D (WORLD)
           MOVE "N" TO ADD-WRITTEN ROADGARD-WRITTEN NON-FILE-WRITTEN
           PERFORM VARYING SUB FROM 1 BY 1 UNTIL SUB > 10
              IF LN-FEECODE(SUB) NOT = SPACES
                 IF LN-FEECODE(SUB) = GP-NON-FILE-FEECODE
                    SUBTRACT 1 FROM TY-FEE-NO-WROTE(1)
                    SUBTRACT LN-FEEAMT(SUB) FROM TY-FEE-AMT-WROTE(1)
                    MOVE "Y" TO NON-FILE-WRITTEN
                 END-IF
                 IF LN-FEECODE(SUB) = BR-ADD-INS-FEECODE
                    SUBTRACT 1 FROM TY-FEE-NO-WROTE(2)
                    SUBTRACT LN-FEEAMT(SUB) FROM TY-FEE-AMT-WROTE(2)
                    MOVE "Y" TO ADD-WRITTEN
                 END-IF
                 IF LN-FEECODE(SUB) = BR-ROAD-G-SGL-FEECODE OR
                    LN-FEECODE(SUB) = BR-ROAD-G-FAM-FEECODE
                    SUBTRACT 1 FROM TY-FEE-NO-WROTE(3)
                    SUBTRACT LN-FEEAMT(SUB) FROM TY-FEE-AMT-WROTE(3)
                    MOVE "Y" TO ROADGARD-WRITTEN
                 END-IF
              END-IF
           END-PERFORM
           IF NON-FILE-WRITTEN = "N"
              SUBTRACT 1 FROM TY-FEE-NO-NOTWROTE(1)
           END-IF
           IF ADD-WRITTEN = "N"
              SUBTRACT 1 FROM TY-FEE-NO-NOTWROTE(2)
           END-IF
           IF ROADGARD-WRITTEN = "N"
              SUBTRACT 1 FROM TY-FEE-NO-NOTWROTE(3).

       UPDATE-TY1-FILE-EXIT.
           PERFORM REWRITE-TY1-FILE.
           PERFORM CLOSE-TY1-FILE.

      **************************************************
       UPDATE-TY1-FILE-INS SECTION.
      **************************************************
           IF LN-INSPREM(SUB) NOT = 0
              SUBTRACT 1 FROM TY-INS-NO-WROTE(SUB)
              SUBTRACT LN-INSPREM(SUB) FROM TY-INS-AMT-WROTE(SUB)
           ELSE
              SUBTRACT 1 FROM TY-INS-NO-NOTWROTE(SUB).

      ************************
       UPDATE-LE1-FILE SECTION.
      ************************
           MOVE "UPDATING LOAN ENDORSER FILE............." TO MESS.
           PERFORM SEND-LEGEND.

           PERFORM OPEN-LE1-FILE.
           MOVE 1 TO B-SUB.

       START-NEXT-LE1.
           MOVE LN-OWNBR       TO LE-BRNO
                                  QLE1-WBEG-BRNO
                                  QLE1-WEND-BRNO.
           MOVE LN-ACCTNO      TO LE-ACCTNO
                                  QLE1-WBEG-ACCTNO
                                  QLE1-WEND-ACCTNO.
           MOVE LN-SSNO(B-SUB) TO LE-SSNO
                                  QLE1-WBEG-SSNO.
           MOVE ALL "9"        TO QLE1-WEND-SSNO.
           MOVE SPACES         TO LE-TYPE LE-FLAG
                                  QLE1-WBEG-TYPE
                                  QLE1-WBEG-FLAG.
           MOVE ALL "Z"        TO QLE1-WEND-TYPE
                                  QLE1-WEND-FLAG.
           MOVE ZEROS          TO LE-OSACCTNO
                                  QLE1-WBEG-OSACCTNO.
           MOVE ALL "9"        TO QLE1-WEND-OSACCTNO.

           PERFORM START-LE1-FILE.
       NEXT-LE1-DELETE.
           PERFORM READ-LE1-FILE-NEXT.
           IF (IO-FG = 9) OR (LE-BRNO NOT = LE-PATH-OWNBR)
              GO TO COMPLETE-LE1-DELETE.

           IF LE-ACCTNO NOT = LN-ACCTNO
              GO TO COMPLETE-LE1-DELETE.

           IF LE-TYPE = "E"
              IF LE-SSNO NOT = LN-SSNO(B-SUB)
                 GO TO COMPLETE-LE1-DELETE.

           PERFORM DELETE-LE1-FILE.
           GO TO NEXT-LE1-DELETE.
       COMPLETE-LE1-DELETE.
           IF B-SUB < 4
              ADD 1 TO B-SUB
              GO TO START-NEXT-LE1.
           PERFORM CLOSE-LE1-FILE.

      *****************************************
       UPDATE-BW-UMC-EXPIRE SECTION.
      *****************************************
           MOVE 0 TO UMC-TERM.

           IF LN-FEECODE(FEE-SUB) = BR-ROAD-G-SGL-FEECODE
              COMPUTE UMC-TERM = LN-FEEAMT(FEE-SUB) / 8
           ELSE
              IF LN-FEECODE(FEE-SUB) = BR-ROAD-G-FAM-FEECODE
                 COMPUTE UMC-TERM = LN-FEEAMT(FEE-SUB) / 10.

           IF UMC-TERM = 0
              GO TO EXIT-UPDATE-BW-UMC.

      * IF REMAINDER, BUMP UP 1 MONTH
           IF NOT ( UMC-DEC1 = 0 AND UMC-DEC2 = 0 )
              ADD 1 TO UMC-TERM.

      * NB 11/12/24 9 MO. LOAN, SOLD 3 MONTHS, UMC EXPIRE 2/12/25
      * PB 03/20/26 9 MO. LOAN, SOLD 8 MONTHS, UMC EXPIRE 11/20/26
      *     GOES OFF LOANDATE AS NEW LOAN IS PAST PREVIOUS EXPIRATION

      * BW-UMC-PR-EXPIRE-DATE IS ONLY USED HERE. ONLY FOR A RENEWED ACCOUNT
      * THAT WAS DONE AFTER THE PRIOR UMC EXPIRED. I HAD NO WAY TO GO BACK
      * TO THE PRIOR LOAN AS THIS NEW LOAN CREATED AN EXPIRE DATE FROM ITS
      * LOANDATE WHERE OTHER LOANS MADE INSIDE THE EXPIRATION DATE INCREMENTED
      * THE EXPIRE DATE BY THE TERM OF UMC SOLD. I COULDNT BACK THAT OUT ON
      * THESE TYPES OF RENEWALS SO WE ADDED THE PRIOR DATE JUST FOR THIS.

      * VOID AND IT WANTS TO PUT IT BACK TO 3/20/26 BY INCREMENTING
      * MONTHS BACK BY TERM SOLD BUT THAT IS NOT CORRECT
      * SO TEST IF THIS IS A LOAN WITH AN EXPIRATION DATE OUT BY THE TERM SOLD
      * AND PB, IF SO, PUT THE BW-UMC-PR-EXPIRE-DATE BACK IN THE CURRENT ONE
           MOVE LN-LOANDATE TO NDTE-DATE.
           MOVE UMC-TERM    TO NDTE-HOLD.
           PERFORM INCREMENT-MONTHS.
           IF NDTE-DATE = BW-UMC-EXPIRE-DATE
              IF LN-SRCD = "PB" OR "RN" OR "SC" OR "RB" OR "RO"
                 MOVE BW-UMC-PR-EXPIRE-DATE TO BW-UMC-EXPIRE-DATE
                 GO TO EXIT-UPDATE-BW-UMC.
            
           MOVE BW-UMC-EXPIRE-DATE TO NDTE-DATE.
           COMPUTE NDTE-HOLD = UMC-TERM * -1.
           PERFORM INCREMENT-MONTHS.
           MOVE NDTE-DATE TO BW-UMC-EXPIRE-DATE.

       EXIT-UPDATE-BW-UMC.
           EXIT.

      ************************
       UPDATE-FM1-FILE SECTION.
      ************************
           MOVE "UPDATING FORM REQUEST FILE............." TO MESS.
           PERFORM SEND-LEGEND.

           PERFORM OPEN-FM1-FILE.

           MOVE FM-PATH-OWNBR TO FM-BRNO.
           MOVE LN-ACCTNO     TO FM-INITKEY.
           MOVE 0             TO FM-FMNO.

           PERFORM START-FM1-FILE.
       NEXT-FM1-DELETE.
           PERFORM READ-FM1-FILE-NEXT.
           IF IO-FG = 0 AND (FM-BRNO = FM-PATH-OWNBR)
              MOVE FM-INITKEY TO LPWSFM-EDIT-FM-INITKEY
              IF LPWSFM-EDIT-FM-ACCTNO = LN-ACCTNO 
                 PERFORM DELETE-FM1-FILE
                 GO TO NEXT-FM1-DELETE.

           PERFORM CLOSE-FM1-FILE.

      ***********************************
       UPDATE-CI1-FILE SECTION.
      ***********************************
           MOVE "DELETING CUSTOMER INFORMATION....." TO MESS.
           PERFORM SEND-LEGEND.

           PERFORM OPEN-CI1-FILE.
           MOVE LN-OWNBR  TO CI-BRNO.
           MOVE LN-ACCTNO TO CI-ACCTNO.
           PERFORM READ-CI1-FILE.
           IF IO-GOOD
              PERFORM DELETE-CI1-FILE.

           PERFORM CLOSE-CI1-FILE.

       EXIT-UPDATE-CI1-FILE.
           EXIT.

      ************************
       UPDATE-PD1-FILE SECTION.
      ************************
      * TEST FOR VOID OF A RENEWED LOAN
           IF NOT (LN-SRCD = "RN" OR "PB" OR "SC" OR "RB" OR "RO")
              GO TO UPDATE-PD1-TEST-MISS.

           MOVE "UPDATING SCRATCH PAD FILE..............." TO MESS.
           PERFORM SEND-LEGEND.

           PERFORM OPEN-PD1-FILE.
           PERFORM SET-PD1-KEY.
           PERFORM READ-PD1-FILE.
           IF IO-FG = 0
              MOVE LN-REC  TO PD-RECORD
              MOVE ZEROES  TO PD-APPROVAL-LIMIT
              MOVE PD-SRCD TO PD-PO-TRCD
              PERFORM SET-PRIOR-LOAN-DEL
              PERFORM GET-PRIOR-LOAN-INFO
              PERFORM REWRITE-PD1-FILE
           ELSE
              PERFORM SET-PD1-KEY
              MOVE LN-REC TO PD-RECORD
              MOVE ZEROES TO PD-APPROVAL-LIMIT
              MOVE PD-SRCD TO PD-PO-TRCD
              PERFORM SET-PRIOR-LOAN-DEL
              PERFORM GET-PRIOR-LOAN-INFO
              PERFORM WRITE-PD1-FILE.
           IF IO-FG = 9
              GO TO IO-ERROR.
           PERFORM CLOSE-PD1-FILE.

      * GET GOOD LN-REC BACK, GET-PRIOR-LOAN-INFO READS PRLNNO AND MISS ADJ.
           MOVE WK-SAVE-REC TO LN-REC.
           PERFORM GET-LN.
           IF IO-FG = 9
              GO TO IO-ERROR.

       UPDATE-PD1-TEST-MISS.
      * TEST FOR MISSISSIPPI REBATE-ADJ:
           IF LN-STATE-FLAG NOT = "A" OR LN-STATE-WORKER = 0
              GO TO EXIT-UPDATE-PD1.

           MOVE LN-ACCTNO TO SAVE-LN-ACCTNO.

           MOVE "UPDATING MISSISSIPPI REBATE ADJ LOAN...." TO MESS.
           PERFORM SEND-LEGEND.

           MOVE LN-OLDACCTNO TO LN-ACCTNO.

           PERFORM OPEN-LN1-FILE.
           PERFORM READ-LN1-FILE.
           IF IO-FG = 9
              GO TO IO-ERROR.

           MOVE " " TO LN-STATE-FLAG.

           PERFORM REWRITE-LN1-FILE.
           PERFORM CLOSE-LN1-FILE.

           PERFORM CREATE-LM1-ADJ.

      * GET GOOD LN-REC BACK, CURRENT LN-REC IS OLDACCTNO
           MOVE WK-SAVE-REC TO LN-REC.
           PERFORM GET-LN.
           IF IO-FG = 9
              GO TO IO-ERROR.

       EXIT-UPDATE-PD1.
           EXIT.

      ***********************
       CREATE-LM1-ADJ SECTION.
      ***********************
      * CREATE MEMO ON ADJUSTED PRIOR LOAN, ACCTNO STILL ACCTNO
           PERFORM OPEN-LM1-FILE.

           MOVE LM-PATH-OWNBR TO LM-BRNO.
           MOVE LN-ACCTNO     TO LM-ACCTNO.
           MOVE 0             TO LM-DATE.
           MOVE 0             TO LM-SEQ SAVE-LM-SEQ.

           PERFORM START-LM1-FILE.
       UPDATE-LM1-NEXT.
           PERFORM READ-LM1-FILE-NEXT.
           IF IO-FG = 0
              IF (LM-ACCTNO = LN-ACCTNO) AND (LM-BRNO = LM-PATH-OWNBR)
                 IF LM-DATE = 0
                    MOVE LM-SEQ TO SAVE-LM-SEQ
                    GO TO UPDATE-LM1-NEXT.

           MOVE SPACES TO LM-REC.
           MOVE LM-PATH-OWNBR TO LM-BRNO.
           MOVE LN-ACCTNO     TO LM-ACCTNO.
           MOVE 0             TO LM-DATE.
           ADD 1 SAVE-LM-SEQ GIVING LM-SEQ.

           MOVE TRANS-DATE TO DATE-YYYYMMDD.
           PERFORM CONVERT-YYYYMMDD-TO-MMDDYY.
           MOVE DATE-MMDDYY    TO WK-DATE.
           MOVE SAVE-LN-ACCTNO TO WK-ACCTNO.
           INSPECT WK-ACCTNO REPLACING ALL "/" BY "-".
           MOVE WK-MMEMO TO LM-MEMO.
           PERFORM WRITE-LM1-FILE.
           PERFORM CLOSE-LM1-FILE.

      ************************
       UPDATE-LN1-FILE SECTION.
      ************************
           MOVE "UPDATING LOAN MASTER...................." TO MESS.
           PERFORM SEND-LEGEND.

           PERFORM OPEN-LN1-FILE.

           MOVE WK-SAVE-REC TO LN-REC.
           PERFORM READ-LN1-FILE.
           IF IO-FG = 9
              GO TO IO-ERROR.

           SUBTRACT LN-ACCERN(1) LN-ACCERN(2) LN-ACCERN(3)
                    LN-ACCERN(4) LN-ACCERN(5) FROM LN-NONACRERN.
           IF NOT GP-INCL-OTH-IN-EARNED-N
              SUBTRACT LN-ACCERN(6) FROM LN-NONACRERN.

           MOVE 0 TO LN-CURBAL LN-TOTERN.
           MOVE TRANS-DATE TO LN-POFFDATE.

           PERFORM REWRITE-LN1-FILE.

       UPDATE-LN1-FILE-EXIT.
           PERFORM CLOSE-LN1-FILE.


      * ONCE COMBINED, WAS NOT SURE IF WE SHOULD BE REMOVING THE ENTIRE
      * LX RECORD WITH ALL THE DIFFERENT CODES IN IT.
      *UPDATE-LXX-FILE SECTION.
      *    MOVE "DELETING INSURANCE TAX TRAILER FILE ....." TO MESS.
      *    PERFORM SEND-LEGEND.

      *    PERFORM OPEN-LX1-FILE.

      *    MOVE LN-OWNBR  TO LX-BRNO.
      *    MOVE LN-ACCTNO TO LX-ACCTNO.
      *    MOVE 1         TO LX-SEQNO.
      *    PERFORM READ-LX1-FILE.
      *    IF IO-BAD
      *       GO TO END-UPDATE-LXX-FILE.

      *    PERFORM DELETE-LX1-FILE.

      *END-UPDATE-LXX-FILE.
      *    PERFORM CLOSE-LX1-FILE.
      *EXIT-UPDATE-LXX-FILE.
      *    EXIT.


      *********************************
       VOID-CHECK-ROUTINE SECTION.
      *********************************

           PERFORM OPEN-CK1-FILE.
           MOVE CK-PATH-OWNBR TO CK-BRNO.
           MOVE 0             TO CK-NO.

           PERFORM START-CK1-FILE.
       NEXT-CHECK.
           PERFORM READ-CK1-FILE-NEXT.
           IF IO-FG = 9 OR (CK-BRNO NOT = CK-PATH-OWNBR)
              GO TO EXIT-VOID-CHECK.

           IF CK-ACCTNO NOT = LN-ACCTNO
              GO TO NEXT-CHECK.

           IF CK-CLASS NOT = LN-CLASS
              GO TO NEXT-CHECK.

           IF CK-VOID = "Y" OR "R"
              GO TO NEXT-CHECK.

           IF CK-PGM NOT = "D0"
              GO TO NEXT-CHECK.

           MOVE "Y" TO CK-VOID.
           MOVE LPLOAN-SYSDATE TO ALP-DATE.
           PERFORM CONVERT-ALPDATE-TO-YYYYMMDD.
           MOVE DATE-YYYYMMDD TO CK-VOID-DATE.
           PERFORM REWRITE-CK1-FILE.

           PERFORM VOID-UPD-TC-FILE.
           PERFORM VOID-UPD-TY1-FILE.

           GO TO NEXT-CHECK.

       EXIT-VOID-CHECK.
           PERFORM CLOSE-CK1-FILE.

      *************************************
       VOID-UPD-TC-FILE SECTION.
      *************************************

           MOVE "UPDATING CHECK VOID TRANSACTION........" TO MESS.
           PERFORM SEND-LEGEND.

           PERFORM OPEN-TRC1-FILE.
           MOVE 1 TO SAVE-TC-SEQ.
       NEXT-TC-REC.
           PERFORM CLEAR-TRC-FILE.

           MOVE CK-BRNO     TO TC-BRNO.
           MOVE TRANS-DATE  TO TC-KEY-DATE.
           MOVE CK-NO       TO TC-CKNO.
           MOVE CK-CLASS    TO TC-CLASS.
           MOVE SAVE-TC-SEQ TO TC-SEQ.

           MOVE CK-TYPE    TO TC-TYPE.
           MOVE CK-DLRVNO  TO TC-DLRVNO.
           MOVE TRANS-DATE TO TC-DATE.
           COMPUTE TC-AMT = CK-AMT * -1.
           MOVE CK-PAYEE   TO TC-PAYEE.
           MOVE CK-REF     TO TC-REF.
           MOVE CK-REASON  TO TC-REASON.
           MOVE CK-ACCTNO  TO TC-ACCTNO.
           MOVE CK-PGM     TO TC-PGM.
           MOVE CK-VOID    TO TC-VOID.
           MOVE CK-PAID    TO TC-PAID.
           MOVE 0 TO SUB.
       NEXT-TC-GL.
           ADD 1 TO SUB.
           MOVE CK-GLNO(SUB) TO TC-GLNO(SUB).
           COMPUTE TC-GLAMT(SUB) = CK-GLAMT(SUB) * -1.
           IF SUB < 5
              GO TO NEXT-TC-GL.

           PERFORM SET-POSTDATE.
           MOVE POST-USERID TO TC-USERID.
           MOVE POST-TIME   TO TC-POSTTIME.
           MOVE POST-DATE   TO TC-POSTDATE.

           PERFORM WRITE-TRC1-FILE.
           IF IO-FG = 0
              GO TO EXIT-VOID-UPD-TRC.

           IF SAVE-TC-SEQ < 99999
              ADD 1 TO SAVE-TC-SEQ
              GO TO NEXT-TC-REC.

           MOVE "CHECK REGISTER # = 99999, UPDATE SKIPPED !" TO MESS.
           PERFORM SEND-MESS.
       EXIT-VOID-UPD-TRC.
           PERFORM OPEN-TRC1-FILE.

      ***********************************************
       UPDATE-NOTICE-FILE SECTION.
      ***********************************************
           PERFORM OPEN-NO1-FILE.
           MOVE LN-OWNBR  TO NO-BRNO.
           MOVE LN-ACCTNO TO NO-ACCTNO CRNOR2-ACCTNO.
           PERFORM READ-NO1-FILE.

           MOVE TRANS-DATE     TO CRNOR2-SYSDATE.
           MOVE "CL/CRNOR2"    TO FORM-NAM.
           CALL FORM-PROGX USING FORM-PATH CRNOR2-LINK NO-REC
                                 POFF-WORKERS LN-REC.
           CANCEL FORM-PROGX.

           MOVE LN-OWNBR  TO NO-BRNO.
           MOVE LN-ACCTNO TO NO-ACCTNO.
           IF IO-FG = 0
              PERFORM REWRITE-NO1-FILE
           ELSE
              PERFORM WRITE-NO1-FILE.

           PERFORM CLOSE-NO1-FILE.
       EXIT-UPDATE-NOTICE-FILE.
           EXIT.

      ************************************
       VOID-UPD-TY1-FILE SECTION.
      ************************************
           MOVE "UPDATING VOID - TODAY'S TOTAL FILE...." TO MESS.
           PERFORM SEND-LEGEND.
           PERFORM OPEN-TY1-FILE.
           MOVE "N" TO REC-EXISTS.
       VOID-UPDATE-TY1-FILE-CHECK.
           PERFORM SET-TY-KEY.
           PERFORM READ-TY1-FILE.
           IF IO-FG = 0
              GO TO VOID-UPDATE-TY1-FILE-UPD-REC.

           IF REC-EXISTS = "Y"
              GO TO IO-ERROR.

           MOVE "Y" TO REC-EXISTS.
           PERFORM CLEAR-TY-FILE.
           PERFORM SET-TY-KEY.
           PERFORM WRITE-TY1-FILE.
           IF IO-FG = 9 AND E-CODE NOT = "22"
              GO TO IO-ERROR.

           GO TO VOID-UPDATE-TY1-FILE-CHECK.

       VOID-UPDATE-TY1-FILE-UPD-REC.
      * ALWAYS LOAN CHECKS, ALWAYS CKWRITTEN(1) (PAYEE-SUB)
           SUBTRACT CK-AMT FROM TY-CKWRITTEN(1).
           PERFORM REWRITE-TY1-FILE.
           PERFORM CLOSE-TY1-FILE.

      *********************************
       REMOVE-TEMP-CHECKS SECTION.
      *********************************
           PERFORM OPEN-CX1-FILE.

           MOVE CX-PATH-OWNBR TO CX-BRNO.
           MOVE SPACES        TO CX-TYPE.
           MOVE 0             TO CX-SEQNO.

           PERFORM START-CX1-FILE.
       NEXT-TEMP-CHECK.
           PERFORM READ-CX1-FILE-NEXT.
           IF IO-FG = 9 OR (CX-BRNO NOT = CX-PATH-OWNBR)
              GO TO EXIT-REMOVE-TEMP-CHECKS.

           IF CX-REF NOT = LN-ACCTNO
              GO TO NEXT-TEMP-CHECK.

           PERFORM REMOVE-CHECK-FORM-REQ.
           PERFORM DELETE-CX1-FILE.

           GO TO NEXT-TEMP-CHECK.
       EXIT-REMOVE-TEMP-CHECKS.
           PERFORM CLOSE-CX1-FILE.

      ****************************************
       REMOVE-CHECK-FORM-REQ SECTION.
      ****************************************

           MOVE "REMOVING CHECK FORM REQUEST........." TO MESS.
           PERFORM SEND-LEGEND.

           PERFORM OPEN-FM1-FILE.

           MOVE FM-PATH-OWNBR TO FM-BRNO.
           MOVE SPACES        TO LPWSFM-EDIT-FM-INITKEY.
           MOVE CX-BRNO       TO LPWSFM-EDIT-FM-BRNO.
           MOVE CX-TYPE       TO LPWSFM-EDIT-FM-TYPE
           MOVE CX-SEQNO      TO LPWSFM-EDIT-FM-SEQNO.
           MOVE 0             TO LPWSFM-EDIT-FM-CKFMNO FM-FMNO.
           MOVE LPWSFM-EDIT-FM-INITKEY TO FM-INITKEY.

           PERFORM START-FM1-FILE.

       REMOVE-NEXT-CHECK-REQUEST.
           PERFORM READ-FM1-FILE-NEXT.
           MOVE FM-INITKEY TO LPWSFM-EDIT-FM-INITKEY.
           IF IO-FG = 0
              IF (LPWSFM-EDIT-FM-BRNO NOT = CX-BRNO) OR
                  (LPWSFM-EDIT-FM-TYPE NOT = CX-TYPE)
                    MOVE 9 TO IO-FG.

           IF IO-FG NOT = 0 OR (FM-BRNO NOT = FM-PATH-OWNBR)
              GO TO EXIT-REMOVE-FM1.

           IF LPWSFM-EDIT-FM-SEQNO NOT = CX-SEQNO
      *       UNLOCK FM-FILE
              GO TO REMOVE-NEXT-CHECK-REQUEST.

           PERFORM DELETE-FM1-FILE.
       EXIT-REMOVE-FM1.
           PERFORM CLOSE-FM1-FILE.

      *******************************************
       SET-DELINQUENCY SECTION.
      *******************************************
           MOVE TRANS-DATE TO AGEING-DATE.
           PERFORM COMPUTE-CONTRACTUAL.
           PERFORM COMPUTE-RECENCY.

      ***************************************************
      * /USR/DATA/R1/FTP/REPAY/VOIDS/V.BRNO.NUMBER
      *                                1234.123456
      * THERE IS A PROCESS SCANNING THIS DIRECTORY TO SEND
      * THESE IMMEDIATELY TO REPAY SO THEY CAN SET UP
      * AUTOMATIC PAYMENTS WHILE THE CUSTOMER IS STILL IN THE OFFICE
      * NEW LOANS ARE IN /REPAY/NEWLOANS
      ***************************************************
       UPDATE-REPAY-FILE SECTION.
      ***************************************************
           MOVE "UPDATING REPAY FILE.........." TO MESS.
           PERFORM SEND-LEGEND.

      * USR/DATA/R1
           MOVE EXT-FILPATH-BASE    TO WK-REPAY-LN-PATH-BASE.
           MOVE EXT-FILPATH-MACHINE TO WK-REPAY-LN-PATH-MACHINE.
           CALL "C$TOLOWER" USING WK-REPAY-LN-PATH-DIR, VALUE 10.
           MOVE BR-NO               TO WK-REPAY-LN-PATH-BRNO.
           MOVE LN-ACCTNO           TO WK-REPAY-LN-PATH-NUMBER.

           PERFORM OPEN-WK-REPAY-LN-FILE-OUTPUT.

           PERFORM OPEN-BW1-FILE.
           MOVE LN-SSNO(1) TO BW-SSNO.
           PERFORM READ-BW1-FILE.
           PERFORM CLOSE-BW1-FILE.

      * REPAY WORKERS
           MOVE SPACES TO WK-REPAY-LN-REC WS-REPAY-LN-REC.

           MOVE "V"        TO WS-REPAY-VOID-FLAG.

           MOVE LN-OWNBR   TO WS-OWNBR.
           MOVE LN-ACCTNO  TO WS-NUMBER.
           CALL "C$JUSTIFY" USING WS-BRACCT, JUSTIFY-TYPE.

           MOVE WS-BRACCT TO WS-REPAY-LN-BRACCTNO.

           MOVE LN-ACCTNO  TO WS-REPAY-LN-NUMBER.
           CALL "C$JUSTIFY" USING WS-REPAY-LN-NUMBER, JUSTIFY-TYPE.

           MOVE LN-OWNBR   TO WS-REPAY-LN-OWNBR.
           CALL "C$JUSTIFY" USING WS-REPAY-LN-OWNBR, JUSTIFY-TYPE.
           MOVE BW-FNAME   TO WS-REPAY-BW-FNAME.
           MOVE BW-LNAME   TO WS-REPAY-BW-LNAME.

           STRING
              WS-REPAY-VOID-FLAG           ,"|"
              WS-REPAY-LN-BRACCTNO         ,"|"
              WS-REPAY-LN-NUMBER           ,"|"
              WS-REPAY-LN-OWNBR            ,"|"
              WS-REPAY-BW-FNAME            ,"|"
              WS-REPAY-BW-LNAME, CR-1
              DELIMITED BY "  " INTO WK-REPAY-LN-REC.

           PERFORM WRITE-WK-REPAY-LN-FILE.

       UPDATE-REPAY-EXIT.
           EXIT.

       COPY "LIBGB/DATER.CPY".
       COPY "LIBLP/POSTDATE.CPY".
       COPY "LIBLP/LPSRCD.CPY".
       COPY "LIBLP/LPPDUE.CPY".
       COPY "LIBGB/AGEING.CPY".
       COPY "LIBLP/LPUPWK.CPY".
       COPY "LIBLP/LPCEPP.CPY".
       COPY "LIBLP/LPEARN.CPY".
       COPY "LIBLP/LPMDTE.CPY".
       COPY "LIBGB/SPPDTH.CPY".
       COPY "LIBGB/GPENV.CPY".
       COPY "LIBGB/SCREEN.CPY".
       COPY "LIBLP/RNFOR10LN.CPY".

      *********************************
       MISC-PARAGRAPHS SECTION.
      *********************************
       COPY "LIBGB/GETENV.CPY".
       FORM-RESET.
       SETUP-LINK-INFO.
       VDU-CONVERT-FIELD.
       SET-PRIOR-LOAN-DEL.
           MOVE LN-PRLNAMT     TO PD-PRLNAMT.
           MOVE LN-PRPDUE-DAYS TO PD-PRPDUE-DAYS.
           MOVE LN-PRDELINQAMT TO PD-PRDELINQAMT.
      * FOR (GA) SCFRMLA = 12 OR 13:
           IF PD-PRDELINQAMT > 0
              MOVE PD-PRDELINQAMT TO PD-PRPAID-AHEAD
           ELSE
              MOVE 0 TO PD-PRPAID-AHEAD.
           MOVE 0 TO PD-RESTRICT-ADJ.

       GET-LC1-RECORD.
           PERFORM OPEN-LC1-FILE.
           PERFORM READ-LC1-FILE.
           PERFORM CLOSE-LC1-FILE.

       GET-PRIOR-LOAN-INFO.
           MOVE LN-PRLNNO TO LN-ACCTNO.
           PERFORM OPEN-LN1-FILE.
           PERFORM READ-LN1-FILE.
           IF IO-FG = 0
              MOVE LN-LOANDATE TO PD-LOANDATE
              MOVE LN-SMALLOAN TO PD-SMALLOAN
              MOVE LN-SALEFIN TO PD-SALEFIN
              MOVE LN-SERVACCT TO PD-SERVACCT
              MOVE LN-LOANTYPE TO PD-LOANTYPE
              MOVE LN-ORGST TO PD-ORGST
              MOVE LN-CLASS TO PD-CLASS
              MOVE LN-SUBCLASS TO PD-SUBCLASS.

           PERFORM CLOSE-LN1-FILE.

       GET-LXX-TAX.
           PERFORM OPEN-LX1-FILE.
           MOVE LN-OWNBR  TO LX-BRNO.
           MOVE LN-ACCTNO TO LX-ACCTNO.
           MOVE 1         TO LX-SEQNO.
           PERFORM READ-LX1-FILE.
           IF IO-BAD
              MOVE SPACES TO LXX-INS-TAX-COUNTY-CD
              MOVE SPACES TO LXX-INS-TAX-CITY-CD.
           PERFORM CLOSE-LX1-FILE.

       GET-LN.
           PERFORM OPEN-LN1-FILE.
           PERFORM READ-LN1-FILE.
           PERFORM CLOSE-LN1-FILE.
       GET-COLLATERAL.
           PERFORM OPEN-CD1-FILE.
           MOVE "CO"      TO CD-TYPE.
           MOVE LN-COLLCD TO CD-CODE.
           PERFORM READ-CD1-FILE.
           IF IO-FG NOT = 0
              MOVE " " TO CD-CO-SECURED.
           PERFORM CLOSE-CD1-FILE.
       SET-PD1-KEY.
           MOVE SPACES    TO PD-RECORD.
           MOVE "R"       TO PD-ID.
           MOVE LN-OWNBR  TO PD-BRANCH.
           MOVE LN-PRLNNO TO PD-SOC-SEC.
       SET-DS1-KEY.
           MOVE TRANS-DATE TO DATE-YYYYMMDD.
           PERFORM CONVERT-YYYYMMDD-TO-YYYYMM.
           MOVE DATE-YYYYMM TO DS-DATE-CCYYMM.
           MOVE LN-OWNBR TO DS-BRANCH.
           MOVE LN-DLNO TO DS-DLNO.
           MOVE LN-CLASS TO DS-CLASS.
       SET-TY-KEY.
           MOVE LN-OWNBR TO TY-BRNO.
           MOVE TRANS-DATE TO TY-DATE.
           MOVE LN-CLASS TO TY-CLASS.
       SEND-LEGEND.
           MOVE MESS TO WR-BUF.
           MOVE MSEL-LIST TO WR-LIST.
           PERFORM CALL-WRFORM.

      *********************************
      *      F I L E   I / O          *
      *********************************
       COPY "LIBLP/LPTYCL.CPY".
       COPY "LIBLP/LPDSCL.CPY".
       COPY "LIBLP/LPTRLCL.CPY".
       COPY "LIBLP/LPTRCCL.CPY".

      ************************************
       IO-ROUTINE SECTION.
      ************************************
       COPY "LIBGB/CONNECT_SQL.CPY".
       COPY "LIBLP/LPTYGS_SQL.CPY".
       COPY "LIBLP/LPTRLGS_SQL.CPY".
       COPY "LIBLP/LPTRCGS_SQL.CPY".
       COPY "LIBLP/LPSPGS_SQL.CPY".
       COPY "LIBLP/LPPDGS_SQL.CPY".
       COPY "LIBLP/LPNOGS_SQL.CPY".
       COPY "LIBLP/LPLXGS_SQL.CPY".
       COPY "LIBLP/LPLTPGS_SQL.CPY".
       COPY "LIBLP/LPLTLGS_SQL.CPY".
       COPY "LIBLP/LPLTIGS_SQL.CPY".
       COPY "LIBLP/LPLTFGS_SQL.CPY".
       COPY "LIBLP/LPLTDGS_SQL.CPY".
       COPY "LIBLP/LPLTCGS_SQL.CPY".
       COPY "LIBLP/LPLTBGS_SQL.CPY".
       COPY "LIBLP/LPLTAGS_SQL.CPY".
       COPY "LIBLP/LPLNGS_SQL.CPY".
       COPY "LIBLP/LPLMGS_SQL.CPY".
       COPY "LIBLP/LPLEGS_SQL.CPY".
       COPY "LIBLP/LPLCGS_SQL.CPY".
       COPY "LIBLP/LPFMGS_SQL.CPY".
       COPY "LIBLP/LPDSGS_SQL.CPY".
       COPY "LIBLP/LPCXGS_SQL.CPY".
       COPY "LIBLP/LPCKGS_SQL.CPY".
       COPY "LIBLP/LPCIGS_SQL.CPY".
       COPY "LIBLP/LPCDGS_SQL.CPY".
       COPY "LIBLP/LPCAGS_SQL.CPY".
       COPY "LIBLP/LPBWGS_SQL.CPY".
       COPY "LIBGB/GBBRGS_SQL.CPY".
       COPY "LIBLP/LPLC1RN.CPY".
       COPY "LIBLP/LPLE1IN.CPY".
       COPY "LIBLP/LPLM1IN.CPY".
       COPY "LIBLP/LPSP1RN.CPY".
       COPY "LIBLP/LPLN1IN.CPY".
       COPY "LIBLP/LPLTA1IN.CPY".
       COPY "LIBLP/LPLTB1IN.CPY".
       COPY "LIBLP/LPLTC1IN.CPY".
       COPY "LIBLP/LPLTD1IN.CPY".
       COPY "LIBLP/LPLTF1IN.CPY".
       COPY "LIBLP/LPLTI1IN.CPY".
       COPY "LIBLP/LPLTL1IN.CPY".
       COPY "LIBLP/LPLTP1RN.CPY".
       COPY "LIBLP/LPLX1IN.CPY".
       COPY "LIBLP/LPBW1IN.CPY".
       COPY "LIBLP/LPCA1IN.CPY".
       COPY "LIBLP/LPTY1IN.CPY".
       COPY "LIBLP/LPDS1IN.CPY".
       COPY "LIBLP/LPPD1IN.CPY".
       COPY "LIBLP/LPWKI.CPY".
       COPY "LIBLP/LPTRL1IN.CPY".
       COPY "LIBLP/LPTRC1IN.CPY".
       COPY "LIBLP/LPFM1IN.CPY".
       COPY "LIBLP/LPNO1IN.CPY".
       COPY "LIBLP/LPCD1RN.CPY".
       COPY "LIBLP/LPCK1IN.CPY".
       COPY "LIBLP/LPCX1IN.CPY".
       COPY "LIBGB/GBBRRN.CPY".
       COPY "LIBLP/LPCI1IN.CPY".
      * REPAY VOIDED LOANS
       OPEN-WK-REPAY-LN-FILE-OUTPUT.
           PERFORM OPEN-IT.
           MOVE WK-REPAY-LN-IFN TO E-FILE.
           OPEN OUTPUT WK-REPAY-LN-FILE.
           IF IO-FG = 8
              GO TO OPEN-WK-REPAY-LN-FILE-OUTPUT.
           IF IO-FG = 7
              CLOSE WK-REPAY-LN-FILE
              GO TO OPEN-WK-REPAY-LN-FILE-OUTPUT.
       WRITE-WK-REPAY-LN-FILE.
           PERFORM WRITE-IT.
           MOVE WK-REPAY-LN-IFN TO E-FILE.
           WRITE WK-REPAY-LN-REC.
           IF IO-FG = 8
              GO TO WRITE-WK-REPAY-LN-FILE.
       CLOSE-WK-REPAY-LN-FILE.
           PERFORM CLOSE-IT.
           CLOSE WK-REPAY-LN-FILE.
       COPY "LIBGB/FERRORS.CPY".
           MOVE "E" TO ERRCD.

      *********************************
       END-ROUTINE SECTION.
      *********************************
           PERFORM CLOSE-FILES.
       EXIT-PROG.
       STOP-RUN.
           EXIT PROGRAM.
