       IDENTIFICATION DIVISION.
       PROGRAM-ID.      LONPJ2.
       DATE-WRITTEN.    012484.
      *****************************************************************
      *                 (LP)
      *  DESCRIPTION:   SALES FINANCE LOAN
      *                 CONTRACT DELETE
      * REV:
      *  062695 BAH AUTOMATICALLY VOID SYSTEM OR MANUAL CHECKS
      *  062895 MJD ADDED COPY MEMBERS LIBLP/POSTDATE AND POSTDATEW.
      *             SET TC-USERID, POSTTIME, AND POSTDATE.
      *             SET TS-USERID, POSTTIME, AND POSTDATE.
      *  071295 JTG REMOVED TS-PR,FIELDS AND ADDED TS-INSPREM-DLADJ
      *  073195 BAH COMMENT OUT VOIDING CHECKS
      *  081195 JTG ADDED LOGIC FOR LPSRCD
      *  081195 MJD ADDED GP-FILE INFO FOR LIBLP/LPSRCD :
      *                   GP-THE-ONLY-SF-SRCD.
      *  081695 MJD ADDED GP-THE-ONLY-SF-SRCD LOGIC FOR DS UPDATES.
      *  MJD 091895 ADDED DS-DATE LOGIC
      *  MJD 110995 ADDED LOGIC TO UPDATE TY-(DELINQ TABLE) BYPASSING
      *             VOLUME.
      *  MJD 012596 ADDED LOGIC FOR UPDATE OF SEPATATE PL TABLE.
      *  MJD 020896 ADDED UPDATE OF DEALER RESERVE FIELDS IN TY-REC.
      *               (NET GAIN/LOSS AND NEW VOLUME).
      *  MJD 021396 ADDED UPDAT OF P&L TABLES.
      *  JTG 052496 CORRECTED BYFILE UPDATE OF CONTRACTUAL ERROR DUE
      *             TO SPR RECORD NOT BEING READ
      *  JTG 061896 ADDED MA-RECORD LOGIC TO CHECK FOR LOAN APPLICATION
      *  MJD 071696 ADDED LIBLP/LPUPWK FOR CHANGES IN LIBLP/AGEING
      *  JTG 093096 ADDED UPDATE OF DSFILE (DELINQUENCY)
      *  KEC 112596 CHANGED LN-POCD FOR RESCISSION.
      *  MJD 012297 REMOVED REFERENCE TO LN-INSTBL(9-10) AND TY-INS-TYPE(9-10)
      *  KEC 012397 REVISED ACCESSING MA-FILE: NEW FORMAT FOR MA-FILE.
      *  KEC 043097 REVISED FOR LOAN APPLICATIONS: ADDED UPDATE TO
      *             APPLICATION MEMOS.
      *  GLF 050797 ADDED COPY MEMBERS: SETCTBLW AND SETCTBL
      *  BAH 051697 CHANGED DT-TRCD TO "RV" AND SET DT-REVERSE-TRCD "NW"
      *             LIKE A50 DOES..
      *  BLV 051997 ADD UPDATE OF APPLICATIONS STATS - TY & DS, ADD BRFILE
      *  MJD 070297 ADDED DS-DLDISC-VOL AND TY-DLDISC
      *  JTG 080497 ADDED UPDATE OF DS-UNPAIDCK FOR SERVICE ACCOUNTS RE:
      *             INSURANCE & FEES THAT ARE OURS
      *  GLF 090897 REPLACED 'IO' ON GP-FILE WITH GET 'GPENV'
      *  MJD 091797 REMOVED DELSUB-SET LOGIC; ADDED LPPDUE(W) COPY MEMBERS
      *  BLV 020698 DELETE BI RECORD WHEN LOAN IS VOIDED
      *   CS 031998 ADD TEST OF LC-APP-REQ-FOR-LOAN IF BR-APP-REQUIRED-FOR-
      *             INPUT = Y TO DETERMINE IF APP STATS ARE UPDATED FOR CLASS
      *   CS 031998 VOID WAS NOT UPDATING APPLICATION STATS
      *  MJD 032098 ADDED GETENV(W) FOR USE IN LIBLP/POSTDATE
      *  KEC 051498 ADDED 'SET-TRACK-STATUS'.  WAS NOT UPDATING THE
      *              MA-STATUS-TRACK.
      *  JTG 061598 ADDED LOGIC TO UPDATE BANKRUPT DELINQUENT STATISTICS
      *  JTG 062398 ADDED LOGIC FOR S.F. RENEWALS (PB/RN/RB/RO)
      *  JTG 063098 EXPANDED DS-APP-TYPE-TBL TO 3 LEVELS
      *  JTG 071598 ADDED SET OF TS-PRDLDISC-UNEARN TO CARRY OVER
      *             DISCOUNT UNEARNED ON RENEWALS
      *  JTG 081898 ALLOWED 'PB' ON BANKRUPT ACCOUNTS IF GP-I-AM-WORLD <JGW
      *  KEC 090498 ADDED 'MOVE LN-LOANTYPE TO PD-LOANTYPE';
      *              FOR REGENCY REVOLVING.
      *  JTG 102398 CHANGED DS..APP..UPDATE FOR RENEWALS TO LEVEL 2 OTHER
      *  JTG 101598 ADDED LOGIC FOR UPDATE OF REVOLVING SALES CONTRACT, REGENCY
      *  GLF 111298 MADE MEMO DATE 2000 COMPATIBLE
      *  JTG 990226 ALLOWED UPDATE OF BW-FILE FOR REVOLVING, REGENCY
      *             AND STOPPED UPDATE OF DS-FILE & TY-FILE
      *  JTG 990322 ADDED UPDATE OF TS-SP-LNCOST-FRMLA RE: TFC #008
      *  MJD 990420 CHANGED TO INCLUDE/EXCLUDE O2 IN DELINQ BASED
      *             ON GP-DELINQ-WITH-O2
      *  MJD 990720 CHANGED TO UPDATE BI FILE LAST AFTER ALL OTHER FILES
      *             HAVE BEEN UPDATED.
      *  JTG 990721 STOPPED UPDATE OF BW-FILE FOR REVOLVING   REGENCY #45
      *  JTG 990726 ADDED LOGIC FOR UPDATE OF REVOLVING LOAN APPLICATION STATS
      *  MJD 990806 ADDED GP-INCL-OTH-IN-EARNED FLAG. IF "Y" THEN INCLUDE
      *             EARNINGS BUCKET 6 (OTHER) IN ACCUMULATED TOTALS
      *             OTHERWISE EXCLUDE BUCKET 6.
      *  MJD 990910 CHANGED TO SKIP INSURANCE BUCKETS 7 & 8 (O4 & O5) WHEN
      *             UPDATING TY. CHANGED TO UPDATE INS-ELIG FOR O1 IF
      *             APPLICABLE. ADDED LOGIC TO UPDATE INS-NOT-WRITTEN (9 & 10)
      *             IF AD&D OR ROADGARD ARE NOT WRITTEN (WORLD ONLY)
      *             (WORLD PR# AS PER JOHN)
      *  KEC 991217 {PR#0000} WAS A PROBLEM THAT THE MA-TRACK-STATUS
      *             WAS NOT BEING PROPERLY SETUP.
      *                CHANGED THE FOLLOWING:
      *                  1.  ORIGINAL-TRACK-STATUS WAS X(1) NOT X(6).
      *                  2.  WAS MOVING MA-STATUS NOT MA-TRACK-STATUS
      *                       TO ORIGINAL-TRACK-STATUS.
      * JTG 000324 MODIFIED TO UPDATE UNEARNED STATISTICS, MERCURY #223
      * MJD 000406 ADDED UPDATE OF TY-PROCEEDS AND DS-PROCEEDS (MERC PR#223)
      * MJD 000814 CHANGED TO COMPUTE EARNINGS AFTER SETTING UP
      *            LN-PRDLDISC-UNEARN ON RENEWALS  AND CHANGED TO SKIP
      *            UPDATE OF VOLUME ON DLDISC UNEARNED IF CARRIED OVER
      *            FROM PREVIOUS LOAN, CHANGED TO COMPUTE EARNINGS BEFORE
      *            SETTING POCD TO "**".
      * GLB 010702 NEW ROUTINE 'UPDATE-ORIGINAL-DT' TO SET DT-REVERSE IN
      *            ORIGINAL PROCEEDS DT-REC (NW, PU, AV)
      * GLB 010702 TEST WHETHER UPDATE DS-UNPAIDCK OR DS-UNPAID-MANUAL
      * GLB 010702 IF REVERSE, DONT MARK DT-PAID FOR 'B'ATCH
      * GLB 011127 BAD 'IF' STATEMENT TESTING "NW" "PU" DT-RECORDS-DIDNT MATTER
      * BAH 020206 INITIALIZE HOLD-DS-UNPAIDCK IN WORKING-STORAGE TO VALUE 0,
      *            ADDING GARBAGE INTO THE DS RECORD !!
      * BAH 020305 ADDED TEST OF CK-VOID = "R", REPLACEMENT VOID, WAS VOIDING
      *            AN ALREADY VOIDED/REPLACEMENT CHECK AGAIN !!!!
      * BAH 020403 DONT DELETE THE PAYMENT SCHEDULE LT, ITS NEEDED FOR THE
      *            CREDIT BUREAU (TRWTAP), CREATIVE PL#359
      * BLV 020910 MOVE LN-MIP-FG TO TS-MIP-FG ON VOID, REGWAN PR# 1801
      * JTG 020917 ADDED LN-DLCOMM-03-CMSI LN-DLCOMM-PP-CMSI      REGACC #1846
      *            ADDED TS-DLCOMM-PP-CMSI TS-DLCOMM-O3-CMSI
      *
      * KEC 2003.0819 {PR#00000} PARADATA <A15> [KEN CISZEWSKI]
      *               ADDED NEW ROUTINE TO CREATE LOAN APPLICATION
      *               STATUS CHANGE MEMO (LIBLA/MEMOSTATUS)
      * BAH 031120 ADDED GP-NON-FILE-FEECODE TO UPDATE TY/BY FILE WITH
      *            NON FILE INSURANCE FOR WORLD SMITH REPLACEMENT
      * JTG 041014 ADDED GP-AMORTIZE-ACQUIST-FEE, THIS OPTION WILL TAKE THE
      *            AMOUNT ENTERED IN RESERVE PRINCIPAL 3 AND MOVE IT TO
      *            THE LOANS UNEARNED SERVICE CHARGE               REGACC #0081
      * JTG 041105 CORRECTED LOGIC FOR GP-AMORTIZE-ACQUIST-FEE, THIS OPTION
      *            TAKES THE AMOUNT ENTERED IN RESERVE PRINCIPAL 3 AND
      *            MOVE IT TO THE LOANS UNEARNED SERVICE CHARGE.
      *            THE LOGIC TO CLEAR TS-ANTICERN(3) IS CORRECT FOR SF CONTRACTS
      *            THAT NEVER WENT THROUGH AN ACCRUAL. HOWEVER ACCOUNTS THAT
      *            DID TAKE EARNINGS AND REDUCED UNEARNED, THOSE GL ENTRIES
      *            NEED TO BE OFFSET, WILL LET LIBLP/LPEOD5TR TEST FOR AND
      *            MAKE THOSE ENTRIES                               REGACC #0081
      * BLF 050119 ADDED UPDATE OF DNFILE (DEALER ACH FAX NOTIFICATION FILE),
      *            WRITES FILE IN FAX DIRECTORY WITH DEALER INFO & PROCEEDS,
      *            REGACC PR# 96
      * BLF 050202 ADD BRANCH NAME TO DN-FILE, FIX NOT UPDATING DN-CUST-NAME,
      *            REGACC PR# 96
      * BLF 050418 REVISE DEALER FAX LOGIC.  INSTEAD OF READING DNFILE FROM
      *            DEAL TO CHECK DEPOSIT DATE, SAVE DT-STATUS TO DETERMINE
      *            IF CHECK WAS RELEASED TO GET SUFFIX FOR "R" (REFUND) OR
      *            "V" OFFSET, REGACC PR# 96
      * JTG 051006 ADDED LN-POCD-UNWIND "*U" A LN-POCD-NONLOAN
      *                   AND LN-UNWIND-REASON-CODE                 REGACC #0105
      *            ADDED UPDATE OF LN-UNWIND-REASON-CODE
      *                            LN-TILLNO
      *                            TS-UNWIND-REASON-CODE
      *                            TS-TILLNO
      *                            CA-RUNNING (CASH DRAWER)
      * BLF 070801 STARRED OFF "IF I-AM-WORLD" SO ROADGARD, AD&D & NONFILE
      *            INSURANCE WILL UPDATE TO STATS IF CODES ARE SET UP IN
      *            BRANCH OR GLOBAL PARAMTER RECORDS, CONSUMER PR# 3025
      * BLF 071217 SAVE DT-PART-HELD-STATUS FROM NW DT RECORD FOR ACH
      *            DEAL (PAREXT WILL SET THIS STATUS TO "R" WHEN ACH
      *            FOR PARTICIPATION HELD IS RELEASED.  THEN CHECK
      *            THIS STATUS TO DETERMINE HOW TO SET LN-PARVHELD-PAID-FG:
      *            IF STATUS IS "R" RELEASED, SET FLAG TO "R" FOR REFUND,
      *            OTHERWISE SET IT TO "V" FOR VOID (SO DEAL & VOID WILL
      *            OFFSET EACH OTHER). REGACC PR# 185

      *  CS 080103 FIX TS-DLPARVPAID-CODE TO BE SET WITH IB IF LN-LOANTYPE= I"
      *            WAS FORCING DL-PARVPAID-CODE INSTEAD OF DL-PARVPAID-CODE-IB
      *            LPEOD5TR HAD SPACES IN IT CAUSING OUT OF BALANCE.. THIS IS
      *            THE SAME CHANGE BETH MADE TO THE SF UPDATE (LONPB2)
      *                                                   CITIZENS PR#614
      * BLM 081016 PROGRAM WAS SETTING LN-PARVHELD-PAID-FG TO "R" FOR REGACC
      *            IF DEALER WAS ACH & PARTICIPATING HELD HAD BEEN PREVIOUSLY
      *            RELEASED.  CHANGE TO SET FLAG TO "R" EVEN IF DEALER NOT
      *            ACH.   NOW PAREXT EXTRACTION WILL CREATE ACH & PLATINUM
      *            AP FILES INSTEAD OF ACH & CX.  REGACC PR# 221
      *  CS 081117 ADD TEST ON GP-INCL-OTH-IN-EARNED = 'R'. IF 'R' EXCLUDE
      *            OTHER(ANTIC 6) AND MAINTFEE(ANTIC 4) FROM EARNINGS
      *                                                    REGACC PR#210
      * BLM 090216 PROGRAM WAS CHECKING SAVE-DT-PART-HELD-STATUS TO SET
      *            LN-PARVHELD-PAID-FG, BUT DT-PART-HELD-STATUS WAS NOT
      *            GETTING MOVED TO SAVE FIELD WHEN "NW" DT RECORD WAS
      *            READ IN.  THIS MESSED UP REFUND REPORTING IN
      *            SP/PAREXT, REGACC PR# 221
      * BAH 100916 UPDATE-TRE-FILE FOR REVOLVING WAS MOVING "S" TO TE-CODE
      *            INSTEAD OF "E", CAUSING VOIDS TO APPEAR ON SALES REGISTER
      *                                                     REGACC PL# 705
      * BLM 110511 REMOVED I-AM-REGACC CHECK WHEN UPDATING LN-PROC-DISBURSED
      *            IF DL-ACH-FG = "Y", REGMGM PR# 3782
      * KEC 2013.0206 {PR#04196} REGENCY <A15> [LORI MCBEE]
      *         MADE CHANGES TO LOAN APPS - ADDED MX-FILE.
      * KEC 2013.0625 {PR#04285} SOUTHERN AUTO <A15> [DAVID SINCLAIR]
      *            MADE IT TO RESTORE APPROVED STATUS ITEMS.
      * BLM 151217 DON'T UPDATE LN-PROC-DISBURSED FOR ACH DEALER UNLESS
      *            DTPAID = B, KSTREET PR# 4865
      *  CS 151221 ADD DELETE OF ELECTRONIC PROCESS (EP) ON VOID
      *  CS 151221 ADD SET OF LP-BRNO AND LP-LTOUCH-DATE, SAVE-LP-SEQNO TO 9(5)
      *  BAH 160118 SPLIT OUT LTFILE, PD0003
      *
      * KEC 2016.0205 {PD#00003} PARADATA <A30> [JAY CISZEWSKI]
      *         FIXED KEYS & RECS FOR FD-REC.
      *         CHANGED FMNO/FORMNO/NEXTFM/SETFM FROM 3 TO 4 BYTES.
      *
      * BAH 160218 EXPANDED LM-REC ADDED LM-BRNO, PD0003
      * BAH 160308 MADE UNWIND-DRAWER NUMERIC
      * BLM 160310 FIX FOR NEW TY DATE FORMAT, PD0003
      * BAH 160422 ADDED BI-LN-SMALLOAN FOR DUAL TEST, REGENCY PR#5030
      *
      * KEC 2016.0505 {PD#00003} PARADATA <A30> [JAY CISZEWSKI]
      *         CHANGED MA-APPNO TO MA-APP-NUMBER TO MATCH MA1-KEY.
      *         CHANGED AM-APPNO TO AM-APP-NUMBER TO MATCH MA1-KEY.
      *         CHANGED MEMOSTATUS-APPNO TO MEMOSTATUS-APP-NUMBER
      *          AND INCREASE FROM 6 TO 9 BYTES.
      *         CHANGED MEMOSTATUS-NUMBER TO MEMOSTATUS-ACCTNO 
      *          AND INCREASE FROM 6 TO 8 BYTES.
      *         CHANGED MEMOSTATUS-STATUS-DATE FROM 6 TO 8 BYTES.
      *         CHANGED MA-STATUS-CD TO MA-STATUS-REASON.
      * BLM 160513 FIX FOR NEW DS-DATE FORMAT, FIX DS-UNEARNED, PD0003
      * KEC 2016.0524 {PD#00003} PARADATA <A30> [JAY CISZEWSKI]
      *         CHANGED USE OF MA-APPDATE & MA-STATUS-DATE
      *          TO USE CCYYMMDD.
      * KEC 2016.0525 {PD#00003} PARADATA <A30> [JAY CISZEWSKI]
      *         CHANGED USE OF MA-TRACK-STATUS TO BE X(10).
      *
      * KEC 2016.0531 {PD#00003} PARADATA <A30> [JAY CISZEWSKI]
      *         REMOVED USE OF THE MX-FILE [LIBLA/LAFDMX].
      *         ADDED THE SETTING MA-STATUS-HISTORY FIELDS BY USING
      *         MASTATUS-SET-TRACK-HISTORY SECTION [LIBLA/MASTATUS].
      *
      * KEC 2016.0621 {PD#00003} PARADATA <A30> [JAY CISZEWSKI]
      *         REMOVED USE OF THE MX-FILE [LIBLA/LAFDMX].
      *         REMOVED USE OF OLD STATUS ROUTINES [LIBLA/MASTATUS(W)].
      *         ADDED USE OF NEW MA-STATUS-INFO FIELDS; AS WELL AS
      *         ADDED USE OF NEW ROUTINES [LIBLA/LAFRMA & LAWSMA].
      *  CS 160830 CHANGED TEST ON SAVE-DT-SEQ < 9999 TO < 999, SAVE-DT-SEQ
      *            IS MOVED TO DT-SEQ WHICH IS ONLY 9(3), CHANGED SAVE-DT-SEQ
      *            FROM PIC 9(4) TO PIC 9(3)
      * BLM 161128 MOVE BRANCH TO DT-BRNO IN UPDATE-ORIGINAL-DT
      * KEC 2017.0105 {PR#00000} PARADATA <A30> [JAY CISZEWSKI]
      *          CHANGED DS-DATE TO DS-DATE-CCYYMM FOR CONSISTENCY
      *          WITH THE DATE FIELDS IN BY & UD FILE.
      * BAH 170629 ACTIVATE 8 DIGIT DATES, PD0006
      * BLM 180228 FIX TO UPDATE TY-FEE TABLE INSTEAD OF TY-INS TABLE
      *            FOR NONFILE, AD&D AND ROADGARD
      * BAH 180316 ADDED LN-PLDATE TO BICREA CALL, WORLD #1308
      * KEC 180824 LOAN APP MODULE REMOVAL FROM A30 [LARM30]
      * BAH 181008 GLOBAL CKFILE
      * BAH 181010 REMOVED CXFILE
      * BAH 181029 GLOBAL DTFILE
      * BAH 181115 GLOBAL FMFILE
      *  CS 181121 GLOBAL LTAFILE
      *  CS 181121 GLOBAL LTBFILE
      * BAH 181128 GLOBAL LMFILE
      *  CS 181205 GLOBAL LTCFILE
      * BAH 181210 GLOBAL LNFILE
      *  CS 181227 GLOBAL LTCFILE
      *  CS 181227 GLOBAL LTFFILE
      * BAH 181231 GLOBAL LPFILE
      *  CS 190103 GLOBAL LTIFILE
      *  CS 190107 GLOBAL LTLFILE
      * BAH 190108 GLOBAL NOFILE
      *  CS 190206 COMMENT AW COPIES, REPLACE WITH LIBLP/LPCPAW
      * BAH 2019.0313 MOVED EDIT-FM-INITKEY TO LPWSFM-EDIT-FM-INITKEY 
      * BAH 2019.0625 REMOVED LN-REVOLVING, PT0153
      * BAH 2022.0524 ADDED ATB-ACTIONCD2, SECURITIZATION #1551
      * BAH 2022.0823 HARD CODED START
      * BAH 2023.1011 REMOVED EPFILE
      * BAH 2024.0130 CALL CRNOR2 AND WRITE NOTICE RECORD
      ******************************************************************
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       SOURCE-COMPUTER. 3B.
       OBJECT-COMPUTER. 3B.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
       COPY "LIBLP/LPFSWK.CPY".
       I-O-CONTROL.
       DATA DIVISION.
       FILE SECTION.
       COPY "LIBLP/LPFDWK.CPY".

       WORKING-STORAGE SECTION.
       77  SCCS-IDENTIFIER PIC X(41) VALUE
           "@(#)LP/LONPJ2.CBL S35 02/20/24-15:05:15 >".
       COPY "LIBLP/LP01TRS.CPY".
       COPY "LIBLP/LP01TRS_SQL.CPY".
       COPY "LIBLP/LPWSTRS.CPY".
       COPY "LIBLP/LP01DL.CPY".
       COPY "LIBLP/LP01DL_SQL.CPY".
       COPY "LIBLP/LPWSDL.CPY".
       COPY "LIBLP/LP01LC.CPY".
       COPY "LIBLP/LP01LC_SQL.CPY".
       COPY "LIBLP/LPWSLC.CPY".
       COPY "LIBLP/LP01LM.CPY".
       COPY "LIBLP/LP01LM_SQL.CPY".
       COPY "LIBLP/LPWSLM.CPY".
       COPY "LIBLP/LP01NO.CPY".
       COPY "LIBLP/LP01NO_SQL.CPY".
       COPY "LIBLP/LPWSNO.CPY".
       COPY "LIBLP/LP01LN.CPY".
       COPY "LIBLP/LP01LN_SQL.CPY".
       COPY "LIBLP/LPWSLN.CPY".
       COPY "LIBLP/LP01CA.CPY".
       COPY "LIBLP/LP01CA_SQL.CPY".
       COPY "LIBLP/LPWSCA.CPY".
       COPY "LIBLP/LP01LP.CPY".
       COPY "LIBLP/LP01LP_SQL.CPY".
       COPY "LIBLP/LPWSLP.CPY".
       COPY "LIBLP/LP01LTA.CPY".
       COPY "LIBLP/LP01LTA_SQL.CPY".
       COPY "LIBLP/LPWSLTA.CPY".
       COPY "LIBLP/LP01LTB.CPY".
       COPY "LIBLP/LP01LTB_SQL.CPY".
       COPY "LIBLP/LPWSLTB.CPY".
       COPY "LIBLP/LP01LTC.CPY".
       COPY "LIBLP/LP01LTC_SQL.CPY".
       COPY "LIBLP/LPWSLTC.CPY".
       COPY "LIBLP/LP01LTF.CPY".
       COPY "LIBLP/LP01LTF_SQL.CPY".
       COPY "LIBLP/LPWSLTF.CPY".
       COPY "LIBLP/LP01LTI.CPY".
       COPY "LIBLP/LP01LTI_SQL.CPY".
       COPY "LIBLP/LPWSLTI.CPY".
       COPY "LIBLP/LP01LTL.CPY".
       COPY "LIBLP/LP01LTL_SQL.CPY".
       COPY "LIBLP/LPWSLTL.CPY".
       COPY "LIBLP/LP01LTP.CPY".
       COPY "LIBLP/LP01LTP_SQL.CPY".
       COPY "LIBLP/LPWSLTP.CPY".
       COPY "LIBLP/LP01BW.CPY".
       COPY "LIBLP/LP01BW_SQL.CPY".
       COPY "LIBLP/LPWSBW.CPY".
       COPY "LIBLP/LP01TY.CPY".
       COPY "LIBLP/LP01TY_SQL.CPY".
       COPY "LIBLP/LPWSTY.CPY".
       COPY "LIBLP/LP01DS.CPY".
       COPY "LIBLP/LP01DS_SQL.CPY".
       COPY "LIBLP/LPWSDS.CPY".
       COPY "LIBLP/LP01DT.CPY".
       COPY "LIBLP/LP01DT_SQL.CPY".
       COPY "LIBLP/LPWSDT.CPY".
       COPY "LIBLP/LP01FM.CPY".
       COPY "LIBLP/LP01FM_SQL.CPY".
       COPY "LIBLP/LPWSFM.CPY".
       COPY "LIBLP/LP01PD.CPY".
       COPY "LIBLP/LP01PD_SQL.CPY".
       COPY "LIBLP/LPWSPD.CPY".
       COPY "LIBLP/LP01CD.CPY".
       COPY "LIBLP/LP01CD_SQL.CPY".
       COPY "LIBLP/LPWSCD.CPY".
       COPY "LIBLP/LP01SP.CPY".
       COPY "LIBLP/LP01SPA.CPY".
       COPY "LIBLP/LP01SPB.CPY".
       COPY "LIBLP/LP01SPC.CPY".
       COPY "LIBLP/LP01SP_SQL.CPY".
       COPY "LIBLP/LPWSSP.CPY".
       COPY "LIBGB/GB01BR.CPY".
       COPY "LIBGB/GB01BR_SQL.CPY".
       COPY "LIBGB/GBWSBR.CPY".

       01  WK-IFN          PIC X(2) VALUE "WK".
       01  WK-KEY          PIC 9(2).

       01  MSEL-LIST       PIC X(5)    VALUE "MSEL.".
       01  SUB             PIC 9(6) COMP-3.
       01  REC-EXISTS      PIC X       VALUE "N".

       01  HOLD-LN-ACCTNO       PIC 9(8)     COMP.

       01  HOLD-DT-UNPAID          PIC X.
       01  HOLD-DS-UNPAIDCK        PIC S9(9)V99 COMP-3 VALUE 0.

       01  MISC-INS-WORKERS.
           03  ADD-WRITTEN         PIC X(01)   VALUE "N".
           03  ROADGARD-WRITTEN    PIC X(01)   VALUE "N".
           03  NON-FILE-WRITTEN    PIC X(01)   VALUE "N".

       01  SAVE-LP1-KEY.
           03  SAVE-LP-BRNO    PIC 9(4).
           03  SAVE-LP-ACCTNO  PIC 9(8).
           03  SAVE-LP-SEQNO   PIC 9(5).
       01  SAVE-DT-SEQ         PIC 9(3).
       01  SAVE-TS-LINE        PIC 9(3).
       01  SAVE-ORIG-DT-STATUS PIC X VALUE SPACES.
       01  SAVE-DT-PART-HELD-STATUS
                               PIC X VALUE SPACES.
       01  SAVE-DT-PAID        PIC X VALUE SPACES.

       01  SAVE-OUTBAL         PIC S9(7)V99 COMP VALUE 0.
       01  HOLD-BW-FIRST-LAST  PIC X(70).

       01  SAVE-LN-ACCTNO      PIC 9(8).
       01  SAVE-LM-SEQ         PIC 9(3).
       01  WK-MMEMO.
           03  FILLER          PIC X(3) VALUE "ON ".
           03  WK-DATE         PIC 99/99/99.
           03  FILLER          PIC X(5) VALUE
               " RE: ".
           03  WK-ACCTNO       PIC 999999/99.
           03  FILLER          PIC X(26) VALUE
               ", DELETED INTEREST REB ADJ".

       COPY "LIBLP/LPWKPATH.CPY".
       COPY "LIBGB/CONNECTW_SQL.CPY".
       COPY "LIBGB/FILEWK.CPY".
       COPY "LIBGB/MESSWK.CPY".
       COPY "LIBCL/CREAIXW.CPY".
       COPY "LIBLP/LPMENUW.CPY".
       COPY "LIBLP/LPSETTYW.CPY".
       COPY "LIBCL/CRNOR2W.CPY".
       COPY "LIBLP/LPPOFFW.CPY".
       COPY "LIBLP/LPDSCLW.CPY".
       COPY "LIBGB/DATERW.CPY".
       COPY "LIBLP/POSTDATEW.CPY".
       COPY "LIBGB/GETENVW.CPY".
       COPY "LIBLP/LPSRCDW.CPY".
       COPY "LIBLP/LPPDUEW.CPY".
       COPY "LIBGB/AGEINGW.CPY".
       COPY "LIBGB/GPENVW.CPY".
       COPY "LIBLP/LPUPWKW.CPY".
       COPY "LIBLP/LPCEPPW.CPY".
       COPY "LIBGB/SPPDTHW.CPY".
       COPY "LIBLP/LPEARNW.CPY".
       COPY "LIBLP/LPMDTEW.CPY".
       COPY "LIBGB/ACCESSW.CPY".
       COPY "LIBGB/SCREENW.CPY".
       COPY "LIBGB/CONAME_EXT.CPY".
       COPY "LIBGB/HELPLINK.CPY".

       COPY "LIBGB/GETFMW.CPY".
       COPY "LIBLP/LPLOANW.CPY".
       COPY "LIBLP/LPPFBUF.CPY".
       SCREEN SECTION.

       PROCEDURE DIVISION
             USING FORM-PATHNAME EXIT-PATHNAME PROG-BUF UPDATE-BUF.
       DECLARATIVES.
       DECLARE-ERROR SECTION.
           USE AFTER ERROR PROCEDURE ON
               WK-FILE.
       COPY "LIBGB/DECLARE.CPY".
           PERFORM CLOSE-TY1-FILE.
           PERFORM CLOSE-TRS1-FILE.
           PERFORM CLOSE-SP1-FILE.
           PERFORM CLOSE-PD1-FILE.
           PERFORM CLOSE-NO1-FILE.
           PERFORM CLOSE-LTP1-FILE.
           PERFORM CLOSE-LTL1-FILE.
           PERFORM CLOSE-LTI1-FILE.
           PERFORM CLOSE-LTF1-FILE.
           PERFORM CLOSE-LTC1-FILE.
           PERFORM CLOSE-LTB1-FILE.
           PERFORM CLOSE-LTA1-FILE.
           PERFORM CLOSE-LP1-FILE.
           PERFORM CLOSE-LN1-FILE.
           PERFORM CLOSE-LM1-FILE.
           PERFORM CLOSE-LC1-FILE.
           PERFORM CLOSE-FM1-FILE.
           PERFORM CLOSE-DT1-FILE.
           PERFORM CLOSE-DS1-FILE.
           PERFORM CLOSE-DL1-FILE.
           PERFORM CLOSE-CD1-FILE.
           PERFORM CLOSE-CA1-FILE.
           PERFORM CLOSE-BW1-FILE.
           PERFORM CLOSE-BR-FILE.
           CLOSE WK-FILE.
       COPY "LIBGB/DECLAR2.CPY".

      /**********************************
      *   C O N T R O L   M O D U L E   *
      ***********************************
       MAIN-PROGRAM SECTION.

           PERFORM SQL-CONNECT.

      D    STOP MESS.
           MOVE FORM-PATHNAME TO FORM-PATH.

           MOVE USER-PID TO TEMP-PID.

           MOVE 5 TO WK-KEY.
           PERFORM OPEN-WK-FILE.
           PERFORM READ-WK-FILE.
           IF IO-FG = 9
              GO TO IO-ERROR.

           MOVE WK-SAVE-REC TO LN-REC HOLD-LN-REC.

           MOVE LN-ACCTNO TO HOLD-LN-ACCTNO.

           MOVE LN-ORGST TO SP-ORGST.
           MOVE LN-SPRCLASS TO SP-SPRCLASS.
           MOVE LN-SUBCLASS TO SP-SUBCLASS.
           MOVE LN-LAWCODE TO SP-LAWCODE.
           PERFORM OPEN-SP1-FILE.
           PERFORM READ-SP1-FILE.
           PERFORM CLOSE-SP1-FILE.
           IF IO-FG = 9
              GO TO IO-ERROR.

           PERFORM GET-GPENV.

           PERFORM OPEN-BR-FILE.
           MOVE LN-OWNBR TO BR-NO.
           PERFORM READ-BR-FILE.
           PERFORM CLOSE-BR-FILE.
           IF IO-FG = 9
              GO TO IO-ERROR.

           MOVE LN-DLNO TO DL-DLNO.
           PERFORM OPEN-DL1-FILE.
           PERFORM READ-DL1-FILE.
           IF IO-FG = 9
              GO TO IO-ERROR.
           PERFORM CLOSE-DL1-FILE.

           PERFORM OPEN-LN1-FILE.
           PERFORM READ-LN1-FILE.
           IF IO-FG = 9
              GO TO IO-ERROR.

           IF ( LN-POCD-VOID )
              MOVE "ACCOUNT HAS BEEN DELETED" TO MESS
              PERFORM SEND-MESS
              GO TO FINISH-UPDATE.

           IF ( LN-POCD-INRESCISSION )
              MOVE "ACCOUNT IS IN RESCISSION" TO MESS
              PERFORM SEND-MESS
              GO TO FINISH-UPDATE.

           IF ( LN-POCD-RESCIND )
              MOVE "ACCOUNT HAS BEEN RESCINDED" TO MESS
              PERFORM SEND-MESS
              GO TO FINISH-UPDATE.

      *NEED THE LC-FILE TO CHECK LC-APP-REQ-FOR-LOAN FLAG
           MOVE LN-CLASS TO LC-CODE.
           PERFORM GET-LC1-RECORD.

           IF (IO-FG NOT = 0)
              MOVE "N" TO LC-APP-REQ-FOR-LOAN.

      * NEED TO COMPUTE EARNINGS BEFORE SETTING POCD TO "**" OR "*U"
      * (COMPUTE-EARNINGS WILL NOT COMPUTE EARNINGS FOR VOIDED ACCOUNTS
      *  WHICH IS NEEDED FOR STAT UPDATES OF UNEARNED)
      
           PERFORM COMPUTE-EARNINGS.

      * ADDED QUICK UPDATE HERE TO PREVENT TWO USERS FROM DELETING OR UNWINDING
      
           IF WK-UNWIND-REASON-CD = " "
              MOVE "**" TO LN-POCD
           ELSE
              MOVE "*U" TO LN-POCD.

           PERFORM REWRITE-LN1-FILE.
           PERFORM CLOSE-LN1-FILE.

           PERFORM UPDATE-TRS-FILE.

      * GLB 07/02/01   RE-UPDATE ORIGINAL DT-REC "NW" FIRST
           PERFORM UPDATE-ORIGINAL-DT.
           PERFORM UPDATE-DT1-FILE.
           PERFORM UPDATE-LM1-FILE.
           PERFORM UPDATE-LT1-FILE.

           PERFORM UPDATE-BW1-FILE.

           PERFORM SET-DELINQUENCY.

           IF LN-PRLNNO NOT = 0
              PERFORM UPDATE-PD1-FILE.

      * UPDATE-MA-FILE MUST BE DONE BEFORE DS & TY UPDATE:
           PERFORM UPDATE-DS1-FILE.
           PERFORM UPDATE-TY1-FILE.

           PERFORM UPDATE-FM1-FILE.

           PERFORM UPDATE-LN1-FILE.

           PERFORM UPDATE-NOTICE-FILE.

           IF WK-UNWIND-REASON-CD NOT = " "
              PERFORM UPDATE-CA1-FILE.

           IF LN-PROCASH NOT = LN-PROCEEDS
              MOVE "*** REMEMBER TO VOID THE CHECK(S)..." TO MESS
              PERFORM SEND-MESS.

           MOVE HOLD-LN-REC TO LN-REC.

       FINISH-UPDATE.
           MOVE " " TO WK-SAVE-REC LN-REC.
           MOVE 0      TO LN-ACCTNO LOAN-KEY.
           MOVE LN-REC TO WK-SAVE-REC.
           MOVE 0      TO WK-ELE.
           PERFORM REWRITE-WK-FILE.

           MOVE "J0J" TO NEW-PROG-FORM.
           MOVE "P"   TO ERRCD.

           GO TO END-ROUTINE.

      ************************
       UPDATE-TRS-FILE SECTION.
      ************************
           MOVE "UPDATING S.F. LOAN TRANSACTION FILE.........." TO MESS.
           PERFORM SEND-LEGEND.

           PERFORM OPEN-TRS1-FILE.
           MOVE 1 TO SAVE-TS-LINE.
       UPDATE-TRS-NEXT.
           PERFORM CLEAR-TRS-FILE.

           MOVE TRANS-DATE   TO TS-DATE.
           MOVE BRANCH       TO TS-BRNO.
           MOVE LN-CLASS     TO TS-CLASS.
           MOVE LN-ACCTNO    TO TS-ACCTNO.
           MOVE LN-LAWCODE   TO TS-LAWCODE.
           MOVE SAVE-TS-LINE TO TS-LINE.

           MOVE LN-LOANTYPE   TO TS-LOANTYPE.
           MOVE LN-LOANDATE   TO TS-LOANDATE.
           MOVE LN-INTDATE    TO TS-INTDATE.
           MOVE LN-PURCD      TO TS-PURCD.
           MOVE LN-SRCD       TO TS-SRCD.
           MOVE LN-COLLCD     TO TS-COLLCD.
           MOVE LN-ORGTERM    TO TS-ORGTERM.
           MOVE LN-DLRECOURSE TO TS-DLRECOURSE.
           MOVE LN-ANTICTERM  TO TS-ANTICTERM.
           SUBTRACT LN-LNAMT FROM TS-LNAMT.
           SUBTRACT LN-FINCHG FROM TS-FINCHG.
           SUBTRACT LN-1STPYAMT FROM TS-1STPYAMT.
           MOVE LN-NOREGPY TO TS-NOREGPY.
           SUBTRACT LN-REGPYAMT FROM TS-REGPYAMT.
           SUBTRACT LN-LASTPYAMT FROM TS-LASTPYAMT.
           MOVE LN-INSURED TO TS-INSURED.
           MOVE LN-DLNO    TO TS-DLNO.
           SUBTRACT LN-DLDISC FROM TS-DLDISC.
           SUBTRACT LN-DLRVPRIN(1) FROM TS-DLRVPRIN(1).
           SUBTRACT LN-DLRVPRIN(2) FROM TS-DLRVPRIN(2).
           SUBTRACT LN-DLRVPRIN(3) FROM TS-DLRVPRIN(3).
           SUBTRACT LN-DLRVCHGS(1) FROM TS-DLRVCHGS(1).
           SUBTRACT LN-DLRVCHGS(2) FROM TS-DLRVCHGS(2).
           SUBTRACT LN-DLRVCHGS(3) FROM TS-DLRVCHGS(3).
           SUBTRACT LN-DLPARVHELD FROM TS-DLPARVHELD.
           SUBTRACT LN-DLPARVPAID FROM TS-DLPARVPAID.
           MOVE DL-RVCHGS-CODE(1) TO TS-DL-RVCHGS-CODE(1).
           MOVE DL-RVCHGS-CODE(2) TO TS-DL-RVCHGS-CODE(2).
           MOVE DL-RVCHGS-CODE(3) TO TS-DL-RVCHGS-CODE(3).
           MOVE DL-PARVHELD-CODE TO TS-DL-PARVHELD-CODE.
           IF LN-LOANTYPE = "P"
               MOVE DL-PARVPAID-CODE TO TS-DL-PARVPAID-CODE
           ELSE
               MOVE DL-PARVPAID-CODE-IB TO TS-DL-PARVPAID-CODE.

           SUBTRACT LN-PROCEEDS FROM TS-PROCEEDS.

           MOVE LN-PRLNNO TO TS-PRLNNO.
           SUBTRACT LN-PRDLDISC-UNEARN FROM TS-PRDLDISC-UNEARN.

           SUBTRACT LN-INSPREM-DLADJ FROM TS-INSPREM-DLADJ.
           MOVE LN-SERVACCT TO TS-SERVACCT.
           SUBTRACT LN-DLPARTRCAMT FROM TS-DLPARTRCAMT.
           SUBTRACT LN-PURBAL FROM TS-PURBAL.
           MOVE LN-ANTICTERM TO TS-ANTICTERM.
           SUBTRACT LN-MAINTFEE FROM TS-MAINTFEE.
           SUBTRACT LN-SERCHG FROM TS-SERCHG.
           SUBTRACT LN-EXTCHG FROM TS-EXTCHG.
           MOVE LN-INSTYPES TO TS-INSTYPES.
           MOVE LN-INSELIGIBLE TO TS-INSELIGIBLE.
           SUBTRACT LN-INTCHG FROM TS-INTCHG.
           COMPUTE TS-O3INS-DLPROC = 0 - LN-O3INS-DLPROC.

           MOVE SP-LNCOST-FRMLA TO TS-SP-LNCOST-FRMLA.

           COMPUTE TS-DLCOMM-PP-CMSI = 0 - LN-DLCOMM-PP-CMSI.
           COMPUTE TS-DLCOMM-O3-CMSI = 0 - LN-DLCOMM-O3-CMSI.

           MOVE 1 TO SUB.
       UPDATE-TRS-BORR.
           IF SUB NOT > 4
              MOVE LN-SSNO(SUB) TO TS-SSNO(SUB)
              MOVE LN-MAKERCD(SUB) TO TS-MAKERCD(SUB).
           IF SUB NOT > 8
              MOVE LN-INSCOMP(SUB) TO TS-INSCOMP(SUB)
              COMPUTE TS-INSPREM(SUB) = 0 - LN-INSPREM(SUB)
              MOVE LN-INSOURS(SUB) TO TS-INSOURS(SUB)
              MOVE LN-INSEFF-DATE(SUB) TO TS-INSEFF-DATE(SUB)
              MOVE LN-INSEXP-DATE(SUB) TO TS-INSEXP-DATE(SUB)
              COMPUTE TS-INSCOMM(SUB) = 0 - LN-INSCOMM(SUB).
           IF SUB NOT > 6
              SUBTRACT LN-ACCERN(SUB) FROM TS-EARNINGS(SUB)
              SUBTRACT LN-ANTICERN(SUB) FROM TS-ANTICERN(SUB)
      *       ------------------------------------------------------------------
      *       ADDED GP-AMORTIZE-ACQUIST-FEE, THIS OPTION WILL TAKE THE
      *       AMOUNT ENTERED IN RESERVE PRINCIPAL 3 AND MOVE IT TO
      *       THE LOANS UNEARNED SERVICE CHARGE               REGACC #0081
      *
      *       NOTE: NO GL ENTRY MAKE DURING SF DELETE FOR SERVICE CHARGE
      *        EXCEPT
      *            CORRECTED LOGIC FOR GP-AMORTIZE-ACQUIST-FEE, THIS OPTION
      *            TAKES THE AMOUNT ENTERED IN RESERVE PRINCIPAL 3 AND
      *            MOVE IT TO THE LOANS UNEARNED SERVICE CHARGE.
      *            THE LOGIC TO CLEAR TS-ANTICERN(3) IS CORRECT FOR SF CONTRACTS
      *            THAT NEVER WENT THROUGH AN ACCRUAL. HOWEVER ACCOUNTS THAT
      *            DID TAKE EARNINGS AND REDUCED UNEARNED, THOSE GL ENTRIES
      *            NEED TO BE OFFSET, WILL LET LIBLP/LPEOD5TR TEST FOR AND
      *            MAKE THOSE ENTRIES                               REGACC #0081
      *       ------------------------------------------------------------------
      *       IF SUB = 3
      *          IF GP-AMORTIZE-ACQUIST-FEE = "Y"
      *             MOVE 0 TO TS-ANTICERN(3).

           IF SUB NOT > 10
              COMPUTE TS-FEEAMT(SUB) = 0 - LN-FEEAMT(SUB)
              MOVE LN-FEECODE(SUB) TO TS-FEECODE(SUB)
              MOVE LN-FEEOURS(SUB) TO TS-FEEOURS(SUB).
           IF SUB < 10
              ADD 1 TO SUB
              GO TO UPDATE-TRS-BORR.

           PERFORM SET-POSTDATE.
           MOVE POST-USERID TO TS-USERID.
           MOVE POST-TIME TO TS-POSTTIME.
           MOVE POST-DATE TO TS-POSTDATE.

           MOVE LN-MIP-FG TO TS-MIP-FG.

           IF WK-UNWIND-REASON-CD = " "
              MOVE " "                 TO TS-UNWIND-REASON-CODE
              MOVE 0                   TO TS-TILLNO
           ELSE
              MOVE WK-UNWIND-REASON-CD TO TS-UNWIND-REASON-CODE
              MOVE WK-UNWIND-DRAWER    TO TS-TILLNO.

           MOVE LN-ACTIONCD2 TO TS-ACTIONCD2.

           PERFORM WRITE-TRS1-FILE.
           IF IO-FG = 0
              IF WK-UNWIND-REASON-CD NOT = " "
                 PERFORM UPDATE-DUMMY-LPREC-UW
              END-IF
              GO TO UPDATE-TRS-EXIT.

           IF SAVE-TS-LINE < 999
              ADD 1 TO SAVE-TS-LINE
              GO TO UPDATE-TRS-NEXT.

           MOVE "TRANSACTION SEQ = 999, REC NOT CREATED !" TO MESS.
           PERFORM SEND-MESS.

       UPDATE-TRS-EXIT.
           PERFORM CLOSE-TRS1-FILE.

      ************************
       UPDATE-DUMMY-LPREC-UW SECTION.
      ************************
           MOVE "UPDATING DUMMY LPREC-UW FILE..." TO MESS.
           PERFORM SEND-LEGEND.

           PERFORM OPEN-LP1-FILE.

           MOVE LP-PATH-OWNBR TO SAVE-LP-BRNO.
           MOVE LN-ACCTNO     TO SAVE-LP-ACCTNO.
           MOVE 1             TO SAVE-LP-SEQNO.
       UPDATE-DUMMY-LPREC-UW-CREATE.
           PERFORM CLEAR-LP-FILE.

           MOVE SAVE-LP1-KEY TO LP1-KEY.

           MOVE TRANS-DATE   TO LP-TRDATE
                                LP-PAYDATE.
           MOVE "UW"         TO LP-TRCD.
           MOVE 0            TO LP-PDTH-DATE.
           MOVE 0            TO LP-LCPDTH-DATE.
           MOVE 0            TO LP-EARNED(1)
                                LP-EARNED(2)
                                LP-EARNED(3).
           MOVE 0            TO LP-CURBAL.

           PERFORM SET-POSTDATE.
           MOVE POST-USERID TO LP-USERID.
           MOVE POST-TIME   TO LP-POSTTIME.
           MOVE POST-DATE   TO LP-POSTDATE.
           PERFORM WRITE-LP1-FILE.
           IF IO-FG = 0
              GO TO UPDATE-DUMMY-LPREC-UW-EXIT.

           IF SAVE-LP-SEQNO < 99999
              ADD 1 TO SAVE-LP-SEQNO
              GO TO UPDATE-DUMMY-LPREC-UW-CREATE.

           MOVE "LOAN PMT = 99999, PMT REC NOT CREATED !" TO MESS.
           PERFORM SEND-MESS.

       UPDATE-DUMMY-LPREC-UW-EXIT.
           PERFORM CLOSE-LP1-FILE.

      **********************************************
       UPDATE-CA1-FILE SECTION.
      **********************************************

      * UPDATE CASH DRAWER FOR UNWIND

           MOVE "UPDATING (UNWIND) CASH DRAWER FILE........" TO MESS.
           PERFORM SEND-LEGEND.

           PERFORM OPEN-CA1-FILE.
           MOVE TRANS-DATE       TO CA-DATE.
           MOVE BRANCH           TO CA-BRNO.
           MOVE WK-UNWIND-DRAWER TO CA-TILLNO.
           PERFORM READ-CA1-FILE.
           IF IO-FG NOT = 0
              GO TO IO-ERROR.

           ADD LN-PROCEEDS TO CA-RUNNING.

           PERFORM REWRITE-CA1-FILE.
           PERFORM CLOSE-CA1-FILE.

       UPDATE-CA1-FILE-EXIT.
           EXIT.

      ***************************************
       UPDATE-ORIGINAL-DT SECTION.
      ***************************************

      * FIND ORIGINAL "NW", SET REVERSE FLAGS
           MOVE SPACES TO HOLD-DT-UNPAID.

           PERFORM OPEN-DT1-FILE.

           MOVE "UPDATING DEALER TRANSACTION FILE........" TO MESS.
           PERFORM SEND-LEGEND.

           MOVE DT-PATH-OWNBR TO DT-BRNO
                                 QDT1-WBEG-BRNO
                                 QDT1-WEND-BRNO.
           MOVE LN-DLNO       TO DT-DLNO.
           MOVE DT-NUM        TO QDT1-WBEG-NUM
                                 QDT1-WEND-NUM.
           MOVE LN-CLASS      TO DT-CLASS
                                 QDT1-WBEG-CLASS
                                 QDT1-WEND-CLASS.
           MOVE LN-ACCTNO     TO DT-ACCTNO
                                 QDT1-WBEG-ACCTNO
                                 QDT1-WEND-ACCTNO.
           MOVE 19000101      TO DT-DATE SQL-DATE-YYYYMMDD
           PERFORM SQL-SET-DATE.
           MOVE SQL-DATE-YYYY-MM-DD TO QDT1-WBEG-DATE.
           MOVE EXT-JULIAN-END TO SQL-DATE-YYYYMMDD.
           PERFORM SQL-SET-DATE.
           MOVE SQL-DATE-YYYY-MM-DD TO QDT1-WEND-DATE.
           MOVE 0             TO DT-SEQ
                                 QDT1-WBEG-SEQ.
           MOVE ALL "9"       TO QDT1-WEND-SEQ.

           PERFORM START-DT1-FILE.
       FIND-ORIGINAL-DT-NEXT.
           PERFORM READ-DT1-FILE-NEXT.
           IF (IO-FG = 9) OR (DT-BRNO NOT = DT-PATH-OWNBR)
              GO TO EXIT-UPDATE-ORIGINAL-DT.

           IF DT-DLNO NOT = LN-DLNO
              GO TO EXIT-UPDATE-ORIGINAL-DT.
           IF DT-ACCTNO NOT = LN-ACCTNO
              GO TO EXIT-UPDATE-ORIGINAL-DT.

      *  GLB, ADDED TEST OF TRCD
           IF DT-TRCD NOT = "NW" AND
              DT-TRCD NOT = "PU"
                 GO TO FIND-ORIGINAL-DT-NEXT.

           MOVE DT-UNPAID TO HOLD-DT-UNPAID.
           MOVE DT-STATUS TO SAVE-ORIG-DT-STATUS.
           MOVE DT-PART-HELD-STATUS TO SAVE-DT-PART-HELD-STATUS.
           MOVE DT-PAID TO SAVE-DT-PAID.
           MOVE "Y" TO DT-REVERSE.

           PERFORM REWRITE-DT1-FILE.
           GO TO FIND-ORIGINAL-DT-NEXT.
       EXIT-UPDATE-ORIGINAL-DT.
           PERFORM CLOSE-DT1-FILE.


      ***************************************
       UPDATE-DT1-FILE SECTION.
      ***************************************
           MOVE "UPDATING DEALER TRANSACTION FILE........" TO MESS.
           PERFORM SEND-LEGEND.

           PERFORM OPEN-DT1-FILE.
           MOVE 1 TO SAVE-DT-SEQ.
       NEXT-DT-SEQ.
           PERFORM CLEAR-DT-FILE.

           MOVE DT-PATH-OWNBR TO DT-BRNO.
           MOVE LN-DLNO       TO DT-DLNO.
           MOVE LN-CLASS      TO DT-CLASS.
           MOVE LN-ACCTNO     TO DT-ACCTNO.
           MOVE TRANS-DATE    TO DT-DATE.
           MOVE SAVE-DT-SEQ   TO DT-SEQ.
           IF LN-SERVACCT = "Y"
              MOVE "S"        TO DT-TYPE
           ELSE
              MOVE "P"        TO DT-TYPE.
           MOVE "RV"          TO DT-TRCD.
           MOVE "NW"          TO DT-REVERSE-TRCD.
           SUBTRACT LN-DLRVPRIN(1) FROM DT-DLRVPRIN(1).
           SUBTRACT LN-DLRVPRIN(2) FROM DT-DLRVPRIN(2).
           SUBTRACT LN-DLRVPRIN(3) FROM DT-DLRVPRIN(3).
           SUBTRACT LN-DLRVCHGS(1) FROM DT-DLRVCHGS(1).
           SUBTRACT LN-DLRVCHGS(2) FROM DT-DLRVCHGS(2).
           SUBTRACT LN-DLRVCHGS(3) FROM DT-DLRVCHGS(3).
           SUBTRACT LN-DLPARVHELD FROM DT-DLPARVHELD.
           SUBTRACT LN-DLPARVPAID FROM DT-DLPARVPAID.
           SUBTRACT LN-PROCEEDS FROM DT-PROCEEDS.

      * FOR SERVICE ACCOUNTS, UPDATE DT-PROCEEDS WITH INSURANCE & FEES
      * THAT OUR OURS:
           IF LN-SERVACCT = "Y"
              PERFORM VARYING SUB FROM 1 BY 1 UNTIL SUB > 10
                 IF SUB < 9 AND LN-INSOURS(SUB) = "Y"
                    ADD LN-INSPREM(SUB) TO DT-PROCEEDS
                 END-IF
                 IF LN-FEEOURS(SUB) = "Y"
                    ADD LN-FEEAMT(SUB) TO DT-PROCEEDS
                 END-IF
              END-PERFORM.

      * DT-PURBAL GETS NEGATIVE IN NWJRNL
           MOVE LN-PURBAL TO DT-PURBAL.
      *  GLB 07/02/01 REVERSE IS NOT PART OF BATCH RELEASE ANYMORE
      *    MOVE "B" TO DT-PAID.

           MOVE " " TO DT-PAID.
           MOVE "Y" TO DT-REVERSE.
           PERFORM WRITE-DT1-FILE.
           IF IO-FG = 0
              GO TO EXIT-UPDATE-DT1.

           IF SAVE-DT-SEQ < 999
              ADD 1 TO SAVE-DT-SEQ
              GO TO NEXT-DT-SEQ.

           MOVE "DEALER TRANSACTION = 9999, REC NOT CREATED !" TO MESS.
           PERFORM SEND-MESS.
       EXIT-UPDATE-DT1.
           PERFORM CLOSE-DT1-FILE.


      ************************
       UPDATE-LM1-FILE SECTION.
      ************************
           MOVE "DELETING LOAN MEMO FILE................." TO MESS.
           PERFORM SEND-LEGEND.

           PERFORM OPEN-LM1-FILE.

           MOVE LM-PATH-OWNBR TO LM-BRNO
                                 QLM1-WBEG-BRNO
                                 QLM1-WEND-BRNO.
           MOVE LN-ACCTNO     TO LM-ACCTNO
                                 QLM1-WBEG-ACCTNO
                                 QLM1-WEND-ACCTNO.
           MOVE 0             TO LM-DATE
                                 QLM1-WBEG-DATE-REV.
           MOVE 1             TO LM-SEQ
                                 QLM1-WBEG-SEQ.
           MOVE ALL "9"       TO QLM1-WEND-DATE-REV
                                 QLM1-WEND-SEQ.
           COMPUTE QLM1-WSBEG-DATE-REV = LM-DATE-REVERSED + 1.

           PERFORM START-LM1-FILE.
       NEXT-LM1-DELETE.
           PERFORM READ-LM1-FILE-NEXT.
           IF IO-FG = 0
              IF (LM-ACCTNO = LN-ACCTNO) AND (LM-BRNO = LM-PATH-OWNBR)
                 PERFORM DELETE-LM1-FILE
                 GO TO NEXT-LM1-DELETE.

           PERFORM CLOSE-LM1-FILE.

      ***************************************
       UPDATE-LT1-FILE SECTION.
      ***************************************

      * DELETE ALL THE TRAILERS EXCEPT PAYMENT SCHEDULES, NEED THAT FOR
      * REPORTING VOIDS SOMEWHERE ?
      * DO NOT DELETE DEBIT CARD TRAILERS EITHER. LOAN PURGE WILL DO THAT.

           MOVE "DELETING LOAN TRAILER FILE............" TO MESS.
           PERFORM SEND-LEGEND.

           PERFORM DELETE-LTA-RECORDS.
           PERFORM DELETE-LTB-RECORDS.
           PERFORM DELETE-LTC-RECORDS.
           PERFORM DELETE-LTF-RECORDS.
           PERFORM DELETE-LTI-RECORDS.
           PERFORM DELETE-LTL-RECORDS.

       EXIT-UPDATE-LT1-FILE.
           EXIT.
  
      ****************************************
       DELETE-LTA-RECORDS SECTION.
      ****************************************
           PERFORM OPEN-LTA1-FILE.

           MOVE LTA-PATH-OWNBR TO LTA-BRNO
                                  QLTA1-WBEG-BRNO
                                  QLTA1-WEND-BRNO.
           MOVE LN-ACCTNO      TO LTA-ACCTNO
                                  QLTA1-WBEG-ACCTNO
                                  QLTA1-WEND-ACCTNO.
           MOVE 0              TO LTA-SEQNO
                                  QLTA1-WBEG-SEQNO.
           MOVE ALL "9"        TO QLTA1-WEND-SEQNO.

           PERFORM START-LTA1-FILE.
       NEXT-LTA-DELETE.
           PERFORM READ-LTA1-FILE-NEXT.
           IF IO-FG NOT = 0 OR (LTA-BRNO NOT = LTA-PATH-OWNBR)
              GO TO DELETE-LTA-RECORDS-EXIT.

           IF LTA-ACCTNO = LN-ACCTNO
              PERFORM DELETE-LTA1-FILE.

           GO TO NEXT-LTA-DELETE.

       DELETE-LTA-RECORDS-EXIT.
           PERFORM CLOSE-LTA1-FILE.

      ****************************************
       DELETE-LTB-RECORDS SECTION.
      ****************************************
           PERFORM OPEN-LTB1-FILE.

           MOVE LTB-PATH-OWNBR TO LTB-BRNO
                                  QLTB1-WBEG-BRNO
                                  QLTB1-WEND-BRNO.
           MOVE LN-ACCTNO      TO LTB-ACCTNO
                                  QLTB1-WBEG-ACCTNO
                                  QLTB1-WEND-ACCTNO.
           MOVE 0              TO LTB-SEQNO
                                  QLTB1-WBEG-SEQNO.
           MOVE ALL "9"        TO QLTB1-WEND-SEQNO.

           PERFORM START-LTB1-FILE.
       NEXT-LTB-DELETE.
           PERFORM READ-LTB1-FILE-NEXT.
           IF IO-FG NOT = 0 OR (LTB-BRNO NOT = LTB-PATH-OWNBR)
              GO TO DELETE-LTB-RECORDS-EXIT.

           IF LTB-ACCTNO = LN-ACCTNO
              PERFORM DELETE-LTB1-FILE.

           GO TO NEXT-LTB-DELETE.

       DELETE-LTB-RECORDS-EXIT.
           PERFORM CLOSE-LTB1-FILE.

      ****************************************
       DELETE-LTC-RECORDS SECTION.
      ****************************************
           PERFORM OPEN-LTC1-FILE.

           MOVE LTC-PATH-OWNBR TO LTC-BRNO
                                  QLTC1-WBEG-BRNO
                                  QLTC1-WEND-BRNO.
           MOVE LN-ACCTNO      TO LTC-ACCTNO
                                  QLTC1-WBEG-ACCTNO
                                  QLTC1-WEND-ACCTNO.
           MOVE 0              TO LTC-SEQNO
                                  QLTC1-WBEG-SEQNO.
           MOVE ALL "9"        TO QLTC1-WEND-SEQNO.

           PERFORM START-LTC1-FILE.
       NEXT-LTC-DELETE.
           PERFORM READ-LTC1-FILE-NEXT.
           IF IO-FG NOT = 0 OR (LTC-BRNO NOT = LTC-PATH-OWNBR)
              GO TO DELETE-LTC-RECORDS-EXIT.

           IF LTC-ACCTNO = LN-ACCTNO
              PERFORM DELETE-LTC1-FILE.

           GO TO NEXT-LTC-DELETE.

       DELETE-LTC-RECORDS-EXIT.
           PERFORM CLOSE-LTC1-FILE.

      ****************************************
       DELETE-LTF-RECORDS SECTION.
      ****************************************
           PERFORM OPEN-LTF1-FILE.

           MOVE LTF-PATH-OWNBR TO LTF-BRNO
                                  QLTF1-WBEG-BRNO
                                  QLTF1-WEND-BRNO.
           MOVE LN-ACCTNO      TO LTF-ACCTNO
                                  QLTF1-WBEG-ACCTNO
                                  QLTF1-WEND-ACCTNO.
           MOVE 0              TO LTF-SEQNO
                                  QLTF1-WBEG-SEQNO.
           MOVE ALL "9"        TO QLTF1-WEND-SEQNO.

           PERFORM START-LTF1-FILE.
       NEXT-LTF-DELETE.
           PERFORM READ-LTF1-FILE-NEXT.
           IF IO-FG NOT = 0 OR (LTF-BRNO NOT = LTF-PATH-OWNBR)
              GO TO DELETE-LTF-RECORDS-EXIT.

           IF (LTF-ACCTNO = LN-ACCTNO)
              PERFORM DELETE-LTF1-FILE.

           GO TO NEXT-LTF-DELETE.

       DELETE-LTF-RECORDS-EXIT.
           PERFORM CLOSE-LTF1-FILE.

      ****************************************
       DELETE-LTI-RECORDS SECTION.
      ****************************************
           PERFORM OPEN-LTI1-FILE.

           MOVE LTI-PATH-OWNBR TO LTI-BRNO
                                  QLTI1-WBEG-BRNO
                                  QLTI1-WEND-BRNO.
           MOVE LN-ACCTNO      TO LTI-ACCTNO
                                  QLTI1-WBEG-ACCTNO
                                  QLTI1-WEND-ACCTNO.
           MOVE 0              TO LTI-SEQNO
                                  QLTI1-WBEG-SEQNO.
           MOVE ALL "9"        TO QLTI1-WEND-SEQNO.

           PERFORM START-LTI1-FILE.
       NEXT-LTI-DELETE.
           PERFORM READ-LTI1-FILE-NEXT.
           IF IO-FG NOT = 0 OR (LTI-BRNO NOT = LTI-PATH-OWNBR)
              GO TO DELETE-LTI-RECORDS-EXIT.

           IF (LTI-ACCTNO = LN-ACCTNO)
              PERFORM DELETE-LTI1-FILE.

           GO TO NEXT-LTI-DELETE.

       DELETE-LTI-RECORDS-EXIT.
           PERFORM CLOSE-LTI1-FILE.

      ****************************************
       DELETE-LTL-RECORDS SECTION.
      ****************************************
           PERFORM OPEN-LTL1-FILE.

           MOVE LTL-PATH-OWNBR TO LTL-BRNO
                                  QLTL1-WBEG-BRNO
                                  QLTL1-WEND-BRNO.
           MOVE LN-ACCTNO      TO LTL-ACCTNO
                                  QLTL1-WBEG-ACCTNO
                                  QLTL1-WEND-ACCTNO.
           MOVE 0              TO LTL-SEQNO
                                  QLTL1-WBEG-SEQNO.
           MOVE ALL "9"        TO QLTL1-WEND-SEQNO.

           PERFORM START-LTL1-FILE.
       NEXT-LTL-DELETE.
           PERFORM READ-LTL1-FILE-NEXT.
           IF IO-FG NOT = 0 OR (LTL-BRNO NOT = LTL-PATH-OWNBR)
              GO TO DELETE-LTL-RECORDS-EXIT.

           IF (LTL-ACCTNO = LN-ACCTNO)
              PERFORM DELETE-LTL1-FILE.

           GO TO NEXT-LTL-DELETE.

       DELETE-LTL-RECORDS-EXIT.
           PERFORM CLOSE-LTL1-FILE.

      ************************
       UPDATE-BW1-FILE SECTION.
      ************************
           MOVE "UPDATING BORROWER FILE.................." TO MESS.
           PERFORM SEND-LEGEND.

           PERFORM GET-COLLATERAL.
           MOVE 1 TO SUB.
           PERFORM OPEN-BW1-FILE.
       UPDATE-NEXT-BW.
           MOVE LN-SSNO(SUB) TO BW-SSNO.
           PERFORM READ-BW1-FILE.
           IF IO-FG = 9
              GO TO IO-ERROR.
           SUBTRACT 1 FROM BW-LOANS.
           IF SUB = 1
              STRING BW-FNAME," ",BW-LNAME DELIMITED BY "   "
                INTO HOLD-BW-FIRST-LAST
              IF CD-CO-SECURED = "R"
                 SUBTRACT LN-YTDINTPD FROM BW-RE-INTYTD
              ELSE
                 SUBTRACT LN-YTDINTPD FROM BW-INTYTD.
           IF LN-SERVACCT = "Y"
              SUBTRACT LN-LNAMT FROM BW-LIAOTH
            ELSE
              IF LN-SMALLOAN = "Y"
                 SUBTRACT LN-LNAMT FROM BW-LIASMLN
               ELSE
                 SUBTRACT LN-LNAMT FROM BW-LIAOTH.
           PERFORM REWRITE-BW1-FILE.
           IF SUB = 4
              GO TO EXIT-UPDATE-BW1.
           ADD 1 TO SUB.
           IF LN-SSNO(SUB) NOT = 0
              GO TO UPDATE-NEXT-BW.
       EXIT-UPDATE-BW1.
           PERFORM CLOSE-BW1-FILE.

      ************************
       UPDATE-DS1-FILE SECTION.
      ************************
           MOVE "UPDATING DEALER STATISTICS FILE........." TO MESS.
           PERFORM SEND-LEGEND.

           PERFORM OPEN-DS1-FILE.
           MOVE "N" TO REC-EXISTS.
       UPDATE-DS1-FILE-CHECK.
           PERFORM SET-DS1-KEY.
           PERFORM READ-DS1-FILE.
           IF IO-FG = 0
              GO TO UPDATE-DS1-FILE-UPD-REC.
           IF REC-EXISTS = "Y"
              GO TO IO-ERROR.
           MOVE "Y" TO REC-EXISTS.
           PERFORM CLEAR-DS-FILE.
           PERFORM SET-DS1-KEY.
           PERFORM WRITE-DS1-FILE.
           IF IO-FG = 9 AND E-CODE NOT = "22"
              GO TO IO-ERROR.
           GO TO UPDATE-DS1-FILE-CHECK.

       UPDATE-DS1-FILE-UPD-REC.

           IF (GP-THE-ONLY-SF-SRCD NOT = " ") AND
                   (LN-SRCD = GP-THE-ONLY-SF-SRCD)
              SUBTRACT 1 FROM DS-NPURUNIT(1) DS-NPURUNIT(2)
              SUBTRACT LN-LNAMT FROM DS-NPURAMT(1) DS-NPURAMT(2)
           ELSE
           IF LN-SRCD = "NB"
              SUBTRACT 1 FROM DS-NPURUNIT(1) DS-NPURUNIT(2)
              SUBTRACT LN-LNAMT FROM DS-NPURAMT(1) DS-NPURAMT(2)
           ELSE
           IF LN-SRCD = "PB" OR "RN" OR "RB" OR "RO"
              SUBTRACT 1 FROM DS-RNEWUNIT(1) DS-RNEWUNIT(2)
              SUBTRACT LN-LNAMT FROM DS-RNEWAMT(1) DS-RNEWAMT(2)
           ELSE
              SUBTRACT 1 FROM DS-OPURUNIT(1) DS-OPURUNIT(2)
              SUBTRACT LN-LNAMT FROM DS-OPURAMT(1) DS-OPURAMT(2).

      * UPDATE UNEARNED STATISTICS NET AND VOLUME
      
           IF GP-UNEARNED-STATS = "Y"
              PERFORM VARYING DSSUB3 FROM 1 BY 1 UNTIL DSSUB3 > 6
                        AFTER DSSUB2 FROM 1 BY 1 UNTIL DSSUB2 > 2
                  IF DSSUB2 = 1
                     SUBTRACT EARN-UNEARN(DSSUB3) FROM
                                 DS-UNEARNED-AMT(1 DSSUB2 DSSUB3)
                  ELSE
                     IF (DSSUB3 < 4) OR
                        ((DSSUB3 = 4 AND NOT GP-INCL-OTH-IN-EARNED-R) OR
                        (DSSUB3 = 5 AND LN-PRDLDISC-UNEARN = 0) OR
                        (DSSUB3 = 6 AND NOT (GP-INCL-OTH-IN-EARNED-N OR
                                        GP-INCL-OTH-IN-EARNED-R)))
                         SUBTRACT LN-ANTICERN(DSSUB3)
                                FROM DS-UNEARNED-AMT(1 DSSUB2 DSSUB3)
                                     DS-UNEARNED-AMT(2 DSSUB2 DSSUB3)
                     END-IF
                  END-IF
              END-PERFORM
      *       ----------------------------------------------------------
      *       UPDATE PROCEEDS: WE DO NOT CARE ABOUT ACCOUNT STATUS.
      *       ----------------------------------------------------------
              SUBTRACT LN-PROCEEDS FROM DS-PROCEEDS(1)
                                        DS-PROCEEDS(2).


           SUBTRACT LN-ORGTERM FROM DS-SUMWGTTM.
           SUBTRACT LN-LNAMT FROM DS-SUMWGTAV.
           COMPUTE DS-SUMWGTTA =
              DS-SUMWGTTA - (LN-ORGTERM * LN-LNAMT).

           IF LN-SERVACCT = "Y"
              MOVE 0 TO LN-DLDISC LN-DLRVPRIN(1) LN-DLRVPRIN(2)
                        LN-DLRVPRIN(3) LN-DLRVCHGS(1) LN-DLRVCHGS(2)
                        LN-DLRVCHGS(3) LN-DLPARVHELD LN-DLPARVPAID
      * FOR SERVICE ACCOUNTS, UPDATE DS-UNPAIDCK WITH INSURANCE & FEES
      * THAT OUR OURS:
              PERFORM VARYING SUB FROM 1 BY 1 UNTIL SUB > 10
                 IF SUB < 9 AND LN-INSOURS(SUB) = "Y"
      *  GLB 07/02/01 USE WORKER FOR UNPAID
      *             ADD LN-INSPREM(SUB) TO DS-UNPAIDCK
                    ADD LN-INSPREM(SUB) TO HOLD-DS-UNPAIDCK
                 END-IF
                 IF LN-FEEOURS(SUB) = "Y"
      *             ADD LN-FEEAMT(SUB) TO DS-UNPAIDCK
                    ADD LN-FEEAMT(SUB) TO HOLD-DS-UNPAIDCK
                 END-IF
              END-PERFORM
            ELSE
              SUBTRACT LN-DLDISC FROM DS-DLDISC
                                      DS-DLDISC-VOL(1)
                                      DS-DLDISC-VOL(2)
              SUBTRACT LN-DLRVPRIN(1) FROM DS-DLRVPRIN(1)
              SUBTRACT LN-DLRVPRIN(2) FROM DS-DLRVPRIN(2)
              SUBTRACT LN-DLRVPRIN(3) FROM DS-DLRVPRIN(3)
              SUBTRACT LN-DLRVCHGS(1) FROM DS-DLRVCHGS(1)
              SUBTRACT LN-DLRVCHGS(2) FROM DS-DLRVCHGS(2)
              SUBTRACT LN-DLRVCHGS(3) FROM DS-DLRVCHGS(3)
              SUBTRACT LN-DLPARVHELD FROM DS-DLPARVHELD
              SUBTRACT LN-DLPARVPAID FROM DS-DLPARVPAID
      *       SUBTRACT LN-PROCEEDS FROM DS-UNPAIDCK.
              SUBTRACT LN-PROCEEDS FROM HOLD-DS-UNPAIDCK.
           SUBTRACT 1 FROM DS-OUTSUNIT(1).
           SUBTRACT LN-LNAMT FROM DS-OUTSAMT(1).

           IF LN-SERVACCT NOT = "Y"
              IF LN-DLRECOURSE = "F"
                 SUBTRACT 1 FROM DS-RCOURSE-UNITS(1)
                 SUBTRACT LN-LNAMT FROM DS-RCOURSE-AMT(1)
               ELSE
                 IF LN-DLRECOURSE = "P"
                    SUBTRACT 1 FROM DS-RCOURSE-UNITS(2)
                    SUBTRACT LN-DLPARTRCAMT FROM DS-RCOURSE-AMT(2)
                  ELSE
                    IF LN-DLRECOURSE = "N"
                       SUBTRACT 1 FROM DS-RCOURSE-UNITS(3)
                       SUBTRACT LN-LNAMT FROM DS-RCOURSE-AMT(3).

      * UPDATE DELINQUENCY NORMAL:
      *    -------------------------------------------------------------
      *    SETUP OUTBAL WORKER FOR UPDATE OF STATS
      *    -------------------------------------------------------------
           IF GP-DELINQ-WITH-O2 = "Y"
              MOVE LN-CURBAL TO SAVE-OUTBAL
           ELSE
              SUBTRACT LN-OT2BAL FROM LN-CURBAL
                         GIVING SAVE-OUTBAL.

           SUBTRACT 1 FROM DS-UNITS(DSNOW DSREC RDSUB)
                           DS-UNITS(DSNOW DSCON CSUB).
           SUBTRACT SAVE-OUTBAL FROM DS-OUTBAL(DSNOW DSREC RDSUB)
                                     DS-OUTBAL(DSNOW DSCON CSUB).
      * UPDATE DELINQUENCY BANKRUPT:
      *JGW IF LN-SRCD = "RB"
           IF LN-BNKRPTDATE NOT = 0
              SUBTRACT 1 FROM DS-UNITS-BNKRPT(DSNOW DSREC RDSUB)
                              DS-UNITS-BNKRPT(DSNOW DSCON CSUB)
              SUBTRACT SAVE-OUTBAL
                         FROM DS-OUTBAL-BNKRPT(DSNOW DSREC RDSUB)
                              DS-OUTBAL-BNKRPT(DSNOW DSCON CSUB).

      *  GLB 07/02/01 NEW FLAG INDICATING WHETHER TO UPDATE
      *      THE NEW 'UNPAID MANUAL' BUCKET OR 'REGULAR' UNPAID
           IF HOLD-DT-UNPAID = "N"
              ADD HOLD-DS-UNPAIDCK TO DS-UNPAID-MANUAL
           ELSE
              ADD HOLD-DS-UNPAIDCK TO DS-UNPAIDCK.

       UPDATE-DS1-FILE-EXIT.
           PERFORM REWRITE-DS1-FILE.
           PERFORM CLOSE-DS1-FILE.

      **************************************************
       UPDATE-TY1-FILE SECTION.
      **************************************************
           MOVE "UPDATING TODAY'S TOTAL FILE............." TO MESS.
           PERFORM SEND-LEGEND.

           PERFORM OPEN-TY1-FILE.
           MOVE "N" TO REC-EXISTS.
       UPDATE-TY1-FILE-CHECK.
           PERFORM SET-TY-KEY.
           PERFORM READ-TY1-FILE.
           IF IO-FG = 0
              GO TO UPDATE-TY1-FILE-UPD-REC.
           IF REC-EXISTS = "Y"
              GO TO IO-ERROR.

           MOVE "Y" TO REC-EXISTS.
           PERFORM CLEAR-TY-FILE.
           PERFORM SET-TY-KEY.
           PERFORM WRITE-TY1-FILE.
           IF IO-FG = 9 AND E-CODE NOT = "22"
              GO TO IO-ERROR.
           GO TO UPDATE-TY1-FILE-CHECK.

       UPDATE-TY1-FILE-UPD-REC.

           PERFORM GET-SRCD-SUB.

      * UPDATE UNEARNED STATISTICS NET AND VOLUME
      
           IF GP-UNEARNED-STATS = "Y"
              PERFORM VARYING TYSUB3 FROM 1 BY 1 UNTIL TYSUB3 > 6
                        AFTER TYSUB2 FROM 1 BY 1 UNTIL TYSUB2 > 2
                  IF TYSUB2 = 1
                     SUBTRACT EARN-UNEARN(TYSUB3) FROM
                                 TY-UNEARNED-AMT(TYSUB2 TYSUB3)
                  ELSE
                    IF (TYSUB3 < 4) OR
                       ((TYSUB3 = 4 AND NOT GP-INCL-OTH-IN-EARNED-R) OR
                       (TYSUB3 = 5 AND LN-PRDLDISC-UNEARN = 0) OR
                       (TYSUB3 = 6 AND NOT( GP-INCL-OTH-IN-EARNED-N OR
                                            GP-INCL-OTH-IN-EARNED-R)))
                        SUBTRACT LN-ANTICERN(TYSUB3)
                                 FROM TY-UNEARNED-AMT(TYSUB2 TYSUB3)
                    END-IF
                  END-IF
              END-PERFORM
      *       ----------------------------------------------------------
      *       UPDATE PROCEEDS: WE DO NOT CARE ABOUT ACCOUNT STATUS.
      *       ----------------------------------------------------------
              SUBTRACT LN-PROCEEDS FROM TY-PROCEEDS.


           SUBTRACT LN-ORGTERM FROM TY-SUMWGTTM.
           SUBTRACT LN-LNAMT FROM TY-SUMWGTAV.
           COMPUTE TY-SUMWGTTA =
              TY-SUMWGTTA - (LN-ORGTERM * LN-LNAMT).

      *----------------------------------------------------
      * UPDATE DEALER RESERVES:
      *
           IF LN-SERVACCT NOT = "Y"
              SUBTRACT LN-DLDISC FROM TY-DLDISC
              SUBTRACT LN-DLRVPRIN(1) FROM TY-DLRVPRIN(1 1)
                                           TY-DLRVPRIN(2 1)
              SUBTRACT LN-DLRVPRIN(2) FROM TY-DLRVPRIN(1 2)
                                           TY-DLRVPRIN(2 2)
              SUBTRACT LN-DLRVPRIN(3) FROM TY-DLRVPRIN(1 3)
                                           TY-DLRVPRIN(2 3)
              SUBTRACT LN-DLRVCHGS(1) FROM TY-DLRVCHGS(1 1)
                                           TY-DLRVCHGS(2 1)
              SUBTRACT LN-DLRVCHGS(2) FROM TY-DLRVCHGS(1 2)
                                           TY-DLRVCHGS(2 2)
              SUBTRACT LN-DLRVCHGS(3) FROM TY-DLRVCHGS(1 3)
                                           TY-DLRVCHGS(2 3)
              SUBTRACT LN-DLPARVHELD FROM TY-DLPARVHELD(1)
                                          TY-DLPARVHELD(2).

           IF SRCD-NOT-PL
              SUBTRACT 1 FROM TY-NO(1 SRCD)
                              TY-NO(2 SRCD)
              SUBTRACT LN-LNAMT FROM TY-AMT(1 SRCD)
                                     TY-AMT(2 SRCD)
              SUBTRACT LN-OT2BAL FROM TY-O2(1 SRCD)
                                      TY-O2(2 SRCD)
              SUBTRACT LN-INTCHG FROM TY-INT(1 SRCD)
                                      TY-INT(2 SRCD)
              SUBTRACT LN-EXTCHG FROM TY-INT(1 SRCD)
                                      TY-INT(2 SRCD)
           ELSE
              SUBTRACT 1 FROM TY-NO-PL(1 SRCD)
                              TY-NO-PL(2 SRCD)
              SUBTRACT LN-LNAMT FROM TY-AMT-PL(1 SRCD)
                                     TY-AMT-PL(2 SRCD)
              SUBTRACT LN-OT2BAL FROM TY-O2-PL(1 SRCD)
                                      TY-O2-PL(2 SRCD)
              SUBTRACT LN-INTCHG FROM TY-INT-PL(1 SRCD)
                                      TY-INT-PL(2 SRCD)
              SUBTRACT LN-EXTCHG FROM TY-INT-PL(1 SRCD)
                                      TY-INT-PL(2 SRCD).

      * UPDATE DELINQUENCY NORMAL:
      *    -------------------------------------------------------------
      *    SETUP OUTBAL WORKER FOR UPDATE OF STATS
      *    -------------------------------------------------------------
           IF GP-DELINQ-WITH-O2 = "Y"
              MOVE LN-CURBAL TO SAVE-OUTBAL
           ELSE
              SUBTRACT LN-OT2BAL FROM LN-CURBAL
                         GIVING SAVE-OUTBAL.

           SUBTRACT 1 FROM TY-UNITS(1 RDSUB)
                           TY-UNITS(2 CSUB).
           SUBTRACT SAVE-OUTBAL FROM TY-OUTBAL(1 RDSUB)
                                     TY-OUTBAL(2 CSUB).
      * UPDATE DELINQUENCY BANKRUPT:
      *JGW IF LN-SRCD = "RB"
           IF LN-BNKRPTDATE NOT = 0
              SUBTRACT 1 FROM TY-UNITS-BNKRPT(1 RDSUB)
                              TY-UNITS-BNKRPT(2 CSUB)
              SUBTRACT SAVE-OUTBAL FROM TY-OUTBAL-BNKRPT(1 RDSUB)
                                        TY-OUTBAL-BNKRPT(2 CSUB).

      ***************************************
      * UPDATE INSURANCES

      *  DO NOT INCLUDE 7&8 HERE THEY ARE NOT BEING USED YET
      *  AND WE DO NOT WANT TO COUND THEN AS NOT WRITTEN IN STATS
      *                                         (MJD 09/10/1999).
           PERFORM UPDATE-TY1-FILE-INS
              VARYING SUB FROM 1 BY 1
      *-------------------->UNTIL SUB > 8.
                            UNTIL SUB > 6.

           IF LN-INSELIGIBLE-DL OR LN-INSELIGIBLE-LL
              IF LN-INSOURS(1) = "Y"
                 SUBTRACT 1 FROM TY-INS-NO-ELIG(1).

           IF LN-INSELIGIBLE-AH
              IF LN-INSOURS(2) = "Y"
                 SUBTRACT 1 FROM TY-INS-NO-ELIG(2).

           IF LN-INSELIGIBLE-PP OR LN-INSELIGIBLE-PP2
              IF LN-INSOURS(3) = "Y"
                 SUBTRACT 1 FROM TY-INS-NO-ELIG(3).

           IF LN-INSELIGIBLE-O1
              IF LN-INSOURS(4) = "Y"
                 SUBTRACT 1 FROM TY-INS-NO-ELIG(4).

      * TEST FOR ROADGARD INSURANCE AND AD&D (WORLD)
      *    IF GP-I-AM-WORLD
              MOVE "N" TO ROADGARD-WRITTEN ADD-WRITTEN NON-FILE-WRITTEN
              PERFORM VARYING SUB FROM 1 BY 1 UNTIL SUB > 10
                 IF LN-FEECODE(SUB) NOT = SPACES
                    IF LN-FEEOURS(SUB) = "Y"
                     IF LN-FEECODE(SUB) = GP-NON-FILE-FEECODE
                        SUBTRACT 1 FROM TY-FEE-NO-WROTE(1)
                        SUBTRACT LN-FEEAMT(SUB) FROM TY-FEE-AMT-WROTE(1)
                        MOVE "Y" TO NON-FILE-WRITTEN
                     END-IF
                     IF LN-FEECODE(SUB) = BR-ADD-INS-FEECODE
                        SUBTRACT 1 FROM TY-FEE-NO-WROTE(2)
                        SUBTRACT LN-FEEAMT(SUB) FROM TY-FEE-AMT-WROTE(2)
                        MOVE "Y" TO ADD-WRITTEN
                     END-IF
                     IF LN-FEECODE(SUB) = BR-ROAD-G-SGL-FEECODE OR
                       LN-FEECODE(SUB) = BR-ROAD-G-FAM-FEECODE
                       SUBTRACT 1 FROM TY-FEE-NO-WROTE(3)
                       SUBTRACT LN-FEEAMT(SUB) FROM TY-FEE-AMT-WROTE(3)
                        MOVE "Y" TO ROADGARD-WRITTEN
                     END-IF
                    END-IF
                 END-IF
              END-PERFORM
              IF NON-FILE-WRITTEN = "N"
                 SUBTRACT 1 FROM TY-FEE-NO-NOTWROTE(1)
              END-IF
              IF ADD-WRITTEN = "N"
                 SUBTRACT 1 FROM TY-FEE-NO-NOTWROTE(2)
              END-IF
              IF ROADGARD-WRITTEN = "N"
                 SUBTRACT 1 FROM TY-FEE-NO-NOTWROTE(3).

       UPDATE-TY1-FILE-EXIT.
           PERFORM REWRITE-TY1-FILE.
           PERFORM CLOSE-TY1-FILE.

      **************************************************
       UPDATE-TY1-FILE-INS SECTION.
      **************************************************
           IF LN-INSOURS(SUB) = "Y"
              IF LN-INSPREM(SUB) NOT = 0
                 SUBTRACT 1 FROM TY-INS-NO-WROTE(SUB)
                 SUBTRACT LN-INSPREM(SUB) FROM TY-INS-AMT-WROTE(SUB)
              ELSE
                 SUBTRACT 1 FROM TY-INS-NO-NOTWROTE(SUB).

      ************************
       UPDATE-FM1-FILE SECTION.
      ************************
           MOVE "UPDATING FORM REQUEST FILE............." TO MESS.
           PERFORM SEND-LEGEND.

           PERFORM OPEN-FM1-FILE.

           MOVE FM-PATH-OWNBR TO FM-BRNO.
           MOVE LN-ACCTNO     TO FM-INITKEY.
           MOVE 0             TO FM-FMNO.

           PERFORM START-FM1-FILE.
       NEXT-FM1-DELETE.
           PERFORM READ-FM1-FILE-NEXT.
           IF IO-FG = 0 AND (FM-BRNO = FM-PATH-OWNBR)
              MOVE FM-INITKEY TO LPWSFM-EDIT-FM-INITKEY
              IF (LPWSFM-EDIT-FM-ACCTNO = LN-ACCTNO) 
                 PERFORM DELETE-FM1-FILE
                 GO TO NEXT-FM1-DELETE.

           PERFORM CLOSE-FM1-FILE.

      ************************
       UPDATE-LN1-FILE SECTION.
      ************************
           MOVE "UPDATING LOAN MASTER...................." TO MESS.
           PERFORM SEND-LEGEND.

           PERFORM OPEN-LN1-FILE.

           MOVE WK-SAVE-REC TO LN-REC.
           PERFORM READ-LN1-FILE.
           IF IO-FG = 9
              GO TO IO-ERROR.

           SUBTRACT LN-ACCERN(1) LN-ACCERN(2) LN-ACCERN(3)
                    LN-ACCERN(5) FROM LN-NONACRERN.

           IF NOT( GP-INCL-OTH-IN-EARNED-N OR
                                   GP-INCL-OTH-IN-EARNED-R)
              SUBTRACT LN-ACCERN(6) FROM LN-NONACRERN.

           IF NOT (GP-INCL-OTH-IN-EARNED-R)
              SUBTRACT LN-ACCERN(4) FROM LN-NONACRERN.

           MOVE 0 TO LN-CURBAL LN-TOTERN.
           MOVE TRANS-DATE TO LN-POFFDATE.

      * UPDATE LN-PROC-DISBUSED, V=NEGATIVE TO OFFSET DIRECT DEPOSIT
      *                          R=REFUND FROM DEALER TO OFFSET DEPOSIT

           IF DL-ACH-FG = "Y"
              IF SAVE-DT-PAID = "B"
                 IF SAVE-ORIG-DT-STATUS = "R"
                    MOVE "R" TO LN-PROC-DISBURSED
                 ELSE
                    MOVE "V" TO LN-PROC-DISBURSED.

      * IN SP/PAREXT, IF PARTICIPATING HAS ALREADY BEEN RELEASED TO
      * ACH OR PLATIUM ACCOUNTS PAYABLE, FLAGGING LOAN AS "R"
      * WILL CAUSE VOID TO SHOW UP ON REFUND REPORT & THEY WILL
      * MANUALLY COLLECT REFUND THE DEALER....

      *    IF GP-I-AM-REGACC
      *       IF SAVE-DT-PART-HELD-STATUS = "R"
      *          MOVE "R" TO LN-PARVHELD-PAID-FG
      *       ELSE
      *          MOVE "V" TO LN-PARVHELD-PAID-FG.

           IF WK-UNWIND-REASON-CD = " "
              MOVE " " TO LN-UNWIND-REASON-CODE
           ELSE
              MOVE WK-UNWIND-REASON-CD TO LN-UNWIND-REASON-CODE
              MOVE WK-UNWIND-DRAWER    TO LN-TILLNO.

           PERFORM REWRITE-LN1-FILE.

       UPDATE-LN1-FILE-EXIT.
           PERFORM CLOSE-LN1-FILE.

      ************************
       UPDATE-PD1-FILE SECTION.
      ************************
      * TEST FOR VOID OF A RENEWED LOAN
           IF NOT (LN-SRCD = "RN" OR "PB" OR "RB" OR "RO")
              GO TO UPDATE-PD1-TEST-MISS.

           MOVE "UPDATING SCRATCH PAD FILE..............." TO MESS.
           PERFORM SEND-LEGEND.

           PERFORM OPEN-PD1-FILE.
           PERFORM SET-PD1-KEY.
           PERFORM READ-PD1-FILE.
           IF IO-FG = 0
              MOVE LN-REC TO PD-RECORD
              MOVE ZEROES TO PD-APPROVAL-LIMIT
              MOVE PD-SRCD TO PD-PO-TRCD
              PERFORM SET-PRIOR-LOAN-DEL
              PERFORM GET-PRIOR-LOAN-INFO
              PERFORM REWRITE-PD1-FILE
           ELSE
              PERFORM SET-PD1-KEY
              MOVE LN-REC TO PD-RECORD
              MOVE ZEROES TO PD-APPROVAL-LIMIT
              MOVE PD-SRCD TO PD-PO-TRCD
              PERFORM SET-PRIOR-LOAN-DEL
              PERFORM GET-PRIOR-LOAN-INFO
              PERFORM WRITE-PD1-FILE.
           IF IO-FG = 9
              GO TO IO-ERROR.
           PERFORM CLOSE-PD1-FILE.

      * GET GOOD LN-REC BACK, GET-PRIOR-LOAN-INFO READS PRLNNO AND MISS ADJ.
           MOVE WK-SAVE-REC TO LN-REC.
           PERFORM GET-LN.
           IF IO-FG = 9
              GO TO IO-ERROR.

       UPDATE-PD1-TEST-MISS.
      * TEST FOR MISSISSIPPI REBATE-ADJ:
           IF LN-STATE-FLAG NOT = "A" OR LN-STATE-WORKER = 0
              GO TO EXIT-UPDATE-PD1.

           MOVE LN-ACCTNO TO SAVE-LN-ACCTNO.

           MOVE "UPDATING MISSISSIPPI REBATE ADJ LOAN...." TO MESS.
           PERFORM SEND-LEGEND.
           MOVE BRANCH       TO LN-OWNBR.
           MOVE LN-OLDACCTNO TO LN-ACCTNO.
           PERFORM OPEN-LN1-FILE.
           PERFORM READ-LN1-FILE.
           IF IO-FG = 9
              GO TO IO-ERROR.

           MOVE " " TO LN-STATE-FLAG.

           PERFORM REWRITE-LN1-FILE.
           PERFORM CLOSE-LN1-FILE.

           PERFORM CREATE-LM1-ADJ.

      * GET GOOD LN-REC BACK, CURRENT LN-REC IS OLDACCTNO
           MOVE WK-SAVE-REC TO LN-REC.
           PERFORM GET-LN.
           IF IO-FG = 9
              GO TO IO-ERROR.

       EXIT-UPDATE-PD1.

      ******************************************************
       CREATE-LM1-ADJ SECTION.
      ******************************************************

      * CREATE MEMO ON ADJUSTED PRIOR LOAN, ACCTNO STILL ACCTNO

           PERFORM OPEN-LM1-FILE.

           MOVE LM-PATH-OWNBR TO LM-BRNO
                                 QLM1-WBEG-BRNO
                                 QLM1-WEND-BRNO.
           MOVE LN-ACCTNO     TO LM-ACCTNO
                                 QLM1-WBEG-ACCTNO
                                 QLM1-WEND-ACCTNO.
           MOVE 0             TO LM-DATE
                                 QLM1-WBEG-DATE-REV.
           MOVE ALL "9"       TO QLM1-WEND-DATE-REV.
           MOVE 0             TO LM-SEQ SAVE-LM-SEQ
                                 QLM1-WBEG-SEQ.

           MOVE ALL "9"       TO QLM1-WEND-SEQ.
           COMPUTE QLM1-WSBEG-DATE-REV = LM-DATE-REVERSED + 1.

           PERFORM START-LM1-FILE.
       UPDATE-LM1-NEXT.
           PERFORM READ-LM1-FILE-NEXT.
           IF IO-FG = 0
              IF (LM-ACCTNO = LN-ACCTNO) AND (LM-BRNO = LM-PATH-OWNBR)
                 IF LM-DATE = 0
                    MOVE LM-SEQ TO SAVE-LM-SEQ
                    GO TO UPDATE-LM1-NEXT.

           MOVE SPACES TO LM-REC.

           MOVE LM-PATH-OWNBR TO LM-BRNO.
           MOVE LN-ACCTNO     TO LM-ACCTNO.
           MOVE 0             TO LM-DATE.
           ADD 1 SAVE-LM-SEQ GIVING LM-SEQ.

           MOVE TRANS-DATE TO DATE-YYYYMMDD.
           PERFORM CONVERT-YYYYMMDD-TO-MMDDYY.
           MOVE DATE-MMDDYY   TO WK-DATE.
           MOVE SAVE-LN-ACCTNO TO WK-ACCTNO.
           INSPECT WK-ACCTNO REPLACING ALL "/" BY "-".
           MOVE WK-MMEMO TO LM-MEMO.
           PERFORM WRITE-LM1-FILE.
           PERFORM CLOSE-LM1-FILE.


      ***********************************************
       SET-DELINQUENCY SECTION.
      ***********************************************
           MOVE TRANS-DATE TO AGEING-DATE.
           PERFORM COMPUTE-CONTRACTUAL.
           PERFORM COMPUTE-RECENCY.

      ***********************************************
       UPDATE-NOTICE-FILE SECTION.
      ***********************************************
           PERFORM OPEN-NO1-FILE.
           MOVE LN-OWNBR  TO NO-BRNO.
           MOVE LN-ACCTNO TO NO-ACCTNO CRNOR2-ACCTNO.
           PERFORM READ-NO1-FILE.

           MOVE TRANS-DATE     TO CRNOR2-SYSDATE.
           MOVE "CL/CRNOR2"    TO FORM-NAM.
           CALL FORM-PROGX USING FORM-PATH CRNOR2-LINK NO-REC
                                 POFF-WORKERS LN-REC.
           CANCEL FORM-PROGX.

           MOVE LN-OWNBR  TO NO-BRNO.
           MOVE LN-ACCTNO TO NO-ACCTNO.
           IF IO-FG = 0
              PERFORM REWRITE-NO1-FILE
           ELSE
              PERFORM WRITE-NO1-FILE.

           PERFORM CLOSE-NO1-FILE.
       EXIT-UPDATE-NOTICE-FILE.
           EXIT.

       COPY "LIBLP/LPDSCL.CPY".
       COPY "LIBLP/LPTYCL.CPY".
       COPY "LIBLP/LPTRSCL.CPY".
       COPY "LIBLP/LPDTCL.CPY".
       COPY "LIBGB/DATER.CPY".
       COPY "LIBLP/POSTDATE.CPY".
       COPY "LIBLP/LPSRCD.CPY".
       COPY "LIBLP/LPPDUE.CPY".
       COPY "LIBGB/AGEING.CPY".
       COPY "LIBGB/GPENV.CPY".
       COPY "LIBLP/LPUPWK.CPY".
       COPY "LIBLP/LPCEPP.CPY".
       COPY "LIBLP/LPEARN.CPY".
       COPY "LIBLP/LPMDTE.CPY".
       COPY "LIBGB/SPPDTH.CPY".
       COPY "LIBGB/SCREEN.CPY".

      *********************************
       MISC-PARAGRAPHS SECTION.
      *********************************
       COPY "LIBGB/GETENV.CPY".
       COPY "LIBGB/ACCESS.CPY".
       FORM-RESET.
       SETUP-LINK-INFO.
       VDU-CONVERT-FIELD.
       SET-PRIOR-LOAN-DEL.
           MOVE LN-PRLNAMT     TO PD-PRLNAMT.
           MOVE LN-PRPDUE-DAYS TO PD-PRPDUE-DAYS.
      * LOOK AT THE NEXT 5 LINES RE: LOGIC FROM A90
           MOVE LN-PRDELINQAMT TO PD-PRDELINQAMT.
      * FOR (GA) SCFRMLA = 12 OR 13:
           IF PD-PRDELINQAMT > 0
              MOVE PD-PRDELINQAMT TO PD-PRPAID-AHEAD
           ELSE
              MOVE 0              TO PD-PRPAID-AHEAD.
           MOVE 0              TO PD-RESTRICT-ADJ.
      *-----------------------------------------------------------------
       GET-PRIOR-LOAN-INFO.
           MOVE BRANCH    TO LN-OWNBR.
           MOVE LN-PRLNNO TO LN-ACCTNO.
           PERFORM OPEN-LN1-FILE.
           PERFORM READ-LN1-FILE.
           IF IO-FG = 0
              MOVE LN-LOANDATE TO PD-LOANDATE
              MOVE LN-SMALLOAN TO PD-SMALLOAN
              MOVE LN-SALEFIN TO PD-SALEFIN
              MOVE LN-SERVACCT TO PD-SERVACCT
              MOVE LN-LOANTYPE TO PD-LOANTYPE.
           PERFORM CLOSE-LN1-FILE.

       GET-LC1-RECORD.
           PERFORM OPEN-LC1-FILE.
           PERFORM READ-LC1-FILE.
           PERFORM CLOSE-LC1-FILE.
       GET-LN.
           PERFORM OPEN-LN1-FILE.
           PERFORM READ-LN1-FILE.
           PERFORM CLOSE-LN1-FILE.
       GET-COLLATERAL.
           PERFORM OPEN-CD1-FILE.
           MOVE LN-COLLCD TO CD-CODE.
           MOVE "CO"      TO CD-TYPE.
           PERFORM READ-CD1-FILE.
           IF IO-FG NOT = 0
              MOVE " " TO CD-CO-SECURED.
           PERFORM CLOSE-CD1-FILE.
       SET-PD1-KEY.
           MOVE SPACES    TO PD-RECORD.
           MOVE "R"       TO PD-ID.
           MOVE LN-OWNBR  TO PD-BRANCH.
           MOVE LN-PRLNNO TO PD-SOC-SEC.
       SET-DS1-KEY.
           MOVE TRANS-DATE TO DATE-YYYYMMDD.
           PERFORM CONVERT-YYYYMMDD-TO-YYYYMM.
           MOVE DATE-YYYYMM TO DS-DATE-CCYYMM.
           MOVE LN-OWNBR    TO DS-BRANCH.
           MOVE LN-DLNO     TO DS-DLNO.
           MOVE LN-CLASS    TO DS-CLASS.
       SET-TY-KEY.
           MOVE LN-OWNBR   TO TY-BRNO.
           MOVE TRANS-DATE TO TY-DATE.
           MOVE LN-CLASS   TO TY-CLASS.
       SEND-LEGEND.
           MOVE MESS TO WR-BUF.
           MOVE MSEL-LIST TO WR-LIST.
           PERFORM CALL-WRFORM.

       COPY "LIBLP/LPLPCL.CPY".

      *******************************************
       IO-ROUTINE SECTION.
      *******************************************
       COPY "LIBGB/CONNECT_SQL.CPY".
       COPY "LIBLP/LPTYGS_SQL.CPY".
       COPY "LIBLP/LPTRSGS_SQL.CPY".
       COPY "LIBLP/LPSPGS_SQL.CPY".
       COPY "LIBLP/LPPDGS_SQL.CPY".
       COPY "LIBLP/LPNOGS_SQL.CPY".
       COPY "LIBLP/LPLTPGS_SQL.CPY".
       COPY "LIBLP/LPLTLGS_SQL.CPY".
       COPY "LIBLP/LPLTIGS_SQL.CPY".
       COPY "LIBLP/LPLTFGS_SQL.CPY".
       COPY "LIBLP/LPLTCGS_SQL.CPY".
       COPY "LIBLP/LPLTBGS_SQL.CPY".
       COPY "LIBLP/LPLTAGS_SQL.CPY".
       COPY "LIBLP/LPLPGS_SQL.CPY".
       COPY "LIBLP/LPLNGS_SQL.CPY".
       COPY "LIBLP/LPLMGS_SQL.CPY".
       COPY "LIBLP/LPLCGS_SQL.CPY".
       COPY "LIBLP/LPFMGS_SQL.CPY".
       COPY "LIBLP/LPDTGS_SQL.CPY".
       COPY "LIBLP/LPDSGS_SQL.CPY".
       COPY "LIBLP/LPDLGS_SQL.CPY".
       COPY "LIBLP/LPCDGS_SQL.CPY".
       COPY "LIBLP/LPCAGS_SQL.CPY".
       COPY "LIBLP/LPBWGS_SQL.CPY".
       COPY "LIBGB/GBBRGS_SQL.CPY".
       COPY "LIBLP/LPDL1RN.CPY".
       COPY "LIBLP/LPLC1RN.CPY".
       COPY "LIBLP/LPLM1IN.CPY".
       COPY "LIBLP/LPLN1IN.CPY".
       COPY "LIBLP/LPLP1IN.CPY".
       COPY "LIBLP/LPCA1IN.CPY".
       COPY "LIBLP/LPSP1RN.CPY".
       COPY "LIBLP/LPLTA1IN.CPY".
       COPY "LIBLP/LPLTB1IN.CPY".
       COPY "LIBLP/LPLTC1IN.CPY".
       COPY "LIBLP/LPLTF1IN.CPY".
       COPY "LIBLP/LPLTI1IN.CPY".
       COPY "LIBLP/LPLTL1IN.CPY".
       COPY "LIBLP/LPLTP1RN.CPY".
       COPY "LIBLP/LPBW1IN.CPY".
       COPY "LIBLP/LPTY1IN.CPY".
       COPY "LIBLP/LPDS1IN.CPY".
       COPY "LIBLP/LPWKI.CPY".
       COPY "LIBLP/LPTRS1IN.CPY".
       COPY "LIBLP/LPDT1IN.CPY".
       COPY "LIBLP/LPFM1IN.CPY".
       COPY "LIBLP/LPPD1IN.CPY".
       COPY "LIBLP/LPNO1IN.CPY".
       COPY "LIBLP/LPCD1RN.CPY".
       COPY "LIBGB/GBBRRN.CPY".

       COPY "LIBGB/FERRORS.CPY".
           MOVE "E" TO ERRCD.

      **********************************
       END-ROUTINE SECTION.
      **********************************
           PERFORM CLOSE-FILES.
       EXIT-PROG.
       STOP-RUN.
           EXIT PROGRAM.
